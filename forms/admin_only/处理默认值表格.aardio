import fonts.fontAwesome;
import win.ui;
/*DSG{{*/
var winform_get_default = win.form(text="获取默认值";right=743;bottom=527)
winform_get_default.add(
button2={cls="button";text="开始导入CCR...";left=40;top=56;right=248;bottom=104;color=14120960;dr=1;dt=1;font=LOGFONT(h=-14);note="SAP数据整理 - 开利研发部";z=2};
button5={cls="button";text="开始导入QHC...";left=336;top=56;right=640;bottom=104;color=14120960;dr=1;dt=1;font=LOGFONT(h=-14);note="SAP数据整理 - QHC开发部(只读,仅用于参考)";z=3};
edit3={cls="edit";left=0;top=136;right=744;bottom=504;ah=1;aw=1;db=1;dl=1;dr=1;dt=1;edge=1;multiline=1;vscroll=1;z=1}
)
/*}}*/

//import console; 
import fsys;
import config
import string.keywords;
_skip_dir_name = "归档|存档|副本|旧版本|操作手册|Archieve|oldVersion"
_skip_file = {
	"岛柜系列产品默认配置2022.5.17 - 副本.xlsx",
	"冷风机默认配置.xlsx",
}

var default_con = {
	_CCR_path = "D:\users\CCL100100\Carrier Corporation\SAP数据整理 - 开利研发部\";
	_QHC_path = "D:\users\CCL100100\Carrier Corporation\SAP数据整理 - QHC开发部(只读,仅用于参考)\";
	_fieldsName = {"产品编码", "分类", "特性", "特性值"}; //, 
	_fieldsColumn = {"B", "C", "D", "F"}; //, 
	_testFields = {"SAP语句", "更改标记"};
	_testColumn = {"L", "M"};
	_title = "默认值";  
}

import sqlite;
var db = sqlite(config.dbServer.path + "qhc_OD_lib.db");

if db.existsTable(default_con._title) db.exec("DROP table " + default_con._title);
db.exec("CREATE TABLE "+ default_con._title +"( " + string.join(default_con._fieldsName,", ") + ");")

/*获取当前excel数据getListFromXlsx{{*/
var getListFromXlsx = function(_xlsx_path, winform_get_default, default_con){
	import com.excel;
	var excel = com.excel(true);
	
	import console;
	console.open();
	
	var wb = null;
	try{
			wb = excel.WorkBooks.Open(
			_xlsx_path /* filename */,
			false /* UpdateLinks */,
			true /* ReadOnly */,
			/* Format */,
			/* Password */,
			/* WriteResPassword */,
			/* IgnoreReadOnlyRecommended */,
			/* Origin */,
			/* Delimiter */,
			/* Editable */,
			/* Notify */,
			/* Converter */,
			/* AddToMru */,
			/* Local */,
			/* CorruptLoad */
		);
	}
	if !wb 
		winform_get_default.edit3.print("无法打开文件: ", _xlsx_path, /*"完整路径："+fullpath*/)
	if !wb
		return ; 
	excel.alerts = false;
	var sht = wb.Sheets.Item(1);
	sht.AutoFilterMode = false;
	if sht.ListObjects.Count
		if sht.ListObjects("表1").AutoFilter
			sht.ListObjects("表1").AutoFilter.ShowAllData();
	var tab ={};
	for (i=1; #default_con._fieldsName)
		tab[default_con._fieldsName[i]] = sht.UsedRange.Columns(default_con._fieldsColumn[i]).value2;
	var testTab = {};	
	for (i=1; #default_con._testFields)
		testTab[default_con._testFields[i]] = sht.UsedRange.Columns(default_con._testColumn[i]).value2;
			
	var rc = sht.usedRange.Rows.Count;
	wb.close(false);
	excel.alerts = true;
	
	import sqlite;
	import config;
	var db = sqlite(config.dbServer.path + "qhc_OD_lib.db");
	
	import string;
	import math;
	
	db.beginTrans();
	for (j=2; rc){
		if tab[default_con._fieldsName[1]][j] {
			
			var cmd = db.prepare("INSERT INTO [默认值] VALUES( " + "@" + string.join(default_con._fieldsName, ", @") + ");");
			var t={};
			var st = {};
			for(i=1;#default_con._fieldsName)
				t[default_con._fieldsName[i]] = tab[default_con._fieldsName[i]][j][1]; //行数据即使只有一个单元格也是按行列保存的,所有要有[1]
			for(i=1;#default_con._testFields)
				st[default_con._testFields[i]] = testTab[default_con._testFields[i]][j][1];			

			
			if !t["产品编码"] or !t["分类"] or !t["特性"] continue; //跳过无值的行			
			t["产品编码"] = string.trim(t["产品编码"]);
			t["分类"] = string.trim(t["分类"]);	
			t["特性"] = string.trim(t["特性"]);			
			if !#t["产品编码"] or !#t["分类"] continue; //跳过无值的行	
			
			if math.isFinite(t["特性值"])			
				t["特性值"] ?= tostring((math.modf(t["特性值"]))); //去除结尾的0
			
			st["SAP语句"] ?= string.replace(st["SAP语句"]," ","");
			st["SAP语句"] ?= string.upper(st["SAP语句"]);
			var checkod = "$SET_DEFAULT($SELF," + t["特性"] + ",'" + t["特性值"] + "'),";
			if st["SAP语句"] !== checkod
				winform_get_default.edit3.print("!!!语句不匹配:",checkod, st["SAP语句"]);
			if st["更改标记"]
				if string.indexOf(st["更改标记"],"删")
					if !string.indexOf(st["更改标记"],"增")	//有些人先删又加烦死了
						continue;
			cmd.step(t);
		}
	}
	db.commitTrans();
	db.close();
}
/*}}*/
var deepSearchFile = function(path,filter = "*.*"){
	
	fsys.enum( path, filter,
		function(dirname,filename,fullpath,findData){
			if(filename){
				if !string.startWith(filename,"~$"){	//跳过临时文件
					if !table.find(_skip_file, filename){	//跳过指定文件
           				winform_get_default.edit3.print("发现文件：", filename, /*"完整路径："+fullpath*/)
           				thread.invokeAndWait(getListFromXlsx, fullpath, winform_get_default, default_con);
           			}
           		}   		
			}
		},
		function(dirpath,dirname){ //忽略目录
			if string.keywords(_skip_dir_name).findOne(dirname) return false; 
			else return true;
		} 
	);
	winform_get_default.edit3.print("完成")
}





winform_get_default.onDestroy = function(){
    if excel excel.alerts = true;    
}

winform_get_default.button2.oncommand = function(id,event){
	deepSearchFile(default_con._CCR_path,"*默认*.xlsx");
}

winform_get_default.button5.oncommand = function(id,event){
	deepSearchFile(default_con._QHC_path,"*默认*.xlsx");
}

winform_get_default.show();
//deepSearchFile(default_con._CCR_path,"*默认*.xlsx");
//deepSearchFile(default_con._QHC_path,"*默认*.xlsx");

win.loopMessage();
return winform_get_default;