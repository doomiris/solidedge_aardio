import win.ui;
/*DSG{{*/
var winform = win.form(text="填写专用号申请表";right=759;bottom=400;image=$"\res\SE BG.jpg";bkBottom=0;bkLeft=0;bkRight=0;bkTop=0)
winform.add(
button={cls="button";text="开始";left=9;top=6;right=137;bottom=47;dl=1;dt=1;z=1};
combobox2={cls="combobox";left=146;top=16;right=749;bottom=42;dl=1;dr=1;dt=1;edge=1;items={};mode="dropdownlist";z=3};
listview={cls="listview";left=0;top=60;right=760;bottom=369;ah=1;db=1;dl=1;dr=1;dt=1;edge=1;fullRow=1;gridLines=1;z=2};
progress={cls="progress";left=0;top=370;right=760;bottom=400;db=1;dl=1;dr=1;edge=1;max=100;min=0;z=4}
)
/*}}*/

import tarray;
import com.excel;
import string.keywords;
import config;
import fsys.table;
import QHC;
import domisoft;

var excel_0 = com.excel(false);
var excel_2;
var getErr = {};
var etab = {};

winform.frmReLoad = function(){
	winform.combobox2.clear();
	com.enumRunning( 
		function(displayName,object){
			try{
				var s = com.GetTypeInfo(object).GetDocumentation().name;
				if s = "_Workbook"
					if string.keywords('新增|专用号|申请').findOne(displayName)
						winform.combobox2.add(displayName);
			}
		}
	)
	
	if winform.combobox2.count {
		var findActiveWB;
		excel_0 = com.excel(false);
		if excel_0.ActiveWorkbook
			findActiveWB = table.indexOf(winform.combobox2.items, excel_0.ActiveWorkbook.Name)
		winform.combobox2.selIndex = findActiveWB ? findActiveWB : 1;
	}
}
winform.frmReLoad();
winform.combobox2.onDropDown = winform.frmReLoad;

winform.button.oncommand = function(id,event){
	if !#winform.combobox2.selText return ; 	

	sht = domisoft.getRunningWorkSheet(winform.combobox2.selText)
	excel_2 = sht.Parent.Application;
	
	if sht.AutoFilterMode
		sht.AutoFilterMode = false;
	if sht.ListObjects.Count {
		for (i=1;sht.ListObjects.Count){			
			select(sht.ListObjects.item(i).Name) {
				case "Table1","表1" {
					sht.ListObjects.item(i).AutoFilter.ShowAllData();
					break;
				}
			}

		}
	}
	
	if !sht.Rows("2:2").find("物料描述") return error("专用号申请表格式错误"); 
	if !sht.Rows("2:2").find("产品大类") return error("不是最新版专用号申请表"); 


	//物料描述列???
	var etr = function(){
		return  sht.usedrange.rows[sht.usedrange.rows.count].row;
	}
	
	var tRng = sht.Range("A3:V" ++ etr());

	var etab = tRng.value;
	var etab_count = #etab;
	
	var bar = winform.progress;
	bar.min = 0;
	bar.max = etab_count;
	
	var str_today = function(separator){
		var tm = time.now();
		tm.format = string.join({"%Y";"%m";"%d"},separator);
		return tostring(tm);
	}
	
	import config;
	import win.inputBox;
	
	config.tbm.Designer :=  winform.inputBox("这是第一次使用, 请先输入姓名:", "姓名") ;
	config.tbm.save();
	
	var username = config.tbm.Designer;
	
	import string.keywords;
	
	_plastics = 'PC|PVC|PS|PP|ABS|夜帘|橡胶';
	_glass = '玻璃|钢化玻璃|有机玻璃';
	_glass_sys = '钢化玻璃|钢化玻璃|有机玻璃';
	_metal_sys = '铝合金|不锈钢板|焊接件|焊接件';
	_metal = '铝合金|不锈钢|焊接件|铆接件';
	_cooper = '管|TP2M|蒸发器|冷凝器|阀';
	_cooper_sys = '铜管|铜管|蒸发器|冷凝器|阀';
	
	winform.listview.clear(true);
	winform.listview.addItem(sht.parent.name);
	
	for(i=1; #etab){
    	bar.pos = i;
    	
		for(j=1; 22 /* V */)
			if !etab[i][j] etab[i][j] = "";  // EXCEL SAFE ARRAY
		if !string.len(etab[i][5 /* E 物料描述 */]) continue;
		etab[i][1] = i;
		etab[i][2] = "=len(E" ++ (i+2) ++ ")";
		
		if !#etab[i][3] etab[i][3] = username;
		if !#etab[i][4] etab[i][4] = str_today("/");
		if !#etab[i][7] etab[i][6 /* F */] = "ZA";
		if !#etab[i][9] etab[i][8 /* H */] = "柜体";
		if !#etab[i][13] etab[i][13 /* M */] = "新品BOM";
		if !#etab[i][19] etab[i][19 /* S */] = table.shift(string.split(etab[i][5 /* E */]," "));
		
		var str = etab[i][5 /* E */];
		
		var ePush = function(val, index){
			index := {7 /* G */,9 /* I */,10 /* J */,11 /* K */,12 /* L */, 22 /* V */};
			for(k=1; #index)
				if !#etab[i][index[k]] etab[i][index[k]] = val[k];
			//TODO: 根据已经填写的信息计算空白内容
		}	
	
		
		if string.keywords('可选项').findOne(str)
			ePush({"SG","KMAT","配置","09","可选项","否"});
			
		elseif string.keywords('总装|组装').findOne(str)
			ePush({"SG","HALB","配置","08","总装","否"});
			
		elseif string.keywords('组件').findOne(str)	
			ePush({"SA","HALB","非配置","08","总装","否"});
			
		elseif string.keywords('已喷').findOne(str)	
			ePush({"SC","HALB","非配置","01","喷粉件","否"});
			
		elseif string.keywords('玻璃门').findOne(str)	
			ePush({"R08","ROH","非配置","08","玻璃门","否"});		
				
		elseif string.keywords(_plastics).findOne(str)
			ePush({"R02","ROH","非配置","02", string.split(_plastics,"|")[string.keywords(_plastics).findOne(str)],"否"});
	
		elseif string.keywords('亚克力|EPS|尼龙|GFRP|GFK').findOne(str)
			ePush({"R02","ROH","非配置","02","其他塑料件","否"});
	
		elseif string.keywords('底箱体|发泡板|发泡箱体').findOne(str)
			ePush({"SD","HALB","非配置","08","发泡","否"});
			
		elseif string.keywords(_glass).findOne(str)		
			ePush({"R14","ROH","非配置","14", string.split(_glass_sys,"|")[string.keywords(_glass).findOne(str)],"否"});
			
		elseif string.keywords('点焊').findOne(str)
			ePush({"SH","HALB","非配置","01","焊接件","否"});
			
		elseif string.keywords(_metal).findOne(str)
			ePush({"R01","ROH","非配置","01", string.split(_metal_sys,"|")[string.keywords(_metal).findOne(str)],"否"});
			
		elseif string.keywords('木包装').findOne(str)
			ePush({"R15","ROH","非配置","16","木包装","否"});
			
		elseif string.keywords('板|连接件|支撑|未喷').findOne(str)
			ePush({"SB","HALB","非配置","01","钣金","否"});
			
		elseif string.keywords(_cooper).findOne(str)
			ePush({"R07","ROH","非配置","07",string.split(_cooper_sys,"|")[string.keywords(_cooper).findOne(str)],"否"});
	
	}
	//table.sort(etab, lambda(b) owner[12]>b[12]);
	sht.Columns(9 /* I */).ColumnWidth = #etab[1][9 /* I */] + 2.75;
	sht.Columns(19 /* S */).ColumnWidth = #etab[1][19 /* S */] + 2.75;
		tRng.value2 = etab;
	
	var filename = sht.parent.name;
	filename = string.replace(filename, "姓名", username);
	if etab[1][19 /* S */]
		filename = string.replace(filename, "型号", etab[1][19 /* S */]);
	filename = string.replace(filename, "日期", str_today("."));
	
	import win.clip
	win.clip.write(string.left(filename,#filename-1))
	
   	if excel_0 excel_0.alerts = true;    //防止意外关闭后Excel假死
   	if excel_2 excel_2.alerts = true;    //防止意外关闭后Excel假死
    sht = null;
/*
    excel_2 = null;
    excel_0 = null;
*/
	com.Release(sht)
    com.Release(excel_2)
    com.Release(excel_0)
    collectgarbage("collect");
	
}

/*点击定位{{*/
winform.listview.onSelChanged = function(selected,item,subItem,nmListView){
	if !selected return ; 
	if !item return ;
	var row = owner.getItemText(item,1);
	if !row return ; 
	sht = domisoft.getRunningWorkSheet(winform.combobox2.selText);
	if !sht return ; 
	excel_2 = sht.Parent.Application;
	excel_2.ActiveWindow.ScrollRow = row;
	sht = null;
	excel_2 = null;
}
/*}}*/

/*右键菜单{{*/
winform.listview.onnotify = function(id,code,ptr){
	select(code) {
    	case 0xFFFFFFFB/*_NM_RCLICK*/ {
    		var selectedRow = owner.getSelection();
    		if !selectedRow return ; 
			var x,y = win.getMessagePos();
		
			var menu = win.ui.popmenu(winform);			
			menu.add("隐藏可忽略项", function(){
				var t = table.filter(getErr,
					function(v,index){
						if v["错误描述"]
							return !string.indexOf(v["错误描述"], "可忽略"); 
					}
				);
				t.fields = getErr.fields;
				winform.listview.setTable(t);
			});
			menu.add("显示可忽略项", λ() winform.listview.setTable(getErr));
			menu.popup(x,y,true);
    	}
    }
}
/*}}*/
/* CTRLC复制模块 */
winform.listview.translateAccelerator = domisoft.ctrlC;
winform.combobox2.listBox.ellipsis = "path";
winform.show();
if !winform.parent
	win.setForeground(winform.hwnd);
if !winform.parent
	winform.frmReLoad();
win.loopMessage();
return winform;