import console; 
console.open();
import etip;
/*
_SAP物料组 = {
	["S"] = {"SA"; "SB"; "SC"; "SD"; "SE"; "SG"; "SH"; "SI"};
	["R"] = {"R01"; "R02"; "R03"; "R04"; "R15"; "R05"; "R06"; "R07"; "R08"; "R10"; "R14"; "R40"};
}
_物料类型 = {"HALB";"ROH";"KMAT"};
_属性 = {"配置";"非配置";"委外加工"};
*/
import com.excel;
var excel,err = com.excel(false);

//excel.ActiveWorkbook.Save();

var sht = excel.ActiveSheet;
sht.AutoFilterMode = false;

if !sht.Columns("A:A").find("4.基本计量:后附") return error("不是专用号申请表"); 
if !sht.Rows("2:2").find("产品大类") return error("不是最新版专用号申请表"); 
var etr = function(){
	for (i=sht.range("A:A").find("4.基本计量:后附").row-4; 3; -1){
		if (sht.cells(i,"F").value2) return i;
	}
}
var tRng = sht.Range("A3:S" ++ etr());

var etab = tRng.value;


import console.progress;
var bar = console.progress();

var str_today = function(separator){
	var tm = time.now();
	tm.format = string.join({"%Y";"%m";"%d"},separator);
	return tostring(tm);
}
import config;
var username = config.tbm.Designer;

import string.keywords;

for(i=1; #etab){
    bar.setProgress(math.round(i*100/#etab), math.round(i*100/#etab) +"% processing ......");
    
	for(j=1; 19 /* S */)
		if !etab[i][j] etab[i][j] = "";  // EXCEL SAFE ARRAY
		
	etab[i][1] = i;
	etab[i][2] = "=len(F" ++ (i+2) ++ ")";
	
	if !#etab[i][3] etab[i][3] = username;
	if !#etab[i][4] etab[i][4] = str_today("/");
	if !#etab[i][7] etab[i][7] = "ZA";
	if !#etab[i][9] etab[i][9] = "柜体";
	if !#etab[i][13] etab[i][13 /* M */] = "4.新品BOM";
	if !#etab[i][19] etab[i][19 /* S */] = table.shift(string.split(etab[i][6 /* F */]," "));
	
	var str = etab[i][6];
	
	var ePush = function(val, index){
		index := {8,10,11,12};
		for(k=1; #index)
			if !#etab[i][index[k]] etab[i][index[k]] = val[k];
		//TODO: 根据已经填写的信息计算空白内容
	}	
	
	if string.keywords('可选项').findOne(str)
		ePush({"SG","KMAT","配置","09"});
	elseif string.keywords('总装|组装').findOne(str)
		ePush({"SG","HALB","配置","08"});
	elseif string.keywords('组件').findOne(str)	
		ePush({"SA","HALB","非配置","08"});
	elseif string.keywords('已喷').findOne(str)	
		ePush({"SC","HALB","非配置","01"});
	elseif string.keywords('ABS|PVC|亚克力|EPS|PC|尼龙').findOne(str)
		ePush({"R02","ROH","非配置","02"});	
	elseif string.keywords('玻璃').findOne(str)		
		ePush({"R14","ROH","非配置","14"});
	elseif string.keywords('点焊').findOne(str)
		ePush({"SH","HALB","非配置","01"});	
	elseif string.keywords('铝合金|不锈钢|焊接件|铆接件').findOne(str)
		ePush({"R01","ROH","非配置","01"});
	elseif string.keywords('木包装').findOne(str)
		ePush({"R05","ROH","非配置","16"});
	elseif string.keywords('板|连接件|支撑|未喷').findOne(str)
		ePush({"SB","HALB","非配置","01"});
	elseif string.keywords('底箱体|发泡板|发泡箱体').findOne(str)
		ePush({"SD","HALB","非配置","08"});
	elseif string.keywords('管|TP2M|蒸发器').findOne(str)
		ePush({"R07","ROH","非配置","07"});	

}
//table.sort(etab, lambda(b) owner[12]>b[12]);
sht.Columns(10 /* J */).ColumnWidth = #etab[1][10 /* J */] + 2.75;
sht.Columns(19 /* S */).ColumnWidth = #etab[1][19 /* S */] + 2.75;

tRng.value2 = etab;

var filename = excel.ActiveWorkbook.name;
filename = string.replace(filename, "姓名", username);
filename = string.replace(filename, "型号", etab[1][19 /* S */]);
filename = string.replace(filename, "日期", str_today("."));

import win.clip
win.clip.write(string.left(filename,#filename-1))


console.pause(true, "完成! 按任意键退出...");