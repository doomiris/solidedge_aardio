import win.ui;
/*DSG{{*/
var winform = win.form(text="原材料用量计算 (ctrl复制板材料用量, alt复制粉末用量, ctrl+alt同时复制, 选择多行时用\分隔) 价格仅供参考";right=1215;bottom=575)
winform.add(
button={cls="button";text="计算";left=80;top=8;right=150;bottom=52;dl=1;dt=1;z=5};
checkbox={cls="checkbox";text="去除重复项";left=160;top=37;right=255;bottom=54;dl=1;dt=1;z=8};
edit={cls="edit";left=8;top=56;right=152;bottom=568;ah=1;db=1;dl=1;dt=1;edge=1;font=LOGFONT(h=-16);multiline=1;vscroll=1;z=3};
groupbox={cls="groupbox";text="钣材选择";left=285;top=4;right=843;bottom=57;dl=1;dt=1;edge=1;z=2};
groupbox2={cls="groupbox";text="喷粉面数";left=878;top=4;right=1096;bottom=57;dl=1;dt=1;edge=1;z=1};
listview={cls="listview";left=160;top=59;right=1208;bottom=568;ah=1;aw=1;db=1;dl=1;dr=1;dt=1;edge=1;font=LOGFONT(h=-13);z=4};
radiobutton={cls="radiobutton";text="热锌板";left=443;top=26;right=504;bottom=43;dl=1;dt=1;group=1;z=9};
radiobutton2={cls="radiobutton";text="不锈钢";left=510;top=26;right=575;bottom=43;dl=1;dt=1;z=10};
radiobutton3={cls="radiobutton";text="双面喷粉";left=908;top=26;right=984;bottom=43;checked=1;dl=1;dt=1;group=1;z=12};
radiobutton4={cls="radiobutton";text="单面喷粉";left=1000;top=26;right=1079;bottom=43;dl=1;dt=1;z=13};
radiobutton5={cls="radiobutton";text="根据主数据自动选择";left=293;top=26;right=434;bottom=43;checked=1;dl=1;dt=1;z=11};
radiobutton6={cls="radiobutton";text="塑料板";left=581;top=26;right=646;bottom=43;dl=1;dt=1;z=14};
static={cls="syslink";text="清空列表";left=9;top=38;right=65;bottom=54;dl=1;dt=1;transparent=1;z=6};
static2={cls="syslink";text="copyall";left=1148;top=37;right=1204;bottom=53;align="right";dr=1;dt=1;transparent=1;z=7}
)
/*}}*/

/*
import console;
console.open();
*/
winform.group();

import win.ui.grid;
var grid = win.ui.grid(winform.listview);//创建数据视图

import win.clip;
import QHC;
import domisoft;


var tab = {};
var fields =  {
	"SAP原材料";
	"生产尺寸";
	"长,宽,厚";
	"原材料";
	"原材料描述";
	"钣金用量";
	"粉末用量";
	"钣材价格";
	"喷粉后总价";
}

winform.button.oncommand = function(id,event){
	var txt = winform.edit.text;
	for k, v in {`、`, `，`, `;`, `；`, `,`, `|`,'\t'} 
		txt = string.replace(txt, v,'\r\n');		
	var pArr = string.split(txt,'\r\n');
	if winform.checkbox.checked pArr = table.unique(pArr);
	winform.edit.text = string.replace( string.join(pArr,'\r\n'), '\r\n\r\n', '\r\n'); //失效了

	table.clear(tab);
	
	var mental_type_prefer,  painting_faces;
	winform.enumControl( function(ctrl){
		select(ctrl.getParent().text){
			case "钣材选择" {
				if ctrl.checked
					mental_type_prefer = ctrl.text;
			}
			case "喷粉面数" {
				if ctrl.checked
					painting_faces = (ctrl.text=="双面喷粉" ? 2 : 1);
			}
		}
	}, "radiobutton")

	for (i=1; #pArr){
		if !pArr[i] continue;
		//var d_size = pArr[i];
		var mental_flat_size, mental_type;	
		var hitem = null;
		if string.startWith(pArr[i], "H", true) {
			hitem = QHC.getSapItem(pArr[i]);
			if !hitem continue;
			//d_size = hitem.设计展平尺寸 : "";
			mental_flat_size = hitem.生产展平尺寸;
			hitem["SAP原材料"] =  string.indexOf(hitem["物料描述"], "不锈") ?  "不锈钢" : "热锌板";
			if string.len( mental_type_prefer ) > 3
				mental_type = hitem["SAP原材料"];	//仅当选择自动时才获取查询值
		}
		mental_type := string.len( mental_type_prefer ) > 3 ? "热锌板" : mental_type_prefer;	
		
		mental_flat_size := pArr[i];
		var rez = QHC.下料尺寸( mental_flat_size , mental_type, painting_faces );
		var t={};
		if rez {
			var qitem = QHC.getSapItem(rez.raw.物料);
			if !qitem
				qitem = rez.raw;
			var pitem = QHC.getSapItem(rez.powderItem.物料);
			//t["设计尺寸"] = d_size;
			t["SAP原材料"] = mental_type;
			t["生产尺寸"] = mental_flat_size;
			t["长,宽,厚"] = string.join({rez.l, rez.w, rez.t}, ",");
			t["原材料"] = rez.raw.物料 ;
			t["原材料描述"] = rez.raw.物料描述 ;
			t["钣金用量"]= rez.metel;
			t["粉末用量"]= rez.powder;
			t["钣材价格"]=  math.round( rez.metel * qitem.价格, 2);
			t["喷粉后总价"]= math.round(rez.powder * rez.powderItem.价格 + t["钣材价格"], 2);
		}
		
		if table.count(t)
			table.push(tab,t); //保留空行
	}
	
	tab.fields = fields;
	grid.source = tab
	grid.setTable(grid.source);

	var col_width = {100;120;120;120;250;50;50;80;100};
	for (i=1; #col_width)
		winform.listview.setColumn({cx=col_width[i]}, i);


}

/*CTRLC复制模块{{*/
winform.listview.translateAccelerator = function( msg ){ 
	
    var ctrl = ::GetKeyState(0x11/*_VK_CTRL*/) & 0x8000;
//    var shift =  ::GetKeyState(0x10/*_VK_SHIFT*/) & 0x8000;
    var alt = ::GetKeyState(0x12/*_VK_ALT*/) & 0x8000;

    var vk = msg.wParam ;

	if (vk == 'C'#) && ctrl && !alt && (msg.message == 0x100/*_WM_KEYDOWN*/) {	//CTRL_C 复制钣金用量
	    	var tab = {};
	    	for (k, v in winform.listview.selected){
	        	table.push(tab, winform.listview.getItemText(v, table.indexOf(fields,"钣金用量")));
	    	}           
			win.clip.write(string.join(tab,"\"));
	}
	if (vk == 'C'#) && !ctrl && alt && (msg.message == 0x104/*_WM_SYSKEYDOWN*/){ //ALT_C 复制粉末用量
        	var tab = {};
        	for (k, v in winform.listview.selected){
            	table.push(tab, winform.listview.getItemText(v, table.indexOf(fields,"粉末用量")));
        	}           
			win.clip.write(string.join(tab,"\"));
	}
	if (vk == 'C'#) && ctrl && alt && (msg.message == 0x100/*_WM_KEYDOWN*/){ //CTRL+ALT+C 同时复制钣金和粉末用量
	    	var t={};
	    	var tab1 = {};
	    	var tab2 = {};
	    	for (k, v in winform.listview.selected){
	        	table.push(tab1, winform.listview.getItemText(v, table.indexOf(fields,"钣金用量")));
	        	table.push(tab2, winform.listview.getItemText(v, table.indexOf(fields,"粉末用量")));
	    	} 
	    	table.push(t, string.join(tab1,"\"));
	    	table.push(t, string.join(tab2,"\"));
	              	
			win.clip.write(string.join(t,'\n'));
	}
}
/*}}*/

winform.static.text = "<a>" + winform.static.text + "</a>";
winform.static2.text = "<a>" + winform.static2.text + "</a>";

winform.static.onHyperlinkClick = function(nmSysLink,url,id){
	winform.edit.text = "";
	winform.listview.clear(true);
}
winform.static2.onHyperlinkClick = function(nmSysLink,url,id){
	domisoft.copyTable(winform.listview.items);
}

grid.onRightClick = function(item,subItem,nmListView){
	if !item return; 
	var selectedIndex = owner.selected;
	if !#selectedIndex return;
	
	var 钣金tab={};
	var 粉末tab={};
	
	for (i=1; #selectedIndex){
		table.push(钣金tab, {
			grid.source[selectedIndex[i]].原材料,
			grid.source[selectedIndex[i]].原材料描述,
			grid.source[selectedIndex[i]].钣金用量,
			"KG",
			"ROH",
		})
		table.push(粉末tab, {
			"H0088200085",
			"标准白色",
			grid.source[selectedIndex[i]].粉末用量,
			"KG",
			"ROH",
		})		
	}

	var x,y = win.getMessagePos();
	var menu = win.ui.popmenu(winform);

	menu.add("复制:板材原材料[专用号+物料描述+用量+KG+ROH]",
		λ() domisoft.copyTable(钣金tab)
	);
	menu.add();

	menu.add("复制:标准白色[专用号+物料描述+用量+KG+ROH]",
		λ() domisoft.copyTable(粉末tab)
	);
	menu.popup(x,y,true);
}


winform.show();
win.loopMessage();
return winform;