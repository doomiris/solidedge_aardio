import win.ui;
/*DSG{{*/
var winform_calc = win.form(text="原材料用量计算 (ctrl复制板材料用量, alt复制粉末用量, ctrl+alt同时复制, 选择多行时用\分隔) 价格仅供参考";right=1215;bottom=575)
winform_calc.add(
button={cls="button";text="计算";left=80;top=8;right=152;bottom=32;dl=1;dt=1;z=3};
checkbox={cls="checkbox";text="去除重复项";left=8;top=8;right=80;bottom=24;z=6};
edit={cls="edit";left=8;top=56;right=152;bottom=568;ah=1;db=1;dl=1;dt=1;edge=1;font=LOGFONT(h=-16);multiline=1;vscroll=1;z=1};
listview={cls="listview";left=160;top=8;right=1208;bottom=568;ah=1;aw=1;db=1;dl=1;dr=1;dt=1;edge=1;font=LOGFONT(h=-13);z=2};
static={cls="syslink";text="<a>清空列表</a>";left=8;top=32;right=64;bottom=48;dl=1;dt=1;transparent=1;z=4};
static2={cls="syslink";text="<a>copyall</a>";left=94;top=33;right=150;bottom=49;dl=1;dt=1;transparent=1;z=5}
)
/*}}*/

/*
import console;
console.open();
*/

import win.ui.grid;
var grid = win.ui.grid(winform_calc.listview);//创建数据视图

import win.clip;
import QHC;

var tab = {};
_fields =  {
	"生产尺寸";
	"长,宽,厚";
	"原材料";
	"原材料描述";
	"钣金用量";
	"粉末用量";
	"钣材价格";
	"喷粉后总价";
}

winform_calc.button.oncommand = function(id,event){
	var txt = winform_calc.edit.text;
	for k, v in {`、`, `，`, `;`, `；`, `,`, `|`,'\t'} 
		txt = string.replace(txt, v,'\r\n');		
	var pArr = string.split(txt,'\r\n');
	if winform_calc.checkbox.checked pArr = table.unique(pArr);
	winform_calc.edit.text = string.replace( string.join(pArr,'\r\n'), '\r\n\r\n', '\r\n'); //失效了

	table.clear(tab)
	
	for (i=1;#pArr;1){
		if !pArr[i] continue;
		//var d_size = pArr[i];
		var m_size;	
		if string.startWith(pArr[i],"H",true) {
			var hitem = QHC.getSapItem(pArr[i]);
			if !hitem continue;
			//d_size = hitem.设计展平尺寸 : "";
			m_size = hitem.生产展平尺寸;
		}
		m_size := pArr[i];
		var rez = QHC.下料尺寸( m_size );
		var t={};
		if rez {
			var qitem = QHC.getSapItem(rez.raw.物料);
			var pitem = QHC.getSapItem(rez.powderItem.物料);
			//t["设计尺寸"] = d_size;
			t["生产尺寸"] = m_size;
			t["长,宽,厚"] = string.join({rez.l, rez.w, rez.t}, ",");
			t["原材料"] = rez.raw.物料 ;
			t["原材料描述"] = rez.raw.物料描述 ;
			t["钣金用量"]= rez.metel;
			t["粉末用量"]= rez.powder;
			t["钣材价格"]= math.round( rez.metel * qitem.价格, 2);
			t["喷粉后总价"]= math.round(rez.powder * rez.powderItem.价格 + t["钣材价格"], 2);
		}
		if table.count(t)
			table.push(tab,t); //保留空行
	}
	
	tab.fields = _fields;
	grid.setTable(tab);

	var col_width = {120;120;120;250;50;50;80;100};
	for (i=1; #col_width)
		winform_calc.listview.setColumn({cx=col_width[i]}, i);


}

/*CTRLC复制模块{{*/
winform_calc.listview.translateAccelerator = function( msg ){ 
	
    var ctrl = ::GetKeyState(0x11/*_VK_CTRL*/) & 0x8000;
//    var shift =  ::GetKeyState(0x10/*_VK_SHIFT*/) & 0x8000;
    var alt = ::GetKeyState(0x12/*_VK_ALT*/) & 0x8000;

    var vk = msg.wParam ;

	if (vk == 'C'#) && ctrl && !alt && (msg.message == 0x100/*_WM_KEYDOWN*/) {	//CTRL_C 复制钣金用量
	    	var tab = {};
	    	for (k, v in winform_calc.listview.selected){
	        	table.push(tab, winform_calc.listview.getItemText(v, table.indexOf(_fields,"钣金用量")));
	    	}           
			win.clip.write(string.join(tab,"\"));
	}
	if (vk == 'C'#) && !ctrl && alt && (msg.message == 0x104/*_WM_SYSKEYDOWN*/){ //ALT_C 复制粉末用量
        	var tab = {};
        	for (k, v in winform_calc.listview.selected){
            	table.push(tab, winform_calc.listview.getItemText(v, table.indexOf(_fields,"粉末用量")));
        	}           
			win.clip.write(string.join(tab,"\"));
	}
	if (vk == 'C'#) && ctrl && alt && (msg.message == 0x100/*_WM_KEYDOWN*/){ //CTRL+ALT+C 同时复制钣金和粉末用量
	    	var t={};
	    	var tab1 = {};
	    	var tab2 = {};
	    	for (k, v in winform_calc.listview.selected){
	        	table.push(tab1, winform_calc.listview.getItemText(v, table.indexOf(_fields,"钣金用量")));
	        	table.push(tab2, winform_calc.listview.getItemText(v, table.indexOf(_fields,"粉末用量")));
	    	} 
	    	table.push(t, string.join(tab1,"\"));
	    	table.push(t, string.join(tab2,"\"));
	              	
			win.clip.write(string.join(t,'\n'));
	}
}
var copyTable = function(){
	var items = winform_calc.listview.items;
	var tab={}
	for k, v in items {
		var t = string.join(v,'\t');
		t = string.crlf(t,'\r');
		t = string.replace(t, '\r',"");
		table.push(tab, t);

	}
	//win.msgbox(tab);
	win.clip.write(string.join(tab,'\n'));	
}

/*}}*/

winform_calc.static.onHyperlinkClick = function(nmSysLink,url,id){
	winform_calc.edit.text = "";
	winform_calc.listview.clear(true);
}

winform_calc.static2.onHyperlinkClick = function(nmSysLink,url,id){
	copyTable();
}

winform_calc.listview.onnotify = function(id,code,ptr){//code很多 select外放代码很容易overflow
    select(code) {
    	case 0xFFFFFFFD/*_NM_DBLCLK*/ {

    	}
    	case 0xFFFFFFFB/*_NM_RCLICK*/  {
			var selectedRow,tvht = owner.hitTest(x,y,true);
    		//var selectedRow = owner.getSelection();
    		if !selectedRow return ; 
    		var mcode = owner.getItemText(selectedRow, table.indexOf(_fields,"原材料"));
    		var mdesp = owner.getItemText(selectedRow, table.indexOf(_fields,"原材料描述"));
    		var mweight = owner.getItemText(selectedRow, table.indexOf(_fields,"钣金用量"));
    		var pweight = owner.getItemText(selectedRow, table.indexOf(_fields,"粉末用量"));
			var x,y = win.getMessagePos();
			var menu = win.ui.popmenu(winform_calc);			
			menu.add("复制:板材原材料[专用号+物料描述]",
				λ() win.clip.write(string.join(
						{mcode, mdesp},
					'\t')));
			menu.add("复制:板材原材料[用量]",
				λ() win.clip.write(string.join(
						{mweight},
					'\t')));
			menu.add("复制:板材原材料[专用号+物料描述+用量+KG+ROH]",
				λ() win.clip.write(string.join(
						{mcode, mdesp, mweight,"KG","ROH"},
					'\t')));
			menu.add();
			menu.add("复制:标准白色[专用号+物料描述]",
				λ() win.clip.write(string.join(
						{"H0088200085","标准白色"},
					'\t')));
			menu.add("复制:标准白色[用量]",
				λ() win.clip.write(string.join(
						{pweight},
					'\t')));
			menu.add("复制:标准白色[专用号+物料描述+用量+KG+ROH]",
				λ() win.clip.write(string.join(
						{"H0088200085","标准白色",pweight,"KG","ROH"},
					'\t')));
			menu.popup(x,y,true);
    	}
    }
}

winform_calc.show();
win.loopMessage();
return winform_calc;