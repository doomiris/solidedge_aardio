import fonts.fontAwesome;
import win.ui;
/*DSG{{*/
var update_sap_item_form = win.form(text="选择需要更新的列...";right=303;bottom=359)
update_sap_item_form.add(
button={cls="button";text="开始更新";left=160;top=328;right=296;bottom=352;db=1;dr=1;tabstop=1;z=2};
button2={cls="button";text='\uF0E2';left=264;top=8;right=296;bottom=40;color=255;dr=1;dt=1;font=LOGFONT(h=-16;name='FontAwesome');z=3};
button3={cls="button";text="H";left=8;top=328;right=32;bottom=352;z=5};
edit={cls="edit";text="1";left=48;top=328;right=80;bottom=352;autohscroll=false;autovscroll=false;edge=1;hidesel=1;num=1;tabstop=1;z=6};
edit2={cls="edit";text="2";left=96;top=328;right=160;bottom=352;autohscroll=false;autovscroll=false;edge=1;hidesel=1;num=1;tabstop=1;z=7};
listview={cls="listview";left=0;top=48;right=304;bottom=320;ah=1;asel=false;aw=1;db=1;dl=1;dr=1;dt=1;edge=1;msel=false;tabstop=1;z=1};
static={cls="static";left=8;top=8;right=248;bottom=40;aw=1;dl=1;dr=1;dt=1;transparent=1;z=4}
)
/*}}*/

import win.ui.tooltip; 
var tooltipCtrl = win.ui.tooltip( update_sap_item_form );//在窗口上创建tooltip控件
tooltipCtrl.addTool(update_sap_item_form.button3,"为Excel选中的列添加 H开头" )
import config;
import sqlite;
var db = sqlite(io.joinpath(config.dbServer.path,"QHCSAP.db"));

import QHC;
var tab = {};	//可用列
table.push(tab, QHC.getColDef());
table.push(tab, table.array(#tab[1],"")); //对应列号初始化

db.close();

import tarray;
tab = tarray.replaceValue(tab, "MTyp", "物料类型");
tab = tarray.replaceValue(tab, "BUn", "单位");
tab = tarray.replaceValue(tab, "PGr", "采购组");
tab = tarray.transpose(tab);
//table.push(tab, { "长", ""},{ "宽", ""},{ "厚", ""});
var fields = { [1] = "可用字段"; [2] = "绑定到列"}
tab = tarray.mapfields(tab, fields);

import win.ui.grid
var grid = win.ui.grid(update_sap_item_form.listview);
grid.setTable(tab);
grid.setReadonlyColumns(1)
update_sap_item_form.listview.checkbox = true;
//update_sap_item_form.listview.setChecked(0);
	
import com.excel;
var excel;
var sht;

var refresh = function(){
	import etip;
	grid.clear();

	update_sap_item_form.titleRow = null;
	
	excel := com.excel(false);
	if !excel.ActiveWorkbook return ; 
	sht = excel.ActiveWorkbook.ActiveSheet;
	update_sap_item_form.static.text = sht.parent.Name ++ "![" ++ sht.name ++ "]";
	
	var titleRng = sht.range("A1:Z10");
	var f = titleRng.find("物料描述") || titleRng.find("名称");
	if !f return ; 
	update_sap_item_form.titleRow = f.row;
	titleRng = titleRng.value2[[update_sap_item_form.titleRow]];	//查找范围前10行
	titleRng = tarray.replaceValue(titleRng,"专用号", "物料"); //考虑其它命名方式
	titleRng = tarray.replaceValue(titleRng,"SAP展开尺寸", "生产展平尺寸"); //考虑其它命名方式
	titleRng = tarray.replaceValue(titleRng,"SAP展平尺寸", "生产展平尺寸"); //考虑其它命名方式
	for (i=1; #tab){
		var match = tarray.indexContains(titleRng, tab[i][fields[1]]);
		//win.msgbox(tab[i][fields[1]])
		if !match continue;
		tab[i][fields[2]] = etip.toColName(match) ;
		update_sap_item_form.titleRow := update_sap_item_form.titleRow;
		update_sap_item_form.edit.text = update_sap_item_form.titleRow + 1;
		update_sap_item_form.edit2.text = sht.usedrange.rows[sht.usedrange.rows.count].row;
	}
	grid.setTable(tab);
/*
	for(i=1;#tab)
		update_sap_item_form.listview.setChecked(i, tab[i][fields[2]]!=="")
*/
}

update_sap_item_form.button2.oncommand = function(id,event){
	refresh();
}

var update = function(update_sap_item_form, tab){
	//import console;
	//console.open();
	import win;
	import com.excel;
	import tarray;
	import QHC;
	
	var start_row = update_sap_item_form.edit.text;
	var end_row = update_sap_item_form.edit2.text;
	
	var excel = com.excel(false);
	var sht = excel.ActiveWorkbook.ActiveSheet;
	sht.AutoFilterMode = false;
	for (i=1; sht.ListObjects.Count)
		sht.ListObjects.item(i).AutoFilter.ShowAllData();

	excel.alerts = false;
	
	var tCount = sht.usedrange.rows[sht.usedrange.rows.count].row;	
	var items = update_sap_item_form.listview.items;
	
	for (i=1; #tab) 
		tab[i].绑定到列 = items[i][2] : "";
		
	var key = tarray.mapAsItem(tab,  tab.fields[1], tab.fields[2]); //字段和列号对应起来
	var ucol =  update_sap_item_form.listview.checked;	//用户选中的列
	var keyCol = sht.range(key.物料 ++ sht.usedRange.rows[1].row + ":" ++ key.物料 ++ tCount).value2; //获取物料专用号所在列
		
	for (i = start_row; end_row){
		if !keyCol[i][1] continue;	//如果title行之前有空行,string.startWith会报错
		if !string.startWith(keyCol[i][1],"H") and !string.startWith(keyCol[i][1],"BG") continue;
		var hitem = QHC.getSapItem(keyCol[i][1]);
		//console.log(i, keyCol[i][1])
		if !hitem continue;
		for (j=1; #ucol){
			var hitemname = items[ucol[j]][1];
			if hitemname == "物料类型" hitemname="MTyp";
			if hitemname == "单位" hitemname="BUn";
			if hitemname == "采购组" hitemname="PGr";
			sht.cells(i, items[ucol[j]][2]).value2 = hitem[hitemname];
		}
	}
	excel.alerts = true;
	update_sap_item_form.button.disabledText = null;
	//win.loopMessage();
}

update_sap_item_form.button.oncommand = function(id,event){
	owner.disabledText = {"✶";"✸";"✹";"✺";"✹";"✷";text="等待进程执行..."}
	thread.invoke(update, update_sap_item_form, tab);
}
update_sap_item_form.onDestroy = function(){
   	if excel excel.alerts = true;    //防止意外关闭后Excel假死
}

update_sap_item_form.button3.oncommand = function(id,event){
	update_sap_item_form.button3.disabledText = {"✶";"✸";"✹";"✺";"✹";"✷"; text = " "};
	thread.invoke( 
		function(update_sap_item_form){
			loadcodex("\forms\Excel\add_H_by_column.aardio");
			update_sap_item_form.button3.disabledText = null;
		}, update_sap_item_form
	)
}

refresh();




update_sap_item_form.show();
win.loopMessage();
return update_sap_item_form;