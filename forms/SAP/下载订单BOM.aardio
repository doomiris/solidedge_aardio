import fonts.fontAwesome;
import win.ui;
import win.ui.menu;
/*DSG{{*/
var order_down_form = win.form(text="下载订单BOM";right=759;bottom=469;)
order_down_form.add(
button={cls="button";text='\uF110 下载订单BOM';left=583;top=31;right=743;bottom=71;dr=1;dt=1;font=LOGFONT(name='FontAwesome');z=3;};
button2={cls="button";text="刷新";left=8;top=432;right=48;bottom=464;z=9;};
button3={cls="button";text="删除选定文件";left=591;top=435;right=746;bottom=463;z=10;};
button4={cls="button";text="导出订单清单";left=13;top=71;right=156;bottom=103;dr=1;dt=1;z=11;};
button6={cls="button";text="...";left=161;top=71;right=182;bottom=103;z=12;};
edit={cls="edit";left=13;top=31;right=157;bottom=63;dl=1;dt=1;edge=1;hidesel=1;tabstop=1;z=1;};
edit2={cls="edit";left=245;top=31;right=333;bottom=63;dl=1;dt=1;edge=1;hidesel=1;num=1;tabstop=1;z=2;};
edit3={cls="edit";left=389;top=31;right=477;bottom=63;dl=1;dt=1;edge=1;hidesel=1;num=1;tabstop=1;z=6;};
listview={cls="listview";left=0;top=128;right=760;bottom=424;edge=1;fullRow=1;vscroll=1;z=8;};
static={cls="static";text="订单号";left=13;top=7;right=125;bottom=23;dl=1;dt=1;transparent=1;z=4;};
static2={cls="static";text="项目号";left=245;top=7;right=357;bottom=23;dl=1;dt=1;transparent=1;z=5;};
static3={cls="static";text="-";left=341;top=39;right=381;bottom=55;align="center";dl=1;dt=1;transparent=1;z=7;};

)
/*}}*/

//import console;
//console.open();

import config;
import domisoft;

import win.ui.grid;
var grid = win.ui.grid(order_down_form.listview);//创建数据视图
grid.setReadonlyColumns(-1);
var current_list = {};
grid.onSortColumn = function(column,desc){
	var name = current_list.fields[column];
	table.sort(current_list,desc ? (lambda(next) owner[name] > next[name]) : (lambda(next) owner[name] < next[name]));	
	 
	grid.setTable( current_list );
	return true; //返回 true 允许当前列排序
}
order_down_form.bindConfig( config.orderBom, {	//绑定控件值
    edit = "text";
} );

var dcb = function(order_down_form, orderId, itemId, endItemId){
	import config;
    import web.script;
    var vm = web.script("VBScript");
//	import console;
//	console.open();

    vm.script = /*
		If Not IsObject(application) Then
   			Set SapGuiAuto  = GetObject("SAPGUI")
   			Set application = SapGuiAuto.GetScriptingEngine
		End If
		If Not IsObject(connection) Then
   			Set connection = application.Children(0)
		End If
		If Not IsObject(session) Then
   			Set session    = connection.Children(0)
		End If
		If IsObject(WScript) Then
   			WScript.ConnectObject session,     "on"
   			WScript.ConnectObject application, "on"
		End If
    */
	vm.script = /*
		on error resume next
		
		Set testobj = Nothing
		
		Set testobj = session.findById("wnd[0]/tbar[0]/btn[12]")
		If testobj Is Nothing Then Set testobj = session.findById("wnd[0]/tbar[0]/btn[3]")
		
		loopcount = 0
		
		If Not testobj Is Nothing Then
			Do While testobj.Changeable				
				testobj.press
				session.findById("wnd[1]/usr/btnSPOP-OPTION1").press	'不保存退出订单界面
				loopcount = loopcount + 1
				If loopcount > 20 Then Exit Do
			Loop
		End If

		on error goto 0	
	*/

	/* 通过 vm.script 访问变量前需要先声明 */
	var filename;
	vm.script = /*
	    	dim order
	    	dim item
	    	dim code
	    	dim lib_path
	    	dim itemStart
	    	dim itemEnd
	    	dim filename
	 */ 
	 
	vm.script = /*
	 	session.findById("wnd[0]/tbar[0]/okcd").text = "CSK2"
		session.findById("wnd[0]").sendVKey 0
	 */ 
	
    vm.script.order = orderId || order_down_form.edit.text;
	vm.script.lib_path = config.__sapDownload;
    
    var itemStart; 
    var itemEnd;
    
    if orderId {
        itemStart =  itemId;
    	itemEnd = endItemId;
    }else{
    	itemStart = tonumber(order_down_form.edit2.text) ;
    	itemEnd = order_down_form.edit3.text ? tonumber(order_down_form.edit3.text) : itemStart;
    	itemEnd = (itemEnd<itemStart) ? itemStart : itemEnd;
    }

    
	if !itemStart || !itemEnd return ; 

    
//    console.log("SSEE:",itemStart, itemEnd)
    


        
        for (i = itemStart; itemEnd; 10){
			vm.script.item = i;
			vm.script = /*
				session.findById("wnd[0]/usr/ctxtRC29L-VBELN").text = order
				session.findById("wnd[0]/usr/txtRC29L-VBPOS").text = item
				session.findById("wnd[0]/usr/ctxtRC29L-MATNR").text= ""
				session.findById("wnd[0]/usr/ctxtRC29L-CAPID").text = "PP01"
				
				session.findById("wnd[0]").sendVKey 0
				
				code = session.findById("wnd[0]/usr/ctxtRC29L-MATNR").text
				
				session.findById("wnd[0]/tbar[1]/btn[8]").press
				
				session.findById("wnd[0]/tbar[1]/btn[43]").press
				session.findById("wnd[1]/usr/ctxtDY_PATH").text = lib_path
				filename = Join(Array("B2C", order, item, code), "_") + ".xlsx"
				session.findById("wnd[1]/usr/ctxtDY_FILENAME").text = filename
				session.findById("wnd[1]/tbar[0]/btn[0]").press
				session.findById("wnd[0]/tbar[0]/btn[12]").press
	 		*/ 

			order_down_form.publish("down_bom", vm.script.filename);
			vm.script = /*	
				on error resume next			
					Set testobj = Nothing			
					Set testobj = session.findById("wnd[0]/usr/ctxtRC29L-VBELN")
					loopcount = 0
					Do While testobj Is Nothing				
						session.findById("wnd[0]/tbar[0]/btn[12]").press
						Set testobj = session.findById("wnd[0]/usr/ctxtRC29L-VBELN")
						loopcount = loopcount + 1
						If loopcount > 20 Then Exit Do
					Loop
				on error goto 0	
	 		*/ 
		}
	vm.script = /*	
		session.findById("wnd[0]/tbar[0]/btn[12]").press
		session.findById("wnd[0]/tbar[0]/btn[12]").press	
	 	*/ 
	order_down_form.button.disabledText = null;
order_down_form.getFolderList();
	
}
subscribe("down_bom",function(...){
	var filename = ...;	
	win.delay(3*1000);
	thread.invoke( 
		function(filename){
			import domisoft;
			var sht = domisoft.getRunningWorkSheet(filename);
			import QHC;
			QHC.formatBOM(sht)
		}, filename
	)
} )
order_down_form.button.oncommand = function(id,event){
	order_down_form.button.disabledText = {"✶";"✸";"✹";"✺";"✹";"✷",text = "下载中..."};
	thread.invoke(dcb, order_down_form);	
}
order_down_form.getFolderList = function(){
//	thread.invoke( 
//		function(){
			//import console
			//console.open()

			import fsys;
			import fsys.file;
			import process;
			import QHC;
			var list = fsys.list(config.__sapDownload,,"B2C_*.xlsx");
		
			current_list.fields = {[6] = "下载日期";[5] = "缓存文件";[1]="订单";[2]="项目";[3]="物料";[4]="物料描述"};
			for (i=1; #list){
				var f = string.split(string.split(list[i],".")[1],"_");
				var fitem = fsys.file(list[list[i]]);
				table.push(current_list, {
					"订单" = string.upper(f[2]);
					"项目" = tonumber(f[3]);
					"物料" = f[4];
					"物料描述" = QHC.getSapItem(f[4])? QHC.getSapItem(f[4]).物料描述 :"";
					"缓存文件" = list[i];
					"下载日期" = time(fitem.getTime().creation.local(),"%Y/%m/%d %H:%M:%S");
				});
			}
			grid.setTable(current_list);
			order_down_form.listview.setColumn({cx=80; fmt=0x2/*_LVCFMT_CENTER*/}, 1);
			order_down_form.listview.setColumn({cx=40; fmt=0x2/*_LVCFMT_CENTER*/}, 2);
			order_down_form.listview.setColumn({cx=100; fmt=0x2/*_LVCFMT_CENTER*/}, 3);
			order_down_form.listview.setColumn({cx=200; fmt=0x0/*_LVCFMT_LEFT*/}, 4);
			//order_down_form.listview.setColumn({cx=350; fmt=0x0/*_LVCFMT_LEFT*/}, 5);

			order_down_form.listview.fillParent(4);

//		}, order_down_form
//	)
}


order_down_form.button2.oncommand = function(id,event){
	//if event == 0/*_BN_CLICKED*/
		order_down_form.getFolderList();
}

order_down_form.button3.oncommand = function(id,event){
	var flist = order_down_form.listview.selected;
	if !#flist return ; 
	for k, v in flist {
		io.remove(
			io.joinpath(
				config.__sapDownload,
				order_down_form.listview.getItemText(v, 5)
			)
		);
	}
	order_down_form.getFolderList();
}
order_down_form.listview.onnotify = function(id,code,ptr){
    select(code) {
    	case 0xFFFFFFFD/*_NM_DBLCLK*/ {
    		var selectedRow = owner.getSelection();
    		if !selectedRow return ; 
    	    var pcode = owner.getItemText(selectedRow,5);
			process.execute(config.__sapDownload + pcode)
    	}
		case 0xFFFFFFFB/*_NM_RCLICK*/ {
    		var selectedRow = owner.getSelection();
    		if !selectedRow return ; 
    		var pcode = owner.getItemText(selectedRow, 1);
    		var desp = owner.getItemText(selectedRow, 2);
			var x,y = win.getMessagePos();
			var menu = win.ui.popmenu(order_down_form);			
			menu.add("重新下载此项...", λ() thread.invoke(dcb, order_down_form, pcode, desp, desp));
			menu.add("删除...", λ() order_down_form.button3.oncommand() );
			menu.popup(x,y,true);
		}
    }
}
order_down_form.button4.oncommand = function(id,event){
	thread.invoke( 
		function(wform){
			import config;
    		import web.script;
    		var vm = web.script("VBScript") ;
/*
			import console; 
			console.open();
*/			
			vm.script = /*
					If Not IsObject(application) Then
   					Set SapGuiAuto  = GetObject("SAPGUI")
   					Set application = SapGuiAuto.GetScriptingEngine
					End If
					If Not IsObject(connection) Then
   					Set connection = application.Children(0)
					End If
					If Not IsObject(session) Then
   					Set session    = connection.Children(0)
					End If
					If IsObject(WScript) Then
   					WScript.ConnectObject session,     "on"
   					WScript.ConnectObject application, "on"
					End If
			*/
			
			/* 通过 vm.script 访问变量前需要先声明 */
			vm.script = /*
	    			dim order
	    			dim startdate
	    			dim lib_path
	 		*/
			vm.script.order = wform.edit.text;
			vm.script.lib_path = config.__sapDownload;
			vm.script = /*
				session.findById("wnd[0]/tbar[0]/okcd").text = "VA03"
				session.findById("wnd[0]").sendVKey 0
				session.findById("wnd[0]/usr/ctxtVBAK-VBELN").text = order
				session.findById("wnd[0]").sendVKey 0
				'获取订单日期
				startdate = session.findById("wnd[0]/usr/tabsTAXI_TABSTRIP_OVERVIEW/tabpT\01/ssubSUBSCREEN_BODY:SAPMV45A:4400/ssubHEADER_FRAME:SAPMV45A:4440/ctxtVBKD-PRSDT").text
			*/
			vm.script.startdate = tostring(time(vm.script.startdate,"%Y.%m.%d").addday(-30))
			vm.script = /*
				session.findById("wnd[0]/mbar/menu[4]/menu[1]/menu[0]").select
				session.findById("wnd[0]/usr/txtVBCOM-BSTKD").text = order
				session.findById("wnd[0]/usr/ctxtVBCOM-AUDAT").text = startdate
				session.findById("wnd[0]/tbar[0]/btn[0]").press
				session.findById("wnd[0]/usr/cntlGRID1/shellcont/shell/shellcont[1]/shell").contextMenu
				session.findById("wnd[0]/usr/cntlGRID1/shellcont/shell/shellcont[1]/shell").selectContextMenuItem "&XXL"
				session.findById("wnd[1]/tbar[0]/btn[0]").press
				session.findById("wnd[1]/usr/ctxtDY_PATH").text = lib_path
				session.findById("wnd[1]/usr/ctxtDY_FILENAME").text = "ORDER_LIST_" & order & ".XLSX"
				session.findById("wnd[1]/tbar[0]/btn[11]").press
				'返回
				session.findById("wnd[0]/tbar[0]/btn[12]").press
				session.findById("wnd[0]/tbar[0]/btn[12]").press
				session.findById("wnd[0]/tbar[0]/btn[12]").press
				session.findById("wnd[0]/tbar[0]/btn[12]").press
			*/
		}, order_down_form
	)
}

order_down_form.button6.oncommand = function(id,event){
	thread.invokeEx( 
		function(){
			loadcodex("\forms\SAP\获取订单清单信息.aardio")
		}
	)
}

order_down_form.listview.translateAccelerator = domisoft.ctrlC;


order_down_form.show();
order_down_form.getFolderList()
win.loopMessage();