import fonts.fontAwesome;
import win.ui;
/*DSG{{*/
var winform = win.form(text="下载订单BOM";right=759;bottom=469)
winform.add(
button={cls="button";text='\uF110 下载';left=592;top=72;right=752;bottom=112;dr=1;dt=1;font=LOGFONT(name='FontAwesome');z=3};
button2={cls="button";text="刷新";left=8;top=432;right=48;bottom=464;z=9};
button3={cls="button";text="删除选定文件";left=591;top=435;right=746;bottom=463;z=10};
edit={cls="edit";left=16;top=72;right=160;bottom=104;dl=1;dt=1;edge=1;hidesel=1;tabstop=1;z=1};
edit2={cls="edit";left=248;top=72;right=336;bottom=104;dl=1;dt=1;edge=1;hidesel=1;num=1;tabstop=1;z=2};
edit3={cls="edit";left=392;top=72;right=480;bottom=104;dl=1;dt=1;edge=1;hidesel=1;num=1;tabstop=1;z=6};
listview={cls="listview";left=0;top=128;right=760;bottom=424;edge=1;fullRow=1;vscroll=1;z=8};
static={cls="static";text="订单号";left=16;top=48;right=128;bottom=64;dl=1;dt=1;transparent=1;z=4};
static2={cls="static";text="项目号";left=248;top=48;right=360;bottom=64;dl=1;dt=1;transparent=1;z=5};
static3={cls="static";text="-";left=344;top=80;right=384;bottom=96;align="center";dl=1;dt=1;transparent=1;z=7}
)
/*}}*/

/*
import console;
console.open();
*/
import config;
winform.bindConfig( config.orderBom, {	//绑定控件值
    edit = "text";
} );

var dcb = function(winform){
	import config;
    import web.script;
    var vm = web.script("VBScript");
/*
	import console;
	console.open();
*/

    vm.script = /*
		If Not IsObject(application) Then
   			Set SapGuiAuto  = GetObject("SAPGUI")
   			Set application = SapGuiAuto.GetScriptingEngine
		End If
		If Not IsObject(connection) Then
   			Set connection = application.Children(0)
		End If
		If Not IsObject(session) Then
   			Set session    = connection.Children(0)
		End If
		If IsObject(WScript) Then
   			WScript.ConnectObject session,     "on"
   			WScript.ConnectObject application, "on"
		End If
    */
	vm.script = /*
		on error resume next
		
		Set testobj = Nothing
		
		Set testobj = session.findById("wnd[0]/tbar[0]/btn[12]")
		If testobj Is Nothing Then Set testobj = session.findById("wnd[0]/tbar[0]/btn[3]")
		
		loopcount = 0
		
		If Not testobj Is Nothing Then
			Do While testobj.Changeable				
				testobj.press
				session.findById("wnd[1]/usr/btnSPOP-OPTION1").press	'不保存退出订单界面
				loopcount = loopcount + 1
				If loopcount > 20 Then Exit Do
			Loop
		End If

		on error goto 0	
	*/

	/* 通过 vm.script 访问变量前需要先声明 */
	vm.script = /*
	    	dim order
	    	dim item
	    	dim code
	    	dim lib_path
	    	dim itemStart
	    	dim itemEnd
	 */ 
	 
	vm.script = /*
	 	session.findById("wnd[0]/tbar[0]/okcd").text = "CSK2"
		session.findById("wnd[0]").sendVKey 0
	 */ 

    vm.script.order = winform.edit.text;
	vm.script.lib_path = config.__sapDownload;
    
    var itemStart =  tonumber(winform.edit2.text) ;
    var itemEnd = winform.edit3.text ? tonumber(winform.edit3.text) : itemStart;
    itemEnd = (itemEnd<itemStart) ? itemStart : itemEnd;
    //console.log(itemStart, itemEnd)
    
		vm.script.itemStart = itemStart
		vm.script.itemEnd = itemEnd
        //vm.script.item = itemStart;
		vm.script = /*
		For item=itemStart To itemEnd Step 10
			session.findById("wnd[0]/usr/ctxtRC29L-VBELN").text = order
			session.findById("wnd[0]/usr/txtRC29L-VBPOS").text = item
			session.findById("wnd[0]/usr/ctxtRC29L-MATNR").text= ""
			session.findById("wnd[0]/usr/ctxtRC29L-CAPID").text = "PP01"
			
			session.findById("wnd[0]").sendVKey 0
			
			code = session.findById("wnd[0]/usr/ctxtRC29L-MATNR").text
			
			session.findById("wnd[0]/tbar[1]/btn[8]").press
			
			session.findById("wnd[0]/tbar[1]/btn[43]").press
			session.findById("wnd[1]/usr/ctxtDY_PATH").text = lib_path
			session.findById("wnd[1]/usr/ctxtDY_FILENAME").text = Join(Array("B2C", order, item, code), "_") + ".xlsx"
			session.findById("wnd[1]/tbar[0]/btn[0]").press
			session.findById("wnd[0]/tbar[0]/btn[12]").press
			
			on error resume next			
				Set testobj = Nothing			
				Set testobj = session.findById("wnd[0]/usr/ctxtRC29L-VBELN")
				loopcount = 0
				Do While testobj Is Nothing				
					session.findById("wnd[0]/tbar[0]/btn[12]").press
					Set testobj = session.findById("wnd[0]/usr/ctxtRC29L-VBELN")
					loopcount = loopcount + 1
					If loopcount > 20 Then Exit Do
				Loop
			on error goto 0	
		Next
	 	*/ 

	vm.script = /*	
		session.findById("wnd[0]/tbar[0]/btn[12]").press
		session.findById("wnd[0]/tbar[0]/btn[12]").press	
	 	*/ 
	winform.button.disabledText = null;
winform.getFolderList();
	
}
winform.button.oncommand = function(id,event){
	winform.button.disabledText = {"✶";"✸";"✹";"✺";"✹";"✷",text = "下载中..."};
	thread.invoke(dcb, winform);	
}
winform.getFolderList = function(){
//	thread.invoke( 
//		function(){
			//import console
			//console.open()

			import fsys;
			import process;
			import QHC;
			var list = fsys.list(config.__sapDownload,,"B2C_*.xlsx");
			var t = {};
			t.fields = {[5] = "缓存文件";[1]="订单";[2]="项目";[3]="物料";[4]="物料描述"};
			for (i=1; #list){
				var f = string.split(string.split(list[i],".")[1],"_");
				table.push(t, {
					"订单" = f[2];
					"项目" = f[3];
					"物料" = f[4];
					"物料描述" = QHC.getSapItem(f[4])? QHC.getSapItem(f[4]).物料描述 :"";
					"缓存文件" = list[i];
				});
			}
			winform.listview.setTable(t);
			winform.listview.setColumn({cx=80; fmt=0x2/*_LVCFMT_CENTER*/}, 1);
			winform.listview.setColumn({cx=40; fmt=0x2/*_LVCFMT_CENTER*/}, 2);
			winform.listview.setColumn({cx=100; fmt=0x2/*_LVCFMT_CENTER*/}, 3);
			winform.listview.setColumn({cx=200; fmt=0x0/*_LVCFMT_LEFT*/}, 4);
			//winform.listview.setColumn({cx=350; fmt=0x0/*_LVCFMT_LEFT*/}, 5);

			winform.listview.fillParent(4);

//		}, winform
//	)
}

/*
import win.imageList;
var iml = win.imageList(16, 15);
iml.add('GIF\56\57a \0\15\0\x80\0\0\x80\x80\x80\xff\0\xff\33\xf9\4\0\0\0\0\0\44\0\0\0\0 \0\15\0\0\2\31\x8c
\x8f\xa9\xcb\xed\15\xa3\x9c\xb4N\xf0\x80\xde\56k\xbfA\\\xd7\x84 \x97Y\xea\xca\xb6\xee\11\xc7F\1\0;', 0xff00ff);
winform.listview.setColumnImageList(iml);
*/

var reloadItemData = function(){
	var sortColumn = winform.listview.itemSortColumn;
	var itemData = winform.listview.items
	
	if(sortColumn<0){
		var i = math.abs(sortColumn)
		if i==2
			table.sort(itemData,lambda(a) tonumber(owner[i])>tonumber(a[i]))
		else
			table.sort(itemData,lambda(a) owner[i]>a[i])
	}
	else {
		if sortColumn==2
			table.sort(itemData,lambda(a) tonumber(owner[sortColumn])<tonumber(a[sortColumn]))
		else
			table.sort(itemData,lambda(a) owner[sortColumn]<a[sortColumn])	
	} 
	
	winform.listview.items = itemData;

	winform.listview.clearColumnImage()
	winform.listview.setColumnImage(math.abs(sortColumn), sortColumn<0 ? 1 : 0); 
}
winform.listview.onnotify = function(id,code,ptr){

    select(code) {
    	case 0xFFFFFFFD/*_NM_DBLCLK*/ {
    		var selectedRow = owner.getSelection();
    		if !selectedRow return ; 
    	    var pcode = owner.getItemText(selectedRow,5);
			process.execute(config.__sapDownload + pcode)
    	}
		case 0xFFFFFF94/*_LVN_COLUMNCLICK*/ {
			var notifyMsg = winform.listview.getNotifyMessage(code,ptr)
			if(winform.listview.itemSortColumn = notifyMsg.iSubItem){
				winform.listview.itemSortColumn = -notifyMsg.iSubItem; 
			}
			else {
				winform.listview.itemSortColumn = notifyMsg.iSubItem; 
			}
			
			reloadItemData();
		} 
    }
}

winform.button2.oncommand = function(id,event){
	//if event == 0/*_BN_CLICKED*/
		winform.getFolderList();
}

winform.button3.oncommand = function(id,event){
	var flist = winform.listview.selected;
	if !#flist return ; 
	for k, v in flist {
		io.remove(
			io.joinpath(
				config.__sapDownload,
				winform.listview.getItemText(v, 5)
			)
		);
	}
	winform.getFolderList();
}

winform.show();
winform.getFolderList()
win.loopMessage();