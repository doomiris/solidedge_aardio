import win.ui;
/*DSG{{*/
var winform = win.form(text="admin数据库刷新工具";right=759;bottom=469)
winform.add(
button={cls="button";text="import MM60";left=72;top=80;right=184;bottom=112;z=1};
button2={cls="button";text="import excel";left=72;top=112;right=184;bottom=144;z=3};
button3={cls="button";text="Merge && Publish";left=72;top=144;right=184;bottom=176;z=4};
button4={cls="button";text="分发";left=72;top=216;right=184;bottom=320;z=5};
log={cls="richedit";left=288;top=80;right=568;bottom=320;edge=1;multiline=1;z=2}
)
/*}}*/

/*Get获取数据{{*/
var getFromSAP = function(winform){
	import win;
	import SAP.session;
	var session = SAP.session();
	//session.findById("wnd[0]").maximize();
	winform.log.print("开始从SAP获取数据...");
	session.findById("wnd[0]/tbar[0]/okcd").text = "mm60";
	session.findById("wnd[0]/tbar[0]/btn[0]").press();
	session.findById("wnd[0]/usr/chkBEWFLG").selected = false;
	session.findById("wnd[0]/usr/ctxtMS_MATNR-LOW").text = "*";
	session.findById("wnd[0]/usr/ctxtMS_WERKS-LOW").text = "0841";
	session.findById("wnd[0]/tbar[1]/btn[8]").press();
	session.findById("wnd[0]/tbar[1]/btn[45]").press();
	session.findById("wnd[1]/tbar[0]/btn[0]").press();
	session.findById("wnd[1]/usr/ctxtDY_FILENAME").text = "allcost.txt";
	session.findById("wnd[1]/tbar[0]/btn[11]").press();
	session.findById("wnd[0]/tbar[0]/btn[12]").press();
	session.findById("wnd[0]/tbar[0]/btn[12]").press();
	winform.log.print("数据已经保存到txt...");
	winform.publish("getFromSAP",true);
	win.loopMessage();
}
/*}}*/
/*deal处理数据{{*/
var dealWithTxt = function(winform){
	import win;
	import fsys;
	winform.log.print("开始处理数据");
	_path = io.getSpecial(0x5/*_CSIDL_MYDOCUMENTS*/) ++ "\SAP\SAP GUI\allcost.txt";
	var file = io.exist(_path) ? io.open(_path,"r") : null;
	if !file error("文件不存在: " ++ _path);
	if file winform.log.print("开始处理多余行", _path);
	import config;
	import sqlite;
	var db = sqlite(config.dbServer.path + "step_qhcsap.db");
	if !db error("dberror");
	if db.existsTable("all_cost") db.exec("DROP table all_cost;");
	db.exec("VACUUM");
	db.exec("CREATE TABLE [all_cost](物料 TEXT collate nocase,物料描述 TEXT collate nocase,MTyp TEXT,物料组 TEXT,BUn TEXT,价格 NUMERIC,PGr TEXT);")  
	db.beginTrans();
	
	var n = 1;
	for line in io.lines(file){
    	if ( n >= 3 ) {    //跳过标题行和制表符号
			if string.startWith(line, "|") {
				nl = string.split(line,"|");
				table.pop(nl,1);
				table.shift(nl,1);
				var cmd = db.prepare("INSERT INTO [all_cost] VALUES(@pcode,@textDesp,@mtype,@mgroup,@bun,@price,@pgr);");
				for (i=1;#nl){
					nl[i]=string.trim(nl[i]);
					nl[i]=string.trim(nl[i],'\t');
				}
				cmd.step(
					pcode = nl[1];
					textDesp = string.fromto(nl[2], 936 /* GB2312 */, 65001 /* UTF8 */);
					mtype = nl[3];
					mgroup = nl[4];
					bun = nl[5];
					price =  nl[6]===null ? null : nl[6]===0 ? price = nl[6] : tonumber((string.replace(nl[6],",","")));
					pgr = nl[7]
				)
			}
		}
		n = n + 1 ;
	}
	
	db.commitTrans();
	file.close();
	db.close();
	winform.log.print("完成, 已加入数据库");
	winform.publish("sentSAPtoDB",true);
	winform.button.disabledText = null;
}
/*}}*/
subscribe("getFromSAP",function(...){
	thread.invoke(dealWithTxt, winform)
} )

winform.button.oncommand = function(id,event){
	owner.disabledText = {"✶";"✸";"✹";"✺";"✹";"✷";text="等待进程执行..."}
	thread.invoke(getFromSAP, winform);
	//thread.invoke(dealWithTxt, winform);
}

/*importFromQHC{{*/
var importFromQHC = function(winform, path){
	import win;	
	import com.excel;
	var excel = com.excel(true);

	var qWb = excel.WorkBooks.Open(path);
	var qSht = qWb.Sheets(1);

	var qTab = qSht.usedRange.Value2;
	var rc = #qTab;

	qWb.close(false);
	qSht = null;
	qWb = null;
	excel.Quit();
	
	//import etip;
	import tarray;
	
	qTab.fields = table.shift(qTab);
	qTab.fields = tarray.replaceValue(qTab.fields,"大小/量纲", "设计展平尺寸");
	qTab.fields = tarray.replaceValue(qTab.fields,"大小/量纲-开发", "设计展平尺寸");
	qTab.fields = tarray.replaceValue(qTab.fields,"行业标准描述", "生产展平尺寸");
	qTab.fields = tarray.replaceValue(qTab.fields,"行业标准描述-工艺", "生产展平尺寸");	
	qTab.fields = tarray.replaceValue(qTab.fields,"禁用物料", "禁用");
	qTab.fields = tarray.replaceValue(qTab.fields,"物料状态", "禁用");
	import config;
	import sqlite;
	var db = sqlite(config.dbServer.path + "step_qhcsap.db");
	if db.existsTable("qhc_export") db.exec("DROP table qhc_export;")
	db.exec("VACUUM");
	var fields = string.join(qTab.fields,", ");
	
	db.exec("CREATE TABLE [qhc_export](" ++ fields ++ ");")  

	db.beginTrans();
	var n = #qTab;
	var vl = string.join(table.array(#qTab.fields,"?"),", ");
	
	for(i=1;n){
		var cmd = db.prepare("INSERT INTO [qhc_export] VALUES(" ++ vl ++ ");");
		cmd.bind.parameters(
			table.unpack(qTab[i])
		).step()
	}
	db.commitTrans();
	db.close();
	winform.log.print("Excel导入完成");
	winform.button2.disabledText = null;
}

/*}}*/

winform.button2.oncommand = function(id,event){
	import fsys.dlg;
	var path = fsys.dlg.open('Excel文件|*.xlsx|Excel文件|*.xls||',"打开QHC导出的数据文件","S:\Admin\SAP\",owner);
	if !path return ;
	owner.disabledText = {"✶";"✸";"✹";"✺";"✹";"✷";text="等待进程执行..."}
	winform.log.print("打开 " ++ path );
	thread.invoke(importFromQHC, winform, path)
}
/*mergeDb{{*/

var mergeDb = function(winform){
	import win;
	import config;
	import sqlite;
	var db = sqlite(config.dbServer.path + "qhcsap.db");

	winform.log.print("备份数据...");
	if (db.existsTable("所有物料") and db.existsTable("备份所有物料"))
		db.exec("DROP table 备份所有物料;");
	if db.existsTable("所有物料") db.exec("ALTER TABLE 所有物料 RENAME TO 备份所有物料;");
	
	winform.log.print("挂接新数据...");
	db.exec("ATTACH DATABASE '" + config.dbServer.path + "step_qhcsap.db' as 'stepDB';")
	
	winform.log.print("合并数据...");	
	db.exec("CREATE TABLE 所有物料 AS
	SELECT
		stepDB.all_cost.物料,
		stepDB.all_cost.物料描述, 
		stepDB.all_cost.MTyp, 
		stepDB.all_cost.物料组, 
		stepDB.all_cost.BUn, 
		stepDB.all_cost.价格, 
		stepDB.all_cost.PGr, 
		stepDB.qhc_export.物料是可配置的, 
		stepDB.qhc_export.禁用,
		stepDB.qhc_export.设计展平尺寸,
		stepDB.qhc_export.生产展平尺寸 
	FROM stepDB.all_cost LEFT JOIN stepDB.qhc_export ON stepDB.all_cost.物料 = stepDB.qhc_export.物料;");
	
	winform.log.print("合并完成, 卸载挂接数据...");
	db.exec("DETACH DATABASE 'stepDB';")
	
	winform.log.print("删除备份...");
	if (db.existsTable("所有物料") and db.existsTable("备份所有物料"))
		db.exec("DROP table 备份所有物料;");
	db.exec("VACUUM");
	
	winform.log.print("更新索引...");
	var t= db.getTable("SELECT name from [sqlite_master] where type='index';")
	import tarray;
	var x = tarray.transpose(t)
	if table.find(x,"物料"){
		db.exec("REINDEX 物料;")
		db.exec("REINDEX 物料描述;")
		db.exec("REINDEX MTyp;")
		db.exec("REINDEX 物料组;")
		db.exec("REINDEX BUn;")
	//	db.exec("REINDEX 物料是可配置的;") //避免大量NULL值的索引
	//	db.exec("REINDEX 禁用;")
	}else{
		db.exec("CREATE UNIQUE INDEX 物料 on 所有物料 (物料 collate nocase);")
		db.exec("CREATE INDEX 物料描述 on 所有物料 (物料描述 collate nocase);")
		db.exec("CREATE INDEX MTyp on 所有物料 (MTyp);")
		db.exec("CREATE INDEX 物料组 on 所有物料 (物料组);")
		db.exec("CREATE INDEX BUn on 所有物料 (BUn);")
	//	db.exec("CREATE INDEX 物料是可配置的 on 所有物料 (物料是可配置的);")
	//	db.exec("CREATE INDEX 禁用 on 所有物料 (禁用);")
	}
	db.close();
	winform.button3.disabledText = null;
	winform.log.print("合并数据完成.");
}

/*}}*/
winform.button3.oncommand = function(id,event){
	owner.disabledText = {"✶";"✸";"✹";"✺";"✹";"✷";text="等待进程执行..."}
	thread.invoke(mergeDb, winform);
}

winform.button4.oncommand = function(id,event){
	var copyfolder = function(winform, tart, dest){
		import fsys;
		winform.log.print("...正在复制到" + dest);
		fsys.copy(tart, dest, 0x4/*_FOF_SILENT*/ + 0x10/*_FOF_NOCONFIRMATION*/);
		winform.log.print("完成:" + dest);
		
	}
	thread.invoke(copyfolder, winform, "S:\Admin\SAP\db\*.db", "\\ccnqia05\kaifa-SAP数据整理—研发中心\_db\");
	thread.invoke(copyfolder, winform, "S:\Admin\SAP\db\*.db", "D:\users\CCL100100\Carrier Corporation\domisoft - General\db\");
}

winform.show();
win.loopMessage();
return winform;