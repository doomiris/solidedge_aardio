import win.ui;
import win.ui.menu;
import win.ui.grid;
import win.ui.toolbar;
/*DSG{{*/
var winform = win.form(text="BOM浏览器";right=1012;bottom=469;clipch=1)
winform.add(
button={cls="button";text="搜索";left=162;top=8;right=225;bottom=40;dl=1;dt=1;z=7};
custom={cls="custom";text="自定义控件";left=232;top=3;right=731;bottom=52;cp=1;dl=1;dr=1;dt=1;z=8};
edit={cls="edit";left=801;top=5;right=1007;bottom=31;dr=1;dt=1;edge=1;z=4};
edit2={cls="edit";left=11;top=8;right=155;bottom=40;dl=1;dt=1;edge=1;hidesel=1;tabstop=1;z=6};
listview={cls="listview";left=228;top=54;right=1012;bottom=468;aw=1;db=1;dr=1;dt=1;edge=1;fullRow=1;vscroll=1;z=1};
listview2={cls="listview";left=0;top=53;right=224;bottom=469;aw=1;db=1;dl=1;dt=1;edge=1;fullRow=1;msel=false;vscroll=1;z=2};
splitter={cls="splitter";left=223;top=53;right=228;bottom=464;db=1;dt=1;z=3};
static={cls="static";text="筛选[描述]";left=734;top=6;right=797;bottom=28;align="right";dr=1;dt=1;transparent=1;z=5}
)
/*}}*/

import fsys;
import fsys.file;
import domisoft;
import tarray;
import config;
import process;
import access;
import com.excel;
import godking.tableToDB;
//import tarray;


winform.listview.enableDoubleBuffering();
winform.listview2.enableDoubleBuffering();

var path = io.joinpath(config.dbServer.path,"BOM_LIB.xls")
var acc, db;
var refreshDB = function(){
	acc = access(path);
	db = godking.tableToDB(acc.getTable("SELECT * FROM [Sheet1$]"),,"Sheet1$");
	acc.close();	
}



var grid = win.ui.grid(winform.listview);
var grid2 = win.ui.grid(winform.listview2);

grid.source = {};
grid2.source = {};
grid.reset = λ() owner.setTable(owner.source);
grid2.reset = grid.reset;

var tbar;
var resetToolbar = function(){
	if tbar tbar.close();
	tbar = win.ui.toolbar(winform.custom);		
	tbar.create( style = 0x1000/*_TBSTYLE_LIST*/ + 0x200/*_TBSTYLE_WRAPABLE*/);	
	tbar.showLabel = true; //在按钮上显示文字
	tbar.imageList = win.imageList(0,0).loadIcon(null); //隐藏图标
	tbar.add("❌ 重置",
		function(){
			for (i=1; #tbar.buttons)
				tbar.buttons[i].checked = false;
			grid.reset()
		}
	)
}


winform.frmReLoad = function(){
	refreshDB();
	var t = db.getTable("SELECT DISTINCT 大类 FROM [Sheet1$]");
	grid2.source = t;
	grid2.setTable(grid2.source);
}

var filterTag = function(id){
	var txt = tbar.getButtonById(id).text;
	var st = table.filter(grid.source, λ(v) table.find(v, txt));
	st.fields = grid.source.fields;
	grid.clear();
	grid.setTable(st);
}

var getTags = function(tab,fields){
	var t= {};
	for (i=1; #fields)
		table.append(t, tarray.getCol(tab, fields[i]));
		
	t = table.unique(t);
	table.removeByValue(t, "");

	resetToolbar();
	
	for (i=1; #t)
		tbar.add(t[i], filterTag, /*img*/ , /* id */,2/*_TBSTYLE_CHECK*/ + 4/*_TBSTYLE_GROUP*/+ 0x10/*_TBSTYLE_AUTOSIZE*/)
}
winform.edit.onChange = function(){ 
	if(owner.onModified)owner.onModified(true);
	var txt = owner.text;
	var st = table.filter(grid.source, λ(v) string.find(v.物料描述, txt));
	st.fields = grid.source.fields;
	grid.clear();
	grid.setTable(st);
}
grid2.onClick = function(item,subItem,nmListView){
	if !item return ; 
	var t = db.getTable(`SELECT * FROM [Sheet1$] WHERE [大类]="` + owner.source[item]["大类"]+`";`)
	for (i=1;#t){
		var f = string.join({t[i]["物料"], t[i]["物料描述"]},"_") ++ ".xlsx";
		f = string.replace(f," ","_"); //在拼接前处理, 否则路径含空格
		f = io.joinpath(config.__sapDownload, f);		
		if io.exist(f)
			t[i]["缓存日期"] = domisoft.getFileDate(f);			
	}
	grid.source = t;
	winform.edit.text = "";

	grid.setTable(grid.source)
	
	grid2.ColumnWidth := domisoft.setColumnWidth(grid, {50,50,50,120,250,50,50,50,50})
	getTags(grid.source,{"中类","小类","属性","可选项分类","喷粉分类","温度等级"})
}


grid.onDoubleClick = function(item,subItem,nmListView){
	if !item return ; 
	var data = owner.getTable(owner.source.fields);
	var n = string.join({data[item]["物料"], data[item]["物料描述"]},"_") ++ ".xlsx";
	n = string.replace(n," ","_");
	var f = io.joinpath(config.__sapDownload, n);	
	if !io.exist(f)
		win.msgboxErr(f,"请先下载")
	else {
		var excel = com.GetOrCreateObject("Excel.Application");
		excel.WindowState = -4137/*_xlMaximized*/
		var wb = excel.WorkBooks.Open(f /* filename */,false /* UpdateLinks */,true /* ReadOnly */);
		win.showForeground(wb.Application.hwnd);
	}

}
grid.copyfiles = function(tab){
	import win.clip.file;
	win.clip.file.write(tab,"copy");
}
grid.getCopyFilesList = function(){
	var tab = table.array();
	var data = owner.getTable(owner.source.fields);
	var selIndx = owner.selected;
	for (i=1; #selIndx ){
		var n = string.join({data[selIndx[i]]["物料"], data[selIndx[i]]["物料描述"]},"_") ++ ".xlsx";
		n = string.replace(n," ","_");
		var f = io.joinpath(config.__sapDownload, n);	
		if io.exist(f)
			table.push(tab, f);
	}
	return tab;   
}


var downloadbom = function(){
	ctrl := grid;
    var tab=table.array();
    var data = ctrl.getTable(ctrl.source.fields);
//    win.msgbox(data)
    for (k, v in ctrl.selected){
        var bg = data[v]["物料"];
//        if string.startWith(bg,"BG")
            table.push(tab,  bg);
    }  
    if !#tab return ;
    thread.invoke( 
    	function(wform,tab){
			import SAP;
			//var session=SAP.session();
			SAP.downloadBOM(wform, tab);
    	},ctrl.getParent(),tab
    )
};
grid.copyPartNo = function(fields){
	var f = owner.source.fields;
	owner.doNotCopyColumns = {};
	for k, v in f
		if !table.find(fields, v)
			table.push(owner.doNotCopyColumns, k);
	domisoft.copySelectedItem(owner);
	owner.doNotCopyColumns = {};
}
grid.deleteItems = function(){
	var selected = owner.selected;
	var flist = {};
	if !#selected return ; 
	for (i=1; #selected)
		table.push(flist, owner.source[selected[i]]["物料"]);
	var dtype = winform.listview2.items[winform.listview2.selIndex]["大类"]
	if !win.msgboxTest("确认删除这些订单所有记录? " + '\n' + table.tostring(flist),"需要确认",winform.hwnd) return; 
	acc = access(path);
	acc.exec("DELETE FROM `Sheet1$` WHERE ??", {{"大类" : dtype, "物料":flist}});
	acc.close();	

	winform.frmReLoad();

}
var menu = win.ui.popmenu(winform);
menu.add("打开BOM", λ()	grid.onDoubleClick(winform.listview.selIndex));
menu.add("下载BOM...", downloadbom);
menu.add()
menu.add("删除选定行", λ() grid.deleteItems());
menu.add()
menu.add("复制[物料]", λ() grid.copyPartNo({"物料"}));
menu.add("复制[物料][物料描述]", λ() grid.copyPartNo({"物料","物料描述"}));
menu.add("复制所选BOM文件...", λ() grid.copyfiles(grid.getCopyFilesList()));


grid.onRightClick = function(item,subItem,nmListView){
	if !item return ; 
	var x,y = win.getMessagePos();
	menu.popup(x,y,true,item);
}

winform.frmReLoad();

winform.show();

winform.splitter.split(winform.listview,winform.listview2);
winform.listview.doNotCopyColumns = {-1};

winform.listview.translateAccelerator = domisoft.ctrlC;
winform.listview2.translateAccelerator = domisoft.ctrlC;


win.loopMessage();
return winform;