import win.ui;
import win.ui.grid;
/*DSG{{*/
var winform = win.form(text="BOM浏览器";right=759;bottom=469;clipch=1)
winform.add(
button={cls="button";text="下载订单清单...";left=162;top=8;right=305;bottom=40;dl=1;dt=1;z=7};
edit={cls="edit";left=539;top=10;right=745;bottom=36;dr=1;dt=1;edge=1;z=4};
edit2={cls="edit";left=11;top=8;right=155;bottom=40;dl=1;dt=1;edge=1;hidesel=1;tabstop=1;z=6};
listview={cls="listview";left=228;top=54;right=760;bottom=468;aw=1;db=1;dr=1;dt=1;edge=1;fullRow=1;vscroll=1;z=1};
listview2={cls="listview";left=0;top=53;right=224;bottom=469;db=1;dl=1;dt=1;edge=1;fullRow=1;items={};msel=false;vscroll=1;z=2};
splitter={cls="splitter";left=223;top=53;right=228;bottom=464;db=1;dt=1;frame=1;z=3};
static={cls="static";text="筛选[描述]";left=456;top=13;right=535;bottom=35;align="right";dr=1;dt=1;transparent=1;z=5}
)
/*}}*/

import fsys;
import fsys.file;
import domisoft;
import config;
import process;
import access;
import access.oleDb;
//import tarray;
import com.excel;
_path = io.joinpath(config.dbServer.path,"BOM_LIB.xlsx")
var db, err = access(_path);

var grid = win.ui.grid(winform.listview);
var grid2 = win.ui.grid(winform.listview2);

var getBOMList = function(){
	var t = db.getTable("SELECT DISTINCT 大类 FROM [Sheet1$]");
	grid2.source = t;
	grid2.setTable(grid2.source);
}


grid2.onClick = function(item,subItem,nmListView){
	if !item return ; 
	var t = db.getTable("SELECT * FROM [Sheet1$] WHERE 大类 = @大类", {大类 = owner.source[item]["大类"]})
	grid.source = t;
	grid.setTable(grid.source);
	grid2.ColumnWidth := domisoft.setColumnWidth(grid, {50,50,50,120,250,50,50,50,50})
}


grid.onDoubleClick = function(item,subItem,nmListView){
	var n = string.join({owner.source[item]["物料"], owner.source[item]["物料描述"]},"_") ++ ".xlsx";
	var f = io.joinpath(config.dbServer.path,"xl_lib", n);
	var f = string.replace(f," ","_");
	if !io.exist(f)
		win.msgboxErr(f)
	else {

		var excel = com.GetOrCreateObject("Excel.Application");
		excel.Application.WindowState = -4137/*_xlMaximized*/
		excel.Application.WorkBooks.Open(f /* filename */,false /* UpdateLinks */,true /* ReadOnly */);
	}

}



var downloadbom = function(){
	ctrl := grid;
    var tab=table.array();
    for (k, v in ctrl.selected){
        var bg = ctrl.source[v]["物料"];
        if string.startWith(bg,"BG")
            table.push(tab,  bg);
    }  
    if !#tab return ;
    thread.invoke( 
    	function(wform,tab){
			import SAP;
			//var session=SAP.session();
			SAP.downloadBOM(wform, tab);
    	},ctrl.getParent(),tab
    )
};
/*格式化BOM{{*/
subscribe("dok", function(...){
	//winform_qlist.button6.disabledText = null;
	import win;
	win.delay(3*1000);
	
	var fname = ...;
	if !fname return ; 
	thread.invoke( 
		function(fname){
			import domisoft;
			//import console;
			import QHC;
			var sht = domisoft.getRunningWorkSheet(fname);
			QHC.formatBOM(sht);
			import fsys;
			fsys.copy(sht.Parent.FullName, io.joinpath(config.dbServer.path,"\xl_lib\"), 0x10/*_FOF_NOCONFIRMATION*/)
		}, fname
	)
} )
/*}}*/
import win.ui.menu;
var menu = win.ui.popmenu(winform);
menu.add("下载...", downloadbom)

grid.onRightClick = function(item,subItem,nmListView){
	var x,y = win.getMessagePos();
	menu.popup(x,y,true);
}

winform.splitter.split(winform.listview,winform.listview2);
winform.show(3/*_SW_MAXIMIZE*/);
getBOMList();
win.loopMessage();
return winform;