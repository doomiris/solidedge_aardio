import win.ui;
import win.ui.grid;
/*DSG{{*/
var winform = win.form(text="订单清单浏览";right=759;bottom=469;image=$"\res\SE BG.jpg";bkBottom=0;bkLeft=0;bkRight=0;bkTop=0)
winform.add(
button={cls="button";text="下载订单清单...";left=162;top=8;right=305;bottom=40;dl=1;dt=1;z=7};
button2={cls="button";text="刷新";left=312;top=8;right=352;bottom=40;dl=1;dt=1;z=8};
button3={cls="button";text="删除选定文件";left=364;top=11;right=452;bottom=39;dl=1;dt=1;z=9};
edit={cls="edit";left=11;top=8;right=155;bottom=40;dl=1;dt=1;edge=1;hidesel=1;tabstop=1;z=6};
editDesp={cls="edit";left=539;top=10;right=745;bottom=36;dr=1;dt=1;edge=1;z=4};
listview={cls="listview";left=228;top=54;right=760;bottom=468;aw=1;db=1;dr=1;dt=1;edge=1;fullRow=1;vscroll=1;z=1};
listview2={cls="listview";left=0;top=53;right=224;bottom=469;aw=1;db=1;dl=1;dt=1;edge=1;fullRow=1;vscroll=1;z=2};
splitter={cls="splitter";left=223;top=53;right=228;bottom=464;db=1;dt=1;frame=1;z=3};
static={cls="static";text="筛选[描述]";left=456;top=13;right=535;bottom=35;align="right";dr=1;dt=1;transparent=1;z=5}
)
/*}}*/

import fsys;
import fsys.file;
import domisoft;
import tarray;
import config;
import process;
import SAP;
import sqlite;
import win.clip;
import win.dlg.message;
/*
import console
console.open()
*/
winform.edit.disableInputMethod();
winform.edit.setCueBannerText("订单号");

winform.listview.enableDoubleBuffering();
winform.listview2.enableDoubleBuffering();
var sdb = sqlite(io.joinpath(config.__sapDownload, "order_list.db"));
/*如果表不存在则创建新表{{*/
if !sdb.existsTable("list")
	sdb.exec(`
		CREATE TABLE "list" (
			"SD 凭证"	TEXT UNIQUE,
			"凭证日期"	TEXT,
			"售达方"	TEXT,
			"名称 1"	TEXT,
			"备注"	TEXT,
			"缓存日期"	TEXT,
			PRIMARY KEY("SD 凭证")
		);
	`);
if !sdb.existsTable("content")
	sdb.exec(`
		CREATE TABLE "content" (
			"SD 凭证"	TEXT,
			"项目(SD)"	INTEGER,
			"物料"	TEXT,
			"描述"	TEXT,
			"订单数量"	INTEGER,
			"备注"	TEXT,
			"B2C"	TEXT,
			"COOIS"	TEXT,
			CONSTRAINT "ORDER_ID" UNIQUE("SD 凭证","项目(SD)")
		);
	`);
/*}}*/
winform.grid = win.ui.grid(winform.listview);//创建数据视图
//var grid = winform.grid;
winform.grid.setReadonlyColumns(1,2,3,4,5,7,8);
winform.grid2 = win.ui.grid(winform.listview2);//创建数据视图
//var grid2 = winform.grid2;
winform.grid2.setReadonlyColumns(1,2,3,4,6,7)


winform.grid.onSortColumn = domisoft.listColSort;
winform.grid2.onSortColumn = domisoft.listColSort;

winform.grid2.onEditChanged = function(text,iItem,iSubItem){
	sdb.exec("UPDATE `list` SET ? WHERE ??",{{"备注":text},{"SD 凭证":owner.source[iItem]["SD 凭证"]}});
}
winform.grid.onEditChanged = function(text,iItem,iSubItem){
	sdb.exec("UPDATE `content` SET ? WHERE ??",{{"备注":text},{
		"SD 凭证":owner.source[iItem]["SD 凭证"];
		"项目(SD)":owner.source[iItem]["项目(SD)"];
	}});
}
/*获取订单清单{{*/
winform.getFolderList = function(newItemAdded){
	var t = sdb.getTable("SELECT * FROM [list]")
	for(i=1;#t)
		t[i]["凭证日期"] = time(t[i]["凭证日期"],"%Y/%m/%d");
	winform.grid2.source = t
	winform.grid2.onSortColumn(6, true);
	winform.grid2.ColumnWidth := domisoft.setColumnWidth(winform.listview2, {70,70,5,120,80,80})
	winform.listview2.fillParent(3);	
}
/*}}*/
winform.getFolderList();

var getDetailFromDb = function(orderId){
	var t = sdb.getTable("SELECT * FROM [content]", {"SD 凭证":orderId});
	return t; 
}

winform.grid2.onClick = function(item,subItem,nmListView){
	if !item and !winform.listview2.selIndex return ; 
	winform.grid.clear();
	var c =  winform.grid2.source[item || winform.listview2.selIndex][["SD 凭证"]];
	if !c return; 
	if subItem {
		var v =  winform.grid2.source[item][["SD 凭证"]];
		var onchange = winform.edit.onChange;
		winform.edit.onChange = null;
		if winform.edit.text !== v	//如果不判断直接修改会触发onchange事件并overflow;
			winform.edit.text =  v;
		winform.edit.onChange = onchange;	
	}
	winform.grid.source = getDetailFromDb(c);
	if !winform.grid.source return ;
	if !#winform.grid.source return;
	
	winform.editDesp.text = "";
	winform.grid.onSortColumn(2, false);
	winform.grid.ColumnWidth := domisoft.setColumnWidth(winform.listview, {80,40,80,350,40,80,120,120})
	winform.listview.fillParent(4);
}

winform.grid.onDoubleClick = function(item,subItem,nmListView){
	if !item return ; 
	var nData = owner.getTable(owner.source.fields);
	var t = nData[item];
	var c = string.join({"B2C", t["SD 凭证"], t["项目(SD)"], t["物料"]},"_");
	c = io.joinpath(config.__sapDownload, c ++ ".xlsx") ;
	if !io.exist(c)
		winform.msgInfo("没有找到" ++ '\n' ++ c ++ '\n' ++ "请下载", 1000)
	else 
		process.execute(c)
}
winform.grid.copyPartNo = function(fields){
	var f = owner.source.fields;
	owner.doNotCopyColumns = {};
	for k, v in f
		if !table.find(fields, v)
			table.push(owner.doNotCopyColumns, k);
	domisoft.copySelectedItem(owner);
	owner.doNotCopyColumns = {};
}
/*
winform.grid.copyfiles = function(tab){
	import win.clip.file;
	win.clip.file.write(tab,"copy");
}
winform.grid.getCopyFilesList = function(){
	var tab = table.array();
	var data = owner.getTable(owner.source.fields);
	var selIndx = owner.selected;
	for (i=1; #selIndx ){
		var n = string.join({data[selIndx[i]]["物料"], data[selIndx[i]]["描述"]},"_") ++ ".xlsx";
		n = string.replace(n," ","_");
		var f = io.joinpath(config.__sapDownload, n);	
		if io.exist(f)
			table.push(tab, f);
	}
	return tab;   
}
*/
winform.grid.onRightClick = function(item,subItem,nmListView){
	if !item return ; 
	var selectedIndex = owner.selected;
	var itemList = table.array();
	var nData = owner.getTable(owner.source.fields);
	for (i=1; #nData)
		if table.indexOf(selectedIndex, i)
			table.push(itemList, nData[i]["项目(SD)"]);
			
	var x,y = win.getMessagePos();
	var menu = win.ui.popmenu(winform);
	
	var t = nData[item];
	var orderBom_file = string.join({"B2C", t["SD 凭证"], t["项目(SD)"], t["物料"]},"_");
	orderBom_file = io.joinpath(config.__sapDownload, orderBom_file + ".xlsx") ;
	var mbom_file = string.join({"COOIS", t["SD 凭证"], t["项目(SD)"]}, "_");
	mbom_file = io.joinpath(config.__sapDownload, mbom_file + ".xlsx") ;
	menu.add("复制[SD 凭证]", λ() winform.grid.copyPartNo({"SD 凭证"}));
menu.add()
	menu.add("复制[物料]", λ() winform.grid.copyPartNo({"物料"}));
	menu.add("复制[物料][物料描述]", λ() winform.grid.copyPartNo({"物料","描述"}));
menu.add()
	if io.exist(orderBom_file)
		menu.add("打开订单BOM...", λ() process.execute(orderBom_file));
	menu.add("下载订单BOM...", λ() SAP.downloadB2CBOM(winform, t["SD 凭证"], itemList));
	if io.exist(mbom_file)
		menu.add("打开COOIS生产订单BOM...", λ() process.execute(mbom_file));
	menu.add("下载COOIS生产订单BOM...", λ() SAP.downloadManufactureBOM(winform, t["SD 凭证"], itemList));
menu.add()
	menu.add("添加备注...", λ() winform.grid.beginEdit(item,6 /*备注列*/));
menu.add()
	menu.add("按itemmaster汇总格式复制...", function(){	
		winform.listview.doNotCopyColumns = {}
		winform.listview2.doNotCopyColumns = {}
		var tab2 = domisoft.copySelectedItem(winform.listview);
		if !winform.listview2.selIndex
			winform.listview2.selIndex = table.indexOf(tarray.getCol(winform.grid2.source, "SD 凭证"), tab2[1][1/*"SD 凭证"*/],) : null;
		if !winform.listview2.selIndex return ; 
		
		var tab1 = winform.listview2.getItemText(,-1);

		var formatTab = {}
		for (i=1; #tab2)
			table.push(formatTab, {
				tab1[2/*"凭证日期"*/],
				tab1[1/*"SD 凭证"*/],
				tab2[i][2/*"项目(SD)"*/],
				tab1[3/*"售达方"*/],
				tab1[4/*"名称 1"*/],
				tab1[5/* 备注 */], 
				tab2[i][3/*"物料"*/],
				tab2[i][4/*"描述"*/],
				tab2[i][5/*"订单数量"*/]
			})
		domisoft.copyTable(formatTab);
	});
	menu.popup(x,y,true);
	winform.listview.doNotCopyColumns = {6,7,8}
	winform.listview2.doNotCopyColumns = {5,6,7}
}
winform.grid2.onRightClick = function(item,subItem,nmListView){
	if !item return ; 
	var x,y = win.getMessagePos();
	var menu = win.ui.popmenu(winform);
	menu.add("复制[SD 凭证]", λ() win.clip.write(winform.grid2.source[item]["SD 凭证"]));
menu.add();
	menu.add("添加备注...", λ() winform.grid2.beginEdit(item,5 /*备注列*/));
menu.add();
	menu.add("刷新此订单记录...", function(){
		var order_number = winform.grid2.source[item]["SD 凭证"];
		if !order_number return ; 
		thread.invoke(SAP.downloadOrderList, winform, order_number)
	});
	menu.add("删除此订单记录...", function(){		
		var order_number = winform.grid2.source[item]["SD 凭证"];
		
		if !winform.msgAsk("确认删除此订单所有记录?" + '\n' + order_number) return; 
		sdb.exec("DELETE FROM `list` WHERE ??",{{"SD 凭证":order_number}})
		sdb.exec("DELETE FROM `content` WHERE ??",{{"SD 凭证":order_number}})
		winform.publish("getFolderList",true);
	});
	menu.add("批量添加...",function(){
		var mdlfrm = winform.loadForm("\dlg\inputbox2row.aardio")
		mdlfrm.text = "批量添加";		
		var item =  mdlfrm.doModal();
		if !item return ; 
		var userinput = item[1];
    	userinput = string.crlf(userinput,'\r\n' /* 默认值 */);
    	
    	//分隔符号转换行
		for k, v in {`、`, `，`, `;`, `；`, `,`, `|`,'\t'} 
			userinput = string.replace(userinput, v,'\r\n');
	
    	var userinputArr = string.split(userinput, '\r\n');
    	
    	for (i=1; #userinputArr){
    	    if !string.len(userinputArr[i]) continue;

    		//更新listview
    		winform.listview2.addItem([userinputArr[i]],1);
    		//更新缓存 
    		table.unshift(winform.grid2.source,{"SD 凭证":userinputArr[i]});
    		//更新数据库   
    		
    		var test = sdb.stepQuery("SELECT * FROM `LIST` WHERE ??",{{"SD 凭证":userinputArr[i]}});
    		if test continue; //如果已经存在则跳过;
    		
			var cmd = sdb.prepare("REPLACE INTO `LIST` VALUES(?,?,?,?,?,?)");
			cmd.step({
	 			userinputArr[i],
	 			null,
	 			null,
	 			null,
	 			null,
	 			null
			})
			cmd.finalize();
    	}   	
	})
	
	menu.popup(x,y,true);
}
winform.editDesp.onChange = function(){ 
	if(owner.onModified)owner.onModified(true);
	var str = owner.text;
	if !str return ; 
	str = string.trim(owner.text);
	if !str or !#str {
		winform.grid.setTable(winform.grid.source);
	}else{
		var t = table.filter(winform.grid.source, λ(v) string.find(v["描述"], "@@"+str));
		t.fields = winform.grid.source.fields;
		winform.grid.clear();
		winform.grid.setTable(t);
	}
}

winform.button.oncommand = function(id,event){
	var order_number = string.trim(winform.edit.text);
	var note = #winform.listview2.selected ? winform.listview2.getItemText(winform.listview2.selIndex,5) : null;
	if !order_number return ; 
	owner.disabledText = {"✶";"✸";"✹";"✺";"✹";"✷"};
	var rez = thread.invokeAndWait(SAP.downloadOrderList, winform, order_number)
	if rez 
		owner.disabledText = null;
	if note
		win.setTimeout(
			function(){
				sdb.exec("UPDATE `list` SET ? WHERE ??",{{"备注":note},{"SD 凭证":order_number}});
			},1000
		)
	win.setTimeout(winform.getFolderList, 1500)
	
}

subscribe("down_b2c_bom",function(filename){
	//var filename = ...;
	win.delay(3*1000);
	thread.invoke(
		function(filename){
			import domisoft;
			var sht = domisoft.getRunningWorkSheet(filename);
			import QHC;
			QHC.formatBOM(sht);
		}, filename
	)
	var t = string.split(filename,"_");
	t[3] = tonumber(t[3]);
	var d = domisoft.getFileDate(io.joinpath(config.__sapDownload,filename));
	sdb.exec("UPDATE `content` SET ? WHERE ??",{{"B2C":d},{
		"SD 凭证":t[2];
		"项目(SD)":t[3];
	}});
	//win.msgbox(t);
	for (i=1; #winform.grid.source) {
		if winform.grid.source[i]["项目(SD)"] == t[3] {
			winform.grid.beginEdit(i,7);
			winform.grid.editBox.text = d;
			winform.grid.editBox.close();
			break;
		}
	}	
})
subscribe("downloadManufactureBOM",function(filename){
	//var filename = ...;
	win.delay(2*1000);
	var t = string.split(filename,"_");
	t[3] = tonumber(t[3]);
	var d = domisoft.getFileDate(io.joinpath(config.__sapDownload,filename));
	sdb.exec("UPDATE `content` SET ? WHERE ??",{{"COOIS":d},{
		"SD 凭证":t[2];
		"项目(SD)":t[3];
	}});
	for (i=1; #winform.grid.source) {
		if winform.grid.source[i]["项目(SD)"] == t[3] {
			winform.grid.beginEdit(i,8);
			winform.grid.editBox.text = d;
			winform.grid.editBox.close();
			break;
		}
	}
})
subscribe("done_job_b2c", function(orderId, itemList){

	
}); 

var getNewListFromXlsx = function(filename,wform){
	import sqlite;
	import config;
	import domisoft;
	var sdb = sqlite(io.joinpath(config.__sapDownload, "order_list.db"));
	var sht = domisoft.getRunningWorkSheet(filename);	
	var etab = sht.usedRange.value2;
	var Text = sht.Parent.Application.WorksheetFunction.Text;
	var fields = table.shift(etab);
	var cmd = sdb.prepare("REPLACE INTO `LIST` VALUES(?,?,?,?,?,?)");
	cmd.step({
	 	etab[1][1 /* A */],
	 	Text(etab[1][7 /* G */],"yyyy/mm/dd"),
	 	etab[1][17 /* Q */],
	 	etab[1][24 /* X */],
	 	null,
	 	(tostring(time(),"%Y/%m/%d %H:%M:%S"))
	})
	cmd.finalize();
	var cmd = sdb.prepare("REPLACE INTO `content` VALUES (?,?,?,?,?,?,?,?)");
	for (j=1; #etab){			
		cmd.step({
			etab[j][1 /* A */],
			etab[j][2 /* B */],
			etab[j][22 /* V */],
			etab[j][5 /* E */],
			(tonumber(etab[j][19 /* S */])),
			null,
			null,
			null
		});
	}
	cmd.finalize();
	sht.Parent.Close(false);
	wform.button.disabledText = null;
	wform.publish("getFolderList",true);
}
subscribe("getFolderList", winform.getFolderList);
subscribe("order_list_job_done",function(...){
	var filename = ...;	
	win.delay(3*1000);
	thread.invoke(getNewListFromXlsx, filename , winform);
} )	
	
winform.button2.oncommand = winform.getFolderList;
winform.frmReLoad = winform.getFolderList;

winform.button3.oncommand = function(id,event){
	var selected = winform.listview2.selected;
	var flist = {};
	if !#selected return ; 
	for (i=1; #selected)
		table.push(flist, winform.grid2.source[selected[i]]["SD 凭证"]);
	if !winform.msgAsk("确认删除这些订单所有记录? " + '\n' + table.tostring(flist)) return; 
	sdb.exec("DELETE FROM `list` WHERE ??", {{"SD 凭证" : flist}});
	sdb.exec("DELETE FROM `content` WHERE ??", {{"SD 凭证" : flist}});
	winform.publish("getFolderList",true);
}

if winform.listview2.count
	winform.grid2.onClick(1,1)


winform.show();

winform.splitter.split(winform.listview, winform.listview2);


winform.edit.onChange = function(){ 
	if(owner.onModified) owner.onModified(true);
	winform.listview2.selIndex = null;
	winform.listview2.selected = null;
	var txt = string.upper(owner.text);
	winform.listview2.selIndex = table.indexOf(tarray.getCol(winform.grid2.source, "SD 凭证"), txt) : null;
	if winform.listview2.selIndex
		winform.grid2.onClick(winform.listview2.selIndex);
}

winform.listview.doNotCopyColumns = {6,7,8}
winform.listview2.doNotCopyColumns = {5,6,7}


winform.listview.translateAccelerator = domisoft.ctrlC;	
winform.listview2.translateAccelerator = domisoft.ctrlC;

win.loopMessage();
return winform;