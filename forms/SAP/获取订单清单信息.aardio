import win.ui;
import win.ui.grid;
import win.ui.atom;
/*DSG{{*/
var winform = win.form(text="订单清单浏览";right=759;bottom=469)
winform.add(
button={cls="button";text="下载订单清单...";left=162;top=8;right=305;bottom=40;dl=1;dt=1;z=7};
button2={cls="button";text="刷新";left=312;top=8;right=352;bottom=40;dl=1;dt=1;z=8};
button3={cls="button";text="删除选定文件";left=364;top=11;right=452;bottom=39;dl=1;dt=1;z=9};
edit={cls="edit";left=11;top=8;right=155;bottom=40;dl=1;dt=1;edge=1;hidesel=1;tabstop=1;z=6};
editDesp={cls="edit";left=539;top=10;right=745;bottom=36;dr=1;dt=1;edge=1;z=4};
listview={cls="listview";left=228;top=54;right=760;bottom=468;aw=1;db=1;dr=1;dt=1;edge=1;fullRow=1;vscroll=1;z=1};
listview2={cls="listview";left=0;top=53;right=224;bottom=469;aw=1;db=1;dl=1;dt=1;edge=1;fullRow=1;vscroll=1;z=2};
splitter={cls="splitter";left=223;top=53;right=228;bottom=464;db=1;dt=1;frame=1;z=3};
static={cls="static";text="筛选[描述]";left=456;top=13;right=535;bottom=35;align="right";dr=1;dt=1;transparent=1;z=5}
)
/*}}*/

import fsys;
import fsys.file;
import domisoft;
import tarray;
import config;
import process;
import SAP;
/*只允许一个实例{{*/
var atom,hwnd = winform.atom("domisoft.aardio.tbm.orderviewer");
if(!atom){ 
	win.showForeground(hwnd);
	win.setActive(hwnd);
	win.quitMessage();	
	return;
}
/*}}*/
winform.grid = win.ui.grid(winform.listview);//创建数据视图
var grid = winform.grid;
grid.setReadonlyColumns(-1);
winform.grid2 = win.ui.grid(winform.listview2);//创建数据视图
var grid2 = winform.grid2;
grid2.setReadonlyColumns(-1);
/*
import console
console.open()
*/
/*
winform.bindConfig( config.orderBom, {	//绑定控件值
    edit = "text";
} );
*/



grid.onSortColumn = function(column, desc){
	var name = owner.source.fields[column];
	table.sort(owner.source,desc ? (lambda(next) owner[name] > next[name]) : (lambda(next) owner[name] < next[name]));	
	owner.setTable( owner.source );
	return true; //返回 true 允许当前列排序	
};
grid2.onSortColumn = grid.onSortColumn;


var getFolderList = function(){
	var list = fsys.list(config.__sapDownload,,"ORDER_LIST_*.xlsx");
	grid2.source={ fields = {[1]="订单号";[2]="下载日期";[3] = "缓存文件"}};
	for (i=1; #list){
		var f = string.split(string.split(list[i],".")[1],"_");
		var fitem = fsys.file(list[list[i]]);	
		table.push(grid2.source, {
			"订单号" = string.upper(f[3]) ;
			"下载日期" = time(fitem.getTime().creation.local(),"%Y/%m/%d %H:%M:%S");
			"缓存文件" = list[i];
		});
		fitem.close(); //关闭句柄, 否则窗口打开期间文件一直是打开占用状态;
	}
	grid2.onSortColumn(2, true);

	//grid2.setTable(flist);
	grid2.ColumnWidth := domisoft.setColumnWidth(winform.listview2, {80,120})
	winform.listview2.fillParent(3);
	winform.button.disabledText = null;
}


getFolderList();

var getDetail = function(wbpath, wform){
	import access;
	import access.oleDb;
	import config;

	wbpath = io.joinpath(config.__sapDownload, wbpath)
	if !io.exist(wbpath) return win.msgbox("文件不存在了, 请重新下载."++'\n'++wbpath); 
	wform.grid.clear();


	var db, err = access(wbpath);
	var n=0;
	while(err){

		collectgarbage("collect");
		n=n+1;
		db, err = access(wbpath);
		if n>1000 break;
	}
	if err {
		wform.grid.clear();

		return ; 
	}
	var t = db.getTable(`
		SELECT DISTINCT
			[凭证日期],
			[SD 凭证],
			[项目(SD)],
			[售达方],
			[名称 1],
			[选择],
			[物料],
			[描述],
			[订单数量]
		FROM
			[Sheet1$]
		WHERE
			[拒绝原因] = "" OR [拒绝原因] IS NULL
	`);

	db.close();
	db = null;
	collectgarbage("collect");
	table.push(t.fields, "订单BOM缓存日期", "生产BOM缓存日期");
	import fsys;
	import fsys.file;
	for (i=1;#t){
		var b2c_filename = string.join({"B2C", t[i]["SD 凭证"], t[i]["项目(SD)"], t[i]["物料"]},"_") ++ ".xlsx";
		var coois_filename = string.join({"COOIS", t[i]["SD 凭证"], t[i]["项目(SD)"]},"_") ++ ".xlsx";
		b2c_filename = io.joinpath(config.__sapDownload, b2c_filename);
		coois_filename = io.joinpath(config.__sapDownload, coois_filename);
		if io.exist(b2c_filename){
			var fitem = fsys.file(b2c_filename);
			t[i]["订单BOM缓存日期"] = fitem.getFileTime().creation.local();
			fitem.close();
		}
		if io.exist(coois_filename){
			var fitem = fsys.file(coois_filename);
			t[i]["生产BOM缓存日期"] = fitem.getFileTime().creation.local();
			fitem.close();
		}
	}
	
	return table.map(t, function(v){
		 v["凭证日期"] = tostring(v["凭证日期"],"yyyy/MM/dd" );
		 v["项目(SD)"] = tonumber(v["项目(SD)"]);
		 v["订单数量"] = tonumber(v["订单数量"]);
		 return v; 
	}); 
}

grid2.onClick = function(item,subItem,nmListView){
	if !item and ! winform.listview2.selIndex return ; 
	var c =  grid2.source[item || winform.listview2.selIndex].缓存文件;
	if !c return; 
	if item
		winform.edit.text = grid2.source[item].订单号;
	grid.source = getDetail(c, winform);
	if !grid.source or !#grid.source return;
	grid.clear();
	grid.onSortColumn(3, false);
	grid.ColumnWidth := domisoft.setColumnWidth(winform.listview, {80,80,40,80,120,10,100,350,40,120,120})
	winform.listview.fillParent(8);
}
grid2.onDoubleClick = function(item,subItem,nmListView){
	var c = owner.source[item].缓存文件;
	if !c return; 
	process.execute(io.joinpath(config.__sapDownload,c))
}
grid.onDoubleClick = function(item,subItem,nmListView){
	var t = owner.source[item];
	var c = string.join({"B2C", t["SD 凭证"], t["项目(SD)"], t["物料"]},"_");
	c = io.joinpath(config.__sapDownload, c ++ ".xlsx") ;
	if !io.exist(c)
		win.msgbox("没有找到" ++ '\n' ++ c ++ '\n' ++ "请下载");
	else 
		process.execute(c)
}

winform.grid.onRightClick = function(item,subItem,nmListView){
	var selectedIndex = owner.selected;
	var itemList = table.array();
	for (i=1; #owner.source)
		if table.indexOf(selectedIndex, i)
			table.push(itemList, owner.source[i]["项目(SD)"]);
			
	var x,y = win.getMessagePos();
	var menu = win.ui.popmenu(winform);
	
	var t = owner.source[item];
	var c = string.join({"B2C", t["SD 凭证"], t["项目(SD)"], t["物料"]},"_");
	c = io.joinpath(config.__sapDownload, c + ".xlsx") ;
	var m = string.join({"COOIS", t["SD 凭证"], t["项目(SD)"]}, "_");
	m = io.joinpath(config.__sapDownload, m + ".xlsx") ;
	
	if io.exist(c)
		menu.add("打开订单BOM...", λ() process.execute(c));
	menu.add("下载订单BOM...", λ() SAP.downloadB2CBOM(winform, t["SD 凭证"], itemList));
	if io.exist(m)
		menu.add("打开COOIS生产订单BOM...", λ() process.execute(m));
	menu.add("下载COOIS生产订单BOM...", λ() SAP.downloadManufactureBOM(winform, t["SD 凭证"], itemList));
	menu.popup(x,y,true);
}
winform.editDesp.onChange = function(){ 
	if(owner.onModified)owner.onModified(true);
	var str = owner.text;
	if !str return ; 
	str = string.trim(owner.text);
	if !str or !#str {
		grid.setTable(grid.source);
	}else{
		var t = table.filter(grid.source, λ(v) string.find(v["描述"], "@@"+str));
		t.fields = grid.source.fields;
		grid.clear();
		grid.setTable(t);
	}
}

winform.button.oncommand = function(id,event){
	var order_number = string.trim(winform.edit.text);
	if !order_number return ; 
	owner.disabledText = {"✶";"✸";"✹";"✺";"✹";"✷"};
	thread.invoke(SAP.downloadOrderList, winform, order_number)
}

subscribe("down_b2c_bom",function(...){
	var filename = ...;	
	win.delay(3*1000);
	thread.invoke( 
		function(filename){
			import domisoft;
			var sht = domisoft.getRunningWorkSheet(filename);
			import QHC;
			QHC.formatBOM(sht)
		}, filename
	)
} )

subscribe("done_job", grid2.onClick); 
subscribe("order_list_job_done", getFolderList); 
subscribe("down_order_list",function(...){
	var filename = ...;	
	win.delay(5*1000);
	thread.invoke(
		function(filename){
			import domisoft;
			var sht = domisoft.getRunningWorkSheet(filename);	
	 		if sht
	 			sht.Parent.Close(false);
		}, filename
	)
} )	
	
winform.button2.oncommand = getFolderList;

winform.button3.oncommand = function(id,event){
	var flist = winform.listview2.selected;
	if !#flist return ; 
	for k, v in flist {
		io.remove(
			io.joinpath(
				config.__sapDownload,
				winform.listview2.getItemText(v, 3)
			)
		);
	}
	getFolderList();
}

if winform.listview2.count
	grid2.onClick(1,1)


winform.show(3/*_SW_MAXIMIZE*/);

winform.splitter.split(winform.listview, winform.listview2);

winform.listview.translateAccelerator = domisoft.ctrlC;	
winform.listview2.translateAccelerator = domisoft.ctrlC;

win.loopMessage();
return winform;