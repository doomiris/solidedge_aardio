import win.ui;
import win.ui.grid;
/*DSG{{*/
var winform = win.form(text="订单清单浏览";right=759;bottom=469;)
winform.add(
button={cls="button";text="下载订单清单...";left=162;top=8;right=305;bottom=40;dr=1;dt=1;z=7;};
edit={cls="edit";left=539;top=10;right=745;bottom=36;dr=1;dt=1;edge=1;z=4;};
edit2={cls="edit";left=11;top=8;right=155;bottom=40;dl=1;dt=1;edge=1;hidesel=1;tabstop=1;z=6;};
listview={cls="listview";left=228;top=54;right=760;bottom=468;aw=1;db=1;dr=1;dt=1;edge=1;fullRow=1;vscroll=1;z=1;};
listview2={cls="listview";left=0;top=53;right=224;bottom=469;db=1;dl=1;dt=1;edge=1;fullRow=1;msel=false;vscroll=1;z=2;};
splitter={cls="splitter";left=223;top=53;right=228;bottom=464;db=1;dt=1;frame=1;z=3;};
static={cls="static";text="筛选[描述]";left=456;top=13;right=535;bottom=35;align="right";dr=1;dt=1;transparent=1;z=5;};

)
/*}}*/

import fsys;
import fsys.file;
import domisoft;
import config;
import process;
var grid = win.ui.grid(winform.listview);//创建数据视图
grid.setReadonlyColumns(-1);
var grid2 = win.ui.grid(winform.listview2);//创建数据视图
grid2.setReadonlyColumns(-1);
/*
import console
console.open()

*/

winform.listview.translateAccelerator = domisoft.ctrlC;

grid.onSortColumn = function(column, desc){
	var name = owner.source.fields[column];
	table.sort(owner.source,desc ? (lambda(next) owner[name] > next[name]) : (lambda(next) owner[name] < next[name]));	
	owner.setTable( owner.source );
	return true; //返回 true 允许当前列排序	
};
grid2.onSortColumn = grid.onSortColumn;

var getFolderList = function(){
			var list = fsys.list(config.__sapDownload,,"ORDER_LIST_*.xlsx");
			grid2.source={ fields = {[1]="订单号";[2]="下载日期";[3] = "缓存文件"}};
			for (i=1; #list){
				var f = string.split(string.split(list[i],".")[1],"_");
				var fitem = fsys.file(list[list[i]]);
				table.push(grid2.source, {
					"订单号" = string.upper(f[3]) ;
					"下载日期" = time(fitem.getTime().creation.local(),"%Y/%m/%d %H:%M:%S");
					"缓存文件" = list[i];
				});
			}
			grid2.onSortColumn(2, true);

			//grid2.setTable(flist);
			winform.listview2.setColumn({cx=80; fmt=0x0/*_LVCFMT_LEFT*/}, 1);
			winform.listview2.setColumn({cx=120; fmt=0x2/*_LVCFMT_CENTER*/}, 2);
			winform.listview2.fillParent(3);
}

getFolderList();

var getDetail = function(wbpath){
	wbpath = io.joinpath(config.__sapDownload, wbpath)
	if !io.exist(wbpath) return error("文件不存在"++'\n'++wbpath); 
	grid.clear(true);
	grid.setColumns({[1]="正在打开..." + wbpath})
	import access;
	import access.oleDb;
	var db, err = access(wbpath);
	if err {
		grid.clear(true);
		grid.setColumns({[1]= err;[2] = wbpath})
		return ; 
	}
	var t = db.getTable(`
		SELECT DISTINCT
			[凭证日期],
			[SD 凭证],
			[项目(SD)],
			[售达方],
			[名称 1],
			[选择],
			[物料],
			[描述],
			[订单数量]
		FROM
			[Sheet1$]
	`);
	db.close();
	return table.map(t, function(v){
		 v["凭证日期"] = tostring(v["凭证日期"],"yyyy/MM/dd" );
		 v["项目(SD)"] = tonumber(v["项目(SD)"]);
		 v["订单数量"] = tonumber(v["订单数量"]);
		 return v; 
	}); 
}

grid2.onClick = function(item,subItem,nmListView){
	var c = owner.source[item].缓存文件;
	if !c return; 
	grid.source = getDetail(c);
	if !grid.source or !#grid.source return;
	grid.clear(true);
	grid.onSortColumn(3, false);
	winform.listview.fillParent(8);
}
grid2.onDoubleClick = function(item,subItem,nmListView){
	var c = owner.source[item].缓存文件;
	if !c return; 
	process.execute(io.joinpath(config.__sapDownload,c))
}

winform.edit.onChange = function(){ 
	if(owner.onModified)owner.onModified(true);
	var str = string.trim(owner.text);
	if !str or !#str {
		grid.setTable(grid.source);
	}else{
		var t = table.filter(grid.source, λ(v) string.find(v["描述"], "@@"+str));
		t.fields = grid.source.fields;
		grid.clear(true);
		grid.setTable(t);
	}
}

winform.button.oncommand = function(id,event){
	owner.disabledText = {"✶";"✸";"✹";"✺";"✹";"✷"};
	thread.invoke( 
		function(wform, order_number){
			import config;
    		import web.script;
    		var vm = web.script("VBScript") ;
/*
			import console; 
			console.open();
*/			
			vm.script = /*
					If Not IsObject(application) Then
   					Set SapGuiAuto  = GetObject("SAPGUI")
   					Set application = SapGuiAuto.GetScriptingEngine
					End If
					If Not IsObject(connection) Then
   					Set connection = application.Children(0)
					End If
					If Not IsObject(session) Then
   					Set session    = connection.Children(0)
					End If
					If IsObject(WScript) Then
   					WScript.ConnectObject session,     "on"
   					WScript.ConnectObject application, "on"
					End If
			*/
			
			/* 通过 vm.script 访问变量前需要先声明 */
			vm.script = /*
	    			dim order
	    			dim startdate
	    			dim lib_path
	 		*/
			vm.script.order = order_number;
			vm.script.lib_path = config.__sapDownload;
			vm.script = /*
				session.findById("wnd[0]/tbar[0]/okcd").text = "VA03"
				session.findById("wnd[0]").sendVKey 0
				session.findById("wnd[0]/usr/ctxtVBAK-VBELN").text = order
				session.findById("wnd[0]").sendVKey 0
				'获取订单日期
				startdate = session.findById("wnd[0]/usr/tabsTAXI_TABSTRIP_OVERVIEW/tabpT\01/ssubSUBSCREEN_BODY:SAPMV45A:4400/ssubHEADER_FRAME:SAPMV45A:4440/ctxtVBKD-PRSDT").text
			*/
			vm.script.startdate = tostring(time(vm.script.startdate,"%Y.%m.%d").addday(-30))
			vm.script = /*
				session.findById("wnd[0]/mbar/menu[4]/menu[1]/menu[0]").select
				session.findById("wnd[0]/usr/txtVBCOM-BSTKD").text = order
				session.findById("wnd[0]/usr/ctxtVBCOM-AUDAT").text = startdate
				session.findById("wnd[0]/tbar[0]/btn[0]").press
				session.findById("wnd[0]/usr/cntlGRID1/shellcont/shell/shellcont[1]/shell").contextMenu
				session.findById("wnd[0]/usr/cntlGRID1/shellcont/shell/shellcont[1]/shell").selectContextMenuItem "&XXL"
				session.findById("wnd[1]/tbar[0]/btn[0]").press
				session.findById("wnd[1]/usr/ctxtDY_PATH").text = lib_path
				session.findById("wnd[1]/usr/ctxtDY_FILENAME").text = "ORDER_LIST_" & order & ".XLSX"
				session.findById("wnd[1]/tbar[0]/btn[11]").press
				'返回
				session.findById("wnd[0]/tbar[0]/btn[12]").press
				session.findById("wnd[0]/tbar[0]/btn[12]").press
				session.findById("wnd[0]/tbar[0]/btn[12]").press
				session.findById("wnd[0]/tbar[0]/btn[12]").press
			*/
			wform.button.disabledText = null;
		}, winform, winform.edit2.text
	)
}

winform.splitter.split(winform.listview,winform.listview2);
winform.show();

win.loopMessage();
return winform;