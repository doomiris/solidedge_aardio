import fonts.fontAwesome;
import win.ui;
import tarray;
import domisoft;
/*DSG{{*/
var winform_od_query = win.form(text="分类和特性查询";right=999;bottom=751)
winform_od_query.add(
button={cls="button";text='\uF0E2';left=8;top=688;right=40;bottom=720;color=255;db=1;dl=1;font=LOGFONT(h=-16;name='FontAwesome');z=7};
button2={cls="button";text="可选项调研表增加下拉菜单";left=8;top=640;right=216;bottom=672;db=1;dl=1;z=9};
edit={cls="edit";left=8;top=32;right=216;bottom=64;dl=1;dt=1;edge=1;z=6};
edit2={cls="edit";left=232;top=32;right=528;bottom=64;dl=1;dt=1;edge=1;z=8};
listview1={cls="listview";left=8;top=88;right=216;bottom=216;aw=1;dl=1;dt=1;edge=1;fullRow=1;msel=false;z=1};
listview2={cls="listview";left=224;top=88;right=528;bottom=728;ah=1;aw=1;db=1;dt=1;edge=1;fullRow=1;z=2};
listview3={cls="listview";left=536;top=88;right=992;bottom=728;ah=1;aw=1;db=1;dr=1;dt=1;edge=1;fullRow=1;z=3};
listview4={cls="listview";left=8;top=227;right=216;bottom=638;ah=1;aw=1;db=1;dt=1;edge=1;fullRow=1;z=13};
splitter={cls="splitter";left=216;top=87;right=223;bottom=637;ah=1;db=1;dt=1;frame=1;z=4};
splitter2={cls="splitter";left=528;top=88;right=536;bottom=728;ah=1;db=1;dt=1;frame=1;z=5};
splitter3={cls="splitter";left=10;top=218;right=214;bottom=225;frame=1;horz=1;z=14};
static={cls="static";text="分类";left=8;top=72;right=88;bottom=88;dl=1;dt=1;transparent=1;z=10};
static2={cls="static";text="特性";left=224;top=72;right=304;bottom=88;dl=1;dt=1;transparent=1;z=11};
static3={cls="static";text="特性值";left=536;top=72;right=616;bottom=88;dl=1;dt=1;transparent=1;z=12}
)
/*}}*/

/*
import console
console.open();
*/
/*sqliteDB{{*/
import config;
import sqlite;
var db, err = sqlite(io.joinpath(config.dbServer.path, "qhc_OD_lib.db"));	//打开数据库连接
if err win.msgbox(err);


/*}}*/

winform_od_query.edit.translateCommand();
winform_od_query.edit2.translateCommand();

var actrls  = winform_od_query.edit.addCtrl({ 
	{cls="button";text="r";font=LOGFONT(name='Marlett';charset=2/*_SYMBOL_CHARSET*/);hide=1;width=16;height=16;marginTop=0.2/*垂直居中*/;marginRight=5 } 
})
var bctrls  = winform_od_query.edit2.addCtrl({ 
	{cls="button";text="r";font=LOGFONT(name='Marlett';charset=2/*_SYMBOL_CHARSET*/);hide=1;width=16;height=16;marginTop=0.2/*垂直居中*/;marginRight=5 } 
})
actrls[1].oncommand = function(id,event){
	owner.parent.text="" 
}
bctrls[1].oncommand = function(id,event){
	owner.parent.text="" 
}
var filterDeleted = function(t){
	if !t or !#t return t; 
	var z = {};
	if t.fields z.fields = t.fields;
	for (i=1; #t) {
		if !t[i].特性 continue;
		if t[i].更改标记	//无标记不代表删除哦
			if string.find(t[i].更改标记,"删") continue;
		table.push(z, t[i])
	}
	if table.indexOf(z.fields,"特性值") table.sort(z, lambda(a) tonumber((owner.特性值)) < tonumber(a.特性值));
	else if table.indexOf(z.fields,"特性") table.sort(z, lambda(a) owner.特性 < a.特性);
	return z;
}
var sortby = function(t,c){
	if !#t return t; 
	table.sort(t, lambda(a) (owner[c]) < a[c]);
	return t;
}
var searchClass = function(str){
	if !str return null; 
	str = string.upper(string.trim(str));
	var t = db.getTable("SELECT * FROM `分类`", { 分类 : str });
	return filterDeleted(t);
}
var searchClassByBG = function(str){
	if !str return null; 
	str = string.upper(string.trim(str));
	var t = db.stepQuery("SELECT * FROM `默认值`", { 产品编码 : str });
	var p = db.stepQuery("SELECT * FROM `喷粉类别`", { 物料 : str });
	return t[["分类"]], p[["喷粉类别"]];
}

var searchOption = function(str){
	if !str return null; 
	str = string.upper(string.trim(str));
	var t = db.getTable("SELECT * FROM `特性参数规划表`", { 特性 : str });
	return filterDeleted(t);
}
var searchBGbyOption = function(str){
	if !str return null; 
	str = string.upper(string.trim(str));
	var t = db.getTable("SELECT DISTINCT 产品编码 FROM `默认值`", { 分类 : str });
	return t;
}
var 喷粉部分表;
var searchPaintClass = function(str){
	if !str return null; 
	str = string.upper(string.trim(str));
	var t = db.getTable("SELECT * FROM `喷粉分类组成`", { 产品系列号 : str });
	return t;
}
var searchColor = function(colorGroupId){
	if !colorGroupId return null; 
	var t = db.getTable("SELECT * FROM `颜色分组`", { 颜色分组 : colorGroupId });
	return sortby(t,"默认颜色可选项类别");
}

var initPaintClass = function(){
	var d = db.getTable("SELECT * FROM `喷粉部位`");
	if !d or !#d return null; 
	var t = {};
	for (i=1; #d)
		t[d[i]["喷粉部位编码"]] = d[i]["喷粉部位描述"];
	return t; 
}

var bgTab = {fields = {[1]="物料";[2]="物料描述"}};
var keepBG = function(hcode){
	import QHC;
	if !hcode return ;  
	if tarray.findValue(bgTab, hcode) return ; 
	var hitem = QHC.getSapItem(hcode);
	table.push(bgTab, {
			"物料" : hitem.物料;
			"物料描述" : hitem.物料描述
		}
	)
	winform_od_query.listview4.setTable(bgTab);
}

var abb = null; //延迟线程ID
_delay_query = 600 //延迟时间 mm

var editChange = function(text){
	//winform_od_query.listview1.clear(true);
	winform_od_query.listview2.clear(true);
	winform_od_query.listview3.clear(true);
	//winform_od_query.listview4.clear(true);
	var t;
	var bt /*分类*/, pt/*喷粉分类*/;
	if #text==11 and string.startWith(text, "BG",true) {
		bt, pt= searchClassByBG(text);
		
		t = searchClass(bt);
		keepBG(text);
	}
	
	if string.startWith(text, "CG",true) {
		喷粉部分表 := initPaintClass();
		t = searchPaintClass(text);	
	}
	if !table.count(t) {
		t = searchClass(text);
	}
	if !#t return ;
	
	var tClass, tOpts, tBGs;
	
	if string.startWith(text, "CG",true){
		tClass = tarray.uniqueCol(tarray.sliceCol(t,2,3));
		for (i=1; #tClass)
			tClass[i].喷粉部位描述 = 喷粉部分表[tClass[i]["喷粉部位编码"]];
					
		table.push(tClass.fields,"喷粉部位描述");

		var ct = searchColor(tClass[1].颜色分组);
		tOpts = tarray.uniqueCol(tarray.sliceCol(ct,2,3));
		if tClass	winform_od_query.listview2.setTable(tClass);
		if tOpts	winform_od_query.listview3.setTable(tOpts);
	}else{
		tClass = tarray.uniqueCol(tarray.sliceCol(t,1,2));
		tOpts = tarray.uniqueCol(tarray.sliceCol(t,4,5));

		if pt
			table.push(tClass,{"分类":pt;"分类描述":"喷粉类别"});
		
		if tClass
			if !tarray.findValue(winform_od_query.listview1.items, tClass[1]["分类"])
				winform_od_query.listview1.setTable(tClass);
		if #tOpts
			winform_od_query.listview2.setTable(tOpts);
		
		tBGs = searchBGbyOption(text);
		//win.msgbox(tBGs);
		
		if #tBGs{
			winform_od_query.listview4.clear();
			bgTab = {fields = {[1]="物料";[2]="物料描述"}};
			for(i=1; #tBGs){
				keepBG(tBGs[i].产品编码);
			}
		}
	}

}

winform_od_query.edit.onChange = function(){ 
	if(owner.onModified) owner.onModified(true);
	var text = owner.text;
	actrls[1].hide = !#text;
	owner.redraw();
	win.clearTimeout(abb)
	abb = win.setTimeout(editChange, _delay_query, text)
}
var editChange2 = function(text){
	//winform_od_query.listview1.clear(true);
	winform_od_query.listview2.clear(true);
	winform_od_query.listview3.clear(true);
	
	var t = searchOption(text);
	if !#t return ;

	var tOpts = tarray.uniqueCol(tarray.sliceCol(t,1,2));
	var cType = tarray.uniqueCol(tarray.sliceCol(t,3,4));

	if tOpts	winform_od_query.listview2.setTable(tOpts);
	if cType	winform_od_query.listview3.setTable(cType);
}

winform_od_query.edit2.onChange = function(){ 
	if(owner.onModified)owner.onModified(true);
	var text = owner.text;
	bctrls[1].hide = !#text;
	owner.redraw();
	win.clearTimeout(abb)
	abb = win.setTimeout(editChange2, _delay_query, text);
}
var lastOpt1,lastOpt2,lastOpt3,lastOpt4;//记住上次搜索项, 减少搜索频率
winform_od_query.listview2.onnotify = function(id,code,ptr){
	select(code) {
		case 0xFFFFFF9B/*_LVN_ITEMCHANGED*/ {
			var c = owner.getItemText(owner.getSelection(),1);
			if c == lastOpt2 return ; 
			if !c return; 
			var t = searchOption(c)
			if !#t return;
			var cType = tarray.uniqueCol(tarray.sliceCol(t,3,4));
			winform_od_query.listview3.clear(true);
			if cType	winform_od_query.listview3.setTable(cType);
			lastOpt2 = c;
		}
	}
}
winform_od_query.listview1.onnotify = function(id,code,ptr){
	select(code) {
		case 0xFFFFFF9B/*_LVN_ITEMCHANGED*/ {
			var c = owner.getItemText(owner.getSelection(),1);
			if c == lastOpt1 return ; 
			if !c return; 
			winform_od_query.edit.text = c;
			lastOpt1 = c;	
		}
	}
}
winform_od_query.listview4.onnotify = function(id,code,ptr){
	select(code) {
		case 0xFFFFFF9B/*_LVN_ITEMCHANGED*/ {
			var c = owner.getItemText(owner.getSelection(),1);
			if c == lastOpt4 return ; 
			if !c return; 
			winform_od_query.edit.text = c;
			lastOpt4 = c;	
		}
	}
}
winform_od_query.button.oncommand = function(id,event){
			loadcodex("\forms\admin_only\BOM_OD_LIB.aardio");
}

/*CtrlC复制模块{{*/
for(name,ctrl in winform_od_query.eachControl("listview") ){
	ctrl.translateAccelerator = domisoft.ctrlC;
}
/*}}*/
winform_od_query.button2.oncommand = function(id,event){	
	loadcodex("\forms\Excel\可选项调研表增加下拉菜单.aardio");
}



winform_od_query.splitter3.split(winform_od_query.listview1, winform_od_query.listview4);
winform_od_query.splitter.split({
			winform_od_query.listview1,
			winform_od_query.listview4,
			winform_od_query.splitter3
		}, winform_od_query.listview2);
winform_od_query.splitter2.split(winform_od_query.listview2, winform_od_query.listview3);


winform_od_query.show();
win.loopMessage();
return winform_od_query;