import win.ui;
/*DSG{{*/
var dft_list_form = win.form(text="DFT_LIST";right=663;bottom=471)
dft_list_form.add(
listview={cls="listview";left=0;top=0;right=664;bottom=472;ah=1;aw=1;db=1;dl=1;dr=1;dt=1;edge=1;z=1};
progress={cls="progress";left=0;top=0;right=664;bottom=30;aw=1;dl=1;dr=1;dt=1;max=100;min=0;z=2}
)
/*}}*/

import win.ui.grid;
dft_list_form.grid = win.ui.grid(dft_list_form.listview);//创建数据视图

var getDftList = function(dft_list_form){
	import solidedge.application;
	import solidedge.DraftDocument;
//	import win.ui;

	seApp = solidedge.application();
	if !seApp return ; 
	var seDocuments = seApp.Documents;
	var seDfts = {};
	seDfts.seIndex = {};
	for (i = 1; seDocuments.Count)
    	if (seDocuments.item(i).Type == 2 /*Draft Document Type*/ ) {
    		table.push(seDfts, seDocuments.item(i));
    		table.push(seDfts.seIndex, i);
    	}
    dft_list_form.publish("dft_loaded", seDfts); //把dfts推送给上一层
	vtab = table.create(,{fields = {"物料","版本","型号","名称","属性"}})
	dft_list_form.progress.min = 0;
	dft_list_form.progress.max = #seDfts ;
	dft_list_form.progress.hide = 0;
	for(i=1; #seDfts){
		dft_list_form.progress.pos = i;
		var seDft = solidedge.DraftDocument(seDfts[i]);
		var t={};
		var tb = seDft.getTitleBlock(seDft.getFirstSheet());
		if(tb){
			var lbt = seDft.getLabelTable(tb);
			t.物料 = seDft.getFileName();
			t.名称 = seDft.formatWindowsFileName(lbt["零件名称"]);
			t.型号 = seDft.formatWindowsFileName(lbt["型号/项目名称"]);
			t.版本 = seDft.getDftVer();
			var blkObj = seDft.getLabelObjectTable(seDft.getBlkByName("Version_CCR 2020",seDft.getFirstSheet()));
			if blkObj
				t.属性 = blkObj["属性"].value;
			else {
				blkObj := seDft.getLabelObjectTable(seDft.getBlkByName("SRDC_Ver",seDft.getFirstSheet()));
				t.属性 = blkObj ?  blkObj["阶段"].value : "";
			}
			

		}else{
			t.物料 = seDft.getFileName();
			t.名称 = "";
			t.型号 = "";
			t.版本 = "";
			t.属性 = "";
		}


		//t.index = seDftIndex[i];
		table.push(vtab, t)
	}
	dft_list_form.progress.hide = 1;

	dft_list_form.grid.setTable(vtab);
	dft_list_form.listview.checkbox = true;
	dft_list_form.listview.setChecked(0);

	dft_list_form.listview.setColumn({cx=120	;fmt=0x2/*_LVCFMT_CENTER*/},	1)
	dft_list_form.listview.setColumn({cx=55	;fmt=0x2/*_LVCFMT_CENTER*/},	2)
	dft_list_form.listview.setColumn({cx=120	;fmt=0x0/*_LVCFMT_LEFT*/},		3)
	dft_list_form.listview.setColumn({cx=150	;fmt=0x0/*_LVCFMT_LEFT*/},		4)
	dft_list_form.listview.setColumn({cx=50	;fmt=0x2/*_LVCFMT_CENTER*/},	5)
}
dft_list_form.show();

thread.invoke(getDftList, dft_list_form);

subscribe("refresh_dft_list",function(...){
    dft_list_form.grid.clear();
	thread.invoke(getDftList, dft_list_form);
} )
import domisoft;
dft_list_form.listview.translateAccelerator = domisoft.ctrlC;

dft_list_form.listview.onnotify = function(id,code,ptr){
    select(code) {
    	case 0xFFFFFFFB/*_NM_RCLICK*/  {
			var x,y = win.getMessagePos();
			var menu = win.ui.popmenu(dft_list_form);			
			menu.add("全部", λ() dft_list_form.listview.setChecked(0));
			menu.add("取消全选", λ() dft_list_form.listview.setChecked(0,false));
			menu.add("反向选择", function(){
				if dft_list_form.listview.count {
					var t = {};
					for (i=1; dft_list_form.listview.count)
						if !table.find(dft_list_form.listview.checked, i) table.push(t, i)
					dft_list_form.listview.checked = t;
				}
			})
			
			menu.popup(x,y,true);
    	}
    }
}

win.loopMessage();
return dft_list_form;