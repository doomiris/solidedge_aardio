import win.ui;
/*DSG{{*/
var winform = win.form(text="DFT_LIST";right=663;bottom=471)
winform.add(
listview={cls="listview";left=0;top=0;right=664;bottom=472;ah=1;aw=1;db=1;dl=1;dr=1;dt=1;edge=1;z=1};
progress={cls="progress";left=0;top=0;right=664;bottom=30;aw=1;dl=1;dr=1;dt=1;max=100;min=0;z=2}
)
/*}}*/

import win.ui.grid;
winform.grid = win.ui.grid(winform.listview);//创建数据视图
winform.seDfts = null;
var getDftList = function(wform){
	import solidedge.application;
	import solidedge.DraftDocument;
	import domisoft;

	wform.seDfts = null;
	
	seApp = solidedge.application();
	if !seApp return ; 
	var seDocuments = seApp.Documents;
	var seDfts = {};
	seDfts.seIndex = {};
	for (i = 1; seDocuments.Count)
    	if (seDocuments.item(i).Type == 2 /*Draft Document Type*/ ) {
    		table.push(seDfts, seDocuments.item(i));
    		table.push(seDfts.seIndex, i);
    	}
    wform.seDfts = seDfts; //把dfts推送给上一层
	vtab = table.create(,{fields = {"物料","版本","型号","名称","属性"}})
	wform.progress.min = 0;
	wform.progress.max = #seDfts ;
	wform.progress.hide = 0;
	for(i=1; #seDfts){
		wform.progress.pos = i;
		var seDft = solidedge.DraftDocument(seDfts[i]);
		var t={};
		var tb = seDft.getTitleBlock(seDft.getFirstSheet());
		if(tb){
			var lbt = seDft.getLabelTable(tb);
			t.物料 = seDft.getFileName();
			t.名称 = domisoft.formatWindowsFileName(lbt["零件名称"]);
			t.型号 = domisoft.formatWindowsFileName(lbt["型号/项目名称"]);
			t.版本 = seDft.getDftVer();
			var blkObj = seDft.getLabelObjectTable(seDft.getBlkByName("Version_CCR 2020",seDft.getFirstSheet()));
			if blkObj
				t.属性 = blkObj["属性"].value;
			else {
				blkObj := seDft.getLabelObjectTable(seDft.getBlkByName("SRDC_Ver",seDft.getFirstSheet()));
				t.属性 = blkObj ?  blkObj["阶段"].value : "";
			}
			

		}else{
			t.物料 = seDft.getFileName();
			t.名称 = "";
			t.型号 = "";
			t.版本 = "";
			t.属性 = "";
		}


		//t.index = seDftIndex[i];
		table.push(vtab, t)
	}
	wform.progress.hide = 1;

	wform.grid.setTable(vtab);
	wform.listview.checkbox = true;
	wform.listview.setChecked(0);

	wform.listview.setColumn({cx=120	;fmt=0x2/*_LVCFMT_CENTER*/},	1)
	wform.listview.setColumn({cx=55	;fmt=0x2/*_LVCFMT_CENTER*/},	2)
	wform.listview.setColumn({cx=120	;fmt=0x0/*_LVCFMT_LEFT*/},		3)
	wform.listview.setColumn({cx=150	;fmt=0x0/*_LVCFMT_LEFT*/},		4)
	wform.listview.setColumn({cx=50	;fmt=0x2/*_LVCFMT_CENTER*/},	5)
}
winform.show();

thread.invoke(getDftList, winform);

winform.refresh_dft_list = function(){
    winform.grid.clear();
	thread.invoke(getDftList, winform);
}
import domisoft;
winform.listview.translateAccelerator = domisoft.ctrlC;

winform.listview.onnotify = function(id,code,ptr){
    select(code) {
    	case 0xFFFFFFFB/*_NM_RCLICK*/  {
			var x,y = win.getMessagePos();
			var menu = win.ui.popmenu(winform);			
			menu.add("全部", λ() winform.listview.setChecked(0));
			menu.add("取消全选", λ() winform.listview.setChecked(0,false));
			menu.add("反向选择", function(){
				if winform.listview.count {
					var t = {};
					for (i=1; winform.listview.count)
						if !table.find(winform.listview.checked, i) table.push(t, i)
					winform.listview.checked = t;
				}
			})
			
			menu.popup(x,y,true);
    	}
    }
}

win.loopMessage();
return winform;