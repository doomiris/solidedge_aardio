import win.ui;
/*DSG{{*/
var winform = win.form(text="paper";right=759;bottom=474)
winform.add(
button={cls="button";text="开始打印";left=632;top=438;right=752;bottom=469;db=1;dr=1;z=2};
combobox={cls="combobox";left=10;top=438;right=247;bottom=464;db=1;dl=1;edge=1;items={};mode="dropdownlist";z=3};
listview={cls="listview";left=0;top=0;right=760;bottom=424;ah=1;db=1;dl=1;dr=1;dt=1;edge=1;z=1};
progress={cls="progress";left=0;top=425;right=760;bottom=435;aw=1;color=0x808000;db=1;dl=1;dr=1;max=100;min=0;z=4};
static={cls="syslink";text="<a>清空列表</a>";left=560;top=448;right=616;bottom=464;db=1;dr=1;notify=1;transparent=1;z=5}
)
/*}}*/

import fsys;
import config;
import process;
import tarray;

import win.util.tray;
var tray = win.util.tray(mainForm) //创建托盘图标,用于通知 

import win.ui.grid;
var grid = win.ui.grid(winform.listview);//创建数据视图

import fsys.lnk;
var aid;
var apath = fsys.lnk.search("AcroRd32.exe");
if !io.exist(apath)
	apath = fsys.lnk.searchInDesktop("Acrobat.exe")

var tab;
var renew = function(){
	tab = {};	
	tab.fields = {[1] = "#"; [2] = "文件名"; [3]="Path"; }
}
renew();

winform.onDropFiles = function(files){
	var t = {}
	for (i=1; #files){
		var info = io.splitpath(files[i]);
		if !string.cmpMatch(info.ext, ".pdf") continue;
		if !table.find(tarray.getCol(tab, "文件名"), info.file)
		table.push(tab, {
			"#" = #tab + 1;
			"文件名" =  info.file;
			"Path" = info.dir;
			//"output" = tf;
		})
	}
	grid.setTable(tab);
	winform.listview.checked = tarray.getCol(tab, "#");
	winform.listview.setColumn({cx=45 ;fmt=0x2/*_LVCFMT_CENTER*/},1);
	winform.listview.setColumn({cx=500 ;fmt=0x0/*_LVCFMT_LEFT*/},2);
	winform.listview.setColumn({cx=0 ;fmt=0x0/*_LVCFMT_LEFT*/},3);
	
	if !aid aid = process.execute(apath, "/n /s /h", ,6/*_SW_MINIMIZE*/);
}


winform.listview.setExtended(0x4/*_LVS_EX_CHECKBOXES*/);

var addSysPrinter = function(winform){
	import sys.printer;
	var combobox = winform.combobox;
	for printerName,serverName,attributes in sys.printer.each(4/*_PRINTER_ENUM_CONNECTIONS*/ + 2/*_PRINTER_ENUM_LOCAL*/){
    	combobox.add(printerName);    
	}
	combobox.selectString(sys.printer.default());
}
thread.invoke(addSysPrinter, winform);

var print2printer = function(agent, filename, printername){
	import process;
	var pid = process.execute(agent, '/s /h /t "' + filename + '" "' + printername + '"',,6/*_SW_MINIMIZE*/);
	/**	
	Print Agent: C:\Program Files (x86)\Adobe\Acrobat Reader DC\Reader\AcroRd32.exe
	Parameters:  /p /h 
	/n - 启动一个新的 Reader 实例，即使一个已经打开
	/s - 不显示启动画面
	/o - 不显示打开文件对话框
	/h - 作为最小化窗口打开
	/p <filename> - 打开并直接进入打印对话框
	/t <filename> <printername> <drivername> <portname> - 在指定的打印机上打印文件.
	**/
	return pid; 
}


winform.button.oncommand = function(id,event){
	owner.disabledText = {"✶";"✸";"✹";"✺";"✹";"✷"; text = "正在发送..."};	
	var progress = winform.progress;
	var checked = winform.listview.checked;
	progress.pos = 0;
	progress.max = #checked;
	var printername = winform.combobox.text;
	for (k, v in checked){
		progress.pos ++;
		var row = tab[v];
		var filename = io.joinpath(row["Path"], row["文件名"]);
		var pid = thread.invokeAndWait(print2printer, apath, filename, printername);
		win.delay(1000);
	}
	
	owner.disabledText = null;
	
	tray.message = 0xACCF/*_WM_TRAYMESSAGE*/
	tray.pop("All paper Print Done","TBM");
}

winform.static.onHyperlinkClick = function(nmSysLink,url,id){
	winform.listview.clear(true);
	renew();
}

winform.show();
win.loopMessage();