import QHC;
import com.excel;
import config;
import console;
console.open();

var excel = com.excel(false) ;
var osht = excel.ActiveWorkbook.ActiveSheet;
if osht.cells(1,1).value2 !== "展开层" error("格式错误");

/*
var getColIndex = function(A){
	return A[1]-'A'# + 1; 
}
*/
import etip;

import console.progress;
var progress = console.progress();

var etab = osht.UsedRange.value2;
var ts_count = #etab;
 if etab[3][1 /* A */] == ".1" etab[3][1 /* A */] = "'.1"; //阻止识别为0.1
//console.dump(etab);
//console.pause(true);
excel.Application.CutCopyMode = false;

_start_col = 9 /* I */;

_col_count = console.getNumber( "从I列开始, 共有几列需要导出?");

//是否导出为单一文件
_save_as_single_bom_file = true;

var tbk, tsht, tsht_tab;
if _save_as_single_bom_file {
	 tbk = excel.WorkBooks.Add(
    	io.joinpath(config.__templateLib,  "新产品BOM申请单模板-姓名-日期.xlsx")
	);
	
	 tsht = tbk.Sheets(1);
	
	 tsht_tab = {};
}
excel.alerts = false;
for(j = _start_col; _start_col + _col_count - 1 ){
    if !#etab[2][j] continue;

	if !_save_as_single_bom_file {
		 tbk = excel.WorkBooks.Add(
    		io.joinpath(config.__templateLib,  "新产品BOM申请单模板-姓名-日期.xlsx")
		);
		
		 tsht = tbk.Sheets(1);
		
		 tsht_tab = {};
	}

    for(i=1; ts_count){
        progress.setProgress( (i+(j-_start_col)*ts_count)*100/(ts_count*_col_count), "wait... ["++(j-_start_col+1) ++ "/" + _col_count + "]");

        if i == 1 continue; //跳过标题行

        if !#etab[i][1 /* A */] continue; //跳过层级为空的行
       
        
        if #etab[i][j] and string.len(string.trim(etab[i][j])) == 1
        	if string.cmpMatch(etab[i][j], "X") continue;	// 跳过X标记, 也会跳过含X的专用号

    	var bun = etab[i][4 /* D */]; 
    	
    	if !bun bun = "1";
    	
    	if string.indexOf(bun, "\") {
    		var arr = string.split(bun,"\");
    		
    		if #arr==2 {
    			select(_col_count) {
    				case 2 {
    				    //arr = arr;
    				}
    				case 4 {
    				    arr = table.concat(arr,arr);
    				}
    				case 8 {
    				    arr = {arr[1],arr[1],arr[2],arr[2],arr[1],arr[1],arr[2],arr[2]};
    				}
    			}
    		}
    		if #arr==4 {
    			arr = table.flat(
    				table.array(
    					math.ceil(_col_count/#arr), arr
    				)
    			);
    		}
    		
    		bun = arr[j - _start_col + 1];
    	}
    	if !bun or bun=="0" continue; //跳过数量为0的行
    	
        var tar={};
        
       	table.push(tar, etab[i][1 /* A */])	//列名:层级
       	table.push(tar, "","L")	//列名:BOM项目,项目类别
       	
    	var hcode = etab[i][j]; 
    	if !hcode hcode = etab[i][2 /* B */];   	
    	if !hcode continue;	//如果还是没有物料则跳过， 目标文件内不显示描述等信息， 此种情况不可下发导入．
    	
    	//空格
    	hcode = string.replace(hcode,'\s',"");
    	//Non-breaking space
    	hcode = string.replace(hcode,'\u00A0',"");
    	
      	table.push(tar, hcode);	//列名:物料号
      	
		var hitem = QHC.getSapItem(hcode);
		
      	table.push(tar, hitem ? hitem.物料描述 : "");	//列名:物料描述
      	table.push(tar, bun);			//列名:单位用量
      	table.push(tar, hitem ? hitem.BUn : etab[i][[5] /* E */]);	//列名： 单位
      	table.push(tar, "");	//列名:排序文本
      	table.push(tar, hitem ? hitem.MTyp : etab[i][[6] /* F */]); //列名:物料类型
      	table.push(tar, #etab[i][7 /* G */]  ?  etab[i][7 /* G */] : ""); //列名: 相关性语句
      	
      	if !#tar[1 /* A */] or !#tar[4 /* D */] continue; //跳过 无层级 和 无专用号的行
      	
      	table.push(tsht_tab, tar);
 //     	console.dump(tar);
    }
    
	if !_save_as_single_bom_file {
		var tar_end_row = #tsht_tab + 4 ;
		tsht.Range("6:"++ tar_end_row).EntireRow.insert();
		tsht.range("A" ++ 5 ++ ":J" ++ tar_end_row).value2 = tsht_tab;
		tsht.Activate();
		
		
		excel.alerts = false;	
		tbk.SaveAs(
    		io.joinpath(
    			config.setbm.workingDir,
    			"init_" ++ tsht.Cells(5,"D").Text ++ "_" ++ tsht.Cells(5,"E").Text ++ ".xlsx"
    		)
		);
		QHC.formatBOM(tsht);
		tbk.close();
	}
}

if _save_as_single_bom_file {
	var tar_end_row = #tsht_tab + 4 ;
	tsht.Range("6:"++ tar_end_row).EntireRow.insert();
	tsht.range("A" ++ 5 ++ ":J" ++ tar_end_row).value2 = tsht_tab;
	tsht.Activate();
	
	
	excel.alerts = false;	
	tbk.SaveAs(
    	io.joinpath(
    		config.setbm.workingDir,
    		"init_" ++ tsht.Cells(5,"D").Text ++ "_" ++ tsht.Cells(5,"E").Text ++ ".xlsx"
    	)
	);
	QHC.formatBOM(tsht);
	tbk.close();
}

// console.log((j-9) ++ "/8", tbk.FullName);

excel.alerts = true;
raw.explore(config.setbm.workingDir)

/*
TODO:
1. 通用底箱体不展开
*/
console.pause("完成")