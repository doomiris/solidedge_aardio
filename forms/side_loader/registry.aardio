import win.ui;
/*DSG{{*/
var winform = win.form(text="注册表查看";right=741;bottom=429)
winform.add(
editPath={cls="edit";left=5;top=4;right=739;bottom=33;dl=1;dr=1;dt=1;edge=1;z=4};
listview={cls="listview";left=307;top=37;right=738;bottom=417;db=1;dr=1;dt=1;edge=1;z=2};
splitter={cls="splitter";left=297;top=35;right=302;bottom=417;db=1;dt=1;frame=1;z=3};
treeview={cls="treeview";left=4;top=38;right=295;bottom=419;bgcolor=0xFFFFFF;db=1;dl=1;dt=1;edge=1;fullRow=1;vscroll=1;z=1}
)
/*}}*/

winform.splitter.split(winform.treeview,winform.listview);

import win.ui.menu;
import win.reg;
import win.ui.grid;
import domisoft;
var grid = win.ui.grid(winform.listview);
grid.setColumns({"key";"value";}) //可选自定义显示列名
grid.setReadonlyColumns(1)

var getChild = function(path, parent){
	winform.treeview.clear(parent);
	var reg = win.reg(path,true); 
	if !reg return ;
	var keys = reg.keys();	
	winform.treeview.insertTable(keys, parent)
	reg.close();
}

/*
winform.listview.insertColumn("key",200);
winform.listview.insertColumn("value",100);
*/

var root = winform.treeview.insertItem( {text = "HKEY_CURRENT_USER"; cChildren = 1});
getChild("HKEY_CURRENT_USER", root);
for hChild in winform.treeview.each(root){
	var ctext = winform.treeview.getItemPath(hChild)
	getChild(ctext, hChild)
}
winform.treeview.expand(root);


winform.treeview.onClick = function(hItem,data){
	var text = winform.treeview.getItemPath(hItem)
	getChild(text,hItem);
	winform.treeview.expandPartial(hItem);
	var reg = win.reg(winform.treeview.getItemPath(hItem));
	if reg {		
		winform.listview.clear();
		var vt = reg.queryValueTable()
		for k, v in vt
			winform.listview.addItem({text = {k,v}});
		reg.close();
	}

	for hChild in winform.treeview.each(hItem){
		var ctext = winform.treeview.getItemPath(hChild)
		getChild(ctext, hChild)
	}
	winform.editPath.text = reg.path;

}
winform.treeview.onnotify = function(id,code,ptr){
 	 if( code = 0xFFFFFE3A/*_TVN_ITEMEXPANDINGW*/){  
 	 	var nmtv = winform.treeview.getNotifyMessage(code,ptr) 
 	 	if(nmtv.action & 2/*_TVE_EXPAND*/) { 
 	 		winform.treeview.onClick(nmtv.itemNew.hItem)
		}
 	 }
}
winform.treeview.onRightClick = function(hItem,data){
    winform.treeview.setSelected(hItem);
    
    var menu = win.ui.popmenu(winform)
    menu.add("删除",
        function(){ 
            var reg = win.reg(winform.treeview.getItemPath(winform.treeview.getParentItem(hItem)));
            if !reg return ; 
            if win.msgboxTest("删除此项及所有子项?"+'\n'+winform.treeview.getItemPath(hItem),"确认",winform.hwnd)
                if reg.delKeyTree(winform.treeview.getItemText(hItem))
                	winform.treeview.delItem(hItem);
            reg.close();
        }
    )

    menu.popup(x,y,true);
}

grid.onEditChanged = function(text,iItem,iSubItem){
	var reg = win.reg(winform.treeview.getItemPath(winform.treeview.getSelection()));
	if !reg return;
	var keyname = winform.listview.getItemText(iItem,1);
	reg.setValue(keyname, text);
	reg.close();	
}
grid.onRightClick = function(item,subItem,nmListView){
    var menu = win.ui.popmenu(winform)
    menu.add("删除",
        function(){ 
            var reg = win.reg(winform.treeview.getItemPath(winform.treeview.getSelection()));
            if !reg return ;
            var keyname = winform.listview.getItemText(iItem,1);
            if reg.delValue(keyname)
                winform.listview.delItem(item)
            reg.close();                	
        }
    )
    menu.popup(x,y,true);
}

winform.editPath.onOk = function(ctrl,alt,shift){ 
	var path = owner.text;
	winform.treeview.findPath(path) 
	return true;  	
}
winform.listview.translateAccelerator = domisoft.ctrlC;
grid.onSortColumn = function(column, desc){
	var fields = {"key","value"}
	var tab = grid.getTable(fields);
	var name = fields[column];
	for (i=1; #tab)
		if !tab[i][name]
			tab[i][name]=""
	table.sort(tab,desc ? (lambda(next) owner[name] > next[name]) : (lambda(next) owner[name] < next[name]));	
	owner.setTable( tab );
	return true; //返回 true 允许当前列排序	
};

winform.show();
win.loopMessage();