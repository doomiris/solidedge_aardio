import win.ui;
/*DSG{{*/
var winform = win.form(text="自动关闭代理(不要关闭窗口)";right=501;bottom=235)
winform.add(
checkbox={cls="checkbox";text="关闭系统代理";left=163;top=7;right=284;bottom=22;checked=1;dl=1;dt=1;z=4};
checkbox2={cls="checkbox";text="SAP保持在线(需要退回首页)";left=309;top=7;right=486;bottom=25;checked=1;dl=1;dt=1;z=5};
edit={cls="edit";left=2;top=32;right=499;bottom=222;db=1;dl=1;dr=1;dt=1;edge=1;multiline=1;z=1};
edit2={cls="edit";text="5";left=10;top=7;right=47;bottom=28;dl=1;dt=1;edge=1;num=1;z=2};
static={cls="static";text="分钟 (间隔时间)";left=55;top=9;right=149;bottom=26;dl=1;dt=1;transparent=1;z=3}
)
/*}}*/


import config;
winform.bindConfig( config.intervalJob, {	//绑定控件值
    edit = "text";
    checkbox = "checked"
} );
winform.edit.text = "";

var reset = function(wform){
	import inet;
	var t = inet.getSystemProxy();
	if t.autoConfigUrl
		thread.invoke( 
			function(wform){
				import inet.conn;
				inet.conn.setProxyAutoConfig()
				inet.conn.setProxy();
				inet.reloadSystemProxy();
				wform.edit.log(tostring(time().local()), '\r\n' )
			},wform
		)
	wform.edit.log(".")
}
var refreshSAP = function(wform){
	thread.invoke( 
		function(wform){
    		import SAP.vm;
    		var session = SAP.vm();
    		if !session	return ; 
    		if !session.busy{
				if session.isHomepage {
					session.refresh();
					wform.edit.log(",");
				}
			}
			session = null;
			collectgarbage("collect");
		}, wform
	)
}
var interval = winform.edit2.text;

//立即执行一次, 然后按以下时间重复
var s1, s2

winform.checkbox.oncommand = function(id,event){
	if !owner.checked
		winform.clearInterval(s1);
	else
		s1 = winform.setInterval(reset, interval * 60 * 1000, winform); 
}
winform.checkbox2.oncommand = function(id,event){
	if !owner.checked
		winform.clearInterval(s2);
	else
		s2 = winform.setInterval(refreshSAP, interval * 60 * 1000, winform); 
}



if winform.checkbox.checked
	s1 = winform.setInterval(reset, interval * 60 * 1000, winform); 
if winform.checkbox2.checked
	s2 = winform.setInterval(refreshSAP, interval * 60 * 1000, winform); 

winform.show();
global.onError = function( err,over ){ 
    winform.edit.print(err);
}
reset (winform); 
refreshSAP (winform);

win.loopMessage();