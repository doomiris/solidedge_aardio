import fonts.fontAwesome;
import win.ui;
/*DSG{{*/
var winform = win.form(text="插入变更记录";right=663;bottom=303;image=$"\res\SE BG.jpg";bkBottom=0;bkLeft=0;bkRight=0;bkTop=0)
winform.add(
button={cls="button";text="OK";left=584;top=256;right=656;bottom=296;db=1;dr=1;z=11};
button2={cls="button";text="Cancel";left=504;top=256;right=576;bottom=296;db=1;dr=1;z=12};
button3={cls="button";text='\uF062';left=416;top=59;right=440;bottom=82;bgcolor=16777215;dl=1;dt=1;flat=1;font=LOGFONT(name='FontAwesome');z=14};
button4={cls="button";text='\uF06E';left=448;top=59;right=472;bottom=82;bgcolor=16777215;dl=1;dt=1;flat=1;font=LOGFONT(name='FontAwesome');z=18};
cbAddBalloon={cls="checkbox";text="同时添加此球标";left=160;top=67;right=304;bottom=83;bgcolor=16777215;checked=1;dl=1;dt=1;z=16};
cb_approve_date={cls="edit";left=216;top=192;right=336;bottom=216;dl=1;dt=1;multiline=1;transparent=1;z=10};
cb_approver={cls="combobox";left=88;top=192;right=208;bottom=216;dl=1;dt=1;edge=1;items={};mode="dropdown";z=9};
cb_design_date={cls="edit";left=216;top=160;right=336;bottom=184;dl=1;dt=1;multiline=1;transparent=1;z=7};
cb_designer={cls="combobox";left=88;top=160;right=208;bottom=184;dl=1;dt=1;edge=1;items={};mode="dropdown";z=6};
cb_desp={cls="combobox";left=88;top=107;right=656;bottom=131;aw=1;dl=1;dr=1;dt=1;edge=1;items={};mode="dropdown";multiline=1;tabstop=1;z=4};
cb_mark={cls="combobox";left=88;top=67;right=136;bottom=91;dl=1;dt=1;edge=1;items={"a";"b";"c";"d";"e";"f";"g";"h";"i";"j";"k";"l";"m";"n";"o";"p";"q";"r";"s";"t";"u";"v";"w";"x";"y";"z"};mode="dropdown";z=2};
checkbox={cls="checkbox";text="同时把所有数字球标改为版本标记";left=40;top=248;right=280;bottom=264;db=1;dl=1;z=20};
checkbox2={cls="checkbox";text="更新所有Sheets版本号";left=40;top=224;right=280;bottom=240;checked=1;db=1;dl=1;z=21};
chkCCRV={cls="checkbox";text="同时递增CCR版本";left=40;top=272;right=184;bottom=288;checked=1;db=1;dl=1;z=17};
groupbox={cls="groupbox";text='\uF016  当前文件';left=15;top=3;right=654;bottom=41;aw=1;dl=1;dr=1;dt=1;edge=1;font=LOGFONT(name='FontAwesome');z=22};
seDocPath={cls="static";text="点击此处刷新";left=47;top=15;right=642;bottom=33;align="right";aw=1;dl=1;dr=1;dt=1;notify=1;transparent=1;z=23};
static={cls="static";text="标记";left=32;top=67;right=64;bottom=91;align="right";dl=1;dt=1;transparent=1;z=1};
static2={cls="static";text="描述";left=32;top=107;right=64;bottom=131;align="right";dl=1;dt=1;transparent=1;z=3};
static3={cls="static";text="设计";left=32;top=160;right=64;bottom=184;align="right";dl=1;dt=1;transparent=1;z=5};
static4={cls="static";text="唤起快捷键 Alt+R";left=392;top=272;right=496;bottom=288;align="right";color=32768;db=1;dr=1;transparent=1;z=19};
static5={cls="static";text="版本";left=360;top=67;right=390;bottom=86;dl=1;dt=1;transparent=1;z=15};
static7={cls="static";text="批准";left=32;top=192;right=64;bottom=216;align="right";dl=1;dt=1;transparent=1;z=8};
static8={cls="static";text='\uF0E2';left=23;top=18;right=39;bottom=34;dl=1;dt=1;font=LOGFONT(name='FontAwesome');notify=1;transparent=1;z=24};
version={cls="edit";left=389;top=58;right=413;bottom=84;align="center";dl=1;dt=1;font=LOGFONT(h=-13);multiline=1;z=13}
)
/*}}*/

import solidedge.application;
import solidedge.DraftDocument;
import config;
import key;
import tarray;
var seApp, seDFT, seSht, seLbs;
var initDeclare = function(){
	seApp = solidedge.application();
	if (! seApp) return false; 
	if (seApp.Documents.Count==0) return false; 
	if (seApp.ActiveDocumentType !== 2 /*Draft Document Type*/) return false; 
	seDFT = solidedge.DraftDocument(seApp.ActiveDocument);
	if (seDFT.Sheets.Count == 0) return ; 
	seSht = seDFT.ActiveSheet;
	seLbs = seDFT.getLabelTable(seDFT.getTitleBlock(seSht));	//获取titleblock lable
	if seLbs return true; 
	else return false; 
}
/*loadDefaults{{*/

var savesetting = function(){
	var t = config.revmgr.desp;
	var txt = winform.cb_desp.text;
	table.unshift(t, txt);
	t = table.unique(t);
	if #t > 20 table.pop(t);
	winform.cb_desp.items = t;
	winform.cb_desp.text = txt;
	config.revmgr.desp = t;
	config.revmgr.save();
}

var loadDefaults = function(){
	winform.cb_designer.text = config.tbm.Designer;
	winform.cb_approver.text = config.tbm.Approver;
	winform.cb_design_date.text = time(,"%Y.%m.%d");
	winform.cb_approve_date.text = time(,"%Y.%m.%d");
	if seDFT var mark = seDFT.getTopRecordMark(seSht);
	if mark winform.cb_mark.text = string.pack(mark[1] + 1);
}

/*}}*/
/*loadLists{{*/
var getSuggest = function(tab, cType, keyword){
	var t = table.filter(tab,function(v,index){
		if v[["ListName"]] !== cType return ; 
		return string.find(v[["Title"]], "@@"+keyword); 
	})
	return tarray.getCol(t, "Title");
}

import config;
winform.cb_desp.onEditChange = function(){
	var txt = owner.text;
	owner.autoComplete(
		table.filter(
			config.revmgr.desp,
			λ(v) string.find(v,"@@"++txt)
		)
	)
}
winform.cb_desp.onDropDown = winform.cb_desp.onEditChange;

var loadPromptList = function(){	
	import sqlite;
	var dbpath = io.joinpath(config.dbServer.path, "tbm.db"); 
	var dbRs = sqlite(dbpath);	//打开数据库连接
	winform.promoTab = dbRs.getTable("select * from SE_TBM_Lists");
	dbRs.close();
	var addlist = function(ctrl, ctrlName){
		ctrl.onEditChange = function(){
			owner.autoComplete(getSuggest(winform.promoTab, ctrlName, #owner.items ? owner.text : ""));
		}
		ctrl.onDropDown = ctrl.onEditChange;
	}
	addlist(winform.cb_designer, "designer");
	addlist(winform.cb_approver, "approver");

}
/*}}*/
/*其它基础函数{{*/
var writeToRec = function(Lbs){
    Lbs["标记"].Value = winform.cb_mark.text;
    Lbs["变更描述"].Value = winform.cb_desp.text;
    Lbs["设计"].Value = winform.cb_designer.text;
    Lbs["批准"].Value = winform.cb_approver.text ;
    Lbs["设计日期"].Value = winform.cb_design_date.text;
    Lbs["审核日期"].Value = winform.cb_approve_date.text; //日期重名问题,此处使用审核日期
}
var writeToTB = function(Lbs){
	Lbs["版本"].Value = winform.version.text;
}
var writeToCCRV = function(Lbs){
    var v = tonumber(Lbs["版本"].value);
	v += 1;
	Lbs["版本"].value =  (v >= 10) ? v : "0" ++ v;
}
var addBalloon = function(txt,txtColor,bShape){
    bShape := 1 /* DimBalloonTypeConstants.igDimBalloonCircle */
    txtColor := 255	/* ColorConstants.seColorRed */
	var BBS = seSht.Balloons;
	var bb = BBS.Add(0.25, 0.25, 0);
	bb.BalloonText = txt;
	bb.BalloonType = bShape;
	bb.BalloonSize = 2; /* 形状是字体几倍数 */
	bb.Leader = false; 
	bb.Style.DrivenColor = txtColor;
	return bb; 
}
/*}}*/
/*loadFromTab{{*/
var loadFromTab = function(_blk){
	winform.version.text = _blk["版本"];
}


/*}}*/

/*界面控件事件{{*/
winform.button.oncommand = function(id,event){
	if mainForm mainForm.show(false); //隐藏窗口
	seApp := solidedge.application();
	win.setActive(seApp.hWnd);
	win.setForeground(seApp.hWnd)
	
	initDeclare();
	if #winform.cb_desp.text savesetting()
	//写入记录表
	writeToRec(seDFT.getLabelObjectTable(seDFT.insertRecordTabLine(seSht))); 
	//写入CCR版本
	if (winform.chkCCRV.checked)
		if seDFT.getBlkByName("Version_CCR 2020",seSht)
			writeToCCRV(seDFT.getLabelObjectTable(seDFT.getBlkByName("Version_CCR 2020",seSht)));
	//写入QHC版本
	if (winform.version.changed) 
		writeToTB(seDFT.getLabelObjectTable(seDFT.getTitleBlock(seSht)));
	//写入球标
	if ( winform.cbAddBalloon.checked ){
		var b = addBalloon (winform.cb_mark.text);
		b.Cut();
		win.delay(0.1*1000);
		key.combine("CTRL","V"); 
	}

}
winform.button2.oncommand = function(id,event){
    if mainForm mainForm.show(false); //隐藏窗口
	win.setActive(seApp.hWnd);
}
winform.button3.oncommand = function(id,event){
    winform.version.changed = true
    var v = 0;
    if #winform.version.text v = tonumber(winform.version.text);
	v += 1;
	winform.version.text =  (v >= 10) ? v : "0" ++ v;
}
/*}}*/

winform.show();
winform.frmReLoad = function(){
    var initSucc = initDeclare()
	if !initSucc return ; 
	loadFromTab(seLbs);	//读取当前内容
	winform.cb_desp.setFocus();
	loadDefaults();	//读取默认值(新图纸)
	winform.seDocPath.text = seDFT.FullName ? seDFT.FullName : "无法获取DFT信息, 点击此处刷新重试";
}

winform.button4.oncommand = function(id,event){
    var pcode = seDFT ? string.match(seDFT.FullName,"\d+") : null;
	loadcodex("\forms\main\check_QHC_Release.aardio", pcode);
}

winform.seDocPath.oncommand = winform.frmReLoad;
winform.static8.oncommand = winform.frmReLoad;

loadPromptList();
win.loopMessage();
return winform;