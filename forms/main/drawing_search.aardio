import win.ui;
/*DSG{{*/
var winform = win.form(text="aardio form";right=639;bottom=359)
winform.add(
edit2={cls="edit";left=56;top=12;right=176;bottom=36;aw=1;dl=1;dt=1;font=LOGFONT(h=-13);hide=1;readonly=1;z=2};
listview2={cls="listview";left=0;top=40;right=640;bottom=360;ah=1;aw=1;db=1;dl=1;dr=1;dt=1;edge=1;fullRow=1;gridLines=1;z=1}
)
/*}}*/

import config;
import domisoft;
import process;
import win.clip;
import win.clip.file;
import sqlite;
import win.ui.accelerator;
import godking.rightMenu;
/*
import console;
console.open()
*/




var fileLibMap = function(fullpath){
	if !fullpath return ; 
	import config;
	var maps = {
		"{config.__pdfLib}" = config.__pdfLib,
		"{config.__qhcLib}" = config.__qhcLib,
		"{config.__cabinetModelServer}" = config.__cabinetModelServer
	}
	fullpath = string.replace(fullpath,'%{}', maps)
	return fullpath; 
}
/*查找图纸{{*/
var searchDocument = function (filenumber){
    //filenumber :=  winform.editUser0.text;
/*sqliteDB{{*/
	import sqlite;
	import config;
	var flistDB = sqlite(io.joinpath(config.dbServer.path,"drawing_lists.db"),,1/*_SQLITE_OPEN_READONLY*/);	//打开数据库连接
/*}}*/
	winform.edit2.text = "";

    winform.listview2.clear();
    
    if !filenumber return ; 
    
    filenumber = string.crlf(filenumber,'\r\n' /* 默认值 */);
    
    //分隔符号转换行
	for k, v in {`、`, `，`, `;`, `；`, `,`, `|`,'\t'} 
		filenumber = string.replace(filenumber, v,'\r\n');    

    var userinputArr = string.split(filenumber, '\r\n');

	filenumber = table.shift(userinputArr);
  
    //winform.listview2.insertColumn("正在查找图纸, 请稍等...",300,1);
	var searchFor = string.trimleft(filenumber,"H");

	var fileTab = table.create(,{fields={[2]="文件名";[3]="路径";[1]="类型";[4]="版本"}});

	var sql = "SELECT `文件名`, `路径` FROM `Lib` WHERE ??";
	sql = sqlite.format(sql, {[1]={文件名 = "%" + searchFor + "%"}});
	sql = string.replace(sql, "=", "LIKE");
	
	var flist = flistDB.getTable(sql);
	if (#flist == 0){
		var maptab = flistDB.getTable("SELECT * FROM `Map`", { 专用号  =  searchFor });
		if (#maptab > 0) {
			if #maptab[1].图号 winform.edit2.text = maptab[1].图号;
			winform.edit2.hide = !(#maptab[1].图号);
			sql = string.replace(sql, searchFor, maptab[1].图号);
			flist = flistDB.getTable(sql);
		}
	}
	if (#flist > 0) fileTab = table.concat(fileTab, flist);	
	
	for (i=1; #fileTab){
		var item = fileTab[i];
		var ext = string.right(item["文件名"], 3);
		item["类型"] = string.upper(ext);
		fileTab[i] = item;
	}
	if !table.indexOf(fileTab.fields,"类型")
		table.unshift(fileTab.fields, "类型");
	winform.listview2.header = fileTab.fields;
	
	for(i=1; #fileTab)
		fileTab[i]["路径"] = fileLibMap(fileTab[i]["路径"]);
	
	
	winform.listview2.clear();
	winform.listview2.setTable(fileTab);

//调整列宽	
	winform.listview2.coulumnWidth := domisoft.setColumnWidth(winform.listview2, {50,200,300,100})
/**
	winform.listview2.setColumn({cx=50;fmt=0x2/*_LVCFMT_CENTER*/}, 1);
	winform.listview2.setColumn({cx=200;fmt=0x0/*_LVCFMT_LEFT*/},   2);
	winform.listview2.setColumn({cx=300;fmt=0x0/*_LVCFMT_LEFT*/}, 3);
	//winform.listview2.setColumn({cx=100 ;fmt=0x2/*_LVCFMT_CENTER*/}, 4);
**/

    winform.listview2.fillParent(3);
	flistDB.close();
}
/*}}*/
var refreshFiles = function(){
	var t = winform.listview2.items;
	var header = winform.listview2.header;
	for (i=1; #t){
		//console.log(io.joinpath(t[i][2/*路径*/],t[i][1/*文件名*/]))
		if !io.exist(io.joinpath(t[i][table.indexOf(header,"路径")],t[i][table.indexOf(header,"文件名")]))
			t[i] = null;
	}
	var nt = {}
	for (i=1; #t)
		if t[i]
			table.push(nt, t[i])
	winform.listview2.items = nt;
}



var localMapping = function(fullpath){
	import string;
	fullpath = string.lower(fullpath);
	
/*
	if string.getenv("OneDriveCommercial"){
		var OneDriveCommercial = string.replace(string.getenv("OneDriveCommercial"), "OneDrive - ","");		
		fullpath = string.replace(fullpath,"@\\ccnsia1a\separts\cabinet", io.joinpath(OneDriveCommercial,"CCR Detail Design - S - S")) 
	}else{
		import config;
		fullpath = string.replace(fullpath,"@\\ccnsia1a\separts\cabinet", config.__cabinetModelServer);		
	}
*/
	
	

	return fullpath; 
}


winform.listview2.onnotify = function(id,code,ptr){//code很多 select外放代码很容易overflow
    select(code) {
    	case 0xFFFFFFFD/*_NM_DBLCLK*/ {
    	    var header = owner.header;
    		var selectedRow = owner.getSelection();
    		if !selectedRow return;
    		var pcode = owner.getItemText(selectedRow, table.indexOf(header,"文件名"));
			var fullName = io.joinpath(owner.getItemText(selectedRow, table.indexOf(header,"路径")),pcode);
			fullName = localMapping(fullName);
			if io.exist(fullName)
				process.execute(fullName);
    	}
    	case 0xFFFFFFFB/*_NM_RCLICK*/ {
    		var selectedRow = owner.getSelection();
    		if !selectedRow return;
    		var header = owner.header;
    		var pcode = owner.getItemText(selectedRow, table.indexOf(header,"文件名"));
			var fullName = io.joinpath(owner.getItemText(selectedRow, table.indexOf(header,"路径")), pcode);
			fullName = localMapping(fullName)
			var x,y = win.getMessagePos();
			var menu = win.ui.popmenu(winform);
			menu.add("打开文件...", λ() process.execute(fullName));
			menu.add("删除文件...", λ() io.remove(fullName));
			menu.add("刷新", λ() refreshFiles());
			;
			menu.add("更多...", λ() godking.rightMenu(
				winform/*父窗口*/,
				fullName/*文件路径*/,
				x/*菜单x*/,y/*菜单y*/,
				false/*是否相对坐标*/,
				false/*是否显示文件名*/)
			);
			menu.popup(x,y,true);

    	}
    }
}


winform.listview2.translateAccelerator = function( msg ){ 
    var ctrl = ::GetKeyState(0x11/*_VK_CTRL*/);
    var vk = msg.wParam ;
    var header = owner.header;
    if( ( vk == 'C'# ) && ctrl  ){
		var tab = table.array();
		for (i=1; owner.count){
			if (table.find(owner.selected,i)){
				var fullName = owner.getItemText(i, table.indexOf(header,"路径")) ++ owner.getItemText(i, table.indexOf(header,"文件名"));
				fullName = localMapping(fullName)
				if io.exist(fullName)
					table.push(tab, fullName);
			}
		}     
		if #tab
			win.clip.file.write(tab,"copy");
        return true;
    }
}

winform.qtip_search = function(pcode){
	if !pcode return ; 
	searchDocument(pcode);
};

winform.show();

win.loopMessage();
return winform;
