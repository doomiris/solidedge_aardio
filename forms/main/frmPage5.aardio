import fonts.fontAwesome;
import win.ui;
/*DSG{{*/
var winform = win.form(text="Printing Center";right=759;bottom=599;image=$"\res\SE BG.jpg";bkBottom=0;bkLeft=0;bkRight=0;bkTop=0)
winform.add(
AllowSentToPdfLib={cls="checkbox";text="PDF自动保存到:";left=169;top=85;right=279;bottom=101;bgcolor=0xFFFFFF;checked=1;dl=1;dt=1;hide=1;z=10};
button={cls="button";text='    \uF0F6  Print Active';left=496;top=40;right=611;bottom=85;align="left";clipch=1;dr=1;dt=1;font=LOGFONT(h=-16;name='FontAwesome');hide=1;tabstop=1;z=12};
button2={cls="button";text='    \uF233  Print All';left=624;top=96;right=739;bottom=141;align="left";dr=1;dt=1;font=LOGFONT(h=-16;name='FontAwesome');tabstop=1;z=3};
button3={cls="button";text='\uF1DA';left=624;top=32;right=736;bottom=88;color=0x808000;dr=1;dt=1;font=LOGFONT(h=-48;name='FontAwesome');z=11};
button4={cls="button";text='\uF07C';left=8;top=555;right=28;bottom=575;db=1;dl=1;flat=1;font=LOGFONT(name='FontAwesome');z=9};
groupbox={cls="groupbox";text='\uF097  Actions';left=5;top=5;right=752;bottom=152;aw=1;dl=1;dr=1;dt=1;edge=1;font=LOGFONT(h=-16;name='FontAwesome');z=1};
listbox={cls="listbox";left=24;top=32;right=157;bottom=146;border=1;dl=1;dt=1;font=LOGFONT(h=-16;name='微软雅黑');items={".pdf",".dwg",".dxf",".stp",".png"};msel=1;tabstop=1;z=2};
outpath={cls="syslink";text="d:\workspaces";left=32;top=560;right=750;bottom=573;aw=1;center=1;db=1;dl=1;dr=1;notify=1;transparent=1;z=8};
pdfLibPath={cls="syslink";text="\\ccnsia1a\separts\Cabinet\PDF图纸库";left=185;top=107;right=403;bottom=123;dl=1;dt=1;hide=1;transparent=1;z=13};
progress={cls="progress";left=0;top=576;right=752;bottom=600;aw=1;color=0x008000;db=1;dl=1;dr=1;max=100;min=0;z=6};
radNameRule0={cls="radiobutton";text="仅图号";left=168;top=56;right=228;bottom=76;bgcolor=0xFFFFFF;dl=1;dt=1;group=1;z=4};
radNameRule2={cls="radiobutton";text="图号-版本-型号-名称";left=234;top=56;right=378;bottom=76;bgcolor=0xFFFFFF;dl=1;dt=1;z=5};
tab={cls="tab";left=8;top=160;right=752;bottom=552;ah=1;aw=1;db=1;dl=1;dr=1;dt=1;edge=1;z=7}
)
/*}}*/

import win.dlg.message;

import process;
import config;
import fsys.dlg.dir;
/*
import console
console.open();
*/

winform.bindConfig( config.printingCenter, {	//绑定控件值
    radiobutton = "checked";
    listbox = "selected";
    checkbox = "checked"
} );

//winform.tab.add()
var frms = {
	"\forms\printcenter\dft-list.aardio",
	"\forms\printcenter\psm2dxf.aardio",
	"\forms\printcenter\printpaper.aardio",
	"\forms\printcenter\bmp2png.aardio",
	"\forms\printcenter\pdf_combine.aardio",	
}
//table.reverse(frms)
var tbs = {};
for (i=1; #frms)	
		tbs[i] = winform.tab.loadForm(frms[i])

if config.setbm.workingDir
	winform.outpath.text = string.replace(`<a href="URL">URL</a>`,"@URL", config.setbm.workingDir);
if  config.__pdfLib
	winform.pdfLibPath.text = string.replace(`<a href="URL">URL</a>`,"@URL", config.__pdfLib);

import solidedge.application;
import solidedge.DraftDocument;


/*initDeclare{{*/
var seApp, seDFT, seSht;
var seDfts;
var initDeclare = function(){
	seApp = solidedge.application();
	if (! seApp) return false; 
	seApp.setPdfOptions();
	// TODO: 含多页文档保存为DWG时, 因为未指定多页, 可能导致只有一页.
	//seApp.Documents.AutoCadConfigFile = config.setbm.workingDir + "\SEACAD.INI";
	seApp.DisplayAlerts = 1;
	return true; 
}
/*}}*/
/*printAllFiles{{*/
var getListViewTable = function(listview){
	var tab = listview.getTable(true);
	var fields = table.shift(tab);
	return listview.getTable(fields); 
}
var startPrintAllLV = function(vtab){
	seApp = solidedge.application();
	if !seApp return ; 
	seApp.DisplayAlerts = 0;

	seDfts = tbs[1].seDfts;
	for x,j in tbs[1].listview.checked {
        	var	seDFT = solidedge.DraftDocument(seApp.Documents.item(seDfts.seIndex[j]));  //线程传出来的seDfts会丢失item内容
			seDFT.Activate(); //如果不Activate,只会重复保存当前文件
			//TODO: domisoft.getSelectedText(winform.listbox)
			for y,k in winform.listbox.selected{
				winform.progress.pos += 1 ;
/*
				var tip = "TBM正在打印";
				tip = tip ++ vtab[j].物料;
				tip = tip ++ winform.listbox.getItemText(k);
				tip = tip ++ "...";	
*/			
				//tray.pop(tip, x ++ "/" ++ #tbs[1].listview.checked);
				if !vtab[j] continue;
				var ext = winform.listbox.getItemText(k);
				var fname = seDFT.getNameByExt(ext, vtab[j], config.setbm.workingDir);
				var succ = seDFT.saveToExt(fname);
				
				if mainForm
					mainForm.taskbar.SetProgressValue(mainForm.hwnd, winform.progress.pos, winform.progress.max)	//win taskbar
/*上传云库*/

				var fileinfo = io.splitpath(fname);

        		if ext ==".pdf"
        			and winform.radNameRule2.checked
        			 and winform.AllowSentToPdfLib.checked {
        			 
        				..thread.invoke( 
        					function(fname){
        						import io;
        						import fsys;
        						import config;
        						import sqlite;

        						if !io.exist(config.__pdfLib) return ; 
       							var destFile = io.joinpath(config.__pdfLib, fname);       							
        						fsys.copy(fname, config.__pdfLib, 0x614/*_FOF_NO_UI*/ + 0x10/*_FOF_NOCONFIRMATION*/);
        						var db = sqlite(io.joinpath(config.dbServer.path, "drawing_lists.db"));
        						db.exec("INSERT INTO `Lib`(文件名,路径) VALUES @values",{ values = { {io.splitpath(fname).file, "{config.pdfLib}"} } }); 
        						db.close();
        					}, fname
        				);
        		}
			}
	}
	seApp.DisplayAlerts = 1;
	winform.msgInfo("完成. All Print job done.", 2000);
}
/*}}*/
/*控件事件{{*/
//打印所有DFT
winform.button2.oncommand = function(id,event){
    if !seApp return ; 
  	
	var vtab = getListViewTable(tbs[1].listview);
	winform.progress.min = 0;
	winform.progress.max = #tbs[1].listview.checked * #winform.listbox.selected;
	winform.progress.pos = 1;
	startPrintAllLV(vtab);
}

winform.outpath.onHyperlinkClick = function(nmSysLink,url,id){
	process.explore(url);
}
winform.pdfLibPath.onHyperlinkClick = function(nmSysLink,url,id){
	process.explore(url);
}
winform.button4.oncommand = function(id,event){
	var path = fsys.dlg.dir(,winform,"指定TBM工作目录"); //反应慢?
	if !path return ; 
	config.setbm.workingDir = path;
	config.setbm.save();
	winform.outpath.text = string.replace(`<a href="URL">URL</a>`,"@URL",path);
}

winform.button3.oncommand = function(id,event){
    winform.progress.pos = 0;
	tbs[1].refresh_dft_list();
}
/*}}*/

winform.frmReLoad = tbs[1].refresh_dft_list;



winform.show();

initDeclare();

winform.radNameRule0.oncommand = function(id,event){
	if event !== 0/*_BN_CLICKED*/ return ; 
	winform.AllowSentToPdfLib.hide = owner.checked;
	winform.pdfLibPath.hide = owner.checked;
	config.printingCenter.save();
	//console.log(seApp.useQhcNameRule)
}

winform.radNameRule2.oncommand = function(id,event){
	if event !== 0/*_BN_CLICKED*/ return ; 
	winform.AllowSentToPdfLib.hide = !owner.checked;
	winform.pdfLibPath.hide = !owner.checked;
	config.printingCenter.save();
	//console.log(seApp.useQhcNameRule);
}


winform.AllowSentToPdfLib.hide = !winform.radNameRule2.checked;
winform.pdfLibPath.hide = !winform.radNameRule2.checked;
win.loopMessage();
return winform;