import win.ui;
/*DSG{{*/
var winform = win.form(text="aardio form";right=759;bottom=469;image=$"\res\SE BG.jpg";bkBottom=0;bkLeft=0;bkRight=0;bkTop=0)
winform.add(
custom={cls="custom";text="自定义控件";left=-3;top=1;right=758;bottom=73;dl=1;dr=1;dt=1;z=1};
custom2={cls="custom";text="自定义控件";left=0;top=94;right=455;bottom=470;clip=1;db=1;dl=1;dt=1;z=2};
static={cls="static";text="点击新建常用表格...";left=2;top=77;right=441;bottom=98;dl=1;dt=1;transparent=1;z=3}
)
/*}}*/

/*
import console;
console.open();
*/

import win.ui.menu;
import win.ui.toolbar;

import win.imageList;
var imgList = win.imageList.shell( 0/*_SHIL_LARGE*/ )


/*tbutton2{{*/
var tbutton2 = function(id,event){
	import solidedge.application;
	
	var seApp = solidedge.application();
    if seApp.Documents.Count==0 return ; 
    if seApp.ActiveDocumentType !== 2 /*Draft Document Type*/ return ; 
    var SelSet = seApp.ActiveDocument.SelectSet;
    if SelSet.Count !== 1 return; 
    if (SelSet(1).Type !== 488188096 /* igDimension */) return;
    
    import domisoft;
    var s = domisoft.showInputBox("Edit Dim", "input text to replace dimension:","L");
    if !s return; 
    if ..string.len(..string.trim(s)) == 0 return ; 
    SelSet(1).OverrideString = s;
    SelSet(1).Style.NTSSymbol = 1 /* igDimStyleNTSNone */;
    
    //mainForm.show(false); //隐藏窗口
	if seApp win.setActive(seApp.hWnd);
}
/*}}*/
/*tbutton3{{*/
var tbutton3 = function(id,event){
	import solidedge.application;
	var seApp = solidedge.application();
	import solidedge.DraftDocument;
    if seApp.Documents.Count==0 return ; 
    if seApp.ActiveDocumentType !== 2 /*Draft Document Type*/ return ; 
	var seDFT = solidedge.DraftDocument(seApp.ActiveDocument);
	var seSht = seDFT.ActiveSheet;
	var BBS = seSht.Balloons;
	var ee = seDFT.getTopRecordMark(seSht);
	ee = ee ? string.pack( ee[1] + 1 ) : "a";
	var e = domisoft.showInputBox("Edit Balloon", "What symbol change bolloons to ?",ee);
	if !e return ; 
	if (string.len(string.trim(e)) == 0) return ; 
	var setBB = function(balloonItem, txt){
    	with balloonItem {
    		BalloonText = txt;
    		BalloonType = 1; /* DimBalloonTypeConstants.igDimBalloonCircle */
    		BalloonSize = 2; /* 形状是字体几倍数 */
    		Leader = false; 
    		Style.DrivenColor = 255;	/* ColorConstants.seColorRed */;    				
    	}
	}	
	for (i = 1; BBS.Count) {
		if BBS.Item(i).Leader continue; //跳过有引线的
		select(BBS.Item(i).BalloonType) {
			case 1 /* igDimBalloonCircle */ {
    			setBB( BBS.Item(i), e);
			}
			case 7 /* igDimBalloonTriangle */ {
				if table.indexOf({"F","S","X"},BBS.Item(i).BalloonText) continue;
				setBB( BBS.Item(i), e);
			}
		}
	}
    //mainForm.show(false); //隐藏窗口
	if seApp win.setActive(seApp.hWnd);
}
/*}}*/
/*tbutton4{{*/
var tbutton4 = function(id,event){
	import solidedge.application;
	var seApp = solidedge.application();
	import solidedge.DraftDocument;
	if seApp.Documents.Count==0 return ; 
    if seApp.ActiveDocumentType !== 2 /*Draft Document Type*/ return ; 
	var seDFT = solidedge.DraftDocument(seApp.ActiveDocument);
	var seSht = seDFT.ActiveSheet;
	var BBS = seSht.Balloons;
	var ee = "a";
	for (i = 1 ; BBS.Count) {	
		if BBS.Item(i).BalloonType == 1 /* igDimBalloonCircle */
    		if !BBS.Item(i).Leader
        		ee = BBS.Item(i).BalloonText;
        		break;
	}
	var e = domisoft.showInputBox("delete Balloon", "What symbol bolloons you want to delete ?",ee);
	if !e return ; 
	if (..string.len(..string.trim(e)) == 0) return ; 
	
	for (i = BBS.Count; 1 ;- 1 ) {	
		if BBS.Item(i).BalloonType == 1 /* igDimBalloonCircle */
    		if !BBS.Item(i).Leader
        		if (BBS.Item(i).BalloonText==e)
        			BBS.Item(i).Delete();
	}
    //mainForm.show(false); //隐藏窗口
	if seApp win.setActive(seApp.hWnd);
}
/*}}*/
/*tbutton5{{*/
var tbutton5 = function(id,event){
	import solidedge.application;
	var seApp = solidedge.application();
	import solidedge.AssemblyDocument;
	if (seApp.ActiveDocumentType != 3 /* igAssemblyDocument */) return;
	var seAsm = solidedge.AssemblyDocument(seApp.ActiveDocument); 
	select(seAsm.SelectSet.Count) {
		case 1 {
			if !seAsm.IsFileFamilyByDocument
				seAsm.openDummy(seAsm.SelectSet.Item(1));
			else {
				var ofn = table.shift(string.split(seAsm.FullName,"!"))
				seAsm.Close(false);
				seApp.Open(ofn, false);			
			}
		}
		case 0 {
			if seAsm.IsFileFamilyByDocument {
				var ofn = table.shift(string.split(seAsm.FullName,"!"))
				seAsm.Close(false);
				seApp.Open(ofn, false);				
			}	
		}
		else {
			return ; 
		}
	}
    //mainForm.show(false); //隐藏窗口
	if seApp win.setActive(seApp.hWnd);
}
/*}}*/
/*tbutton6{{*/
var tbutton6 = function(id,event){
	import solidedge.application;
	var seApp = solidedge.application();
	seApp.closeDft(false);
    //mainForm.show(false); //隐藏窗口
	if seApp win.setActive(seApp.hWnd);
}
/*}}*/
/*tbutton7{{*/
var tbutton7 = function(id,event){
	import solidedge.application;
	var seApp = solidedge.application();
	seApp.closeDft(true);
   // mainForm.show(false); //隐藏窗口
	if seApp win.setActive(seApp.hWnd);
}
/*}}*/
/*菜单{{*/
import win.ui;
import win.ui.menu;
import QHC;
import fsys;

var tlist = fsys.list(config.__templateLib,,"*.xlsx");

var opent = function(n){
	import com.excel;
	var excel = com.GetOrCreateObject("Excel.Application");
	if !excel win.msgbox("请先打开Excel程序");
	if !excel return ; 
	if !excel.WorkBooks return ; 

	var wb = excel.WorkBooks.Add(io.joinpath(config.__templateLib, tlist[n]));

	
	excel.Visible = true;
	win.showForeground(excel.Application.hwnd);
	//mainForm.show(false);
}
winform.menuTable={};
winform.menuTable[1] = win.ui.popmenu(winform);//创建弹出菜单
winform.menuTable[2] = win.ui.popmenu(winform);//创建弹出菜单
for(n=1;#tlist)
	winform.menuTable[1].add( tlist[n] , λ() opent(n) )

winform.menuTable[2].addTable({
	{"格式化BOM"; λ() QHC.formatBOM()};
	{"格式化成本BOM"; λ() QHC.formatCostBom()}
})


winform.popmenu = function(index){
	win.setForeground(winform.hwnd)	//弹出托盘菜单以前,一定要前置主窗口中,不然不点击菜单不会消失
	var pt = ::POINT();
	::User32.GetCursorPos(pt);   		
	winform.menuTable[index].popup(pt.x,pt.y,true)
}


/*}}*/
/*tbutton8{{*/
var tbutton8 = function(wform){
	import console;
	wform.popmenu(1);
}
var tbutton82 = function(wform){
	import console;
	wform.popmenu(2);
}
/*}}*/
/*tbutton9{{*/
var tbutton9 = function(id,event){
	import console;
	import solidedge.application;
	var seApp = solidedge.application();
	import solidedge.AssemblyDocument;
	if (seApp.ActiveDocumentType != 3 /* igAssemblyDocument */) return;
	var seAsm = solidedge.AssemblyDocument(seApp.ActiveDocument); 
	if (seAsm.SelectSet.Count != 1) return;
	seAsm.replaceWithPartCopy(seAsm.SelectSet.Item(1));
    //mainForm.show(false); //隐藏窗口
	if seApp win.setActive(seApp.hWnd);
}
/*}}*/

/*toolsDefineTable{{*/
var tools = {
	{
		Name = "Edit Dim";
		img = 13;
		proc = tbutton2;
	};
	{
		Name = "Edit Balloon";
		img = 23;
		proc = tbutton3;
	};
	{
		Name = "delete Balloon";
		img = 28;
		proc = tbutton4;
	};
	{
		Name = "openDummy";
		img = 46;
		proc = tbutton5;
	};
	{
		Name = "closeDft(discardChanges)";
		img = 11;
		proc = tbutton6;
	};
	{
		Name = "closeDft(saveChanges)";
		img = 7;
		proc = tbutton7;
	};
	{
		Name = "新建表格(常用模版)";
		img = 20;
		proc = tbutton8;
	};
	{
		Name = "格式化BOM";
		img = 22;
		proc = tbutton82;
	};
	{
		Name = "replaceWithPartCopy";
		img = 25;
		proc = tbutton9;
	};
}
/*}}*/

var tbar = win.ui.toolbar(winform.custom);
tbar.create( style = 0x1000/*_TBSTYLE_LIST*/ + 0x200/*_TBSTYLE_WRAPABLE*/);	
tbar.showLabel = true; //在按钮上显示文字
//tbar.imageList = win.imageList(0,0).loadIcon(null); //隐藏图标
tbar.imageList = imgList;
for (i=1; #tools)
	tbar.add(
		tools[i].Name,
		λ() thread.invoke(tools[i].proc,winform),
		tools[i].img /* img */,
		i /* id */,
		4/*_TBSTYLE_GROUP*/+ 0x10/*_TBSTYLE_AUTOSIZE*/
	)

var tform = win.ui.toolbar(winform.custom2)
tform.create( style = 0x1000/*_TBSTYLE_LIST*/ + 0x200/*_TBSTYLE_WRAPABLE*/);	
tform.showLabel = true; //在按钮上显示文字
//tform.imageList = win.imageList(0,0).loadIcon(null); //隐藏图标
tform.imageList = imgList;
for (i=1; #tlist){
	tform.add(
		tlist[i],
		λ() opent(i),
		 436/* img */,
		i /* id */,
		4/*_TBSTYLE_GROUP*/
	)

}

winform.show();
win.loopMessage();
