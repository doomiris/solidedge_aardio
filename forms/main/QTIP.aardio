import fonts.fontAwesome;
import win.ui;
import win.ui.tabs;
import win.ui.menu;
import win.ui.accelerator;
import color;
/*
import console;
console.open();
*/
/*DSG{{*/
qtip_winform = win.form(text="QHC物料查询";right=638;bottom=502;image=$"\res\SE BG.jpg";bkBottom=0;bkLeft=0;bkRight=0;bkTop=0)
qtip_winform.add(
bkplus1={cls="plus";text="过滤";left=24;top=8;right=64;bottom=40;bgcolor=12632256;dl=1;dt=1;z=10};
bkplus2={cls="plus";text="SAP";left=64;top=8;right=104;bottom=40;bgcolor=12632256;border={bottom=5;color=-16729089};dl=1;dt=1;z=6};
bkplus3={cls="plus";text="详细";left=104;top=8;right=144;bottom=40;bgcolor=12632256;dl=1;dt=1;z=8};
bkplus4={cls="plus";text="图纸";left=144;top=8;right=184;bottom=40;bgcolor=12632256;dl=1;dt=1;z=7};
bkplus5={cls="plus";text="版次";left=184;top=8;right=224;bottom=40;bgcolor=12632256;dl=1;dt=1;z=9};
button={cls="button";text='\uF002  查询SAP';left=520;top=8;right=619;bottom=38;default=1;dr=1;dt=1;font=LOGFONT(name='FontAwesome');tabstop=1;z=2};
button2={cls="button";text='\uF053';left=0;top=8;right=24;bottom=40;color=8421504;dl=1;dt=1;flat=1;font=LOGFONT(h=-13;name='FontAwesome');z=12};
buttonD={cls="button";text='\uF142';left=235;top=10;right=249;bottom=40;color=8421504;dl=1;dt=1;flat=1;font=LOGFONT(h=-13;name='FontAwesome');z=3};
custom={cls="custom";left=0;top=56;right=640;bottom=504;ah=1;aw=1;bgcolor=16777215;db=1;dl=1;dr=1;dt=1;edge=1;z=1};
edit2={cls="edit";left=56;top=12;right=176;bottom=36;aw=1;dl=1;dt=1;font=LOGFONT(h=-13);hide=1;readonly=1;z=4};
editUser3={cls="edit";left=256;top=8;right=520;bottom=40;aw=1;dl=1;dr=1;dt=1;edge=1;z=5};
splitter={cls="splitter";left=0;top=48;right=640;bottom=56;aw=1;frame=1;horz=1;z=11}
)
/*}}*/

/*拆分条{{*/
with qtip_winform.splitter {
	ltControl = ..qtip_winform.editUser3;
	rbControl = ..qtip_winform.custom;
	ltMin = 32;
	rbMin = 96;
}
qtip_winform.splitter.wndproc = function(hwnd,message,wParam,lParam){
	select(message) {
		case 0x203/*_WM_LBUTTONDBLCLK*/{
			if owner.top <= 48 return ; 
			owner.ltControl.setPos(,,owner.ltControl.width,32);
			owner.rbControl.setPos(0,56,owner.rbControl.width,owner.top+owner.rbControl.height-48);
			owner.setPos(0,48);
			owner.redraw();
		}
	}
}
/*}}*/
/*TABS{{*/
qtbs = win.ui.tabs(
	qtip_winform.bkplus1,
	qtip_winform.bkplus2,
	qtip_winform.bkplus3,
	qtip_winform.bkplus4,
	qtip_winform.bkplus5
);
qtbs.skin({
	foreground={
		active=0xFFFFFFFF;
		default=0x00FFFFFF;
		hover=0x38FFFFFF
	};
	color={
		default=0xFF000000; 
	};
	checked={
		foreground={default=0xFF00BBFF;}; 
		color={default=0xFF002E59;};
	}
})
_qfName = {
	[1] = "\forms\main\qtip_filter.aardio";
	[2] = "\forms\main\qtip_list.aardio";
	[3] = "\forms\main\sap_search.aardio";
	[4] = "\forms\main\drawing_search.aardio";
	[5] = "\forms\main\check_QHC_Release.aardio";
}
for k,v in _qfName
	qtbs.loadForm(k,v );
qtbs.preloadForm(1);
qtbs.selIndex = 2; 
qtbs.currentTab = 2;
qtbs.onSelchange = function(idx,strip,form){
	qtbs.currentTab = idx;
} 
qtbs.open = function(selIndex){
	qtbs.selIndex = selIndex;
	var txt = qtip_winform.editUser3.text;
	qtbs.forms[selIndex].publish("qtip_search", txt);
}

/*}}*/

qtip_winform.show();

qtip_winform.button.oncommand = function(id,event){
	if event !== 0/*_BN_CLICKED*/ return ;
	var txt = qtip_winform.editUser3.text;
	select(qtbs.currentTab) {
		case 1 {
			qtbs.selIndex = 2;
		}
	}
	qtbs.forms[qtbs.currentTab].publish("qtip_search", txt);
}

/*onclose{{*/
qtip_winform.onClose = function(hwnd,message,wParam,lParam){
	if (db) db.close();	
}
/*}}*/
/*弹出历史记录菜单{{*/
qtip_winform.buttonD.oncommand = function(id,event){
	var historyMenu = win.ui.popmenu(qtip_winform)
	historyMenu.add("输入历史:");
	historyMenu.add();//分隔线
	for (i=1; #config.qTipLog){
    	historyMenu.add(
    		text = config.qTipLog[i];
    		proc = function(id){
    			qtip_winform.editUser3.text = config.qTipLog[i];
    			qtip_winform.editUser3.setFocus();
    		}
    	);
	}
	var pt = ::POINT();
	::User32.GetCursorPos(pt);
	//弹出托盘菜单以前,一定要前置主窗口中,不然不点击菜单不会消失
	win.setForeground(qtip_winform.hwnd)
	historyMenu.popup(pt.x,pt.y,true )
}
/*}}*/
/*右键点击快速取消筛选{{*/
qtip_winform.bkplus1.wndproc = function(hwnd,message,wParam,lParam){
	if message == 0x205/*_WM_RBUTTONUP*/ qtbs.forms[1].syslink.onHyperlinkClick();
}
var updateColor = function(btCtrl){
	var t = #qtbs.forms[1].clSapType.selected + #qtbs.forms[1].clUnit.selected + #qtbs.forms[1].clOrderType.selected;
	btCtrl.border = t ? {top=5;color=-16711936} : null;
	btCtrl.redraw();
}
subscribe("filter_Changed",function(...){
	updateColor(qtip_winform.bkplus1);
})
/*}}*/
/*输入框切换单多行{{*/
qtip_winform.custom.adjust = function( cx,cy,wParam ) {
	var txt = qtip_winform.editUser3.text;
	var height = qtip_winform.editUser3.height;
	var tb0 = {cls="edit";left=264;top=8;right=512;bottom=8+height;dl=1;dr=1;dt=1;edge=1;z=5;multiline=0;vscroll=0;hscroll=0};
	var tb1 = {cls="edit";left=264;top=8;right=512;bottom=8+height;dl=1;dr=1;dt=1;edge=1;z=5;multiline=1;vscroll=1;hscroll=0};
    if height < 35	{
    		qtip_winform.add({"editUser3" = tb0});
			qtip_winform.editUser3.text=string.split(txt,'\n\r')[[1]];
    }else{
        if qtip_winform.editUser3.lineCount==1 {
    		qtip_winform.add({"editUser3" = tb1});
			qtip_winform.editUser3.text = txt;
    	}
    }
	qtip_winform.splitter.ltControl = qtip_winform.editUser3;
	qtip_winform.splitter.ltMin=32;
};
/*}}*/

qtip_winform.button2.oncommand = function(id,event){
	if event == 0/*_BN_CLICKED*/
		qtbs.selIndex = 2;
}


win.loopMessage();
return qtip_winform;