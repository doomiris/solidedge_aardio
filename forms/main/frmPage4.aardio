import fonts.fontAwesome;
import win.ui;
import win.ui.menu;
import solidedge.application;
import solidedge.DraftDocument;
/*DSG{{*/
var winform = win.form(text="aardio form";right=783;bottom=471;image=$"\res\SE BG.jpg";bkBottom=0;bkLeft=0;bkRight=0;bkTop=0)
winform.add(
button={cls="button";text='\uF08B  插入图纸';left=688;top=424;right=776;bottom=464;db=1;dr=1;font=LOGFONT(name='FontAwesome');z=5};
button2={cls="button";text="关闭";left=592;top=424;right=680;bottom=464;db=1;dr=1;hide=1;z=6};
checklist={cls="checklist";left=152;top=40;right=776;bottom=416;aw=1;border=1;db=1;dl=1;dr=1;dt=1;items={};z=2};
static={cls="static";text="技术要求:";left=184;top=10;right=344;bottom=32;color=8388608;dl=1;dt=1;font=LOGFONT(h=-16);transparent=1;z=3};
static2={cls="static";text='\uF0CB';left=152;top=8;right=192;bottom=40;color=8388608;dl=1;dt=1;font=LOGFONT(h=-24;name='FontAwesome');transparent=1;z=4};
treeview={cls="treeview";left=8;top=40;right=144;bottom=416;asel=false;border=1;db=1;dl=1;dt=1;vscroll=1;z=1}
)
/*}}*/

/*initDeclare{{*/
var seApp, seDFT, seSht, thk, radii,smdl;
var initDeclare = function(){
	seApp = solidedge.application();
	if !seApp return false; 
	if (seApp.Documents.Count == 0) return false; 
	if (seApp.ActiveDocumentType !== 2 /*Draft Document Type*/) return false; 
	seDFT = solidedge.DraftDocument(seApp.ActiveDocument);
	if (seDFT.Sheets.Count == 0) return false; 
	seSht = seDFT.ActiveSheet;
	
	for (i = 1; seDFT.ModelLinks.Count){
		if seDFT.ModelLinks.Item(i).ModelType == 0 /* igPartLink */
    		if (seDFT.ModelLinks.Item(i).ModelDocument.Type == 4 /*SheetMetal Document Type*/) {
        		smdl = seDFT.ModelLinks.Item(i).ModelDocument;
        		break;
    		}
	}
	thk = smdl ? smdl.GetGlobalParameter(4 /* seSheetMetalGlobalMaterialThickness */) : null;
	radii = smdl ? smdl.GetGlobalParameter(5 /* seSheetMetalGlobalBendRadius */) :null;
	thk ?= string.format("%0.1f", thk * 1000);
	radii ?= string.format("%0.1f", radii * 1000);

	return true; 
}
/*}}*/

import config;
var dbpath = config.dbServer.path + "tbm.db";

import sqlite;
var dbRs = sqlite(dbpath);	//打开数据库连接

//import console;
//console.open();

/*loadLists{{*/
var loadPromptList = function(){
	var tab = dbRs.getTable("select * from SE_SPEC_TEMPLATE order by [ID]");
	var hitem = winform.treeview.insertItem( text="模板清单" );
	//winform.treeview.insertTable(tab);
	//var hSubItem;
	for (i=1; #tab; 1) {
		winform.treeview.insertItem( { text=tab[i].tName },hitem/*父节点*/); 
	}
	winform.treeview.expandAll();
}
/*}}*/

var replacePara = function (str){
	if !string.find(str, "{thk}") and !string.find(str, "{radii}") return str;
	if thk str = string.replace(str, "{thk}", thk);
	if radii str = string.replace(str, "{radii}", radii);
	return str;
}

/*控件事件{{*/
winform.treeview.onnotify = function(id,code,ptr){
    select(code) {
    	case 0xFFFFFE3D/*_TVN_SELCHANGEDW*/ {
			winform.checklist.clear();
			var hItem = winform.treeview.getSelection();
			if(hItem and winform.treeview.getParentItem(hItem)){
				var tab;
				tab = dbRs.stepQuery("select tCombine,tSelect from SE_SPEC_TEMPLATE", { tName = winform.treeview.getItemText(hItem) } );
				var v = string.split(tab.tCombine,",")
				var vs = string.split(tab.tSelect,",")
				
				for (i=1; #v){
					tab = dbRs.stepQuery("select sTxt from SE_SPEC_ITEM", { ID = v[i] } );
					winform.checklist.addItem(replacePara(tab.sTxt));
					winform.checklist.setChecked(i, (vs[i]==1) );
				}
			}
    	}
    	case 0xFFFFFFFB/*_NM_RCLICK*/  {
			var x,y = win.getMessagePos();
			var hItem,tvht = winform.treeview.hitTest(x,y,true);
			var menu = win.ui.popmenu(winform)
			menu.add("删除",
				function(){
					winform.treeview.delItem(hItem)
				}
			)
			menu.popup(x,y,true);
    	}

    }
}

winform.button2.oncommand = function(id,event){
	mainForm.show(false); //隐藏窗口
	
}

winform.button.oncommand = function(id,event){
	mainForm.show(false); //隐藏窗口
	if (seApp) winex.attach(seApp.hWnd);
	win.setActive(seApp.hWnd);
	win.setForeground(seApp.hWnd)
	seApp.DisplayAlerts = false;
	
	if !initDeclare() return false; ;
	
	var txts = seSht.TextBoxes;
	var txt = txts.Add(0.25, 0.12, 0);
    	txt.HorizontalAlignment = 0 /* igTextHzAlignLeft */;
    	txt.FlowOrientation = 0 /* igTextHorizontal */;
    	txt.Justification = 0 /* igTextJustifyTop */;
    	txt.TextControlType = 0 /* igTextFitToContent */;
    	txt.Text = "技术要求:" ++ '\r';
    	txt.Text = txt.Text ++ " " ++ '\r';

    	var n = 1;	//编号
    	for (i = 1 ; winform.checklist.count){
        	if (winform.checklist.getChecked(i)) {
            	txt.Text = txt.Text ++ n ++ ". " ++ winform.checklist.getItemText(i) ++ '\r';
            	n += 1;
        	}
    	}
	txt.Cut();
	
	seApp.DisplayAlerts = true;
	win.delay(0.1*1000);
	winex.key.combine(seApp.hWnd,"CTRL","V"); //调用前必须先winex.attach

}
/*}}*/
winform.show();
initDeclare();
loadPromptList();	//读取提示列表

subscribe("refresh",function(){
//		winform.redraw();
	}
);

win.loopMessage();
return winform;