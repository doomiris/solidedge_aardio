import fonts.fontAwesome;
import win.ui;
/*DSG{{*/
var winform = win.form(text="aardio form";right=759;bottom=470;image=$"\res\SE BG.jpg";bkBottom=0;bkLeft=0;bkRight=0;bkTop=0)
winform.add(
button={cls="button";text='\uF085';left=232;top=16;right=264;bottom=48;dl=1;dt=1;font=LOGFONT(name='FontAwesome');hide=1;z=6};
button2={cls="button";text='\uF013';left=280;top=16;right=312;bottom=48;dl=1;dt=1;font=LOGFONT(name='FontAwesome');z=7};
combobox={cls="combobox";left=85;top=442;right=168;bottom=462;clipch=1;db=1;dl=1;items={"SRDC","QHC","Local Disk"};mode="dropdownlist";transparent=1;z=4};
custom={cls="custom";text="loading...";left=0;top=72;right=760;bottom=432;db=1;dl=1;dr=1;dt=1;z=10};
github={cls="plus";left=731;top=445;right=755;bottom=469;align="right";color=0x800000;db=1;dr=1;font=LOGFONT(h=-19;name='FontAwesome');iconColor=0x2D2D2D;iconStyle={font=LOGFONT(h=-19;name='FontAwesome')};iconText='\uF09B';notify=1;paddingLeft=20;z=8};
lnkVersion={cls="plus";text="检查新版本";left=543;top=8;right=752;bottom=57;align="left";color=0x800000;dr=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="right";font=LOGFONT(h=-16;name='FontAwesome')};iconText='\uF021';notify=1;paddingLeft=20;z=2};
static={cls="static";text="TBM工具组";left=8;top=8;right=248;bottom=56;color=0x800000;dl=1;dt=1;font=LOGFONT(h=-37;name='Microsoft YaHei UI');transparent=1;z=1};
static2={cls="static";text='\uF216  服务器:';left=10;top=445;right=80;bottom=464;align="right";db=1;dl=1;font=LOGFONT(name='FontAwesome');transparent=1;z=5};
static3={cls="static";left=175;top=445;right=287;bottom=461;center=1;db=1;dl=1;notify=1;transparent=1;z=9};
winver={cls="richedit";text="当前系统版本：";left=432;top=448;right=728;bottom=468;align="right";aw=1;db=1;dr=1;readonly=1;transparent=1;z=3}
)
/*}}*/

import config;
import web.form.simpleMarkdown;
var wb = web.form.simpleMarkdown(winform.custom);

winform.bindConfig( config.serverIndex,{
	combobox = "selIndex";	
} );

var adminSession = string.indexOf(string.lower(io.getSpecial(0x1a/*_CSIDL_APPDATA*/)),"ccl100100");
winform.button.hide = !adminSession;

winform.show();

var loadUpdateLog = function(){
	
/*
	var url = "https://doomiris.github.io/TBM/update_history.md";	
	import domisoft;
	var text = thread.invokeAndWait(domisoft.getContentFromHTTP, url);
*/
	var text = string.load("~\update_history.md")
	if text 
		wb.writeDelta(text);
}

var checkUpdate = function(wform){
/*
	import console;
	console.open();
*/
	import global;
	import fsys.version;
	import ide;
	import domisoft;

	var current_exe_path = io._exedir;
	if _STUDIO_INVOKED
		current_exe_path = ide.getPublishPath();
	var current_version = fsys.version.getInfo(current_exe_path).productVersion;

	var url = "https://doomiris.github.io/TBM/version.json";
	
	var t = domisoft.getContentFromHTTP(url, true);
	var new_version = fsys.version(t.productVersion);

	with wform.lnkVersion {
		disabledText = null;
		if	new_version < current_version {
			text = "已是最新版.";
			iconText = '\uF00C';
		}else{
			text = "发现新版本:" + tostring(new_version);
			iconText = '\uF0AB';
		}
	}
}

winform.lnkVersion.oncommand = function(id,event){
	if string.startsWith(owner.text,"发现新版本"){
		win.msgboxTimeout("此功能还没准备好...", config.__appName);
	}else{
		owner.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250';text='checking...'}
		thread.invoke(checkUpdate, winform);
	}
}

winform.winver.text = tostring(config.__appVersion);

winform.combobox.onListChange= function(){ 
	select(owner.selIndex) {
		case 1 {
			config.dbServer.path = "\\ccnsia1a\separts\Admin\SAP\db\";
		}
		case 2 {
			config.dbServer.path = "\\CCNQIA05\temp-\LGP\_db\";
		}
		case 3 {
			import fsys.dlg.dir;
			var path = fsys.dlg.dir(,winform,"指定DB所在目录");
			if !path return ; 
			config.dbServer.path = io.joinpath(path,"\");
		}
	}
	config.dbServer.save();
	winform.static3.refresh();
}


winform.button.oncommand = function(id,event){
	mainForm.custom.loadForm("\forms\admin_only\QTIP_ADMIN.aardio");
}

winform.button2.oncommand = function(id,event){
	mainForm.custom.loadForm("\forms\main\settings.aardio");
}

winform.github.skin(
	background = { 
		hover = "/res/images/btn-hover.png";
		focus = "/res/images/btn-focus.jpg";
		active = "/res/images/btn-active.png";
		disabled = "/res/images/btn-disabled.png"; 
	}
)
winform.github.oncommand = function(id,event){
	thread.invoke( function(){
		import process;
		import config;
		process.openUrl(config.__website);
	}
	, winform);
}
win.setTimeout(loadUpdateLog)

import win.ui.tooltip;
var tooltipCtrl = win.ui.tooltip( winform );
tooltipCtrl.addTool(winform.lnkVersion,"点击检测更新" );
tooltipCtrl.addTool(winform.github, config.__website);


winform.static3.refresh = function(id,event){
	owner.text = thread.invokeAndWait(
		function(){
			import win;
			import config;
			import fsys.table;
			var vtab = fsys.table(io.joinpath(config.dbServer.path,"dblog.table"));
			return vtab.last_updated;
		}
	)
}
winform.static3.oncommand = winform.static3.refresh;

winform.static3.refresh();

win.loopMessage();
