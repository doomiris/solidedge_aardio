import fonts.fontAwesome;
import win.ui;
/*DSG{{*/
var about_winform = win.form(text="aardio form";right=759;bottom=470;image=$"\res\SE BG.jpg";bkBottom=0;bkLeft=0;bkRight=0;bkTop=0)
about_winform.add(
button={cls="button";text='\uF085';left=232;top=16;right=264;bottom=48;dl=1;dt=1;font=LOGFONT(name='FontAwesome');hide=1;z=7};
button2={cls="button";text='\uF013';left=280;top=16;right=312;bottom=48;dl=1;dt=1;font=LOGFONT(name='FontAwesome');z=8};
combobox={cls="combobox";left=85;top=442;right=168;bottom=462;clipch=1;db=1;dl=1;items={"SRDC";"QHC";"Local Disk"};mode="dropdownlist";transparent=1;z=5};
github={cls="plus";left=731;top=445;right=755;bottom=469;align="right";color=8388608;db=1;dr=1;font=LOGFONT(h=-19;name='FontAwesome');iconColor=2960685;iconStyle={font=LOGFONT(h=-19;name='FontAwesome')};iconText='\uF09B';notify=1;paddingLeft=20;z=9};
lnkVersion={cls="plus";text="检查新版本";left=519;top=8;right=752;bottom=57;align="left";color=8388608;dr=1;dt=1;font=LOGFONT(h=-16);iconStyle={align="right";font=LOGFONT(h=-16;name='FontAwesome')};iconText='\uF021';notify=1;paddingLeft=20;z=3};
richedit={cls="richedit";left=8;top=72;right=760;bottom=432;ah=1;autohscroll=false;aw=1;db=1;dl=1;dr=1;dt=1;link=1;multiline=1;readonly=1;transparent=1;vscroll=1;wrap=1;z=2};
static={cls="static";text="TBM工具组";left=8;top=8;right=248;bottom=56;color=8388608;dl=1;dt=1;font=LOGFONT(h=-37;name='Microsoft YaHei UI');transparent=1;z=1};
static2={cls="static";text='\uF216  服务器:';left=10;top=445;right=80;bottom=464;align="right";db=1;dl=1;font=LOGFONT(name='FontAwesome');transparent=1;z=6};
static3={cls="static";left=175;top=445;right=287;bottom=461;center=1;db=1;dl=1;transparent=1;z=10};
winver={cls="richedit";text="当前系统版本：";left=432;top=448;right=728;bottom=468;align="right";aw=1;db=1;dr=1;readonly=1;transparent=1;z=4}
)
/*}}*/

import config;
import fsys.version;
about_winform.ver = fsys.version.getInfo(io._exepath);
var ver = about_winform.ver.productVersion;
about_winform.bindConfig( config.serverIndex,{
	combobox = "selIndex";	
} );

var adminSession = string.indexOf(string.lower(io.getSpecial(0x1a/*_CSIDL_APPDATA*/)),"ccl100100");
about_winform.button.hide = !adminSession;

var loadUpdateLog = function(about_winform){
	var url = "https://doomiris.github.io/TBM/update_history.txt";
	var richedit = about_winform.richedit;
	richedit.disabledText = null;
	
	import domisoft;
	richedit.text = domisoft.getContentFromHTTP(url);
}

var checkUpdate = function(wform){
	var url = "https://doomiris.github.io/TBM/version.json";
	import domisoft;
	t = domisoft.getContentFromHTTP(url, true);
	var server_ver = t.version;
	var user_ver = wform.ver.productVersion;
	var compareVer = function(v1, v2){
		if v1.major < v2.major	return true; 
		elseif v1.minor < v2.minor	return true; 
		elseif v1.revision < v2.revision	return true; 
		else	return false;	
	}
	if	compareVer(user_ver,server_ver){
		var label = wform.lnkVersion;
		label.disabledText = null;
		label.text = string.format("发现新版本: %s.%s [%s]", server_ver.major, server_ver.minor, server_ver.revision)
		label.iconText = '\uF0AB'
	}
}


about_winform.lnkVersion.oncommand = function(id,event){
	owner.disabledText = {'\uF254';'\uF251';'\uF252';'\uF253';'\uF250';text='checking...'}
	thread.invoke( checkUpdate, about_winform);
}

about_winform.winver.text = string.format("当前版本：%s.%s [%s]", ver.major, ver.minor, ver.revision)

about_winform.combobox.onListChange= function(){ 
	select(owner.selIndex) {
		case 1 {
			config.dbServer.path = "\\ccnsia1a\separts\Admin\SAP\db\";
		}
		case 2 {
			config.dbServer.path = "\\CCNQIA05\temp-\LGP\_db\";
		}
		case 3 {
			import fsys.dlg.dir;
			var path = fsys.dlg.dir(,about_winform,"指定DB所在目录");
			if !path return ; 
			config.dbServer.path = io.joinpath(path,"\");
		}
	}
	config.dbServer.save();
	about_winform.static3.refresh();
}

about_winform.show();

about_winform.richedit.disabledText = {"✶";"✸";"✹";"✺";"✹";"✷"; text ="正在查找更新记录..."}

about_winform.button.oncommand = function(id,event){
	mainForm.custom.loadForm("\forms\admin_only\QTIP_ADMIN.aardio");
}

about_winform.button2.oncommand = function(id,event){
	mainForm.custom.loadForm("\forms\main\settings.aardio");
}

about_winform.github.skin(
	background = { 
		hover = "/res/images/btn-hover.png";
		focus = "/res/images/btn-focus.jpg";
		active = "/res/images/btn-active.png";
		disabled = "/res/images/btn-disabled.png"; 
	}
)
about_winform.github.oncommand = function(id,event){
	thread.invoke( function(){
		import process;
		import config;
		process.openUrl(config.__website);
	}
	, about_winform);
}

win.setTimeout(loadUpdateLog, 100, about_winform)

import win.ui.tooltip;
var tooltipCtrl = win.ui.tooltip( about_winform );
tooltipCtrl.addTool(about_winform.lnkVersion,"点击检测更新" );
tooltipCtrl.addTool(about_winform.github, config.__website);


about_winform.static3.refresh = function(){
	owner.text = thread.invokeAndWait(
		function(){
			import win;
			import config;
			import fsys.table;
			var vtab = fsys.table(io.joinpath(config.dbServer.path,"dblog.table"));
			return vtab.last_updated;
		}
	)
}
about_winform.static3.oncommand = about_winform.static3.refresh;

about_winform.static3.refresh();

win.loopMessage();
return about_winform;