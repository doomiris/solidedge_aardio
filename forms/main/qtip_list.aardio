import win.ui;
import win.ui.menu;
import win.clip;
import win.clip.file;
import sqlite;
import fsys;
import fsys.file;
import config;
import process;
//import godking.vlistEx;
/*DSG{{*/
var winform_qlist = win.form(text="aardio form";right=759;bottom=469)
winform_qlist.add(
listview={cls="listview";left=0;top=0;right=760;bottom=472;db=1;dl=1;dr=1;dt=1;edge=1;fullRow=1;gridLines=1;z=1}
)
/*}}*/

var saveInputList = function(str){
    if !(table.find(config.qTipLog, str)){
    	table.unshift(config.qTipLog, str);
    	if ( #config.qTipLog>15 ) table.pop(config.qTipLog);
    	config.qTipLog.save();
    };
};
/*下载BOM{{*/
var downloadbom = function(){
    var tab=table.array();
    for (k, v in winform_qlist.listview.selected){
        var bg = winform_qlist.listview.getItemText(v, 1);
        if string.startWith(bg,"BG")
            table.push(tab,  bg);
    }  
    if !#tab return ;
	import SAP.session
	var session=SAP.session();
	session.downloadBOM(winform_qlist, tab);
};
var copyPartNo = function(withDesp){
    var tab=table.array();
    var listview = winform_qlist.listview;
    for (k, v in listview.selected){
    	var pcode = listview.getItemText(v, 1);
    	var desp = listview.getItemText(v, 2);
        if !withDesp
            table.push(tab,  pcode);
        else
        	table.push(tab,  pcode + '\t' + desp);        
    }  
    if #tab
    	return string.join(tab, '\n');
}

/*}}*/

/*双击打开事件{{*/
winform_qlist.listview.onnotify = function(id,code,ptr){//code很多 select外放代码很容易overflow
    select(code) {
    	case 0xFFFFFFFD/*_NM_DBLCLK*/ {
    		var selectedRow = owner.getSelection();
    		if !selectedRow return ; 
    		var pcode = owner.getItemText(selectedRow, 1);
    	    qtbs.selIndex = 4;
    	    qtbs.forms[4].publish("qtip_search_4", pcode);
    	}
    	case 0xFFFFFFFB/*_NM_RCLICK*/ {
    		var selectedRow = owner.getSelection();
    		if !selectedRow return ; 
    		var pcode = owner.getItemText(selectedRow, 1);
    		var desp = owner.getItemText(selectedRow, 2);
			var x,y = win.getMessagePos();
			//var hItem,tvht = winform_qlist.listview.hitTest(x,y,true);
			var menu = win.ui.popmenu(winform_qlist);			
			if string.startWith(pcode,"BG",true){
				menu.add("下载BOM...", downloadbom);				
				var flist = fsys.list(config.__sapDownload,,pcode + "*.xlsx");
				if #flist {
					var fn = flist[flist[#flist]];
					var f = fsys.file(fn);
					var t = f.lastModified().local();
					f.close();
					f = null;
					menu.add(
						"打开缓存BOM: " + tostring(t,"%Y-%m-%d %H:%M:%S"),
						λ() process.execute(fn)
					);
					
				}
			}

			menu.add("复制[专用号]", λ() win.clip.write(copyPartNo()));
			menu.add("复制[专用号][物料描述]", λ() win.clip.write(copyPartNo(true)));
			menu.add("查询详细信息...", λ() qtbs.open(3,pcode));
			menu.add("查找图纸...", λ() qtbs.open(4,pcode));
			menu.add("查询图纸下发记录...", λ() qtbs.open(5,pcode));
    		var fav = config.qtip.fav;
			var favTab = {
	    			[1] = "添加进收藏"; 
	    			[2] = { 
	    					[1] = {
	    						text = "+ 新的收藏...";
	    						proc = function(id){
	    						    table.push(fav,{
	    						    	"title":"新收藏";
	    						    	"list":{pcode}
	    						    });
	    						};
	        				};
	        				[2] = {/* ---- */};
	    			}
	    	}

			for (i=1; #fav)	{
				if !fav[i] continue;
				table.push(favTab[2], {	
						text = fav[i]["title"];
						proc = function(id){
							if !table.find(fav[i]["list"],pcode)
    								table.push(fav[i]["list"],pcode);
   						}
    			})
    		}
			menu.addTable({favTab});
			menu.popup(x,y,true);
    	}
    }
}
/*}}*/

/*CTRLC复制模块{{*/
winform_qlist.listview.translateAccelerator = function( msg ){ 
    var ctrl = ::GetKeyState(0x11/*_VK_CTRL*/);
//    var shift =  ::GetKeyState(0x10/*_VK_SHIFT*/);
//    var alt = ::GetKeyState(0x12/*_VK_ALT*/);
//    var selectedRow = owner.getSelection();
    var vk = msg.wParam;    
    if( ( vk == 'C'# ) && ctrl ){
		var items = owner.items;
		var selected = owner.selected; 
        var tab=table.array();
        for (i=1; #selected)
            table.push(tab,string.join(items[selected[i]],'\t'));
		win.clip.write(string.join(tab,'\n'));
        return true;
    }
}

/*}}*/

/*searchSapDB{{*/

var searchSapDB = function(userinput, winform_qlist, filter_form){
	import config
	import sqlite;
	import QHC;
	import domisoft;
	import tarray;

/*
import console
console.open();
*/

	var db = QHC.openDb(QHC.sap_db_path);
	var sapTab;
	var fields //= QHC.getColDef();
    if !#userinput return ; 

    winform_qlist.listview.clear(true);
    winform_qlist.listview.insertColumn("正在搜索数据库, 请稍等...",300,1)

    userinput = string.crlf(userinput,'\r\n' /* 默认值 */);
    
    //分隔符号转换行
	for k, v in {`、`, `，`, `;`, `；`, `,`, `|`,'\t'} 
		userinput = string.replace(userinput, v,'\r\n');    

    var userinputArr = string.split(userinput, '\r\n');
    userinputArr = table.unique(userinputArr);
    
    if (#userinputArr == 1)
    	winform_qlist.publish("saveInput", userinputArr[1]);

 	table.clear(sapTab);
	sapTab={};
	
	fields = tarray.replaceValue(fields, "MTyp", "类型");
	fields = tarray.replaceValue(fields, "BUn", "单位");
	fields = tarray.replaceValue(fields, "PGr", "采购组");
	fields = tarray.replaceValue(fields, "物料是可配置的", "可配置");
	//console.dump(fields)
    for (i=1; #userinputArr; 1){
        if string.indexOf(userinputArr[i], '\t') userinputArr[i] = table.pop(string.split(userinputArr[i],'\t'));
		userinput = string.trim(userinputArr[i]);
		if !#userinput continue;
		
    	if (string.startWith(userinput,"008",true)) userinput="H" ++ userinput;
    	userinput=string.replace(userinput, string.fromUnicode('\u00A0') /* NO-BREAK SPACE */,"");
    	userinput=string.replace(userinput,"@%","\%");
    	userinput=string.replace(userinput,"@_","\_");
    	userinput=string.replace(userinput,"@*","%");
    	userinput=string.replace(userinput,"@?","_");
		
		var sqlstr;
			
		if !filter_form.chkMoreCols.checked {
			sqlstr = "
				SELECT
					`物料`,
					`物料描述`,
					`Bun` AS `单位`,
					`MTyp` AS `类型`,
					`物料组`,
					`价格` 
			 	FROM
			 		`所有物料`
			 	WHERE
			 		??
			 " ;
			 fields = {"物料","物料描述","单位","类型","物料组","价格"};
		}else{
			sqlstr = "
				SELECT
					`物料`,
					`物料描述`,
					`Bun` AS `单位`,
					`MTyp` AS `类型`,
					`物料组`,
					`价格`,
					`PGr` AS `采购组`,
					`物料是可配置的` AS `可配置`,
					`禁用`,
					`设计展平尺寸`,
					`生产展平尺寸`
			 	FROM
			 		`所有物料`
			 	WHERE
			 		??
			 " ;
			 fields = {"物料","物料描述","单位","类型","物料组","价格","可配置","禁用","设计展平尺寸","生产展平尺寸"};
		}
    	var searchField = {};	
    	if (string.startWith(userinput,"H00",true) || string.startWith(userinput,"BG",true)) 
			table.assign(searchField, {物料 = userinput});
		else {
			table.assign (searchField, {物料描述 = userinput});

/*
    	if (string.startWith(userinput,"BG",true) || table.indexOf(filter_form.clLegacyType.checked,"BG")){
			sqlstr = sqlstr ++ " AND [MTyp] = 'FERT'" ++ " AND LENGTH([物料])=11"
		}
		
*/

		//直接搜索物料时忽略filter
			if #filter_form.clSapType.selected
				table.assign(searchField, {物料组 = domisoft.getSelectedText(filter_form.clSapType)});
			if #filter_form.clUnit.selected
				table.assign(searchField, {单位 = domisoft.getSelectedText(filter_form.clUnit)});
			if #filter_form.clOrderType.selected
				table.assign(searchField, {类型 = domisoft.getSelectedText(filter_form.clOrderType)});
		}		
/*
		if #winform_qlist.clLegacyType.checked
			sqlstr +=  " AND [物料] LIKE `H008`";
*/
		sqlstr = sqlite.format(sqlstr, {searchField});

		var quickSearch = !string.indexOf(userinput,"%");
		if !quickSearch sqlstr = string.replace(sqlstr,"=","LIKE");
		
		if (filter_form.chkDisabled.checked and !quickSearch)	sqlstr += " AND ifnull(`禁用`, '') = ''"; // 禁用列如果为空或null则是正常物料
		
		/*  预测用的-F物料已经全部禁用了
		if table.indexOf(getSelectedText(filter_form.clOrderType),"FERT") sqlstr += " AND LENGTH(`物料`)=11"; 
		*/
		var t = quickSearch ? {[1] = db.stepQuery(sqlstr)} : db.getTable(sqlstr);

		if !#t
			if !filter_form.chkHideUnknown.checked
				table.push(t, {物料=userinput;物料描述="1没找到SAP物料, 双击可继续查找图纸"/*;物料类型="";价格="";禁用=""*/});

/*
		if (filter_form.chkDisabled.checked)	
			t = table.filter(t, lambda(v) !v.禁用 || !#v.禁用 );
*/

		if #t
			sapTab = table.concat(sapTab, t);
		
		if #sapTab
			sapTab = tarray.uniqueByCol(sapTab, "物料");	//uniqueCol 需要有fields
	}
	
	if !#sapTab {
		if ( !filter_form.chkHideUnknown.checked) 
			table.push(sapTab, {物料=userinputArr[1];物料描述="2没找到SAP物料, 双击可继续查找图纸";物料类型=""/*;禁用=""*/})
	}
	else {

	}
	/*调整列宽*/
	
	var formatListView = function(lv, tab){
		lv.clear(true);
		lv.enableDoubleBuffering();

		lv.setTable( tab );
		//grid.readonlyColums = {
    	//	[1] = true;[2] = true;[3] = true;[4] = true;[5] = true;
    	//}
		lv.setColumn({cx=90 ;fmt=0x2/*_LVCFMT_CENTER*/},1);
	//	lv.setColumn({cx=350;fmt=0x0/*_LVCFMT_LEFT*/},  2);
		lv.setColumn({cx=60 ;fmt=0x2/*_LVCFMT_CENTER*/},3);
		lv.setColumn({cx=60 ;fmt=0x2/*_LVCFMT_CENTER*/},4);
		lv.setColumn({cx=50 ;fmt=0x2/*_LVCFMT_CENTER*/},5);
		lv.setColumn({cx=50 ;fmt=0x2/*_LVCFMT_CENTER*/},6);
    	lv.fillParent(2);
	}
	sapTab.fields = fields;
		//console.dump(sapTab)
	formatListView(winform_qlist.listview, sapTab);
}
/*}}*/
/*格式化BOM{{*/
subscribe("dok",function(...){
	//winform_qlist.button6.disabledText = null;
	win.delay(3*1000);
	var ftab = ...;
	if ftab {
		import com.excel;
		var excel = com.excel(false);
		
		for(i = excel.WorkBooks.Count; 1; -1) // 反向
			if table.indexOf(ftab, excel.WorkBooks(i).Name) {
				try{
					excel.WorkBooks(i).Activate();
					excel.Run("initExportedBom"); //调用宏格式化样式
				}
		 		if #ftab>4 excel.WorkBooks(i).Close(true /* _save_changes */);
		 		//else excel.WorkBooks(i).Save();
			}
	}

} )
/*}}*/
winform_qlist.show();
subscribe("qtip_search_2", function(...){
	var txt = ...;
	var filter_form = qtbs.forms[1];
	if txt
		thread.invoke( 
			searchSapDB, txt, winform_qlist, filter_form
		);
})
subscribe("saveInput",function(...){
	var txt = ...;
	saveInputList(txt);
} )

win.loopMessage();
return winform_qlist;
