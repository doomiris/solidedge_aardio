//SAP vbs 支持库

var table = ..table;
var string = ..string;

namespace SAP;
sapQuickOpen = function(){
	import win;
	import config;
	_title = "SAP一键启动";
	if !config.settings.edit_SAP_system 
		return win.msgboxErr("请先配置登陆信息",_title);
	import domisoft;

	_SAP_install_path := ..io.splitpath(domisoft.getComAppPath("SAP Logon")).dir;
	_SAP_shcut_cmd := ..io.joinpath(_SAP_install_path, "sapshcut.exe");
	if !..io.exist(_SAP_shcut_cmd) 
		return win.msgboxErr("文件不在在:"+'\n'+_SAP_shcut_cmd); 
	var para = `-max -system={SAP_system} -client={SAP_client} -user={SAP_user} -pw={SAP_pw} -language={SAP_language} -cmd=S000`;
	var c = {
		"{SAP_system}" = config.settings.edit_SAP_system;
		"{SAP_client}" = config.settings.edit_SAP_client;
		"{SAP_user}" = config.settings.edit_SAP_user;
		"{SAP_pw}" = config.settings.edit_SAP_pw;
		"{SAP_language}" = config.settings.edit_SAP_language;
	};

	para = ..string.replace(para,'%{}',c);

	..thread.invoke( 
		function(cmd,para){
			..raw.execute(cmd, para);
		}, _SAP_shcut_cmd, para
	)

	win.msgboxTimeout("正在启动SAP, 稍等...", _title, 10*1000);
}

downloadBOM = function(winform, etab){
	import QHC;
	import config;
    import web.script;
    import console;
    
    var vm = web.script("VBScript") ;
    
	vm.script = /*
			If Not IsObject(application) Then
   			Set SapGuiAuto  = GetObject("SAPGUI")
   			Set application = SapGuiAuto.GetScriptingEngine
			End If
			If Not IsObject(connection) Then
   			Set connection = application.Children(0)
			End If
			If Not IsObject(session) Then
   			Set session    = connection.Children(0)
			End If
			If IsObject(WScript) Then
   			WScript.ConnectObject session,     "on"
   			WScript.ConnectObject application, "on"
			End If
	*/
	
	vm.script = /*
		do while session.findById("wnd[0]/tbar[0]/btn[12]").Changeable
			session.findById("wnd[0]/tbar[0]/btn[12]").press
			on error resume next
				session.findById("wnd[1]/usr/btnSPOP-OPTION1").press	'不保存退出订单界面
			on error goto 0
		loop
	*/
	vm.script = /*
		session.findById("wnd[0]/tbar[0]/okcd").text = "ZMDM_001"
		session.findById("wnd[0]").sendVKey 0
	*/
	
	/* 通过 vm.script 访问变量前需要先声明 */
	vm.script = /*
	    	dim filename
	    	dim etab_item
	    	dim lib_path
	*/
	vm.script.lib_path = config.__sapDownload;
	var ftab = {}
	for (i = 1; #etab){
		vm.script.etab_item = etab[i]
		
		vm.script = /*
    		session.findById("wnd[0]/usr/ctxtS_MATNR-LOW").text = etab_item
    		session.findById("wnd[0]/tbar[1]/btn[8]").press
	 	*/

    	var filename;
    	var vt = QHC.getSapItem(etab[i]);
 		if vt filename = vt.物料描述; 			
    	filename = string.replace(filename, " ", "_");
    	filename = etab[i] ++ "_" ++ filename ++ ".xlsx";
    	vm.script.filename = filename;    		
    	table.push(ftab, filename);
    	var fn = ..io.joinpath(config.__sapDownload,filename);
    	if ..io.exist(fn)
    		if !..io.remove(fn)
    			console.debug("删除失败", fn);
    	
    	vm.script = /*	
    	session.findById("wnd[0]/tbar[1]/btn[9]").press	' //zmdm_001 里面的download
    	session.findById("wnd[1]/usr/ctxtDY_PATH").text = lib_path
    	session.findById("wnd[1]/usr/ctxtDY_FILENAME").text = filename
    	session.findById("wnd[1]/tbar[0]/btn[11]").press
    	session.findById("wnd[0]/tbar[0]/btn[12]").press	'//[x]取消
    	*/
    	
    	..publish("bom_down",filename);

	}
	vm.script = /*	
	session.findById("wnd[0]/tbar[0]/btn[12]").press
	*/
	winform.publish("dok", ftab);
}

/**intellisense(SAP)
downloadBOM(arr) = 下载BOM
sapQuickOpen() = 一键打开SAP
end intellisense**/