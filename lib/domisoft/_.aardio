//domisoft
var table = ..table;
var string = ..string;
var math = ..math;
var io = ..io;

var replace = string.replace;
var trim = string.trim;

math.isNumber = function(str){
	if type(str) == "number" return true;
	if type(str) !=="string" return false;
	return !!string.match(str, "^[+-]?\d+\.?\d*$")) ; 
}
table.toHTML = function(tab){
    var html = "<table border=1>";
    for (i = 1; #tab) {
        html += "<tr>";
        for (j = 1; #tab[i]) {
        	tab[i][[j]] := "";
            html += "<td nowrap>" + tab[i][j] + "</td>";
        }
        html += "</tr>";
    }
    html += "</table>";
    return html;
}
namespace domisoft;
showInputBox = function(title, Msg,tips, winform){
	import win.inputBox;
	var inputBox = win.inputBox(winform)
	inputBox.text = title;
	inputBox.info.text = Msg;
	inputBox.input.text = tips;
	return inputBox.doModal();
}

clearExcelCorpse = function(){
	..thread.invoke( 
		function(){//清理异常的excel进程
			import com.excel;
			var excel = com.excel(false);
			if excel.Visible == false and excel.WorkBooks.Count==0 {
				import process.popen;
				var prcs = process.popen.cmd(`taskkill /PID ` + excel.Application.Run("GetCurrentProcessId") + ` /f`)
			}
		}
	)
}
getComAppPath = function(keyword){
	import com.shell;
	import fsys.knownFolder
	var s = com.shell.findApp("@"+ keyword);
	var g = string.match(s.path,'%{}');		
	return (replace(s.path, "@"+g, fsys.knownFolder(g)));
}
getRunningWorkSheet = function(workbookName,worksheetName){
	import com;
	var sht;
	for (i=1;5){
		com.enumRunning( 
			function(displayName,object){
					if com.GetTypeInfo(object) {
						if string.indexOf(com.GetTypeInfo(object).GetDocumentation().name,"_Workbook") {
							if !workbookName return object;							
							if string.endWith(displayName, workbookName, true) {		
								if worksheetName
									sht = object.Sheets(worksheetName) : null; 
								else
									sht = object.Sheets(1); 
								return true; 
							}
						}
					}
			}
		)
		if !sht			sleep(500)
		else break;
	}
	return sht; 
}
stopExcelCutCopyMode = function(){
	import com;
	com.enumRunning( 
		function(displayName,object){
				if com.GetTypeInfo(object) 
					if ..string.indexOf(com.GetTypeInfo(object).GetDocumentation().name,"_Workbook")
						try{
							object.Application.CutCopyMode = false
						}
		}
	)
	return 1; 
}
taobaoURLsimple = function(url){
	import inet.url;
	import inet.urlpart;
	url = inet.url.decode(url);
	var turl = inet.url.split(url ); 
	var para = inet.urlpart.getQuery(url);
	var s = inet.url.splitParameters(para);	
	var n = inet.url.stringify(
		scheme = turl.scheme;
		host = turl.host;
		path = turl.path;
		extraInfo = {
			id = s.id;
		};
	)
	return string.trimright(n,"?"); 
}
getSelectedText = function(listCtrl){
	var t = {}
	for index, text in listCtrl.eachSelected()
		table.unshift(t, text);
	return t; 
}
copySelectedItem = function(ctrl){
		if !ctrl	ctrl = owner;
		var doNotCopyColumns = ctrl.doNotCopyColumns;
		doNotCopyColumns := [/* 防null报错 */];
		
		table.sort(doNotCopyColumns);
		//table.reverse(doNotCopyColumns);

        import win.clip;
        import win.clip.html;
        
        ..thread.invoke(stopExcelCutCopyMode);
        
		var selected = ctrl.selected;
		var sourceTab = ctrl.getTable(true);
		var fields = table.shift(sourceTab)
		var items = sourceTab;

		var selTab = [];
		if ctrl.copyWithHeaders
			table.push(selTab, fields); 
		for (i=1; #selected)
				table.push(selTab, items[selected[i]]);
				
		for (i=1; #selTab){
			for(j=1; #doNotCopyColumns)
				selTab[i][doNotCopyColumns[j]] = null;
			table.removeByValue(selTab[i], null);
			var cacheTab = [];
			for k, v in selTab[i] 
				table.push(cacheTab,v); //处理稀疏数组问题
			selTab[i] = cacheTab;
		};

		/* clip添加纯文本格式 */
        var txtTab = [];
        for (i=1; #selTab)
            table.push(txtTab, string.join(selTab[i], '\t'));
		win.clip.write(string.join(txtTab, '\r\n'));

		/* clip添加HTML格式 */
		var hTab = [];
        for (i=1; #selTab)
            table.push(hTab, selTab[i]);
        var html = table.toHTML(hTab);  
 		var cliphtml = win.clip.html();
        html = cliphtml.stringify(html);
		cliphtml.write(html,true,false);
		return hTab; 
}
copyTable = function(tab, selectedIndex){
		if !#tab return ; 
		select type(selectedIndex) {
			case "number" {selectedIndex = {selectedIndex}}
			case "table" {/*不处理*/}
			case "null" {
				selectedIndex = [];
				for (i=1;#tab) selectedIndex[i] = i;
			}
			else return ; 			
		} 
        import win.clip;
        import win.clip.html;
        var cliphtml = win.clip.html();

		/* clip添加纯文本格式 */
        var txtTab = [];
        for (i=1; #selectedIndex)
            table.push(txtTab,string.join(tab[selectedIndex[i]],'\t'));
		win.clip.write(string.join(txtTab,'\n'));		
		/* clip添加HTML格式 */
		var hTab = [];
        for (i=1; #selectedIndex)
            table.push(hTab, tab[selectedIndex[i]]);            
        var html = table.toHTML(hTab)
        html = cliphtml.stringify(html);
		cliphtml.write(html,true,false);
		return hTab; 
}
selectAll = function(ctrl){
	if !ctrl ctrl = owner
	var t = [];
	for (i=1; ctrl.count) table.push(t, i); 
	ctrl.selected = t; 
}

ctrlC = function(msg){
    var ctrl = ::GetKeyState(0x11/*_VK_CTRL*/);
    var vk = msg.wParam;    
    if( ( vk == 'C'# ) && ctrl ){
		copySelectedItem(owner);
        return true;
    }
    if( ( vk == 'A'# ) && ctrl ){
		selectAll(owner);
        return true;
    }
}


readClipHcode = function(){
	import win.clip;
	import dotNet.ocr;
	var ocr = dotNet.ocr();
	ocr.language = "en-US"
	var hBmp = win.clip.readBitmap()
	if(!hBmp){
		//winform.edit.print("剪贴板未读取到图像")
		return;
	}
	
	var bmp = ..gdip.bitmap(hBmp);
	var ocrRet = ocr.detectBitmap(bmp);
	var t = {};
	if(ocrRet){  
		//winform.edit.text = ocrRet.text;
		//winform.edit.text = "";
		
		import web.json;
		for i,block in table.eachIndex(ocrRet.blocks){
			var s = trim(block.text);
			if string.startWith(s, "008")
				s = "H" ++ s;
			//	return ; 
			s = replace(s,",","");
			s = replace(s,"O","0");
			s = replace(s,"B","8");
			s = replace(s,"g","9");
			s = replace(s,"€","6");
			s = replace(s,"t","6");
			s = replace(s,"S","5");
			s = replace(s,"/","7");
			
			if string.startWith(s, "8008")
				s = replace(s, "8008", "H008", 1);
			if string.startWith(s, "'-1")
				s = replace(s, "'-1", "H");
			if string.startWith(s, "1--1")
				s = replace(s, "1--1", "H");
				
			table.push(t,s);
		}
	}
	return t; 
}
getContentFromHTTP = function(url, parseJson = false){
	import inet.http;
	var http = inet.http();
	var t = http.get(url);
	http.close();
	if !parseJson return t; 
	import web.json;
	return web.json.parse(t);
}
formatWindowsFileName = function (nameStr) {
    nameStr = replace(nameStr, "@\", "＼");
    nameStr = replace(nameStr, "@/", "／");
    nameStr = replace(nameStr, "@:", "：");
    nameStr = replace(nameStr, "@*", "x");
    nameStr = replace(nameStr, "@?", "？");
    nameStr = replace(nameStr, '\"', "“");   //替换双引号
    nameStr = replace(nameStr, "@<", "＜");
    nameStr = replace(nameStr, "@>", "＞");
    nameStr = replace(nameStr, "@|", "｜");
    nameStr = replace(nameStr, '\r', " ");  //替换回车符
    nameStr = replace(nameStr, '\n', " ");  //替换换行符
    return trim(nameStr);
};
setColumnWidth = function(lv, widthTab){
	var count = math.max(lv.columnCount, #widthTab)
	for (i=1; count)
		if i <= lv.columnCount
			lv.setColumn({
				cx = widthTab[i];
				fmt = (widthTab[i] <= 100) ? 0x2 /*_LVCFMT_CENTER*/ : 0x0 /*_LVCFMT_LEFT*/
			}, i);
	return true; 
}
getFileDate = function(filename, formatString = "%Y/%m/%d %H:%M:%S"){
	if !io.exist(filename) return null; 
	import fsys.file;
	var file = fsys.file(filename);
	if !file return null; 
	var d = file.getTime().write.local();
	file.close();
	file = null
	if formatString d = ..time(d, formatString);
	return d; 
}
sendFileTo = function(fullname, dest){
	
}
listColSort = function(column, desc){
	var name = owner.source.fields[column];
	for (i=1; #owner.source)
		if !owner.source[i][name]
			owner.source[i][name]=""
		else{
			if math.isNumber(owner.source[i][name])
				owner.source[i][name] = tonumber(owner.source[i][name]);
			else
				owner.source[i][name] = tostring(owner.source[i][name]);
		}

	table.sort(owner.source,desc ? (lambda(next) owner[name] > next[name]) : (lambda(next) owner[name] < next[name]));	
	owner.setTable( owner.source );
	return true; //返回 true 允许当前列排序	
};
getUserFolder = function(corp){

	corp := "CCR"

	var root_path = "HKEY_CURRENT_USER\SOFTWARE\Microsoft\OneDrive\Accounts\"	
	import win.reg
	var root = win.reg(root_path, true);
	var rootKeys = root.keys();
	var businessKeys = []
	for(i=1; #rootKeys)
		if string.startsWith(rootKeys[i], "business", true)
			table.push(businessKeys, rootKeys[i])
	var getValueInKey = function(keyName, valueName){
		import win.reg
		var wkey = win.reg(keyName, true);
		return wkey.queryValue(valueName); 
	}
	for (i=1; #businessKeys){
		if getValueInKey(root_path ++businessKeys[i], "DisplayName" ) == corp {
			var UserFolder = getValueInKey(root_path ++ businessKeys[i], "UserFolder" );
			if UserFolder
				return	string.replace(UserFolder,"@OneDrive - ","");
		}
	}
	var OneDriveCommercial = string.getenv("OneDriveCommercial");
	if OneDriveCommercial
		return string.replace( OneDriveCommercial ,"@OneDrive - ","");
	return null; 
}
envFix = function(str){
	if !str return str; 
	var outStr = str;
	var matches = string.matches(str, "%%%");
	if !matches or !#matches return str; 
	for k, v in matches {
		if #v
			var envVar = getUserFolder(string.trim(v[1],"%"));
		if envVar 
			outStr = string.replace(outStr, "@"+v[1], envVar);
	}
	//outStr = string.replace(outStr,"@OneDrive - ","");	/*bugfix*/
	return outStr;
}
isWorkingHour = function(startHour = 8, endHour = 18){
	var currentHour = ..time.now().hour;
/*
# 判断currentHour 是否在 startHour 和 endHour 区间内, 返回 true 或 false
*/
	if (startHour <= endHour) {		// Same-day range (e.g., 8 to 18)
		return currentHour >= startHour and currentHour < endHour;
	} 
	else {		// Cross-day range (e.g., 22 to 6)
		return currentHour >= startHour or currentHour < endHour;
	}
}

refreshSAP = function(winform){
	import config;
	if !config.settings.keepRefreshSAPGUI return ; 
	..thread.invoke(
		function(winform){
			import domisoft;
			winform.setInterval( 
				function(){
					try{
						if !domisoft.isWorkingHour() return ; 
    					import SAP.vm;
    					var session = SAP.vm();
    					if !session	return ; 
    					if !session.busy{
							if session.isHomepage {
								session.refresh();
								//wform.edit.log(",");
							}
						}
						session = null;
						collectgarbage("collect");
					}
				}, 1000 * 5 * 60 /*注意毫秒转分钟*/)
		}, winform
	)	
}
getActiveAppName = function(){
	import win;
	import process;
    var hwnd = win.getForeground();
    if !hwnd return;
    var title = win.getText(hwnd);
    if !title or !#title {
    		title = win.getText(win.getParentOwner(hwnd));
    	}
    var proc = process.fromWindowHandle(hwnd);
    var exePath;
    if proc
        exePath = proc.getPath();
     return title, exePath; 
}



/**intellisense(domisoft)
showInputBox(title, Msg,tips, winform) = 显示输入框
clearExcelCorpse( ) = 清理异常的excel进程
getComAppPath(keyword) = 查找com程序路径
getSelectedText(listbox) = 返回选中的文本数组
getRunningWorkSheet(workbookName,worksheetName) = 获取worksheet对象 不指定worksheetName时返回sheets(1)
taobaoURLsimple(taobaoUrl) = 简化taobao链接
ctrlC(translateAccelerator, doNotCopyColumns) = 用于listview.translateAccelerator赋值启用CTRL+C复制功能
readClipHcode() = 获取截图中的专用号
getContentFromHTTP(.(url, parseJson) = 获取网址内容
formatWindowsFileName(nameStr) = 替换Windows文件名非法字符为全角＼"／，．＜＞｜＊
setColumnWidth(listview, widthTable) = 设置列宽
copyTable(tab, selectedIndex) = 复制表格
getFileDate(filename, formatString) = 获取文件修改时间
envFix(str) = 查找并替换%组成的系统参数
getActiveAppName() = 返回上层app完整路径名
end intellisense**/

/**intellisense(math)
isNumber(str) = test string of number, return true or false
end intellisense**/
