//domisoft
var table = ..table;
var string = ..string;
var math = ..math;

var replace = string.replace;
var trim = string.trim;

math.isNumber = function(str){
	return #str = #tostring((tonumber(str))); 
}
table.toHTML = function(tab){
    var html = "<table border=1>";
    for (i = 1; #tab) {
        html += "<tr>";
        for (j = 1; #tab[i]) {
        	tab[i][[j]] := "";
            html += "<td nowrap>" + tab[i][j] + "</td>";
        }
        html += "</tr>";
    }
    html += "</table>";
    return html;
}


namespace domisoft;
showInputBox = function(title, Msg,tips, winform){
	import win.inputBox;
	var inputBox = win.inputBox(winform)
	inputBox.text = title;
	inputBox.info.text = Msg;
	inputBox.input.text = tips;
	return inputBox.doModal();
}

clearExcelCorpse = function(){
	..thread.invoke( 
		function(){//清理异常的excel进程
			import com.excel;
			var excel = com.excel(false);
			if excel.Visible == false and excel.WorkBooks.Count==0 {
				import process.popen;
				var prcs = process.popen.cmd(`taskkill /PID ` + excel.Application.Run("GetCurrentProcessId") + ` /f`)
			}
		}
	)
}
getComAppPath = function(keyword){
	import com.shell;
	import fsys.knownFolder
	var s = com.shell.findApp("@"+ keyword);
	var g = string.match(s.path,'%{}');		
	return (replace(s.path, "@"+g, fsys.knownFolder(g)));
}
getRunningWorkSheet = function(workbookName,worksheetName){
	import com;
	var sht;
	com.enumRunning( 
		function(displayName,object){
				if com.GetTypeInfo(object) {
					if string.indexOf(com.GetTypeInfo(object).GetDocumentation().name,"_Workbook") {
							if string.endWith(displayName, workbookName, true) {		
								if worksheetName
									sht = object.Sheets(worksheetName) : null; 
								else
									sht = object.Sheets(1); 
								return true; 
							}
					}
				}
		}
	)
	return sht; 
}
taobaoURLsimple = function(url){
	import inet.url;
	import inet.urlpart;
	var turl = inet.url.split(url ); 
	var para = inet.urlpart.getQuery(url);
	var s = inet.url.splitParameters(para);	
	var n = inet.url.stringify(
		scheme = turl.scheme;
		host = turl.host;
		path = turl.path;
		extraInfo = {
			id = s.id;
		};
	)
	return string.trimright(n,"?"); 
}
getSelectedText = function(listCtrl){
	var t = {}
	for index, text in listCtrl.eachSelected()
		table.unshift(t, text);
	return t; 
}
copySelectedItem = function(ctrl){
		if !ctrl	ctrl = owner;
		var doNotCopyColumns = ctrl.doNotCopyColumns;
        import win.clip;
        import win.clip.html;
        var cliphtml = win.clip.html();
		var items = ctrl.items;
		import tarray;
		for (i=1; #doNotCopyColumns)
			items = tarray.drop(items, doNotCopyColumns[i])
		var selected = ctrl.selected; 
		/* clip添加纯文本格式 */
        var txtTab = table.array();
        for (i=1; #selected)
            table.push(txtTab,string.join(items[selected[i]],'\t'));
		win.clip.write(string.join(txtTab,'\n'));		
		/* clip添加HTML格式 */
		var hTab = table.array();
        for (i=1; #selected)
            table.push(hTab, items[selected[i]]);            
        var html = table.toHTML(hTab)
        html = cliphtml.stringify(html);
		cliphtml.write(html,true,false);
}
copyTable = function(tab, selectedIndex){
		if !#tab return ; 
		select type(selectedIndex) {
			case "number" {selectedIndex = {selectedIndex}}
			case "table" {/*不处理*/}
			case "null" {
				selectedIndex = table.array();
				for (i=1;#tab) selectedIndex[i] = i;
			}
			else return ; 			
		} 
        import win.clip;
        import win.clip.html;
        var cliphtml = win.clip.html();

		/* clip添加纯文本格式 */
        var txtTab = table.array();
        for (i=1; #selectedIndex)
            table.push(txtTab,string.join(tab[selectedIndex[i]],'\t'));
		win.clip.write(string.join(txtTab,'\n'));		
		/* clip添加HTML格式 */
		var hTab = table.array();
        for (i=1; #selectedIndex)
            table.push(hTab, tab[selectedIndex[i]]);            
        var html = table.toHTML(hTab)
        html = cliphtml.stringify(html);
		cliphtml.write(html,true,false);
}
selectAll = function(ctrl){
	if !ctrl ctrl = owner
	var t = table.array();
	for (i=1; ctrl.count) table.push(t, i); 
	ctrl.selected = t; 
}

ctrlC = function(msg){
    var ctrl = ::GetKeyState(0x11/*_VK_CTRL*/);
    var vk = msg.wParam;    
    if( ( vk == 'C'# ) && ctrl ){
		copySelectedItem(owner);
        return true;
    }
    if( ( vk == 'A'# ) && ctrl ){
		selectAll(owner);
        return true;
    }
}


readClipHcode = function(){
	import win.clip;
	import dotNet.ocr;
	var ocr = dotNet.ocr();
	ocr.language = "en-US"
	var hBmp = win.clip.readBitmap()
	if(!hBmp){
		//winform.edit.print("剪贴板未读取到图像")
		return;
	}
	
	var bmp = ..gdip.bitmap(hBmp);
	var ocrRet = ocr.detectBitmap(bmp);
	var t = {};
	if(ocrRet){  
		//winform.edit.text = ocrRet.text;
		//winform.edit.text = "";
		
		import web.json;
		for i,block in table.eachIndex(ocrRet.blocks){
			var s = trim(block.text);
			if string.startWith(s, "008")
				s = "H" ++ s;
			//	return ; 
			s = replace(s,",","");
			s = replace(s,"O","0");
			s = replace(s,"B","8");
			s = replace(s,"g","9");
			s = replace(s,"€","6");
			s = replace(s,"t","6");
			s = replace(s,"S","5");
			
			if string.startWith(s, "8008")
				s = replace(s, "8008", "H008", 1);
			if string.startWith(s, "'-1")
				s = replace(s, "'-1", "H")
			table.push(t,s);

		}
	}
	return t; 
}
getContentFromHTTP = function(url, parseJson = false){
	import inet.http;
	var http = inet.http();
	var t = http.get(url);
	http.close();
	if !parseJson return t; 
	import web.json;
	return web.json.parse(t);
}
formatWindowsFileName = function (nameStr) {
    nameStr = replace(nameStr, "@\", "＼");
    nameStr = replace(nameStr, "@/", "／");
    nameStr = replace(nameStr, "@:", "：");
    nameStr = replace(nameStr, "@*", "x");
    nameStr = replace(nameStr, "@?", "？");
    nameStr = replace(nameStr, '\"', "“");   //替换双引号
    nameStr = replace(nameStr, "@<", "＜");
    nameStr = replace(nameStr, "@>", "＞");
    nameStr = replace(nameStr, "@|", "｜");
    nameStr = replace(nameStr, '\r', " ");  //替换回车符
    nameStr = replace(nameStr, '\n', " ");  //替换换行符
    return trim(nameStr);
};
setColumnWidth = function(lv, widthTab){
	var count = math.max(lv.columnCount, #widthTab)
	for (i=1; count)
		if i <= lv.columnCount
			lv.setColumn({
				cx = widthTab[i];
				fmt = (widthTab[i] <= 100) ? 0x2 /*_LVCFMT_CENTER*/ : 0x0 /*_LVCFMT_LEFT*/
			}, i);
	return true; 
}



/**intellisense(domisoft)
showInputBox(title, Msg,tips, winform) = 显示输入框
clearExcelCorpse( ) = 清理异常的excel进程
getComAppPath(keyword) = 查找com程序路径
getSelectedText(listbox) = 返回选中的文本数组
getRunningWorkSheet(workbookName,worksheetName) = 获取worksheet对象 不指定worksheetName时返回sheets(1)
taobaoURLsimple(taobaoUrl) = 简化taobao链接
ctrlC(translateAccelerator) = 用于listview.translateAccelerator赋值启用CTRL+C复制功能
readClipHcode() = 获取截图中的专用号
getContentFromHTTP(.(url, parseJson) = 获取网址内容
formatWindowsFileName(nameStr) = 替换Windows文件名非法字符为全角＼"／，．＜＞｜＊
setColumnWidth(listview, widthTable) = 设置列宽
copyTable(tab, selectedIndex) = 复制表格
end intellisense**/

