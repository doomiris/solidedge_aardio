import fonts.fontAwesome;
import win.ui;
import win.ui.simpleWindow;
import win.ui.atom;
import win.ui.tabs;
import win.ui.tooltip; 
import win.inputBox;
import win.util.tray;
import winex;
import winex.key;
import sqlite;
import process;
//import console;

//user lib
import config;
import solidedge.application;
import solidedge.DraftDocument;
/*DSG{{*/
mainForm = win.form(text="TBM";right=623;bottom=551;bgcolor=15793151;border="none")
mainForm.add(
button={cls="button";text='\uF127';left=36;top=4;right=68;bottom=36;bgcolor=255;dl=1;dt=1;font=LOGFONT(h=-27;name='FontAwesome');hide=1;z=20};
caption={cls="bkplus";text="domisoft";left=64;top=10;right=184;bottom=28;color=6052956;dl=1;dt=1;font=LOGFONT(h=-14);z=9};
custom={cls="custom";left=83;top=40;right=624;bottom=552;bgcolor=16777215;db=1;dl=1;dr=1;dt=1;z=10};
logo={cls="bkplus";left=36;top=5;right=65;bottom=30;background="D:\users\Personal LGP\personal Documents\code_space\icon\ENGINE.ICO";color=8388608;dl=1;dt=1;font=LOGFONT(h=-27;name='FontAwesome');z=8};
nav1={cls="plus";text="TBM";left=0;top=48;right=85;bottom=128;bkBottom=3;bkLeft=7;bkRight=8;bkTop=2;border={color=-65536};color=16777215;dl=1;dt=1;font=LOGFONT(h=-13);iconStyle={font=LOGFONT(h=-37;name='FontAwesome');padding={bottom=20}};iconText='\uF1EA';notify=1;textPadding={bottom=10};valign="bottom";x=0.5;y=0.2;z=3};
nav2={cls="plus";text="变更";left=0;top=129;right=85;bottom=209;bkBottom=3;bkLeft=7;bkRight=8;bkTop=2;border={color=-65536};color=16777215;dl=1;dt=1;font=LOGFONT(h=-13);iconStyle={font=LOGFONT(h=-37;name='FontAwesome');padding={bottom=20}};iconText='\uF277';notify=1;textPadding={bottom=10};valign="bottom";x=0.5;y=0.2;z=4};
nav3={cls="plus";text="SAP";left=0;top=210;right=85;bottom=290;bkBottom=3;bkLeft=7;bkRight=8;bkTop=2;border={color=-65536};color=16777215;dl=1;dt=1;font=LOGFONT(h=-13);iconStyle={font=LOGFONT(h=-37;name='FontAwesome');padding={bottom=20}};iconText='\uF1B0';notify=1;textPadding={bottom=10};valign="bottom";x=0.5;y=0.2;z=5};
nav4={cls="plus";text="技术要求";left=0;top=291;right=85;bottom=371;bkBottom=3;bkLeft=7;bkRight=8;bkTop=2;border={color=-65536};color=16777215;dl=1;dt=1;font=LOGFONT(h=-13);iconStyle={font=LOGFONT(h=-37;name='FontAwesome');padding={bottom=20}};iconText='\uF0CB';notify=1;textPadding={bottom=10};valign="bottom";x=0.5;y=0.2;z=6};
nav5={cls="plus";text="Print";left=0;top=372;right=85;bottom=456;bkBottom=3;bkLeft=7;bkRight=8;bkTop=2;border={color=-65536};color=16777215;dl=1;dt=1;font=LOGFONT(h=-13);iconStyle={font=LOGFONT(h=-37;name='FontAwesome');padding={bottom=20}};iconText='\uF1C1';notify=1;textPadding={bottom=10};valign="bottom";x=0.5;y=0.2;z=7};
nav6={cls="plus";text="About";left=0;top=448;right=85;bottom=532;bkBottom=3;bkLeft=7;bkRight=8;bkTop=2;border={color=-65536};color=16777215;dl=1;dt=1;font=LOGFONT(h=-13);iconStyle={font=LOGFONT(h=-37;name='FontAwesome');padding={bottom=20}};iconText='\uF262';notify=1;textPadding={bottom=10};valign="bottom";x=0.5;y=0.2;z=18};
navBar={cls="bkplus";left=0;top=37;right=83;bottom=552;bgcolor=11829830;db=1;dl=1;dt=1;z=1};
static2={cls="static";text='\uF03B';left=8;top=8;right=32;bottom=32;color=8388608;dl=1;dt=1;font=LOGFONT(h=-27;name='FontAwesome');notify=1;transparent=1;z=19};
tbutton1={cls="button";text='\uF01E';left=184;top=4;right=216;bottom=36;dr=1;dt=1;font=LOGFONT(name='FontAwesome');z=15};
tbutton2={cls="button";text='\uF07E';left=216;top=4;right=248;bottom=36;dr=1;dt=1;font=LOGFONT(h=-13;name='FontAwesome');z=11};
tbutton3={cls="button";text='\uF05A';left=248;top=4;right=280;bottom=36;dr=1;dt=1;font=LOGFONT(h=-13;name='FontAwesome');z=12};
tbutton4={cls="button";text='\uF05C';left=280;top=4;right=312;bottom=36;dr=1;dt=1;font=LOGFONT(h=-13;name='FontAwesome');z=13};
tbutton5={cls="button";text='\uF0EE';left=312;top=4;right=344;bottom=36;dr=1;dt=1;font=LOGFONT(h=-13;name='FontAwesome');z=14};
tbutton6={cls="button";text='\uF05E';left=344;top=4;right=376;bottom=36;dr=1;dt=1;font=LOGFONT(name='FontAwesome');z=16};
tbutton7={cls="button";text='\uF05B';left=376;top=4;right=408;bottom=36;dr=1;dt=1;font=LOGFONT(name='FontAwesome');z=17};
tbutton8={cls="button";text='\uF067';left=408;top=4;right=440;bottom=36;dr=1;dt=1;font=LOGFONT(name='FontAwesome');z=21};
tbutton9={cls="button";text='\uF074';left=440;top=4;right=472;bottom=36;dr=1;dt=1;font=LOGFONT(name='FontAwesome');z=22};
titleBar={cls="bkplus";left=0;top=0;right=624;bottom=38;bgcolor=11224832;dl=1;dr=1;dt=1;forecolor=16777215;linearGradient=180;z=2}
)
/*}}*/

/*只允许一个实例{{*/
var atom,hwnd = mainForm.atom("domisoft.aardio.tbm");
if(!atom){ 
	win.showForeground(hwnd);
	win.setActive(hwnd);
	var thrdId = win.getThreadProcessId(hwnd);
	::PostThreadMessage(thrdId,123,0,0);
	win.quitMessage();	
	return;
}
/*}}*/

/*自动更新模块{{*/
/*
import fsys.update.simpleMain;
if( fsys.update.simpleMain(
	"TBM",
	"https://doomiris.github.io/TBM/version.json",
	..io.appData("/domisoft/SETBM/update"),
	function(version,description,status){})){
	return 0;	
}
*/
/*}}*/
/*closeOnEscPress{{*/
mainForm.isDialogMessage = function(hwnd,msg){ 
	if( msg.message == 0x100/*_WM_KEYDOWN*/){
		
/**
		if(  msg.wParam == 0xD/*_VK_RETURN*/ ){ 
			//return true;//告诉消息处理函数这是一个快捷键,阻止按键消息继续分发
		}
**/
		
		if( msg.wParam == 0x1B/*_VK_ESC*/ ){	//按Esc退出窗口
			mainForm.close();
			//return true;//告诉消息处理函数这是一个快捷键,阻止按键消息继续分发
		} 
	}
	
	//检测并响应默认快捷键
	return win.isDialogMessage(hwnd,msg);
}
/*}}*/
			
win.ui.simpleWindow( mainForm );
//mainForm.enableDpiScaling();
var start_time = time();
var seApp = solidedge.application();
mainForm.button.hide = !!seApp;
var sePid;
if seApp sePid = seApp.ProcessID;
//if (seApp) winex.attach(seApp.hWnd);
/*托盘图标{{*/
if config.settings.closeToTray
	mainForm.tray = win.util.tray(mainForm);
mainForm.onMinimize = function(lParam){
	if !mainForm.tray and config.settings.closeToTray
		mainForm.tray = win.util.tray(mainForm);
//	mainForm.modifyStyleEx(0x40000/*_WS_EX_APPWINDOW*/,0x80/*_WS_EX_TOOLWINDOW*/)
	publish("main_window_hide"); //通知qtip窗口中的 orphanWindow 隐藏
	if config.settings.closeToTray {
		mainForm.show(false); 
		return true; 
	}
}

mainForm.onClose = function(hwnd,message,wParam,lParam){
	if time().diffday(start_time) > 1
		win.quitMessage(); //如果程序启动超过1天则退出;
	if config.settings.closeToTray {
		mainForm.onMinimize();
		return true;    
	}	
}

mainForm.onDestroy = function(){
	if(mainForm.tray){
		mainForm.tray.delete()
	}
}

mainForm.onActivateApp = function(activated,threadId){
	if(activated){
		var nse = solidedge.application();
		mainForm.button.hide = !!nse;
		if !nse return ; 
		if sePid  == nse.ProcessID  return ;		
		seApp = nse; 
	}
}

import com.interface.ITaskbarList3;
mainForm.wndproc =  function(hwnd,message,wParam,lParam){
 		select(message) {
 			case 0xACCF/*_WM_TRAYMESSAGE*/ {
				select(lParam) {
					case 0x205/*_WM_RBUTTONUP*/ {
						win.setForeground(mainForm.hwnd)	//弹出托盘菜单以前,一定要前置主窗口中,不然不点击菜单不会消失
	    				var pt = ::POINT();
						::User32.GetCursorPos(pt);   		
	    				mainForm.popmenu2.popup(pt.x,pt.y, true);
					}
					case 0x202/*_WM_LBUTTONUP*/ {
						win.setForeground(mainForm.hwnd); //弹出托盘菜单以前,一定要前置主窗口中,不然不点击菜单不会消失							
						mainForm.show(!win.isVisible(mainForm.hwnd));					
					}
				}
			}
			case _WM_TASKBARBUTTONCREATED { 
            	mainForm.taskbar = com.interface.ITaskbarList3.Create() ;
            } 
	}
}

	
/*}}*/
/*托盘右键菜单{{*/
mainForm.popmenu2 = win.ui.popmenu(mainForm);//创建弹出菜单
mainForm.popmenu2.add('&Open TBM', function(){
	//在下面输入菜单响应代码
	win.setForeground(mainForm.hwnd)
	mainForm.show();
	
});
mainForm.popmenu2.add();//分隔线
mainForm.popmenu2.add('检查专用号申请表', λ() loadcodex("\forms\Excel\check_专用号申请表.aardio"));
mainForm.popmenu2.add('智能填写专用号申请表', λ() loadcodex("\forms\Excel\assist_专用号申请表.aardio"));
mainForm.popmenu2.add();//分隔线
mainForm.popmenu2.add('专用号填入ItemMaster...', λ() loadcodex("\forms\Excel\fillin_item_master.aardio"));
mainForm.popmenu2.add('根据下发清单更新ItemMaster', λ() loadcodex("\forms\Excel\fillin_item_master.aardio"));
mainForm.popmenu2.add('更新ItemMaster物料信息', λ() loadcodex("\forms\Excel\Excel_update_info.aardio"));
mainForm.popmenu2.add('打开图纸下发版次查询窗口...', λ() loadcodex("\forms\main\check_QHC_Release.aardio"));
mainForm.popmenu2.add();//分隔线
mainForm.popmenu2.add('检查新BOM申请表', λ() loadcodex("\forms\Excel\check_新BOM申请表.aardio"));
mainForm.popmenu2.add('原材料用量计算...', λ() loadcodex("\forms\Excel\BOM-CALC.aardio"));
mainForm.popmenu2.add('打开分类查询窗口...', λ() loadcodex("\forms\SAP\OD_Query.aardio"));
mainForm.popmenu2.add();//分隔线
mainForm.popmenu2.add('VA02/03下载成本BOM...', λ() loadcodex("\forms\SAP\download-cost-bom-va02.aardio"));
mainForm.popmenu2.add('为当前Excel表更新SAP主数据...', λ() loadcodex("\forms\Excel\update_SAP_ITEM.aardio"));
mainForm.popmenu2.add();//分隔线
mainForm.popmenu2.add('&Exit', λ() win.quitMessage());
/*}}*/
/*左侧导航按钮{{*/

var tbs = win.ui.tabs( 
	mainForm.nav1,
	mainForm.nav2,
	mainForm.nav3,
	mainForm.nav4,
	mainForm.nav5,
	mainForm.nav6,
);

tbs.skin({
	background={
		active=0xFFFFFFFF;
		default=0x00FFFFFF;
		hover=0x38FFFFFF
	};
	color={
		default=0xFFFFFFFF; 
	};
	checked={
		background={default=0xFFFFFFFF;};
		color={default=0xff0047ab;};
	}
})
_frmName = {
	[1] = "\forms\main\frmPage1.aardio";
	[2] = "\forms\main\frmPage2.aardio";
	[3] = "\forms\main\QTIP.aardio";
	[4] = "\forms\main\frmPage4.aardio";
	[5] = "\forms\main\frmPage5.aardio";
	[6] = "\forms\main\frmPage3.aardio";
}
for k,v in _frmName
	tbs.loadForm(k,v );

var navindex = 6;
if (_ARGV.nav)
	navindex = tonumber(_ARGV.nav);
elseif config.settings.simpleSAPmode
	navindex = 3;

tbs.selIndex = navindex;
tbs.currentTab = navindex;
/*}}*/

/*顶部按钮{{*/
mainForm.tbutton1.oncommand = function(id,event){
	solidedge.application.reOpenDocument();
	//win.quitMessage();
}

mainForm.tbutton2.oncommand = function(id,event){
    if seApp.Documents.Count==0 return ; 
    if seApp.ActiveDocumentType !== 2 /*Draft Document Type*/ return ; 
    var SelSet = seApp.ActiveDocument.SelectSet;
    if SelSet.Count !== 1 return; 
    if (SelSet(1).Type !== 488188096 /* igDimension */) return;

    var inputBox=win.inputBox();
    inputBox.info.text="input text to replace dimension";
    inputBox.text="Edit Dim";
    inputBox.input.text="L";
    var s=inputBox.doModal();
    if !s return; 
    if ..string.len(..string.trim(s)) == 0 return ; 
    SelSet(1).OverrideString = s;
    SelSet(1).Style.NTSSymbol = 1 /* igDimStyleNTSNone */;
    
    mainForm.show(false); //隐藏窗口
	if seApp win.setActive(seApp.hWnd);
}

mainForm.tbutton3.oncommand = function(id,event){
    if seApp.Documents.Count==0 return ; 
    if seApp.ActiveDocumentType !== 2 /*Draft Document Type*/ return ; 
	var seDFT = solidedge.DraftDocument(seApp.ActiveDocument);
	var seSht = seDFT.ActiveSheet;
	var BBS = seSht.Balloons;
	var ee = seDFT.getTopRecordMark(seSht);
	ee = ee ? string.pack( ee[1] + 1 ) : "a";
	var inputBox=win.inputBox();
	inputBox.info.text="What symbol change bolloons to ?";
	inputBox.text = "Edit Balloon";
	inputBox.input.text = ee ;
	var e = inputBox.doModal();
	if !e return ; 
	if (..string.len(..string.trim(e)) == 0) return ; 
	for (i = 1 ; BBS.Count) {	
		if BBS.Item(i).BalloonType == 1 /* igDimBalloonCircle */
    		if !BBS.Item(i).Leader
    			with BBS.Item(i){
    				BalloonText = e;
    				BalloonType = 1; /* DimBalloonTypeConstants.igDimBalloonCircle */
    				BalloonSize = 2; /* 形状是字体几倍数 */
    				Leader = false; 
    				Style.DrivenColor = 255;	/* ColorConstants.seColorRed */;    				
    			}
	}
    mainForm.show(false); //隐藏窗口
	if seApp win.setActive(seApp.hWnd);
}

mainForm.tbutton4.oncommand = function(id,event){
	if seApp.Documents.Count==0 return ; 
    if seApp.ActiveDocumentType !== 2 /*Draft Document Type*/ return ; 
	var seDFT = solidedge.DraftDocument(seApp.ActiveDocument);
	var seSht = seDFT.ActiveSheet;
	var BBS = seSht.Balloons;
	var ee = "a";
	for (i = 1 ; BBS.Count) {	
		if BBS.Item(i).BalloonType == 1 /* igDimBalloonCircle */
    		if !BBS.Item(i).Leader
        		ee = BBS.Item(i).BalloonText;
        		break;
	}
	
	var inputBox=win.inputBox();
	inputBox.info.text = "What symbol bolloons you want to delete ?";
	inputBox.text = "delete Balloon";
	inputBox.input.text = ee ;
	var e = inputBox.doModal();
	if !e return ; 
	if (..string.len(..string.trim(e)) == 0) return ; 
	
	for (i = BBS.Count; 1 ;- 1 ) {	
		if BBS.Item(i).BalloonType == 1 /* igDimBalloonCircle */
    		if !BBS.Item(i).Leader
        		if (BBS.Item(i).BalloonText==e)
        			BBS.Item(i).Delete();
	}
    mainForm.show(false); //隐藏窗口
	if seApp win.setActive(seApp.hWnd);
}

mainForm.tbutton5.oncommand = function(id,event){
	import solidedge.AssemblyDocument;
	if (seApp.ActiveDocumentType != 3 /* igAssemblyDocument */) return;
	var seAsm = solidedge.AssemblyDocument(seApp.ActiveDocument); 
	select(seAsm.SelectSet.Count) {
		case 1 {
			seAsm.openDummy(seAsm.SelectSet.Item(1));
		}
		case 0 {
			if seAsm.IsFileFamilyByDocument {
				var ofn = table.shift(string.split(seAsm.FullName,"!"))
				seAsm.Close(false);
				seApp.Open(ofn, false);				
			}	
		}
		else {
			return ; 
		}
	}
    mainForm.show(false); //隐藏窗口
	if seApp win.setActive(seApp.hWnd);
}


mainForm.tbutton6.oncommand = function(id,event){
	solidedge.application.closeDft(false);
    mainForm.show(false); //隐藏窗口
	if seApp win.setActive(seApp.hWnd);
}

mainForm.tbutton7.oncommand = function(id,event){
	solidedge.application.closeDft(true);
    mainForm.show(false); //隐藏窗口
	if seApp win.setActive(seApp.hWnd);
}

mainForm.tbutton8.oncommand = function(id,event){
	var tlist = {
		_path = "\\CCNSIF0G\sRdc\CCR\A05-Design\Structure Design\QHC\";
		[1] = "新增--专用号申请-型号-姓名-日期.xlsx";
		[2] = "申请表-专用号-维护主数据.xlsx";		
		[3] = "BOM更改申请单模板-姓名-日期-是否涉及选项-是否涉及工单.xlsx";		
		[4] = "新产品BOM申请单模板-姓名-日期.xlsx";		
		[5] = "新增--产成品申请-型号-姓名-申请日期.xlsx";		
		[6] = "图纸明细需求-姓名-日期.xlsx";
	}
	var opent = function(n){
		import com.excel;
		var excel = com.excel(false);
		if !excel win.msgbox("请先打开Excel程序");
		if !excel return ; 
		var wb = excel.WorkBooks.Add(tlist._path ++ tlist[n]);
		excel.Visible = true;
		win.showForeground(excel.Application.hwnd);
		mainForm.show(false);
	}
	
	mainForm.popmenu1 = win.ui.popmenu(mainForm);//创建弹出菜单
	mainForm.popmenu1.addTable({
		{ "新建申请表"; {
    			{ "专用号申请表";		function(){opent(1)}};
    			{ "维护主数据申请表";	function(){opent(2)}};
    			{ "BOM更改申请表";		function(){opent(3)}};
    			{ "新产品BOM申请表";	function(){opent(4)}};
    			{ "产成品编码申请表";	function(){opent(5)}};
    			{ "图纸明细需求表";		function(){opent(6)}};
			}
		};
		{"格式"; {
				{"格式化BOM"; function(){}};
				{"格式化成本BOM"; function(){}};
			}
		}
	})
	win.setForeground(mainForm.hwnd)	//弹出托盘菜单以前,一定要前置主窗口中,不然不点击菜单不会消失
	var pt = ::POINT();
	::User32.GetCursorPos(pt);   		
	mainForm.popmenu1.popup(pt.x,pt.y,true )
}

mainForm.tbutton9.oncommand = function(id,event){
	import solidedge.AssemblyDocument;
	if (seApp.ActiveDocumentType != 3 /* igAssemblyDocument */) return;
	var seAsm = solidedge.AssemblyDocument(seApp.ActiveDocument); 
	if (seAsm.SelectSet.Count != 1) return;
	seAsm.replaceWithPartCopy(seAsm.SelectSet.Item(1));
    mainForm.show(false); //隐藏窗口
	if seApp win.setActive(seApp.hWnd);
}
/*}}*/
/*按钮提示{{*/
var tooltipCtrl = win.ui.tooltip( mainForm );//在窗口上创建tooltip控件
tooltipCtrl.addTool(mainForm.tbutton1,"Re-Open as Read-Only" )
tooltipCtrl.addTool(mainForm.tbutton2,"Edit Dim" )
tooltipCtrl.addTool(mainForm.tbutton3,"Edit Balloon" )
tooltipCtrl.addTool(mainForm.tbutton4,"Delete Balloon" )
tooltipCtrl.addTool(mainForm.tbutton5,"Open Dummy" )
tooltipCtrl.addTool(mainForm.tbutton6,"Close All DFT (discard changes)" )
tooltipCtrl.addTool(mainForm.tbutton7,"Save && Close All DFT" )
tooltipCtrl.addTool(mainForm.static2,"显示/隐藏导航按钮" )
tooltipCtrl.addTool(mainForm.tbutton8,"常用表格模板" )
tooltipCtrl.addTool(mainForm.tbutton9,"replace with part_copy" )
/*}}*/
/*折叠导航按钮{{*/
mainForm.static2.tag = true;
mainForm.static2.tag2 = mainForm.custom.left;
mainForm.static2.oncommand = function(id,event){
    if (mainForm.static2.tag){
        mainForm.static2.text = '\uF03C';
    	mainForm.static2.tag = false;
		tbs.show ( false );
		tbs.dropButton.hide = true;
		mainForm.custom.left = 0;
		
	}else{
		mainForm.static2.text = '\uF03B';
    	mainForm.static2.tag = true;
		tbs.show ( true );
		tbs.dropButton.hide = false;
		mainForm.custom.left = mainForm.static2.tag2;
	}
}

if config.settings.simpleSAPmode
	mainForm.static2.oncommand(); //simpleSAPmode
/*}}*/

/*快捷查询{{*/

var hk_SearchSelected, hk_openTBM, hk_openRevMgr;

//删除之前注册的热键,hkId如果是null空值，此函数可忽略不做任何操
mainForm.unreghotkey(hk_SearchSelected);
mainForm.unreghotkey(hk_openTBM);
mainForm.unreghotkey(hk_openRevMgr);

//重新注册系统热键;
hk_SearchSelected = mainForm.reghotkey(
	function(id,mod,vk){
		loadcodex("\forms\Excel\Excel_notify_selection.aardio");
	}	
	,1/*_MOD_ALT*/, 'J'# 	
);
hk_openTBM = mainForm.reghotkey(
	function(id,mod,vk){
		if !seApp return ; 
		tbs.selIndex = 1;
		win.setForeground(mainForm.hwnd)
		mainForm.show();
	}	
	,1/*_MOD_ALT*/, 'T'# 	
);
hk_openRevMgr = mainForm.reghotkey(
	function(id,mod,vk){
		if !seApp return ; 
		tbs.selIndex = 2;
		win.setForeground(mainForm.hwnd)
		mainForm.show();
	}	
	,1/*_MOD_ALT*/, 'R'# 	
);
/*}}*/

mainForm.button.oncommand = function(id,event){
	seApp = solidedge.application();
	if (seApp) owner.hide = true;
}

if !config.settings.startupMinimized or !config.settings.closeToTray
		mainForm.show();
/*
subscribe("checkupdate",function(...){
	fsys.update.simpleMain.checkUpdate(true);
} )
*/
subscribe("控件丢失",function(...){
	var index = ...;
	tbs.loadForm (index, _frmName[index]);
} )
tbs.onSelchange = function(idx,strip,form){
	form.publish("refresh");
	tbs.currentTab = idx;
} 
/*处理异常的excel进程{{*/
thread.invoke( 
	function(){
		import com.excel;
		var excel = com.excel(false);
		if excel.Visible == false and excel.WorkBooks.Count==0 {
			import process.popen;
			var prcs = process.popen.cmd(`taskkill /im EXCEL.EXE /f`)
		}
	}
)
/*}}*/
return win.loopMessage(function(msg){
	if !msg return ;
	if msg.message == 123 {
//		console.log(mainForm.tbs.currentTab);
		tbs.forms[tbs.currentTab].publish("refresh"); 
	}
}
);