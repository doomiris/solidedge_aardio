import fonts.fontAwesome;
import win.ui;
import win.ui.atom;
import win.ui.tabs;
import win.ui.tooltip; 
import win.util.tray;
import key;
import sqlite;
import process;
import config;
import domisoft;
import SAP;
import solidedge.application;
import godking.listboxEx;
import fsys.table;
/*DSG{{*/
mainForm = win.form(text="TBM";right=750;bottom=550;bgcolor=0xF0FBFF)
mainForm.add(
closeAll={cls="plus";left=715;top=10;right=747;bottom=35;align="left";color=0x3C3C3C;dr=1;dt=1;font=LOGFONT(h=-16);iconStyle={font=LOGFONT(h=-16;name='FontAwesome')};iconText='\uF05C';notify=1;z=8};
custom={cls="custom";left=140;top=40;right=750;bottom=520;ah=1;aw=1;bgcolor=0xFFFFFF;db=1;dl=1;dr=1;dt=1;z=2};
foldchk={cls="plus";left=8;top=8;right=32;bottom=32;align="left";color=0x800000;dl=1;dt=1;font=LOGFONT(h=-27);iconStyle={align="left";font=LOGFONT(h=-27;name='FontAwesome')};iconText='\uF03B';notify=1;textPadding={left=15};z=3};
listbox={cls="listbox";left=0;top=40;right=140;bottom=520;ah=1;bgcolor=0xAB4700;color=0xFFFFFF;db=1;dl=1;dt=1;font=LOGFONT(h=-13;name='FontAwesome');items={};ownerDraw=1;vscroll=1;z=4};
splitter={cls="splitter";left=139;top=40;right=144;bottom=550;db=1;dt=1;z=5};
stripTab1={cls="plus";text="tab1";left=145;top=16;right=245;bottom=40;dl=1;dt=1;font=LOGFONT(name='FontAwesome');iconStyle={align="left";font=LOGFONT(name='FontAwesome')};iconText='\uF0AD';z=6};
stripTab2={cls="plus";text="tab2";left=245;top=16;right=345;bottom=40;dl=1;dt=1;iconStyle={align="left";font=LOGFONT(name='FontAwesome')};iconText='\uF0AD';z=7};
titleBar={cls="bkplus";left=0;top=0;right=750;bottom=40;bgcolor=0xAB4700;dl=1;dr=1;dt=1;forecolor=0xFFFFFF;linearGradient=180;z=1}
)
/*}}*/

mainForm.closeAll.skin({
	color={
		active=0xFF00FF00;
		default=0xFF3C3C3C;
		disabled=0xFF6D6D6D;
		hover=0xFFFF0000		
	}
})

/*只允许一个实例{{*/
var atom,hwnd = mainForm.atom("domisoft.aardio.tbm");
if(!atom and !_STUDIO_INVOKED){ 
	win.showForeground(hwnd);
	win.setActive(hwnd);
	win.quitMessage();	
	return;
}
/*}}*/
			
//win.ui.simpleWindow( mainForm );
mainForm.bindConfig( config.mainForm,{
	splitter = "left";
	listbox = "width";
} );

mainForm.foldchk.skin(
    color = { 
        hover = 0xFFFF0000; 
        active = 0xFF00FF00;
    }
    checked = {
    	//text = '';//' 勾选了'/*_FA_CHECK_SQUARE*/;;
    	iconText = '\uF03C';
    }
)  
/*托盘图标{{*/
mainForm.tray = win.util.tray(mainForm);
mainForm.onMinimize = function(lParam){
	mainForm.show(false); 
	return true; 
}
	
mainForm.onClose = function(hwnd,message,wParam,lParam){
	mainForm.onMinimize();
	if _STUDIO_INVOKED
		win.quitMessage();
	var hasNewVer = thread.invokeAndWait(
		function(current_version){
			import win;
			import global;
			import fsys.version;
			import config;
			if io.exist(io.joinpath(io._exedir,"update\se_tbm.exe"))
				var new_ver = fsys.version.getInfo("update\se_tbm.exe").productVersion;
				if new_ver > current_version
					return true;
		}, config.__appVersion
	)
	if hasNewVer win.quitMessage();
	return true;
}
mainForm.onDestroy = function(){
	if(mainForm.tray){
		mainForm.tray.delete()
	}
}

import com.interface.ITaskbarList3;
mainForm.wndproc =  function(hwnd,message,wParam,lParam){
 		select(message) {
 			case 0xACCF/*_WM_TRAYMESSAGE*/ {
				select(lParam) {
					case 0x205/*_WM_RBUTTONUP*/ {
						win.setForeground(mainForm.hwnd)	//弹出托盘菜单以前,一定要前置主窗口中,不然不点击菜单不会消失
	    				var pt = ::POINT();
						::User32.GetCursorPos(pt);   		
	    				mainForm.popmenu2.popup(pt.x,pt.y, true);
					}
					case 0x202/*_WM_LBUTTONUP*/ {
						win.setForeground(mainForm.hwnd); //弹出托盘菜单以前,一定要前置主窗口中,不然不点击菜单不会消失							
						mainForm.show(!win.isVisible(mainForm.hwnd));
					}
				}
			}
			case _WM_TASKBARBUTTONCREATED { 
            	mainForm.taskbar = com.interface.ITaskbarList3.Create() ;
            } 
	}
}

	
/*}}*/
/*托盘右键菜单{{*/

mainForm.popmenu2 = win.ui.popmenu(mainForm);//创建弹出菜单
mainForm.popmenu2.add('&Open TBM', function(){
	//在下面输入菜单响应代码
	win.setForeground(mainForm.hwnd)
	mainForm.show();
	
});
mainForm.popmenu2.add();//分隔线
mainForm.popmenu2.add("check_专用号申请表",  λ() thread.invoke(λ() loadcodex("\forms\Excel\check_专用号申请表_GUI.aardio")));
mainForm.popmenu2.add("check_BOM更改单",  λ() thread.invoke(λ() loadcodex("\forms\Excel\check_BOM更改单_GUI.aardio")));
mainForm.popmenu2.add("check_新BOM申请表",  λ() thread.invoke(λ() loadcodex("\forms\Excel\check_新BOM申请表_GUI.aardio")));
mainForm.popmenu2.add("fill_专用号申请表", λ() thread.invoke( λ() loadcodex("\forms\Excel\assist_专用号申请表_GUI.aardio")));
mainForm.popmenu2.add();//分隔线
mainForm.popmenu2.add('Teamcenter插件更新...', λ() thread.invoke(λ() loadcodex("\forms\Teamcenter\更新插件.aardio")));
mainForm.popmenu2.add('收集Teamcenter打开的图纸...', λ() thread.invoke(λ() loadcodex("\forms\Teamcenter\收集temp内的PDF文件放入PDF库.aardio")));
mainForm.popmenu2.add();//分隔线
mainForm.popmenu2.add('SE导出当前截图...', λ()	thread.invoke( 
		function(){
			import solidedge.application;
			var seApp = solidedge.application();
			seApp.exportViewAsPng();
		}
	)
);
mainForm.popmenu2.add('SAP一键登入1', function(){
	thread.invoke(SAP.sapQuickOpen)
	mainForm.tray.message = 0xACCF/*_WM_TRAYMESSAGE*/
	mainForm.tray.pop("Starting SAP GUI ...","TBM");
});
mainForm.popmenu2.add('SAP一键登入2', function(){
	thread.invoke(SAP.sapQuickOpen,2)
	mainForm.tray.message = 0xACCF/*_WM_TRAYMESSAGE*/
	mainForm.tray.pop("Starting SAP GUI ...","TBM");
});
mainForm.popmenu2.add();//分隔线


mainForm.popmenu2.add('&Exit', λ() win.quitMessage());
/*}}*/

mainForm.splitter.split(mainForm.listbox, mainForm.custom);
{
	var tWidth = mainForm.stripTab1.width;
	mainForm.stripTab1.left = mainForm.listbox.width;
	mainForm.stripTab1.width = tWidth;	
	mainForm.stripTab2.left = mainForm.listbox.width + mainForm.stripTab1.width;
}
mainForm.mTabs = win.ui.tabs(mainForm.stripTab1, mainForm.stripTab2);

/*tabStle{{*/
mainForm.mTabs.margin = 40;
mainForm.mTabs.clear();
mainForm.mTabs.skin({
    background={
        active=0xFFFFFFFF;
        default=0x00FFFFFF;
        hover=0xFF98FB98;
    };
    color={
        default=0xFF0F0000; 
    };
    checked={
        background={default=0xFFFFFFFF;};
        color={default=0xFF42A875;};
		border = { 
			default = {left=1;right=1;top=1;color=0xFF008000} 
			disabled = {left=1;right=1;top=1;color=0xFF008000} 
		}; 
    }
})
/*}}*/

mainForm.mTabs.onSelchange = function(idx,tab,form){
	mainForm.text = string.join({tab.text, "TBM工具箱"}," - ");
	mainForm.listbox.selected = null;
	for (i=1; mainForm.listbox.count) {
		var txt = table.pop(string.split(mainForm.listbox.items[i],","))
		if txt == tab.text {
			mainForm.listbox.selIndex = i;
			break;
		}
	}
	if form.frmReLoad
		form.frmReLoad();
}


/*左侧导航按钮{{*/
var loadfrmName, menuList, menuTheme = loadcodex("\menulist.aardio");
var navlb = godking.listboxEx(
	mainForm.listbox/*列表框组件*/,
	menuList/*菜单列表*/,
	menuTheme/*主题*/
)
var getExistTabIdx = function(tabname){
	for(tab,form,idx in mainForm.mTabs.each() ){
		if tab.text == tabname
			return idx; 
	}
	return null; 
}

mainForm.listbox.getSelMenuInfo = function(selText){
	selText := mainForm.listbox.selText
	var navArr = string.split(selText,",");
	return {
		index:		tonumber(navArr[1]);
		subindex:		tonumber(navArr[2]);
		iconText:		string.unescape(navArr[3]);
		ActiveIconText:	string.unescape(navArr[4]);
		text:			navArr[5] ;
	}; 
}
var rSelTab = null;
mainForm.mTabPopMenu = win.ui.popmenu(mainForm); 
mainForm.mTabPopMenu.add("重复打开此标签页",function(){
	for (i=1; mainForm.listbox.count) {
		var txt = table.pop(string.split(mainForm.listbox.items[i],","))
		if txt == rSelTab.text {
			var navItem = mainForm.listbox.getSelMenuInfo(mainForm.listbox.items[i]);
			mainForm.listbox.addThisTab(navItem);
			break;
		}
	}
})
mainForm.mTabPopMenu.add("在新窗口中打开此标签页",function(){
	for (i=1; mainForm.listbox.count) {
		var txt = table.pop(string.split(mainForm.listbox.items[i],","))
		if txt == rSelTab.text {
			var navItem = mainForm.listbox.getSelMenuInfo(mainForm.listbox.items[i]);
			mainForm.listbox.openThisTabAsWindow(navItem);
			break;
		}
	}
})
mainForm.mTabPopMenu.add("关闭此标签页",λ() mainForm.mTabs.deleteByTab(rSelTab))
mainForm.listbox.addThisTab = function(navItem){
	win.setTimeout(function(){
		navItem := mainForm.listbox.getSelMenuInfo();
		mainForm.mTabs.selIndex = mainForm.mTabs.add(
				{
					text = navItem.text;
					iconText = navItem.ActiveIconText;
					hasCloseButton = true
				}, loadfrmName[navItem.index][navItem.subindex]
		);
		mainForm.mTabs.selTab.onRightMouseUp = function(wParam,lParam){
		    var x,y = win.getMessagePos();
		    rSelTab = mainForm.mTabs.hitTest(x,y)
			mainForm.mTabPopMenu.popup(x,y,true);
		}
		if mainForm.mTabs.forms[mainForm.mTabs.selIndex].frmReLoad
			mainForm.mTabs.forms[mainForm.mTabs.selIndex].frmReLoad();
	})
}
mainForm.listbox.openThisTabAsWindow = function(navItem){
	win.setTimeout(function(){
		navItem := mainForm.listbox.getSelMenuInfo();
		mainForm.loadForm(loadfrmName[navItem.index][navItem.subindex]);
	})
}
navlb.onClick = function(index,subindex,text){
	if !subindex return ;
	var checkIdx = getExistTabIdx(text);
	if !checkIdx
		mainForm.listbox.addThisTab();
	else {
		mainForm.mTabs.selIndex = checkIdx;
		if mainForm.mTabs.forms[checkIdx].frmReLoad
			mainForm.mTabs.forms[checkIdx].frmReLoad();
	}
		
}

/*}}*/


/*折叠导航按钮{{*/
var foldNave = function(){
    if mainForm.foldchk.checked {
    	mainForm.listbox.show(false);
		mainForm.splitter.show(false);
		mainForm.custom.left = 0;
	} else {
		mainForm.custom.left = mainForm.listbox.width + 3  ;
		mainForm.splitter.width = 3;
		mainForm.listbox.show(true);
		mainForm.splitter.show(true);
	}
}
mainForm.foldchk.oncommand = foldNave
/*}}*/

win.setTimeout(
	function(){
		mainForm.custom.loadForm(loadfrmName[4][1])
	}
)

if !config.settings.startupMinimized or !config.settings.closeToTray
		mainForm.show();
if _STUDIO_INVOKED
	mainForm.show();

/*格式化BOM{{*/
subscribe("dok", function(...){
	import win;
	win.delay(3*1000);
	
	var ftab = ...;
	if !ftab return ; 
	thread.invoke( 
		function(ftab){
			import domisoft;
			import config;
			import QHC;
			var sht = domisoft.getRunningWorkSheet(ftab);
			QHC.formatBOM(sht);
			import fsys;
			var destPath = io.joinpath(config.dbServer.path,"\xl_lib\");
			if sht.ParentPath !== destPath
				fsys.copy(sht.Parent.FullName, destPath, 0x10/*_FOF_NOCONFIRMATION*/)
		}, ftab
	)
} )
/*}}*/

//mainForm.closeAll.oncommand = mainForm.mTabs.clear;
mainForm.closeAll.onMouseClick = function(wParam,lParam){
    //var x,y = win.getMessagePos(lParam);
    var ctrl = ::GetKeyState(0x11/*_VK_CTRL*/) & 0x8000;
    if !ctrl
		mainForm.mTabs.delete(mainForm.mTabs.selIndex);
	else 
		mainForm.mTabs.clear();
}
var tooltipCtrl = win.ui.tooltip( mainForm );
mainForm.closeAll.tooltip = tooltipCtrl.addTool(mainForm.closeAll,"点击: 关闭当前子窗口" ++ '\r\n' ++ "CTRL+点击: 关闭所有子窗口");
mainForm.foldchk.tooltip = tooltipCtrl.addTool(mainForm.foldchk,"折叠左侧导航栏");

mainForm.popmenu = win.ui.popmenu(mainForm); 
mainForm.popmenu.add('在新选项卡中打开', λ() mainForm.listbox.addThisTab());
mainForm.popmenu.add('在新窗口中打开', λ() mainForm.listbox.openThisTabAsWindow());
/*右键新建菜单{{*/
mainForm.listbox.wndproc = function(hwnd,message,wParam,lParam){
	select( message ) { 
		case 0x205/*_WM_RBUTTONUP*/{
			var x,y = win.getMessagePos();  
			var item = mainForm.listbox.hitTest(x,y,true); 
			if( item ){
				mainForm.listbox.selIndex = item;
				mainForm.popmenu.popup(x,y,true)
			}
		}
	}
}
/*}}*/


var hkId;
	//删除之前注册的热键,hkId如果是null空值，此函数可忽略不做任何操
mainForm.unreghotkey(hkId);
//重新注册系统热键;
hkId = mainForm.reghotkey(  /*回调函数,控制键,虚拟键码*/
	function(id,mod,vk){
		/* 
		# 检查最上层应用程序, 如果是有焦点状态, 获取当前打开的文件名称
		*/
		var hwnd = win.getForeground();
		if(!hwnd) return;
		
		var tid, pid = win.getThreadProcessId(hwnd);
		if(!pid) return;

/*
		var exeName = string.lower(
				io.splitpath(
					process(pid).getPath()
				).file
			);
*/
		var current_App;
		for object,interface,name,index in com.eachRunning() {
			if object.hwnd == hwnd {
				current_App = object;
				break;	
			}
		}
		if !current_App return ; 

		select(current_App.Name){
			case "Microsoft Excel" {
				if !current_App.Workbooks.Count return ;
				import string.keywords;
				var kw = string.keywords(["专用号申请","BOM更改","BG"]);
				var wb = current_App.ActiveWorkbook;				
				switch( kw.findOne(wb.Name), {  
					[1]: function(){
						thread.invoke( λ () loadcodex("\forms\Excel\check_专用号申请表_GUI.aardio"))
					};
					[2]: function(){
						thread.invoke( λ () loadcodex("\forms\Excel\check_BOM更改单_GUI.aardio"))
					}; 
					[3]: function(){
						thread.invoke( λ () loadcodex("\forms\Excel\check_新BOM申请表_GUI.aardio"))						
					};  
				})

			}
			case "Solid Edge 2020" {
				if current_App.Documents.Count
					if current_App.ActiveDocumentType == 2 /*Draft Document Type*/
						thread.invoke( λ () loadcodex("\forms\main\frmPage1.aardio"))
			}
		}

	},
	4 /*_MOD_SHIFT*/,
	0x70 /*_VK_F1*/
);


import win.ui.statusbar; 
var statusbar = win.ui.statusbar( mainForm );


statusbar.addItem(tostring(config.__appVersion), 100/*宽度*/);
statusbar.setMinHeight(30);//设置最小高度（含上下边框宽度）
statusbar.addItem("", 200/*宽度*/);

subscribe("status2", function(...){
		statusbar.setText(tostring(...) , 2 /*第几个分块*/)
} )

domisoft.refreshSAP(mainForm);

win.loopMessage();
return mainForm;