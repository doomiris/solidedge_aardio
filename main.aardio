import fonts.fontAwesome;
import win.ui;
import win.ui.simpleWindow;
import win.ui.atom;
import win.ui.tabs;
import win.ui.tooltip; 
import win.util.tray;
import key;
import sqlite;
import process;
import config;
import domisoft;
import SAP;
import solidedge.application;
import godking.listboxEx;
/*DSG{{*/
mainForm = win.form(text="TBM";right=750;bottom=550;bgcolor=15793151;border="none")
mainForm.add(
button={cls="button";text='\uF127';left=36;top=4;right=68;bottom=36;bgcolor=255;dl=1;dt=1;font=LOGFONT(h=-27;name='FontAwesome');hide=1;z=7};
cLogo={cls="bkplus";left=70;top=8;right=94;bottom=32;background="\res\tbm3.ico";color=8388608;dl=1;dt=1;z=5};
caption={cls="bkplus";text="domisoft";left=94;top=10;right=184;bottom=28;color=6052956;dl=1;dt=1;font=LOGFONT(h=-14);z=4};
custom={cls="custom";left=140;top=40;right=750;bottom=550;bgcolor=16777215;db=1;dl=1;dr=1;dt=1;z=2};
listbox={cls="listbox";left=0;top=40;right=140;bottom=550;bgcolor=11829830;color=16777215;db=1;dl=1;dt=1;font=LOGFONT(h=-13;name='FontAwesome');items={};ownerDraw=1;z=8};
logo={cls="bkplus";left=36;top=5;right=65;bottom=30;background="\res\ENGINE.ICO";color=8388608;dl=1;dt=1;z=6};
static2={cls="static";text='\uF03B';left=8;top=8;right=32;bottom=32;color=8388608;dl=1;dt=1;font=LOGFONT(h=-27;name='FontAwesome');notify=1;transparent=1;z=3};
titleBar={cls="bkplus";left=0;top=0;right=750;bottom=40;bgcolor=11224832;dl=1;dr=1;dt=1;forecolor=16777215;linearGradient=180;z=1}
)
/*}}*/

/*只允许一个实例{{*/
var atom,hwnd = mainForm.atom("domisoft.aardio.tbm");
if(!atom){ 
	win.showForeground(hwnd);
	win.setActive(hwnd);
	win.quitMessage();	
	return;
}
/*}}*/
/*closeOnEscPress{{*/
mainForm.isDialogMessage = function(hwnd,msg){ 
	if( msg.message == 0x100/*_WM_KEYDOWN*/){
		if( msg.wParam == 0x1B/*_VK_ESC*/ ){	//按Esc退出窗口
			mainForm.close();
			return true;//告诉消息处理函数这是一个快捷键,阻止按键消息继续分发
		} 
	}
	//检测并响应默认快捷键
	return win.isDialogMessage(hwnd,msg);
}
/*}}*/
			
win.ui.simpleWindow( mainForm );


/*托盘图标{{*/
mainForm.tray = win.util.tray(mainForm);
mainForm.onMinimize = function(lParam){
	mainForm.show(false); 
	return true; 
}
	
mainForm.onClose = function(hwnd,message,wParam,lParam){
	if io.exist(io.joinpath(io._exedir,"update\se_tbm.exe")){
		win.quitMessage();
		return 0;
	}
	if _STUDIO_INVOKED	win.quitMessage();
	mainForm.onMinimize();
	return true;
}

mainForm.onDestroy = function(){
	if(mainForm.tray){
		mainForm.tray.delete()
	}
}

import com.interface.ITaskbarList3;
mainForm.wndproc =  function(hwnd,message,wParam,lParam){
 		select(message) {
 			case 0xACCF/*_WM_TRAYMESSAGE*/ {
				select(lParam) {
					case 0x205/*_WM_RBUTTONUP*/ {
						win.setForeground(mainForm.hwnd)	//弹出托盘菜单以前,一定要前置主窗口中,不然不点击菜单不会消失
	    				var pt = ::POINT();
						::User32.GetCursorPos(pt);   		
	    				mainForm.popmenu2.popup(pt.x,pt.y, true);
					}
					case 0x202/*_WM_LBUTTONUP*/ {
						win.setForeground(mainForm.hwnd); //弹出托盘菜单以前,一定要前置主窗口中,不然不点击菜单不会消失							
						mainForm.show(!win.isVisible(mainForm.hwnd));
					}
				}
			}
			case _WM_TASKBARBUTTONCREATED { 
            	mainForm.taskbar = com.interface.ITaskbarList3.Create() ;
            } 
	}
}

	
/*}}*/
/*托盘右键菜单{{*/
mainForm.popmenu2 = win.ui.popmenu(mainForm);//创建弹出菜单
mainForm.popmenu2.add('&Open TBM', function(){
	//在下面输入菜单响应代码
	win.setForeground(mainForm.hwnd)
	mainForm.show();
	
});
mainForm.popmenu2.add();//分隔线
mainForm.popmenu2.add();//分隔线
mainForm.popmenu2.add('Teamcenter插件更新...', λ() thread.invoke(λ() loadcodex("\forms\Teamcenter\更新插件.aardio")));
mainForm.popmenu2.add('收集Teamcenter打开的图纸...', λ() thread.invoke(λ() loadcodex("\forms\Teamcenter\收集temp内的PDF文件放入PDF库.aardio")));
mainForm.popmenu2.add();//分隔线
//mainForm.popmenu2.add('ChatGPT...', λ() thread.invoke(λ() loadcodex("\forms\OpenAI\chatgpt.aardio")));
//mainForm.popmenu2.add('AI绘图...', λ() thread.invoke(λ() loadcodex("\forms\OpenAI\AI_Draw.aardio")));
//mainForm.popmenu2.add('修复DegMgr...', λ() seApp.fixDesMgr());
mainForm.popmenu2.add('SE导出当前截图...', λ() seApp.exportViewAsPng());
mainForm.popmenu2.add('SAP一键登入1', function(){
	thread.invoke(SAP.sapQuickOpen)
	mainForm.tray.message = 0xACCF/*_WM_TRAYMESSAGE*/
	mainForm.tray.pop("Starting SAP GUI ...","TBM");
});
mainForm.popmenu2.add('SAP一键登入2', function(){
	thread.invoke(SAP.sapQuickOpen,2)
	mainForm.tray.message = 0xACCF/*_WM_TRAYMESSAGE*/
	mainForm.tray.pop("Starting SAP GUI ...","TBM");
});
mainForm.popmenu2.add();//分隔线


mainForm.popmenu2.add('&Exit', λ() win.quitMessage());
/*}}*/
/*左侧导航按钮{{*/
_frmName = {
	[1] = "\forms\main\SE_browser.aardio";
	[2] = "\forms\main\excel_browser.aardio";
	[3] = "\forms\main\sap_browser.aardio";
	[4] = "\forms\main\excel_browser.aardio";
	[5] = "\forms\main\frmPage5.aardio";
	[6] = "\forms\main\frmPage3.aardio";
}
var menuList = {
        {
        	"\uF054,\uF078,菜单一",/*菜单图标，菜单选中时图标，菜单文本*/
        	{
     			"\uF014,\uF015,项目1",/*项目图标，项目选中时图标，项目文本*/
      			"\uF016,\uF017,项目2"
        	},
        	opened = true,/*展开*/
        	alwaysOpened =false/*总是展开*/
	    }, 
        {
        	"\uF054,\uF078,菜单二",
        	{
     			"\uF014,\uF015,项目1",
      			"\uF016,\uF017,项目2"
        	},
        	opened = false,
        	alwaysOpened = false
	    },
        {
        	"\uF054,\uF078,菜单二",
        	{
     			"\uF014,\uF015,项目1",
      			"\uF016,\uF017,项目2"
        	},
        	opened = false,
        	alwaysOpened = false
	    },
        {
        	"\uF054,\uF078,菜单二",
        	{
     			"\uF014,\uF015,项目1",
      			"\uF016,\uF017,项目2"
        	},
        	opened = false,
        	alwaysOpened = false
	    }
}
var menuTheme = {
	bgcolor = 0x4682b4,/*组件背景色*/
	hideOpenedIcon = true,/*隐藏菜单右边小三角图标*/
	useGdip = false,/*使用gdip防锯齿*/
	itemHeight = 30,/*菜单和项目高度*/
	autoClose = true,/*自动关闭非当前菜单，对alwaysOpen的菜单无效*/
	canCloseCurMenu = false,/*可以关闭当前展开的菜单，对alwaysOpen的菜单无效*/
	menuFontName = "微软雅黑",/*菜单字体名称*/
	menuFontH = 16,/*菜单字体高度，单位：像素*/
	menuFontWeight = 400,/*菜单字体粗细，0～1000，正常为400*/
	menuBgColor = 0x302D29,/*菜单背景颜色*/
	menuSelBgColor = 0x302D29,/*菜单选中时背景颜色*/
	menuTextColor = 0xD1D1D1,/*菜单文本颜色*/
	menuSelTextColor = 0xD1D1D1,/*菜单选中时文本颜色*/
	menuUnderlineColor = null,/*菜单下划线颜色，为null时没有下划线*/
	itemFontName = "微软雅黑",/*项目字体名称*/
	itemFontH = 12,/*项目字体高度，单位像素*/
	itemFontWeight = 400,/*项目字体粗细，0～1000，正常为400*/
	itemBgColor = 0x3F3C38,/*项目背景颜色*/
	itemSelBgColor = 0x6E6B67,/*项目选中时背景颜色*/
	itemTextColor = 0xD1D1D1,/*项目文本颜色*/
	itemSelTextColor = 0xD1D1D1,/*项目选中时文本颜色*/
	itemUnderlineColor = null,/*项目下划线颜色，为null时没有下划线*/
	holdSelectedItem = true/*点击菜单时，保持当前选中项目的选中状态*/
}
var navlb = godking.listboxEx(
	mainForm.listbox/*列表框组件*/,
	menuList/*菜单列表*/,
	menuTheme/*主题,内容可通过智能提示 godking.listboxEx.theme 进行修改*/
)

navlb.onClick = function(index,subindex,text){
/*
	if subindex ..win.msgbox("您点击了项目："+text);
	else ..win.msgbox("您点击了菜单："+text);
*/
}


/*}}*/



/*折叠导航按钮{{*/
mainForm.static2.tag = true;
mainForm.static2.tag2 = mainForm.custom.left;
mainForm.static2.oncommand = function(id,event){
    if (mainForm.static2.tag){
        mainForm.static2.text = '\uF03C';
    	mainForm.static2.tag = false;
    	mainForm.listbox.show(false)
		mainForm.custom.left = 0;
		
	}else{
		mainForm.static2.text = '\uF03B';
    	mainForm.static2.tag = true;
		mainForm.listbox.show(true)
		mainForm.custom.left = mainForm.static2.tag2;
	}
}

if config.settings.simpleSAPmode
	mainForm.static2.oncommand(); //simpleSAPmode
/*}}*/



mainForm.button.oncommand = function(id,event){
	seApp = solidedge.application();
	if (seApp) owner.hide = true;
}

if !config.settings.startupMinimized or !config.settings.closeToTray
		mainForm.show();
if _STUDIO_INVOKED
	mainForm.show(3/*_SW_MAXIMIZE*/);



win.loopMessage();
return mainForm;