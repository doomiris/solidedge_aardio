//printingCenter
import fonts.fontAwesome;
import win.ui;
/*DSG{{*/
var winform = win.form(text="Printing Center";right=365;bottom=275;border="dialog frame";exmode="none";max=false;min=false;mode="popup")
winform.add(
button={cls="button";text='    \uF0F6  Print Active';left=208;top=96;right=323;bottom=141;align="left";font=LOGFONT(h=-16;name='FontAwesome');tabstop=1;z=3};
button2={cls="button";text='    \uF233  Print All';left=208;top=152;right=323;bottom=197;align="left";font=LOGFONT(h=-16;name='FontAwesome');tabstop=1;z=4};
button3={cls="button";text='\uF05E  Discard';left=5;top=241;right=87;bottom=271;font=LOGFONT(name='FontAwesome');z=10};
button4={cls="button";text='\uF05B Save';left=86;top=241;right=168;bottom=271;font=LOGFONT(name='FontAwesome');z=11};
groupbox={cls="groupbox";text='\uF097  Actions';left=5;top=5;right=360;bottom=240;edge=1;font=LOGFONT(h=-16;name='FontAwesome');z=1};
listbox={cls="listbox";left=45;top=94;right=178;bottom=208;border=1;cp=1;font=LOGFONT(h=-16;name='微软雅黑');items={".pdf";".dwg";".dxf";".stp"};msel=1;z=2};
outpath={cls="static";text="d:\workspaces";left=29;top=211;right=343;bottom=236;center=1;color=8421504;notify=1;transparent=1;z=8};
progress={cls="progress";left=171;top=241;right=360;bottom=271;color=32768;db=1;dl=1;dr=1;max=100;min=0;z=9};
radNameRule0={cls="radiobutton";text="文件名只用图号";left=34;top=45;right=142;bottom=65;group=1;z=5};
radNameRule1={cls="radiobutton";text="+版本";left=165;top=45;right=226;bottom=65;z=6};
radNameRule2={cls="radiobutton";text="+版本+名称";left=251;top=45;right=339;bottom=65;z=7}
)
/*}}*/

import process;
import key;
import solidedge.application;
import solidedge.DraftDocument;
_ctrl = key.getState(key.VK.CTRL);
_shift = key.getState(key.VK.CTRL);

solidedge.application.setPdfOptions();

var seApp = solidedge.application();

seApp.DisplayAlerts = 1;

/*pringSingleFile{{*/
var startPrintActive = function(fileExt){
    for (i=1; winform.listbox.count)
    	if winform.listbox.getSelected(i)
    		fileExt := winform.listbox.getItemText(i);
    var seDFT, fname;
	seApp.DisplayAlerts = 0;
     if (seApp.ActiveDocumentType == 2 /*Draft Document Type*/) {    //仅处理DFT格式
    	seDFT = solidedge.DraftDocument(seApp.ActiveDocument);
		fname = seDFT.getNameByExt(fileExt);
		seDFT.saveToExt(fname);
	}
	seApp.DisplayAlerts = 1;
	return fname; 
}
/*}}*/
/*printAllFiles{{*/
var startPrintAll = function(){
	var Docs = seApp.Documents;
	seApp.DisplayAlerts = 0;
	for (j = Docs.Count; 1; -1){
    	if (Docs.Item(j).Type == 2 /*Draft Document Type*/) {    //仅处理DFT格式
        	var	seDFT = solidedge.DraftDocument(Docs.Item(j));
			seDFT.Activate(); //如果不Activate,只会重复保存当前文件
			for (k=1; winform.listbox.count; 1){
				winform.progress.pos += 1 ;				
				if (!winform.listbox.getSelected(k)) continue 1;
				var fname = seDFT.getNameByExt(winform.listbox.getItemText(k));
				seDFT.saveToExt(fname)
			}
    	}
	}
	seApp.DisplayAlerts = 1;
}
/*}}*/
/*closeOnEscPress{{*/
winform.isDialogMessage = function(hwnd,msg){ 
	if( msg.message == 0x100/*_WM_KEYDOWN*/){
		
/**
		if(  msg.wParam == 0xD/*_VK_RETURN*/ ){ 
			//return true;//告诉消息处理函数这是一个快捷键,阻止按键消息继续分发
		}
**/
		
		if( msg.wParam == 0x1B/*_VK_ESC*/ ){	//按Esc退出窗口
			winform.close();
			//return true;//告诉消息处理函数这是一个快捷键,阻止按键消息继续分发
		} 
	}
	
	//检测并响应默认快捷键
	return win.isDialogMessage(hwnd,msg);
}
/*}}*/
if (_ctrl) {
	var fname = startPrintActive();
	if (!_shift)
		if io.exist(fname)
			process.execute(fname);	//如果onlyActive则打开结果文件
	win.quitMessage(); 
}
winform.show();	
/*控件初始值{{*/
winform.listbox.setSelected(1);
//win.msgbox(solidedge.application.nameRule);
select(seApp.nameRule) {
	case 0 {
		winform.radNameRule0.checked = 1;
	}
	case 1 {
		winform.radNameRule1.checked = 1;
	}
	case 2 {
		winform.radNameRule2.checked = 1;
	}
}
winform.outpath.text = seApp.toFolder;
/*}}*/
/*选项保存{{*/
winform.radNameRule2.oncommand = function(id,event){
	seApp.nameRule=2
}
winform.radNameRule1.oncommand = function(id,event){
	seApp.nameRule=1
}
winform.radNameRule0.oncommand = function(id,event){
	seApp.nameRule=0
}
/*}}*/
/*button事件{{*/
winform.button.oncommand = function(id,event){
    _c = key.getState(key.VK.CTRL);
    _s = key.getState(key.VK.SHIFT);
	var fname = startPrintActive();
	if (!_c and !_s)
		if io.exist(fname)
			process.execute(fname);	//如果onlyActive则打开结果文件
	winform.close();
}
winform.button2.oncommand = function(id,event){
	winform.progress.min = 0;
	winform.progress.pos = 0;
	winform.progress.max = seApp.Documents.Count * winform.listbox.count;
	startPrintAll();
	winform.close();
}
winform.button3.oncommand = function(id,event){
	solidedge.application.closeDft(false);
	winform.close();
}
winform.button4.oncommand = function(id,event){
	solidedge.application.closeDft(true);
	winform.close();
}
/*}}*/

win.loopMessage();
return winform;