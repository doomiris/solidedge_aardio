import win.ui;
/*DSG{{*/
var winform = win.form(text="原材料用量计算 (ctrl复制板材料用量, alt复制粉末用量, ctrl+alt同时复制, 选择多行时用\分隔) 价格仅供参考";right=1215;bottom=575)
winform.add(
button={cls="button";text="计算";left=80;top=8;right=152;bottom=32;dl=1;dt=1;z=3};
edit={cls="edit";left=8;top=56;right=152;bottom=568;ah=1;db=1;dl=1;dt=1;edge=1;font=LOGFONT(h=-16);multiline=1;vscroll=1;z=1};
listview={cls="listview";left=160;top=8;right=1208;bottom=568;ah=1;aw=1;db=1;dl=1;dr=1;dt=1;edge=1;font=LOGFONT(h=-13);z=2};
static={cls="syslink";text="<a>清空列表</a>";left=8;top=32;right=64;bottom=48;dl=1;dt=1;transparent=1;z=4}
)
/*}}*/

import console
//console.open()
import win.ui.grid;
var grid = win.ui.grid(winform.listview);//创建数据视图



import QHC;

winform.button.oncommand = function(id,event){
	var pArr = string.split(winform.edit.text,'\n');
	pArr = table.unique(pArr);
	winform.edit.text = string.replace( string.join(pArr,'\n'), '\n\r\n', '\n');

	var tab=table.create(,{fields = {
									[1]="尺寸";
									[2]="物料";
									[3]="物料描述";
									[4]="钣金用量";
									[5]="粉末用量";
									[6]="钣材价格";
									[7]="喷粉后总价";
								}
							}
						);
	
	for (i=1;#pArr;1){
		if !pArr[i] continue;
		var rez = QHC.下料尺寸(pArr[i]);
		var t={}
		if !rez continue;
		t["尺寸"] = pArr[i];
		t["物料"] = rez.raw.物料 ;
		t["物料描述"] = rez.raw.物料描述 ;
		t["钣金用量"]= rez.metel;
		t["粉末用量"]= rez.powder;
		t["钣材价格"]= math.round( rez.metel * rez.raw.价格, 2);
		t["喷粉后总价"]= math.round(rez.powder * rez.powderItem.价格 + t["钣材价格"], 2);
		
		table.push(tab,t);
	}

	grid.setTable(tab)

	winform.listview.setColumn({cx=120}, 1)
	winform.listview.setColumn({cx=120}, 2)
	winform.listview.setColumn({cx=250}, 3)
	winform.listview.setColumn({cx=50}, 4)
	winform.listview.setColumn({cx=50}, 5)
	winform.listview.setColumn({cx=80}, 6)
	winform.listview.setColumn({cx=100}, 7)

}
/*CTRLC复制模块{{*/
import win.clip;
winform.listview.translateAccelerator = function( msg ){ 
    var ctrl = ::GetKeyState(0x11/*_VK_CTRL*/) & 0x8000;
//    var shift =  ::GetKeyState(0x10/*_VK_SHIFT*/) & 0x8000;
    var alt = ::GetKeyState(0x12/*_VK_ALT*/) & 0x8000;

    var vk = msg.wParam ;

	if (vk == 'C'#) && ctrl && !alt && (msg.message == 0x100/*_WM_KEYDOWN*/) {
	    	var tab = {};
	    	for (k, v in winform.listview.selected){
	        	table.push(tab, winform.listview.getItemText(v,4));
	    	}           
			win.clip.write(string.join(tab,"\"));
	}
	if (vk == 'C'#) && !ctrl && alt && (msg.message == 0x104/*_WM_SYSKEYDOWN*/){
        	var tab = {};
        	for (k, v in winform.listview.selected){
            	table.push(tab, winform.listview.getItemText(v,5));
        	}           
			win.clip.write(string.join(tab,"\"));
	}
	if (vk == 'C'#) && ctrl && alt && (msg.message == 0x100/*_WM_KEYDOWN*/){
	    	var t={};
	    	var tab1 = {};
	    	var tab2 = {};
	    	for (k, v in winform.listview.selected){
	        	table.push(tab1, winform.listview.getItemText(v,4));
	        	table.push(tab2, winform.listview.getItemText(v,5));
	    	} 
	    	table.push(t, string.join(tab1,"\"));
	    	table.push(t, string.join(tab2,"\"));
	              	
			win.clip.write(string.join(t,'\n'));
	}
}
/*}}*/

winform.static.onHyperlinkClick = function(nmSysLink,url,id){
	winform.edit.text = "";
	winform.listview.clear(true);
}

winform.show();
win.loopMessage();
return winform;