import fonts.fontAwesome;
import win.ui;
import win.ui.grid;
import win.ui.menu;
import win.ui.accelerator;
import win.cur;
import win.clip;
import win.clip.file;
import sqlite.aes256;
import fsys;
import fsys.file;
import fsys.table;
//import console;
//console.open();

//import win.util.tray;
/*DSG{{*/
winform = win.form(text="QHC物料查询";right=638;bottom=502)
winform.add(
button={cls="button";text='\uF002  查询SAP';left=521;top=10;right=620;bottom=40;default=1;dr=1;dt=1;font=LOGFONT(name='FontAwesome');tabstop=1;z=1};
button2={cls="button";text='\uF03C 过滤';left=0;top=464;right=56;bottom=504;db=1;dl=1;flat=1;font=LOGFONT(name='FontAwesome');z=15};
button3={cls="button";text='\uF053 ';left=9;top=7;right=44;bottom=41;color=16711680;font=LOGFONT(h=-16;name='FontAwesome');hide=1;z=4};
button5={cls="button";text='\uF103 ';left=477;top=39;right=515;bottom=54;color=8421504;dr=1;dt=1;flat=1;font=LOGFONT(h=-13;name='FontAwesome');z=5};
buttonD={cls="button";text='\uF142';left=235;top=10;right=249;bottom=40;color=8421504;dl=1;dt=1;flat=1;font=LOGFONT(h=-13;name='FontAwesome');z=7};
chkCost={cls="checkbox";text="显示价格";left=336;top=480;right=456;bottom=496;db=1;dl=1;hide=1;z=13};
chkDisabled={cls="checkbox";text="不显示禁用件";left=216;top=480;right=336;bottom=496;checked=1;db=1;dl=1;hide=1;z=14};
chkPaint={cls="checkbox";text="不显示已喷粉件";left=336;top=464;right=456;bottom=480;db=1;dl=1;hide=1;z=12};
editUser={cls="edit";left=250;top=10;right=515;bottom=40;autohscroll=false;autovscroll=false;aw=1;border=1;dl=1;dr=1;dt=1;font=LOGFONT(h=-13);tabstop=1;z=6};
listview={cls="listview";left=1;top=53;right=638;bottom=463;ah=1;aw=1;db=1;dl=1;dr=1;dt=1;edge=1;fullRow=1;z=3};
plus={cls="plus";left=608;top=472;right=632;bottom=496;db=1;dr=1;foreRepeat="scale";foreground="\res\communication-icon-png-2206.png";notify=1;z=8};
rdAsm={cls="radiobutton";text="仅查询组件: 08,09";left=64;top=480;right=192;bottom=496;db=1;dl=1;hide=1;z=11};
rdBG={cls="radiobutton";text="仅查询成品: BG";left=216;top=464;right=328;bottom=480;db=1;dl=1;group=1;hide=1;z=9};
rdPart={cls="radiobutton";text="仅查询物料: 01,02, ......";left=64;top=464;right=208;bottom=480;checked=1;db=1;dl=1;hide=1;z=10};
static={cls="static";text="Domisoft (C) 2020";left=520;top=464;right=624;bottom=488;align="right";aw=1;center=1;color=12632256;db=1;dl=1;dr=1;font=LOGFONT(h=-11);transparent=1;z=2};
static2={cls="static";left=264;top=480;right=624;bottom=504;align="right";aw=1;center=1;color=12632256;db=1;dl=1;dr=1;font=LOGFONT(h=-11);transparent=1;z=16}
)
/*}}*/

var grid = win.ui.grid(winform.listview);//创建数据视图
var sapTab;
/*sqliteDB{{*/
_sapdb := (function(){
    if io.exist("\qhcsap.db")
    	_path=io.fullpath("\qhcsap.db");
	else
		if io.exist("\\ccnsia1a\separts\Admin\SAP\db\QHCSAP.db")
			_path="\\ccnsia1a\separts\Admin\SAP\db\QHCSAP.db";
    	else
    		win.msgbox("无法访问数据库!");
    return _path; 
})()
winform.static2.text = _sapdb
var db,err = sqlite.aes256(_sapdb, /*"CCL100100"*/);	//打开数据库连接

if (err) error(err)
/*}}*/
/*读取设置{{*/
var logInput,setting;
winform.popmenu = win.ui.popmenu(winform);//创建弹出菜单

var readInputList = function(){
	logInput = table.array()
	setting = fsys.table(fsys.getTempDir()++"/domisoft.qtip.setting.table") 
	setting.load(); //从文件载入表
	if !(setting.inputlog) setting.inputlog = table.array()
}
var updatePopmenu = function(){
    winform.popmenu = win.ui.popmenu(winform)
	winform.popmenu.add("输入历史:");
	winform.popmenu.add();//分隔线
	for (i=1; #setting.inputlog; 1){
    	winform.popmenu.add(
    		text = setting.inputlog[i];
    		proc = function(id){
    			winform.editUser.text= setting.inputlog[i];
    			winform.editUser.setFocus();
    		}
    	);
	}
}
var saveInputList = function(str){
    if !(table.find(setting.inputlog, str)){
    	table.unshift(setting.inputlog,str);
    	if (#setting.inputlog>15) table.pop(setting.inputlog);
    	setting.save();
    };
    table.push(logInput, str);
    updatePopmenu();
};

readInputList();
updatePopmenu();
/*}}*/
/*调整列宽{{*/
var formatListView = function(){
	winform.listview.clear(true)
	grid.setTable( sapTab );
	grid.readonlyColums = {
    	[1] = true;[2] = true;[3] = true;[4] = true;[5] = true;
    }
	winform.listview.setColumn({cx=90 ;fmt=0x2/*_LVCFMT_CENTER*/},1);
	winform.listview.setColumn({cx=350;fmt=0x0/*_LVCFMT_LEFT*/},  2);
	winform.listview.setColumn({cx=60 ;fmt=0x2/*_LVCFMT_CENTER*/},3);
	winform.listview.setColumn({cx=60 ;fmt=0x2/*_LVCFMT_CENTER*/},4);
	winform.listview.setColumn({cx=50 ;fmt=0x2/*_LVCFMT_CENTER*/},5);
	winform.listview.adjust = function(cx,cy){
    	winform.listview.fillParent(2);
	} 	
}
/*}}*/
/*searchSapDB{{*/
var searchSapDB = function(userinput){
    userinput := winform.editUser.text;
    winform.button3.hide=true;
    winform.listview.tag=1;
    winform.listview.clear(true);
    winform.listview.insertColumn("正在搜索数据库, 请稍等...",300,1)
    var userinputArr = string.split(userinput, '\r\n');
    if (#userinputArr == 1) saveInputList(userinputArr[1]);
 	table.clear(sapTab);
	sapTab=table.create(,{fields={[1]="物料";[2]="物料描述";[3]="类型";[4]="价格";[5]="禁用"}});
	
    for (i=1; #userinputArr; 1){
		userinput = string.trim(userinputArr[i]);
		if (string.len(userinput)==0 ) continue;
    	if (string.startWith(userinput,"008",true)) userinput="H" ++ userinput;
    	userinput=string.replace(userinput,"@%","\%");
    	userinput=string.replace(userinput,"@_","\_");
    	userinput=string.replace(userinput,"@*","%");
    	userinput=string.replace(userinput,"@?","_");
    	var searchField;	
    	if (string.startWith(userinput,"H00",true) || string.startWith(userinput,"BG",true)) {
    		searchField = "物料";
		}else{
    		searchField = "物料描述";
		}
		
    	if (string.startWith(userinput,"BG",true)) {
    		winform.rdBG.checked = true;
    		winform.rdAsm.checked = false;
    		winform.rdPart.checked = false;
		}else {
			if (table.find({"H00808","H00809"},string.left(userinput,6))) {	//此方法无法查询08ROH物料
				winform.rdBG.checked = false;
				winform.rdAsm.checked = true;
				winform.rdPart.checked = false;
    			
			}else{
/* //逻辑需要深入完善,否则不能指定类型了
				winform.rdBG.checked = false;
				winform.rdAsm.checked = false;
				winform.rdPart.checked = true;
*/
			}
		}
		
		var sqlstr;		/* 目前结构无法智能判断 是01分类 还是08分类, TODO: 全部选项可选: 不显示xxx  */
		if (!winform.chkCost.checked)
			sqlstr = "SELECT [物料], [物料描述],[MTyp] AS [类型] FROM [所有物料] WHERE [" ++ searchField ++"] LIKE '" ++ userinput ++"'" ;
		else
			sqlstr = "SELECT [物料], [物料描述],[MTyp] AS [类型],[价格] FROM [所有物料] WHERE [" ++ searchField ++"] LIKE '" ++ userinput ++"'";

    	if (winform.rdBG.checked){
			sqlstr = sqlstr ++ " AND [MTyp] = 'FERT'" ++ " AND LENGTH([物料])=11"
		}
		if (winform.rdPart.hide==false){	//解决无法无法查询08ROH物料问题
			if (winform.rdPart.checked){
				if (winform.chkPaint.checked){
					sqlstr = sqlstr ++ " AND [物料组] NOT IN ('SG','SA','SD','SC')" ++ " AND [MTyp] NOT IN ('FERT','KMAT')"
				}else{
					sqlstr = sqlstr ++ " AND [物料组] NOT IN ('SG','SA','SD')" ++ " AND [MTyp] NOT IN ('FERT','KMAT')"
				}
			}
			if (winform.rdAsm.checked){
				sqlstr = sqlstr ++ " AND [物料组] IN ('SG','SA','SD')" ++ " AND [MTyp] NOT IN ('FERT','ROH')"
			}
			if (winform.chkDisabled.checked){
				sqlstr = sqlstr ++ " AND [物料] NOT IN (SELECT [物料] FROM [已禁用物料])";
			}
		}
		
		var t = db.getTable(sqlstr);
		//console.dumpTable(t)
		if (#t==0) table.push(t,{物料=userinput;物料描述="没找到SAP物料, 双击可继续查找图纸";物料类型="";价格=""/*;禁用=""*/});
		sapTab = table.concat(sapTab,t);
	}
	if (#sapTab==0) {
		table.push(sapTab,{物料=userinputArr[1];物料描述="没找到SAP物料, 双击可继续查找图纸";物料类型=""/*;禁用=""*/})
	}
	else {
		//todo: 在新线程里查询是否物料禁用
/*
		if (!winform.chkDisabled.checked){
			for (i=1; #sapTab){
				if db.stepQuery("SELECT * FROM [已禁用物料] where [物料]='" ++ sapTab[i].物料 ++"'") 
				sapTab[i].禁用 = "禁用";			
			}
		}	
*/
	}
	formatListView();
}
/*}}*/
/*查找图纸{{*/
var searchDocument = function (filenumber){
	
	
    if !(filenumber) return;
    winform.listview.tag = 2;
    winform.listview.clear(true);
    winform.listview.insertColumn("正在查找图纸, 请稍等...",300,1);
	var searchFor = string.trimleft(filenumber,"H");
	var fileTab = table.create(,{fields={[1]="文件名";[2]="路径";[3]="日期";[4]="版本"}});
	var flistDB = sqlite.aes256("S:\Admin\SAP\db\drawing_lists.db");	//打开数据库连接
	var sql = "SELECT [文件名], [路径] FROM [Lib] WHERE [文件名] LIKE '%" ++ searchFor ++ "%';"
	var flist = flistDB.getTable(sql);
	if (#flist == 0){
		sql = "SELECT * FROM [Map] WHERE [专用号] = '" ++ searchFor ++ "';";
		var maptab = flistDB.getTable(sql);
		if (#maptab > 0) {
			searchFor = maptab[1].图号; 
			sql = "SELECT [文件名], [路径] FROM [Lib] WHERE [文件名] LIKE '%" ++ searchFor ++ "%';";
			flist = flistDB.getTable(sql);
		}
	}
	if (#flist > 0) fileTab = table.concat(fileTab, flist);	
	winform.listview.clear(true);
	grid.setTable(fileTab);
	grid.readonlyColums = {
    	[1] = true; [2] = true; [3] = true; [4] = true;
    };
//调整列宽		
	winform.listview.setColumn({cx=100;fmt=0x2/*_LVCFMT_CENTER*/}, 1);
	winform.listview.setColumn({cx=350;fmt=0x0/*_LVCFMT_LEFT*/},   2);
	winform.listview.setColumn({cx=100;fmt=0x2/*_LVCFMT_CENTER*/}, 3);
	winform.listview.setColumn({cx=50 ;fmt=0x2/*_LVCFMT_CENTER*/}, 4);
	winform.listview.adjust = function(cx,cy){
    	winform.listview.fillParent(1);
    	winform.listview.fillParent(3);
	} 	
}
/*}}*/
/*搜索按钮事件{{*/
winform.button.oncommand = function(id,event){
	if (winform.button3.hide){		
		searchSapDB();
	}else{
		searchDocument(winform.editUser.text);		
	}
}
/*}}*/
/*双击打开事件{{*/
winform.listview.onnotify = function(id,code,ptr){
    var selectedRow = winform.listview.getSelection();
    select(code) {
    	case 0xFFFFFFFD/*_NM_DBLCLK*/ {
    		if (winform.listview.tag == 1) {
    			winform.button3.hide = false;
				winform.button3.tag  = selectedRow;
				searchDocument(winform.listview.getItemText(selectedRow,1));
				winform.button.text = '\uF002  搜索图纸';
			}else{
				var fullName = winform.listview.getItemText(selectedRow, 2) ++ winform.listview.getItemText(selectedRow, 1);
				if io.exist(fullName) {
					process.execute(fullName);
				}
			}
    	}
    }
}
/*}}*/
/*CTRLC复制模块{{*/
winform.listview.translateAccelerator = function( msg ){ 
    var ctrl = ::GetKeyState(0x11/*_VK_CTRL*/);
//    var shift =  ::GetKeyState(0x10/*_VK_SHIFT*/);
//    var alt = ::GetKeyState(0x12/*_VK_ALT*/);
//    var selectedRow = winform.listview.getSelection();
    var vk = msg.wParam ;    
    if( ( vk == 'C'# ) && ctrl  ){
        if (winform.listview.tag==1) {
            var tab=table.array();
            for (i=1;#winform.listview.selected;1){
                table.push(tab,string.join(winform.listview.items[winform.listview.selected[i]],'\t'))
            }           
			win.clip.write(string.join(tab,'\n'))
		}else{
			var tab = table.array();
			for (i=1; winform.listview.count; 1){

				if (table.find(winform.listview.selected,i)){
					table.push(tab, winform.listview.getItemText(i, 2) ++ winform.listview.getItemText(i, 1));
				}
			}

			win.clip.file.write(tab[1])	//TODO: 多文件copy
		}
        return true;
    }
}
/*}}*/
/*onclose{{*/
winform.onClose = function(hwnd,message,wParam,lParam){
	if (db) db.close();	
}
/*}}*/
/*后退返回搜索结果{{*/
winform.button3.oncommand = function(id,event){
    formatListView();
	winform.listview.setSelected(winform.button3.tag)
	winform.listview.tag=1;
	winform.button3.hide=true;	
	winform.button.text = '\uF002  查询SAP';
}
/*}}*/
/*button5输入框多行功能切换{{*/
winform.button5.oncommand = function(id,event){
	var t=winform.editUser.text;
	var a,b,c,d=winform.getScale();
	if (winform.button5.tag==1){	//恢复搜索框
		winform.button5.tag = 0;
		winform.add(
			editUser={cls="edit";left=winform.editUser.left;top=10;right=winform.editUser.right;bottom=40;autohscroll=false;autovscroll=false;aw=1;border=1;dl=1;dr=1;dt=1;tabstop=1;font=LOGFONT(h=-13);z=8};
		);
		winform.listview.top -= 60;
		winform.button5.top -= 60;
		winform.button5.height = 16;
		winform.button5.text = '\uF078'; //向下	
	}else{	//放大搜索框
		winform.button5.tag=1;
		winform.add(
			editUser={cls="edit";left=winform.editUser.left;top=10;right=winform.editUser.right;bottom=100;autohscroll=false;autovscroll=true;aw=1;border=1;dl=1;dr=1;dt=1;multiline=1;tabstop=1;font=LOGFONT(h=-13);z=8};
		);
		winform.listview.top += 60
		winform.button5.top += 60
		winform.button5.height = 16
		winform.button5.text = '\uF077'; //向上
	}
	winform.editUser.text=string.split(t,'\n\r')[[1]];
	winform.editUser.setFocus();
}
/*}}*/
/*输入框重新定位{{*/
var relocCtl = function(){
   	winform.editUser.right = winform.button5.right;
}
winform.onActivate = function(state,hwndOther,minimized){
	if(state){
		relocCtl();	
	}
}
winform.wndproc = function(hwnd,message,wparam,lparam){
    select(message) {//判断消息类型
        case 0x112/*_WM_SYSCOMMAND*/ {
            if(wparam=0xF120/*_SC_RESTORE*/){   
                relocCtl();
            } 
            if(wparam=0xF030/*_SC_MAXIMIZE*/){   
                relocCtl();
            }
        }
    }
    //无返回值则继续调用默认回调函数
}
/*}}*/
/*弹出历史记录菜单{{*/
winform.buttonD.oncommand = function(id,event){
		    var pt = ::POINT();
			::User32.GetCursorPos(pt);
	    	//弹出托盘菜单以前,一定要前置主窗口中,不然不点击菜单不会消失
	    	win.setForeground(winform.hwnd)
	    	winform.popmenu.popup(pt.x,pt.y,true )
}
/*}}*/
winform.show();
/*foot页脚设置{{*/
var getVer = function(str){
	import fsys.version;
	var v = fsys.version.getInfo(..io._exepath).productVersion;
	return str ++ ..string.join({v.major; v.minor; v.revision},"."); 
}

var hand = win.cur.load(32649 /*_IDC_HAND*/ );
winform.text = winform.text ++ getVer(" Ver: ");

/*
winform.plus.skin(
	background = { 
		hover = "/res/images/btn-hover.png";
		focus = "/res/images/btn-focus.jpg";
		active = "/res/images/btn-active.png";
		disabled = "/res/images/btn-disabled.png"; 
	}
)
*/

winform.plus.wndproc = function(hwnd,message,wParam,lParam){
	if(message = 0x200/*_WM_MOUSEMOVE*/) { 
		win.cur.setCur(hand);
	} 
}

winform.plus.oncommand = function(id,event){
	process.execute("sip:gongpeng.lan@carrier.com");
}
/*}}*/



winform.button2.oncommand = function(id,event){
	winform.rdBG.hide = !winform.rdBG.hide
	winform.rdPart.hide = !winform.rdPart.hide
	winform.rdAsm.hide = !winform.rdAsm.hide
	winform.chkCost.hide = !winform.chkCost.hide
	winform.chkDisabled.hide = !winform.chkDisabled.hide
	winform.chkPaint.hide = !winform.chkPaint.hide
}

winform.editUser.setFocus();
win.loopMessage();