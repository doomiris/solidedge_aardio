import fonts.fontAwesome;
import win.ui;
import solidedge.application;
import solidedge.DraftDocument;
import VBASettingIO;
/*DSG{{*/
var winform = win.form(text="aardio form";right=501;bottom=354;bgcolor=16777215)
winform.add(
bkplus={cls="bkplus";left=0;top=0;right=496;bottom=352;ah=1;aw=1;background="C:\Program Files\Siemens\Solid Edge 2020\Program\ResDLLs\0009\images\SE BG.png";db=1;dl=1;dr=1;dt=1;z=1};
button={cls="button";text='\uF062';left=80;top=145;right=103;bottom=172;dl=1;dt=1;font=LOGFONT(name='FontAwesome');z=10};
button2={cls="button";text='\uF00C  OK';left=387;top=306;right=488;bottom=345;db=1;default=1;dr=1;font=LOGFONT(name='FontAwesome');z=17};
button3={cls="button";text='\uF00D  Cancel';left=280;top=306;right=381;bottom=345;db=1;dr=1;font=LOGFONT(name='FontAwesome');z=18};
button4={cls="button";text="更新日期";left=192;top=312;right=264;bottom=336;z=28};
cb_approve_date={cls="edit";text="Edit";left=189;top=274;right=277;bottom=298;dl=1;dt=1;edge=1;z=16};
cb_approver={cls="combobox";left=46;top=272;right=177;bottom=298;dl=1;dt=1;edge=1;items={};mode="dropdown";z=14};
cb_design_date={cls="edit";text="Edit";left=188;top=185;right=276;bottom=209;dl=1;dt=1;edge=1;z=11};
cb_designer={cls="combobox";left=47;top=185;right=177;bottom=211;dl=1;dt=1;edge=1;items={};mode="dropdown";z=12};
cb_material={cls="combobox";left=47;top=103;right=235;bottom=129;dl=1;dt=1;edge=1;items={};mode="dropdown";z=4};
cb_model_no={cls="combobox";left=300;top=58;right=488;bottom=84;dr=1;dt=1;edge=1;items={};mode="dropdown";z=3};
cb_name_cn={cls="combobox";left=47;top=58;right=235;bottom=84;dl=1;dt=1;edge=1;items={};mode="dropdown";z=2};
cb_review_date={cls="edit";text="Edit";left=189;top=227;right=277;bottom=251;dl=1;dt=1;edge=1;z=15};
cb_reviewer={cls="combobox";left=47;top=227;right=177;bottom=253;dl=1;dt=1;edge=1;items={};mode="dropdown";z=13};
cb_tol={cls="combobox";left=340;top=148;right=488;bottom=174;dr=1;dt=1;edge=1;items={"GB/T 1804-m";"GB/T 1804-c";"GB/T 1804-v"};mode="dropdown";z=19};
cb_weight={cls="combobox";left=300;top=103;right=488;bottom=129;dr=1;dt=1;edge=1;items={};mode="dropdown";z=5};
chkPaint={cls="checkbox";text="喷粉标准 Q/LD J52005";left=118;top=148;right=296;bottom=168;dl=1;dt=1;z=9};
groupbox={cls="groupbox";text='\uF016  当前文件';left=16;top=9;right=488;bottom=47;aw=1;dl=1;dr=1;dt=1;edge=1;font=LOGFONT(name='FontAwesome');z=6};
seDocPath={cls="static";text="Static";left=25;top=24;right=476;bottom=41;align="right";aw=1;dl=1;dr=1;dt=1;transparent=1;z=7};
static={cls="static";text="设计";left=17;top=190;right=47;bottom=209;dl=1;dt=1;transparent=1;z=20};
static2={cls="static";text="审核";left=18;top=231;right=48;bottom=250;dl=1;dt=1;transparent=1;z=21};
static3={cls="static";text="批准";left=17;top=277;right=47;bottom=296;dl=1;dt=1;transparent=1;z=22};
static4={cls="static";text="名称";left=17;top=63;right=47;bottom=82;dl=1;dt=1;transparent=1;z=23};
static5={cls="static";text="材料";left=16;top=106;right=46;bottom=125;dl=1;dt=1;transparent=1;z=24};
static6={cls="static";text="型号";left=269;top=63;right=299;bottom=82;dr=1;dt=1;transparent=1;z=25};
static7={cls="static";text="重量";left=269;top=105;right=299;bottom=124;dr=1;dt=1;transparent=1;z=26};
static8={cls="static";text="版本";left=19;top=149;right=46;bottom=168;dl=1;dt=1;transparent=1;z=27};
version={cls="edit";text="Edit";left=47;top=146;right=80;bottom=172;dl=1;dt=1;edge=1;font=LOGFONT(h=-13);z=8}
)
/*}}*/

/*initDeclare{{*/
var seApp = solidedge.application();
if (seApp.Documents.Count==0) return; 
if (seApp.ActiveDocumentType !== 2 /*Draft Document Type*/) return; 
var seDFT = solidedge.DraftDocument(seApp.ActiveDocument);
if (seDFT.Sheets.Count == 0) return ; 
var seSht = seDFT.ActiveSheet;
if (seSht.BlockOccurrences.Count == 0) return;
/*}}*/
/*loadFromTab{{*/
var loadFromTab = function(_blk){
	winform.cb_name_cn.text = _blk["零件名称"];
	winform.cb_model_no.text = _blk["型号/项目名称"];
	winform.cb_material.text = _blk["材料"];
	winform.cb_weight.text = _blk["质量/体积"];
	_blk["版本"] := solidedge.DraftDocument.getVerFromBlkCorner(seBlk);
	_blk["版本"] := solidedge.DraftDocument.getVerFromShtCorner(seSht);
	winform.version.text = _blk["版本"];
	winform.chkPaint.checked = (_blk["喷粉标准"]=="Q/LD J52005");
	winform.cb_tol.text = _blk["公差等级"];
	winform.cb_designer.text = _blk["设计"];
	winform.cb_design_date.text = _blk["设计日期"];
	winform.cb_reviewer.text = _blk["审核"];
	winform.cb_review_date.text = _blk["审核日期"];
	winform.cb_approver.text = _blk["批准"];
	winform.cb_approve_date.text = _blk["批准日期"];

}
/*}}*/
/*loadDefaults{{*/
var loadDefaults = function(){
	if (winform.cb_designer.text == "" or winform.cb_designer.text == "-")
		winform.cb_designer.text = VBASettingIO.GetSetting("Domisoft", "TBM_SE", "Default_Designer", "");
	if (winform.cb_reviewer.text == "" or winform.cb_reviewer.text == "-")
		winform.cb_reviewer.text = VBASettingIO.GetSetting("Domisoft", "TBM_SE", "Default_Reviewer", "")
	if (winform.cb_approver.text == "" or winform.cb_approver.text == "-")
		winform.cb_approver.text = VBASettingIO.GetSetting("Domisoft", "TBM_SE", "Default_Approver", "")
	if (winform.cb_design_date.text == "YYYY.MM.DD" or winform.cb_design_date.text == "" or winform.cb_design_date.text == "2016.11.19")
		winform.cb_design_date.text = time(,"%Y.%m.%d")
	if (winform.cb_review_date.text == "YYYY.MM.DD" or winform.cb_review_date.text == "" or winform.cb_review_date.text == "2016.11.18")
		winform.cb_review_date.text = time(,"%Y.%m.%d")
	if (winform.cb_approve_date.text == "YYYY.MM.DD" or winform.cb_approve_date.text == "" or winform.cb_approve_date.text == "2016.11.18")
		winform.cb_approve_date.text = time(,"%Y.%m.%d")
	if (winform.version.text == "") winform.version.text = "00"
}
/*}}*/
/*loadLists{{*/
var loadPromptList = function(){
	_dbpath = VBASettingIO.GetSetting("Domisoft", "Config", "Spec_db_path", "");
	import access;
	var dbRs = access(_dbpath);
	var tab = dbRs.getTable("select * from SE_TBM_Lists");
	for (i=1; #tab; 1) {
		select(tab[i].ListName) {
			case "name_cn" {
				winform.cb_name_cn.add(tab[i].Title)
			}
			case "model_no" {
				winform.cb_model_no.add(tab[i].Title)
			}
			case "material" {
				winform.cb_material.add(tab[i].Title)
			}
			case "weight" {
				winform.cb_weight.add(tab[i].Title)
			}
			case "designer" {
				winform.cb_designer.add(tab[i].Title)
			}
			case "reviewer" {
				winform.cb_reviewer.add(tab[i].Title)
			}
			case "approver" {
				winform.cb_approver.add(tab[i].Title)
			}
		}
	}
}
/*}}*/
var updateDate =function(){
    winform.cb_design_date.text = time(,"%Y.%m.%d");
    winform.cb_review_date.text = time(,"%Y.%m.%d");
	winform.cb_approve_date.text = time(,"%Y.%m.%d");
}

var writeToDrw = function(_blk){
	var seLbs = _blk.BlockLabelOccurrences;
	for (i=1; seLbs.Count; 1) {
		select(seLbs.item(i).Name) {
        	case "型号/项目名称" {
            	seLbs.item(i).Value = winform.cb_model_no.text
            }
        	case "零件名称", "名称(中文)"{
            	seLbs.item(i).Value = winform.cb_name_cn.text
            }
        	case "材料"{
            	seLbs.item(i).Value = winform.cb_material.text
            }
        	case "质量/体积"{
            	seLbs.item(i).Value = winform.cb_weight.text
            }
        	case "喷粉标准"{
            	if (winform.chkPaint.checked) seLbs.item(i).Value = "Q/LD J52005"
            }
        	case "公差等级"{
            	seLbs.item(i).Value = winform.cb_tol.text
            }
        	case "设计"{
            	seLbs.item(i).Value = winform.cb_designer.text
            }
        	case "审核"{
            	seLbs.item(i).Value = winform.cb_reviewer.text
            }
        	case "批准"{
            	seLbs.item(i).Value = winform.cb_approver.text
            }
        	case "设计日期"{
            	seLbs.item(i).Value = winform.cb_design_date.text
            }
        	case "审核日期"{
            	seLbs.item(i).Value = winform.cb_review_date.text
            }
        	case "批准日期"{
            	seLbs.item(i).Value = winform.cb_approve_date.text
            }
        	case "版本"{
            	seLbs.item(i).Value = winform.version.text
            }
		}
	}
}

winform.button3.oncommand = function(id,event){
	win.quitMessage();
}

winform.button2.oncommand = function(id,event){
    writeToDrw(seDFT.getTitleBlock(seSht));
	win.quitMessage();
}

winform.button.oncommand = function(id,event){
    var v = tonumber(winform.version.text);
	v += 1;
	winform.version.text =  (v >= 10) ? v : "0" ++ v;
}
winform.button4.oncommand = function(id,event){
	updateDate();
}
winform.show();
var seBlk = seDFT.getTitleBlock(seSht);
var seLbs = seDFT.getLabelTable(seBlk);
loadFromTab(seLbs);
loadDefaults();
winform.seDocPath.text = seDFT.FullName;
loadPromptList();
win.loopMessage();
return winform;