//import fonts.fontAwesome;
//import win.ui;
//import solidedge.application;
//import solidedge.DraftDocument;
//import access;
/*DSG{{*/
var winform = win.form(text="aardio form";right=501;bottom=354;image=$"\SE BG.jpg";bkBottom=0;bkLeft=0;bkRight=0;bkTop=0)
winform.add(
button={cls="button";text='\uF062';left=72;top=144;right=96;bottom=167;bgcolor=16777215;dl=1;dt=1;flat=1;font=LOGFONT(name='FontAwesome');z=9};
button2={cls="button";text='\uF00C  OK';left=387;top=306;right=488;bottom=345;db=1;default=1;dr=1;font=LOGFONT(name='FontAwesome');z=16};
button3={cls="button";text='\uF00D  Cancel';left=280;top=306;right=381;bottom=345;db=1;dr=1;font=LOGFONT(name='FontAwesome');z=17};
button4={cls="button";text='\uF017 ';left=305;top=184;right=329;bottom=208;dl=1;dt=1;font=LOGFONT(name='FontAwesome');hide=1;z=27};
button5={cls="button";text='\uF0C7 ';left=178;top=184;right=202;bottom=208;dl=1;dt=1;font=LOGFONT(name='FontAwesome');hide=1;z=28};
cb_approve_date={cls="edit";left=215;top=248;right=300;bottom=272;dl=1;dt=1;z=15};
cb_approver={cls="combobox";left=48;top=245;right=178;bottom=270;dl=1;dt=1;items={};mode="dropdown";z=13};
cb_design_date={cls="edit";left=215;top=184;right=300;bottom=208;dl=1;dt=1;z=10};
cb_designer={cls="combobox";left=48;top=185;right=178;bottom=210;dl=1;dt=1;items={};mode="dropdown";z=11};
cb_material={cls="combobox";left=47;top=103;right=235;bottom=129;dl=1;dt=1;edge=1;items={};mode="dropdown";z=3};
cb_model_no={cls="combobox";left=300;top=58;right=488;bottom=84;dr=1;dt=1;edge=1;items={};mode="dropdown";z=2};
cb_name_cn={cls="combobox";left=47;top=58;right=235;bottom=84;dl=1;dt=1;edge=1;items={};mode="dropdown";z=1};
cb_review_date={cls="edit";left=215;top=216;right=300;bottom=240;dl=1;dt=1;z=14};
cb_reviewer={cls="combobox";left=48;top=215;right=178;bottom=240;dl=1;dt=1;items={};mode="dropdown";z=12};
cb_tol={cls="combobox";left=340;top=148;right=488;bottom=174;dr=1;dt=1;edge=1;items={"GB/T 1804-m";"GB/T 1804-c";"GB/T 1804-v"};mode="dropdown";z=18};
cb_weight={cls="combobox";left=300;top=103;right=488;bottom=129;dr=1;dt=1;edge=1;items={};mode="dropdown";z=4};
chkPaint={cls="checkbox";text="喷粉标准 Q/LD J52005";left=136;top=152;right=314;bottom=172;bgcolor=16777215;dl=1;dt=1;z=8};
chkReplaceTB={cls="checkbox";text="自动替换成新版图框";left=344;top=272;right=480;bottom=296;checked=1;color=16711680;db=1;dr=1;hide=1;z=29};
groupbox={cls="groupbox";text='\uF016  当前文件';left=16;top=9;right=488;bottom=47;aw=1;dl=1;dr=1;dt=1;edge=1;font=LOGFONT(name='FontAwesome');z=5};
rd_M={cls="radiobutton";text="自制件";left=24;top=328;right=96;bottom=344;bgcolor=16777215;db=1;dl=1;group=1;z=30};
rd_P={cls="radiobutton";text="外购件";left=96;top=328;right=160;bottom=344;bgcolor=16777215;db=1;dl=1;z=31};
seDocPath={cls="static";left=25;top=24;right=476;bottom=41;align="right";aw=1;dl=1;dr=1;dt=1;transparent=1;z=6};
static={cls="static";text="设计";left=15;top=190;right=45;bottom=209;dl=1;dt=1;transparent=1;z=19};
static2={cls="static";text="审核";left=15;top=220;right=45;bottom=243;dl=1;dt=1;transparent=1;z=20};
static3={cls="static";text="批准";left=15;top=250;right=45;bottom=275;dl=1;dt=1;transparent=1;z=21};
static4={cls="static";text="名称";left=15;top=63;right=45;bottom=82;dl=1;dt=1;transparent=1;z=22};
static5={cls="static";text="材料";left=15;top=106;right=45;bottom=125;dl=1;dt=1;transparent=1;z=23};
static6={cls="static";text="型号";left=269;top=63;right=299;bottom=82;dr=1;dt=1;transparent=1;z=24};
static7={cls="static";text="重量";left=269;top=105;right=299;bottom=124;dr=1;dt=1;transparent=1;z=25};
static8={cls="static";text="版本";left=15;top=149;right=45;bottom=168;dl=1;dt=1;transparent=1;z=26};
version={cls="edit";left=48;top=144;right=72;bottom=170;align="center";dl=1;dt=1;font=LOGFONT(h=-13);z=7}
)
/*}}*/

/*initDeclare{{*/
var seApp, seDFT, seSht, seBlk, seLbs;
var initDeclare = function(){
	seApp := solidedge.application();
	if (! seApp) return false; 
	if (seApp.Documents.Count==0) return false; 
	if (seApp.ActiveDocumentType !== 2 /*Draft Document Type*/) return false; 
	seDFT = solidedge.DraftDocument(seApp.ActiveDocument);
	if (seDFT.Sheets.Count == 0) return false ; 
	seSht = seDFT.ActiveSheet;
	seBlk = seDFT.getTitleBlock(seSht); //获取titleblock		
	if !seBlk{
    	var x,y = seDFT.getTitleBlockOrigin(seSht);
    	for (i=1; seDFT.Blocks.Count; 1) {
        	if (seDFT.Blocks.item(i).name=="Title 2020") {
    			seBlk = seSht.BlockOccurrences.Add("Title 2020", x, y);
    			break;
    		}
    	}
    	if !seBlk{
    		win.msgbox("找不到Title Blocks定义");
    	}
	}	
	if ( seBlk ) seLbs = seDFT.getLabelTable(seBlk);	//获取titleblock lable
	return true; 
}
/*}}*/
/*loadFromTab{{*/
var loadFromTab = function(_blk){
	winform.cb_name_cn.text = _blk["零件名称"];
	winform.cb_model_no.text = _blk["型号/项目名称"];
	winform.cb_material.text = _blk["材料"];
	winform.cb_weight.text = _blk["质量/体积"];
	_blk["版本"] := seDFT.getVerFromBlkCorner(seBlk);
	_blk["版本"] := seDFT.getVerFromShtCorner(seSht);
	winform.version.text = _blk["版本"];
	winform.chkPaint.checked = (_blk["喷粉标准"]=="Q/LD J52005");
	winform.cb_tol.text = _blk["公差等级"];
	winform.cb_designer.text = _blk["设计"];
	winform.cb_design_date.text = _blk["设计日期"];
	winform.cb_reviewer.text = _blk["审核"];
	winform.cb_review_date.text = _blk["审核日期"];
	winform.cb_approver.text = _blk["批准"];
	winform.cb_approve_date.text = _blk["批准日期"];


}
/*}}*/
/*loadDefaults{{*/
var loadDefaults = function(){
	if (winform.cb_designer.text == "" or winform.cb_designer.text == "-")
		winform.cb_designer.text = VBASettingIO.GetSetting("Domisoft", "TBM_SE", "Default_Designer", "");
	if (winform.cb_reviewer.text == "" or winform.cb_reviewer.text == "-")
		winform.cb_reviewer.text = VBASettingIO.GetSetting("Domisoft", "TBM_SE", "Default_Reviewer", "");
	if (winform.cb_approver.text == "" or winform.cb_approver.text == "-")
		winform.cb_approver.text = VBASettingIO.GetSetting("Domisoft", "TBM_SE", "Default_Approver", "");
	if (winform.cb_design_date.text == "YYYY.MM.DD" or winform.cb_design_date.text == "" or winform.cb_design_date.text == "2016.11.19")
		winform.cb_design_date.text = tostring(time(,"%Y.%m.%d"));;
	if (winform.cb_review_date.text == "YYYY.MM.DD" or winform.cb_review_date.text == "" or winform.cb_review_date.text == "2016.11.18")
		winform.cb_review_date.text = tostring(time(,"%Y.%m.%d"));;
	if (winform.cb_approve_date.text == "YYYY.MM.DD" or winform.cb_approve_date.text == "" or winform.cb_approve_date.text == "2016.11.18")
		winform.cb_approve_date.text = tostring(time(,"%Y.%m.%d"));;
	if (winform.version.text == "") winform.version.text = "00";
}
/*}}*/
/*loadLists{{*/
var loadPromptList = function(){
	_dbpath = VBASettingIO.GetSetting("Domisoft", "Config", "Spec_db_path", "");

	var dbRs = access(_dbpath);
	var tab = dbRs.getTable("select * from SE_TBM_Lists");
	for (i=1; #tab; 1) {
		select(tab[i].ListName) {
			case "name_cn" {
				winform.cb_name_cn.add(tab[i].Title)
			}
			case "model_no" {
				winform.cb_model_no.add(tab[i].Title)
			}
			case "material" {
				winform.cb_material.add(tab[i].Title)
			}
			case "weight" {
				winform.cb_weight.add(tab[i].Title)
			}
			case "designer" {
				winform.cb_designer.add(tab[i].Title)
			}
			case "reviewer" {
				winform.cb_reviewer.add(tab[i].Title)
			}
			case "approver" {
				winform.cb_approver.add(tab[i].Title)
			}
		}
	}
}
/*}}*/
/*其它基础函数{{*/
var updateDate = function(){
    var t = tostring(time(,"%Y.%m.%d"));;
    winform.cb_design_date.text = t;
    winform.cb_review_date.text = t;
	winform.cb_approve_date.text = t;
}
var showTimeBtn = function(){
    var t = tostring(time(,"%Y.%m.%d"));
	winform.button4.hide = (
		winform.cb_design_date.text == t
		and
		winform.cb_review_date.text == t
		and 
		winform.cb_approve_date.text == t
	
	) ? 1: 0;
}
var showSaveNameBtn = function(){
	var Default_Designer = VBASettingIO.GetSetting("Domisoft", "TBM_SE", "Default_Designer", "");
	var Default_Reviewer = VBASettingIO.GetSetting("Domisoft", "TBM_SE", "Default_Reviewer", "");
	var Default_Approver = VBASettingIO.GetSetting("Domisoft", "TBM_SE", "Default_Approver", "");
	winform.button5.hide = (
		winform.cb_designer.text == Default_Designer
		and
		winform.cb_reviewer.text == Default_Reviewer
		and
		winform.cb_approver.text == Default_Approver
	) ? 1 : 0;
}
var writeToDrw = function(Lbs){
    Lbs["型号/项目名称"].Value = winform.cb_model_no.text;
    Lbs["零件名称"].Value = winform.cb_name_cn.text;
    Lbs["材料"].Value = winform.cb_material.text;
    Lbs["质量/体积"].Value = winform.cb_weight.text;
    Lbs["喷粉标准"].Value = winform.chkPaint.checked ? "Q/LD J52005" : "";
    Lbs["公差等级"].Value = winform.cb_tol.text;
    Lbs["设计"].Value = winform.cb_designer.text;
    Lbs["审核"].Value = winform.cb_reviewer.text;
    Lbs["批准"].Value = winform.cb_approver.text;
    Lbs["设计日期"].Value = winform.cb_design_date.text;
    Lbs["审核日期"].Value = winform.cb_review_date.text;
    Lbs["批准日期"].Value = winform.cb_approve_date.text;
    Lbs["版本"].Value = winform.version.text;
}
var iniFrmCTRL = function(){
	if (seBlk.Block.Name !== "Title 2020")    winform.chkReplaceTB.hide = 0;

	select(seDFT.getSourcingType(seDFT.getFirstSheet())) {
    	case "P" {
    		winform.rd_P.checked =1;
    	}
    	case "M" {
    		winform.rd_M.checked = 1;
    	}
    	else {
    		winform.rd_P.hide = 1;
    		winform.rd_M.hide = 1;
    	}
	}
}

/*}}*/
/*控件事件{{*/
winform.button3.oncommand = function(id,event){
    mainForm.show(false); //隐藏窗口
	win.setActive(seApp.hWnd);
}
winform.button2.oncommand = function(id,event){
    mainForm.show(false); //隐藏窗口
	win.setActive(seApp.hWnd);
	
	if (winform.chkReplaceTB.hide == 0){
		if (winform.chkReplaceTB.checked){
			solidedge.DraftDocument.replaceNewTitleBlock(seDFT, seDFT.ActiveSheet);
			seBlk = seDFT.getTitleBlock(seSht);
		}
	}
    writeToDrw(seDFT.getLabelObjectTable(seDFT.getTitleBlock(seSht)));
	{
		var blkObj = seDFT.getLabelObjectTable(seDFT.getBlkByName("Version_CCR 2020",seSht));
		if blkObj {
			if (winform.rd_P.checked == 1) blkObj["属性"].value = "P";
			if (winform.rd_M.checked == 1) blkObj["属性"].value = "M";
		}
	
	}
	
    if (VBASettingIO.GetSetting("Domisoft", "TBM_SE", "Default_Designer", "")=="") {
		winform.button5.oncommand();    /* 智能保存常用姓名 */
    };
    

}
winform.button.oncommand = function(id,event){
    var v = tonumber(winform.version.text);
	v += 1;
	winform.version.text =  (v >= 10) ? v : "0" ++ v;
}
winform.button4.oncommand = function(id,event){
	updateDate();
}
winform.button5.oncommand = function(id,event){
    VBASettingIO.SaveSetting("Domisoft", "TBM_SE", "Default_Designer", winform.cb_designer.text);
	VBASettingIO.SaveSetting("Domisoft", "TBM_SE", "Default_Reviewer", winform.cb_reviewer.text);
	VBASettingIO.SaveSetting("Domisoft", "TBM_SE", "Default_Approver", winform.cb_approver.text);
}
winform.cb_designer.onEditChange= function(){ 
	showSaveNameBtn();
	winform.button5.top = winform.cb_designer.top - 1;
	winform.button5.bottom = winform.cb_designer.bottom - 3;
	
}
winform.cb_reviewer.onEditChange= function(){ 
	showSaveNameBtn();
	winform.button5.top = winform.cb_reviewer.top - 1;
	winform.button5.bottom = winform.cb_reviewer.bottom - 3;
}
winform.cb_approver.onEditChange= function(){ 
	showSaveNameBtn();
	winform.button5.top = winform.cb_approver.top - 1;
	winform.button5.bottom = winform.cb_approver.bottom - 3;
}

/*}}*/
winform.show();
var frmReLoad = function(){
    var initSucc = initDeclare();
    winform.seDocPath.text = initSucc;
	if  (initSucc){
		iniFrmCTRL();
		loadFromTab(seLbs);	//读取当前内容
		loadDefaults();	//读取默认值(新图纸)
		showTimeBtn();
		showSaveNameBtn();
		winform.seDocPath.text = seDFT.FullName;
	}
}
frmReLoad();
loadPromptList();	//读取提示清单
win.loopMessage();
return winform;