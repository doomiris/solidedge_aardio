import fonts.fontAwesome;
import win.ui;
import solidedge.application;
import solidedge.DraftDocument;
import VBASettingIO;
/*DSG{{*/
var winform = win.form(text="aardio form";right=501;bottom=354;image=$"\SE BG.jpg";bkBottom=0;bkLeft=0;bkRight=0;bkTop=0)
winform.add(
button={cls="button";text='\uF062';left=80;top=145;right=103;bottom=172;dl=1;dt=1;font=LOGFONT(name='FontAwesome');z=9};
button2={cls="button";text='\uF00C  OK';left=387;top=306;right=488;bottom=345;db=1;default=1;dr=1;font=LOGFONT(name='FontAwesome');z=16};
button3={cls="button";text='\uF00D  Cancel';left=280;top=306;right=381;bottom=345;db=1;dr=1;font=LOGFONT(name='FontAwesome');z=17};
button4={cls="button";text='\uF017 ';left=305;top=184;right=329;bottom=208;dl=1;dt=1;font=LOGFONT(name='FontAwesome');z=27};
button5={cls="button";text='\uF0C7 ';left=178;top=184;right=202;bottom=208;dl=1;dt=1;font=LOGFONT(name='FontAwesome');z=28};
cb_approve_date={cls="edit";text="Edit";left=216;top=272;right=304;bottom=296;dl=1;dt=1;edge=1;z=15};
cb_approver={cls="combobox";left=46;top=272;right=177;bottom=298;dl=1;dt=1;edge=1;items={};mode="dropdown";z=13};
cb_design_date={cls="edit";text="Edit";left=216;top=184;right=304;bottom=208;dl=1;dt=1;edge=1;z=10};
cb_designer={cls="combobox";left=47;top=185;right=177;bottom=211;dl=1;dt=1;edge=1;items={};mode="dropdown";z=11};
cb_material={cls="combobox";left=47;top=103;right=235;bottom=129;dl=1;dt=1;edge=1;items={};mode="dropdown";z=3};
cb_model_no={cls="combobox";left=300;top=58;right=488;bottom=84;dr=1;dt=1;edge=1;items={};mode="dropdown";z=2};
cb_name_cn={cls="combobox";left=47;top=58;right=235;bottom=84;dl=1;dt=1;edge=1;items={};mode="dropdown";z=1};
cb_review_date={cls="edit";text="Edit";left=216;top=224;right=304;bottom=248;dl=1;dt=1;edge=1;z=14};
cb_reviewer={cls="combobox";left=47;top=227;right=177;bottom=253;dl=1;dt=1;edge=1;items={};mode="dropdown";z=12};
cb_tol={cls="combobox";left=340;top=148;right=488;bottom=174;dr=1;dt=1;edge=1;items={"GB/T 1804-m";"GB/T 1804-c";"GB/T 1804-v"};mode="dropdown";z=18};
cb_weight={cls="combobox";left=300;top=103;right=488;bottom=129;dr=1;dt=1;edge=1;items={};mode="dropdown";z=4};
chkPaint={cls="checkbox";text="喷粉标准 Q/LD J52005";left=118;top=148;right=296;bottom=168;dl=1;dt=1;z=8};
chkReplaceTB={cls="checkbox";text="自动替换成新版图框";left=344;top=272;right=480;bottom=296;checked=1;color=16711680;db=1;dr=1;hide=1;z=29};
groupbox={cls="groupbox";text='\uF016  当前文件';left=16;top=9;right=488;bottom=47;aw=1;dl=1;dr=1;dt=1;edge=1;font=LOGFONT(name='FontAwesome');z=5};
seDocPath={cls="static";text="Static";left=25;top=24;right=476;bottom=41;align="right";aw=1;dl=1;dr=1;dt=1;transparent=1;z=6};
static={cls="static";text="设计";left=17;top=190;right=47;bottom=209;dl=1;dt=1;transparent=1;z=19};
static2={cls="static";text="审核";left=18;top=231;right=48;bottom=250;dl=1;dt=1;transparent=1;z=20};
static3={cls="static";text="批准";left=17;top=277;right=47;bottom=296;dl=1;dt=1;transparent=1;z=21};
static4={cls="static";text="名称";left=17;top=63;right=47;bottom=82;dl=1;dt=1;transparent=1;z=22};
static5={cls="static";text="材料";left=16;top=106;right=46;bottom=125;dl=1;dt=1;transparent=1;z=23};
static6={cls="static";text="型号";left=269;top=63;right=299;bottom=82;dr=1;dt=1;transparent=1;z=24};
static7={cls="static";text="重量";left=269;top=105;right=299;bottom=124;dr=1;dt=1;transparent=1;z=25};
static8={cls="static";text="版本";left=19;top=149;right=46;bottom=168;dl=1;dt=1;transparent=1;z=26};
version={cls="edit";text="Edit";left=47;top=146;right=80;bottom=172;dl=1;dt=1;edge=1;font=LOGFONT(h=-13);z=7}
)
/*}}*/

/*initDeclare{{*/
var seApp = solidedge.application();
if (seApp.Documents.Count==0) return; 
if (seApp.ActiveDocumentType !== 2 /*Draft Document Type*/) return; 
var seDFT = solidedge.DraftDocument(seApp.ActiveDocument);
if (seDFT.Sheets.Count == 0) return ; 
var seSht = seDFT.ActiveSheet;
//if (seSht.BlockOccurrences.Count == 0) return;
/*}}*/
/*loadFromTab{{*/
var loadFromTab = function(_blk){
	winform.cb_name_cn.text = _blk["零件名称"];
	winform.cb_model_no.text = _blk["型号/项目名称"];
	winform.cb_material.text = _blk["材料"];
	winform.cb_weight.text = _blk["质量/体积"];
	_blk["版本"] := seDFT.getVerFromBlkCorner(seBlk);
	_blk["版本"] := seDFT.getVerFromShtCorner(seSht);
	winform.version.text = _blk["版本"];
	winform.chkPaint.checked = (_blk["喷粉标准"]=="Q/LD J52005");
	winform.cb_tol.text = _blk["公差等级"];
	winform.cb_designer.text = _blk["设计"];
	winform.cb_design_date.text = _blk["设计日期"];
	winform.cb_reviewer.text = _blk["审核"];
	winform.cb_review_date.text = _blk["审核日期"];
	winform.cb_approver.text = _blk["批准"];
	winform.cb_approve_date.text = _blk["批准日期"];

}
/*}}*/
/*loadDefaults{{*/
var loadDefaults = function(){
	if (winform.cb_designer.text == "" or winform.cb_designer.text == "-")
		winform.cb_designer.text = VBASettingIO.GetSetting("Domisoft", "TBM_SE", "Default_Designer", "");
	if (winform.cb_reviewer.text == "" or winform.cb_reviewer.text == "-")
		winform.cb_reviewer.text = VBASettingIO.GetSetting("Domisoft", "TBM_SE", "Default_Reviewer", "")
	if (winform.cb_approver.text == "" or winform.cb_approver.text == "-")
		winform.cb_approver.text = VBASettingIO.GetSetting("Domisoft", "TBM_SE", "Default_Approver", "")
	if (winform.cb_design_date.text == "YYYY.MM.DD" or winform.cb_design_date.text == "" or winform.cb_design_date.text == "2016.11.19")
		winform.cb_design_date.text = time(,"%Y.%m.%d")
	if (winform.cb_review_date.text == "YYYY.MM.DD" or winform.cb_review_date.text == "" or winform.cb_review_date.text == "2016.11.18")
		winform.cb_review_date.text = time(,"%Y.%m.%d")
	if (winform.cb_approve_date.text == "YYYY.MM.DD" or winform.cb_approve_date.text == "" or winform.cb_approve_date.text == "2016.11.18")
		winform.cb_approve_date.text = time(,"%Y.%m.%d")
	if (winform.version.text == "") winform.version.text = "00"
}
/*}}*/
/*loadLists{{*/
var loadPromptList = function(){
	_dbpath = VBASettingIO.GetSetting("Domisoft", "Config", "Spec_db_path", "");
	import access;
	var dbRs = access(_dbpath);
	var tab = dbRs.getTable("select * from SE_TBM_Lists");
	for (i=1; #tab; 1) {
		select(tab[i].ListName) {
			case "name_cn" {
				winform.cb_name_cn.add(tab[i].Title)
			}
			case "model_no" {
				winform.cb_model_no.add(tab[i].Title)
			}
			case "material" {
				winform.cb_material.add(tab[i].Title)
			}
			case "weight" {
				winform.cb_weight.add(tab[i].Title)
			}
			case "designer" {
				winform.cb_designer.add(tab[i].Title)
			}
			case "reviewer" {
				winform.cb_reviewer.add(tab[i].Title)
			}
			case "approver" {
				winform.cb_approver.add(tab[i].Title)
			}
		}
	}
}
/*}}*/
var updateDate =function(){
    winform.cb_design_date.text = time(,"%Y.%m.%d");
    winform.cb_review_date.text = time(,"%Y.%m.%d");
	winform.cb_approve_date.text = time(,"%Y.%m.%d");
}

var writeToDrw = function(seLbs){
    seLbs["型号/项目名称"].Value = winform.cb_model_no.text;
    seLbs["零件名称"].Value = winform.cb_name_cn.text;
    seLbs["材料"].Value = winform.cb_material.text;
    seLbs["质量/体积"].Value = winform.cb_weight.text;
    seLbs["喷粉标准"].Value = winform.chkPaint.checked ? "Q/LD J52005" : "";
    seLbs["公差等级"].Value = winform.cb_tol.text;
    seLbs["设计"].Value = winform.cb_designer.text;
    seLbs["审核"].Value = winform.cb_reviewer.text;
    seLbs["批准"].Value = winform.cb_approver.text;
    seLbs["设计日期"].Value = winform.cb_design_date.text;
    seLbs["审核日期"].Value = winform.cb_review_date.text;
    seLbs["批准日期"].Value = winform.cb_approve_date.text;
    seLbs["版本"].Value = winform.version.text;
}

winform.button3.oncommand = function(id,event){
	win.quitMessage();
}

winform.button2.oncommand = function(id,event){
	if (winform.chkReplaceTB.hide == 0){
		if (winform.chkReplaceTB.checked){
			solidedge.DraftDocument.replaceNewTitleBlock(seDFT, seDFT.ActiveSheet);
			seBlk = seDFT.getTitleBlock(seSht);
		}
	}
    writeToDrw(seDFT.getLabelObjectTable(seDFT.getTitleBlock(seSht)));

    if (VBASettingIO.GetSetting("Domisoft", "TBM_SE", "Default_Designer", "")=="") {
		winform.button5.oncommand();    /* 智能保存常用姓名 */
    }
    
	win.quitMessage();
}

winform.button.oncommand = function(id,event){
    var v = tonumber(winform.version.text);
	v += 1;
	winform.version.text =  (v >= 10) ? v : "0" ++ v;
}
winform.button4.oncommand = function(id,event){
	updateDate();
}

var seBlk = seDFT.getTitleBlock(seSht); //获取titleblock
	
if !seBlk{
    var x,y = seDFT.getTitleBlockOrigin(seSht);
    for (i=1; seDFT.Blocks.Count; 1) {
        if (seDFT.Blocks.item(i).name=="Title 2020") {
    		seBlk = seSht.BlockOccurrences.Add("Title 2020", x, y);
    		break;
    	}
    }
    if !seBlk{
    	win.msgbox("找不到Title Blocks定义");
    	win.quitMessage();
    }
}


if (seBlk.Block.Name !== "Title 2020")    winform.chkReplaceTB.hide = 0;

winform.button5.oncommand = function(id,event){
    VBASettingIO.SaveSetting("Domisoft", "TBM_SE", "Default_Designer", winform.cb_designer.text);
	VBASettingIO.SaveSetting("Domisoft", "TBM_SE", "Default_Reviewer", winform.cb_reviewer.text);
	VBASettingIO.SaveSetting("Domisoft", "TBM_SE", "Default_Approver", winform.cb_approver.text);
}

var seLbs = seDFT.getLabelTable(seBlk);	//获取titleblock lable
winform.show();
loadFromTab(seLbs);	//读取当前内容
loadDefaults();	//读取默认值(新图纸)
winform.seDocPath.text = seDFT.FullName;
loadPromptList();	//读取提示清单
win.loopMessage();
return winform;