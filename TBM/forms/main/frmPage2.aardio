import fonts.fontAwesome;
import win.ui;
/*DSG{{*/
var winform = win.form(text="aardio form";right=759;bottom=470;image=$"\SE BG.jpg";bkBottom=0;bkLeft=0;bkRight=0;bkTop=0)
winform.add(
button={cls="button";text='\uF021 \r\r 一键更新图框';left=35;top=35;right=128;bottom=125;dl=1;dt=1;font=LOGFONT(h=-13;name='FontAwesome');z=1};
button2={cls="button";text='\uF01E  放弃保存, 重新打开当前文档';left=23;top=409;right=248;bottom=450;db=1;dl=1;font=LOGFONT(name='FontAwesome');z=2};
button3={cls="button";text='\uF07E \r\r 强改尺寸';left=144;top=35;right=234;bottom=125;dl=1;dt=1;font=LOGFONT(h=-13;name='FontAwesome');z=3};
button4={cls="button";text='\uF05A \r\r 修改球标';left=251;top=35;right=341;bottom=125;dl=1;dt=1;font=LOGFONT(h=-13;name='FontAwesome');z=4};
button5={cls="button";text='\uF05C \r\r 删除球标';left=360;top=35;right=450;bottom=125;dl=1;dt=1;font=LOGFONT(h=-13;name='FontAwesome');z=5}
)
/*}}*/

import solidedge.application;
var seApp = solidedge.application();
import solidedge.DraftDocument;

winform.button.oncommand = function(id,event){
    if seApp.ActiveDocumentType !== 2 /*Draft Document Type*/ return ; 
    var seDoc = seApp.ActiveDocument;
	solidedge.DraftDocument.replaceNewTitleBlock(seDoc, seDoc.ActiveSheet);
	win.quitMessage();
}

winform.button2.oncommand = function(id,event){
	solidedge.application.reOpenDocument();
	win.quitMessage();
}

winform.button3.oncommand = function(id,event){
    if seApp.Documents.Count==0 return ; 
    if seApp.ActiveDocumentType !== 2 /*Draft Document Type*/ return ; 
    var SelSet = seApp.ActiveDocument.SelectSet;
    if SelSet.Count !== 1 return; 
    if (SelSet(1).Type !== 488188096 /* igDimension */) return;
    import win.inputBox;
    var inputBox=win.inputBox();
    inputBox.info.text="input text to replace dimension";
    inputBox.text="Edit Dim";
    inputBox.input.text="L";
    var s=inputBox.doModal();
    if !s return; 
    if ..string.len(..string.trim(s)) == 0 return ; 
    SelSet(1).OverrideString = s;
    SelSet(1).Style.NTSSymbol = 1 /* igDimStyleNTSNone */;
    win.quitMessage();
}

winform.button4.oncommand = function(id,event){
    if seApp.Documents.Count==0 return ; 
    if seApp.ActiveDocumentType !== 2 /*Draft Document Type*/ return ; 
	var seDFT = solidedge.DraftDocument(seApp.ActiveDocument);
	var seSht = seDFT.ActiveSheet;
	var BBS = seSht.Balloons;
	var ee = seDFT.getTopRecordMark(seSht);
	ee = string.pack( ee[1] + 1 );

	import win.inputBox;
	var inputBox=win.inputBox();
	inputBox.info.text="What symbol change bolloons to ?";
	inputBox.text="Edit Balloon";
	inputBox.input.text= ee ;
	var e = inputBox.doModal();
	if !e return ; 
	if (..string.len(..string.trim(e)) == 0) return ; 
	for (i = 1 ; BBS.Count) {	
		if BBS.Item(i).BalloonType == 1 /* igDimBalloonCircle */
    		if !BBS.Item(i).Leader
        		BBS.Item(i).BalloonText = e;
	}
	win.quitMessage();
}

winform.button5.oncommand = function(id,event){
    if seApp.Documents.Count==0 return ; 
    if seApp.ActiveDocumentType !== 2 /*Draft Document Type*/ return ; 
	var seDFT = solidedge.DraftDocument(seApp.ActiveDocument);
	var seSht = seDFT.ActiveSheet;
	var BBS = seSht.Balloons;
	var ee = "a";
	for (i = 1 ; BBS.Count) {	
		if BBS.Item(i).BalloonType == 1 /* igDimBalloonCircle */
    		if !BBS.Item(i).Leader
        		ee = BBS.Item(i).BalloonText;
        		break;
	}
	
	import win.inputBox;
	var inputBox=win.inputBox();
	inputBox.info.text="What symbol bolloons you want to delete ?";
	inputBox.text="delete Balloon";
	inputBox.input.text= ee ;
	var e = inputBox.doModal();
	if !e return ; 
	if (..string.len(..string.trim(e)) == 0) return ; 
	
	for (i = BBS.Count; 1 ;- 1 ) {	
		if BBS.Item(i).BalloonType == 1 /* igDimBalloonCircle */
    		if !BBS.Item(i).Leader
        		if (BBS.Item(i).BalloonText==e)
        			BBS.Item(i).Delete();
	}
	win.quitMessage();
}
winform.show();
win.loopMessage();
return winform;