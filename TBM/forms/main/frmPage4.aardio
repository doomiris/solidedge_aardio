//import fonts.fontAwesome;
//import win.ui;
//import solidedge.application;
//import solidedge.DraftDocument;
//import VBASettingIO;
/*DSG{{*/
var winform = win.form(text="aardio form";right=783;bottom=471)
winform.add(
button={cls="button";text='\uF08B  插入图纸';left=688;top=424;right=776;bottom=464;db=1;dr=1;font=LOGFONT(name='FontAwesome');z=5};
button2={cls="button";text="关闭";left=592;top=424;right=680;bottom=464;db=1;dr=1;hide=1;z=6};
checklist={cls="checklist";left=152;top=40;right=776;bottom=416;aw=1;db=1;dl=1;dr=1;dt=1;edge=1;items={};z=2};
static={cls="static";text="技术要求:";left=144;top=8;right=304;bottom=32;color=8388608;dl=1;dt=1;font=LOGFONT(h=-16);transparent=1;z=3};
static2={cls="static";text='\uF039';left=8;top=8;right=48;bottom=40;color=8388608;dl=1;dt=1;font=LOGFONT(h=-24;name='FontAwesome');transparent=1;z=4};
treeview={cls="treeview";left=8;top=40;right=144;bottom=416;asel=false;bgcolor=16777215;db=1;dl=1;dt=1;edge=1;vscroll=1;z=1}
)
/*}}*/

/*initDeclare{{*/
var seApp, seDFT, seSht;
var initDeclare = function(){
	seApp := solidedge.application();
	if (! seApp) return false; 
	if (seApp.Documents.Count==0) return; 
	if (seApp.ActiveDocumentType !== 2 /*Draft Document Type*/) return; 
	seDFT = solidedge.DraftDocument(seApp.ActiveDocument);
	if (seDFT.Sheets.Count == 0) return ; 
	seSht = seDFT.ActiveSheet;
	return true; 
}
/*}}*/
_dbpath = VBASettingIO.GetSetting("Domisoft", "Config", "Spec_db_path", "");

var dbRs = access(_dbpath);


/*loadLists{{*/
var loadPromptList = function(){
	var tab = dbRs.getTable("select * from SE_SPEC_TEMPLATE order by [ID]");
	var hitem = winform.treeview.insertItem( text="模板清单" );
	//winform.treeview.insertTable(tab);
	//var hSubItem;
	for (i=1; #tab; 1) {
		winform.treeview.insertItem( { text=tab[i].tName },hitem/*父节点*/); 
	}
	winform.treeview.expandAll();
}
/*}}*/

/*控件事件{{*/
winform.treeview.onnotify = function(id,code,ptr){
	if (code == 0xFFFFFE3D/*_TVN_SELCHANGEDW*/){
		winform.checklist.clear();
		var hItem = winform.treeview.getSelection();
		if(hItem){
			var tab;
			tab = dbRs.getTable("select tCombine,tSelect from SE_SPEC_TEMPLATE where tName='" ++ winform.treeview.getItemText(hItem) ++ "'");
			var v = string.split(tab[1].tCombine,",")
			var vs = string.split(tab[1].tSelect,",")
			for (i=1; #v){
				tab = dbRs.getTable("select sTxt from SE_SPEC_ITEM where ID=" ++ v[i]);
				winform.checklist.addItem(tab[1].sTxt);
				winform.checklist.setChecked(i,(vs[i]==1));
			}
		}
	}
}

winform.button2.oncommand = function(id,event){
	mainForm.show(false); //隐藏窗口
	
}

winform.button.oncommand = function(id,event){
	mainForm.show(false); //隐藏窗口
	win.setActive(seApp.hWnd);
	
	initDeclare();
	
	var txts = seSht.TextBoxes;
	var txt = txts.Add(0.25, 0.12, 0);
    	txt.HorizontalAlignment = 0 /* igTextHzAlignLeft */;
    	txt.FlowOrientation = 0 /* igTextHorizontal */;
    	txt.Justification = 0 /* igTextJustifyTop */;
    	txt.TextControlType = 0 /* igTextFitToContent */;
    	txt.Text = "技术要求:" ++ '\r';
    	txt.Text = txt.Text ++ " " ++ '\r';

    	var n = 1;	//编号
    	for (i = 1 ; winform.checklist.count){
        	if (winform.checklist.getChecked(i)) {
            	txt.Text = txt.Text ++ n ++ ". " ++ winform.checklist.getItemText(i) ++ '\r';
            	n += 1;
        	}
    	}
	txt.Cut();
	sleep(100);
	winex.key.combine(seApp.hWnd,"CTRL","V");

}
/*}}*/
winform.show();
initDeclare();
loadPromptList();	//读取提示列表
win.loopMessage();
return winform;