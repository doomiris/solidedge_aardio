import fonts.fontAwesome;
import win.ui;
import win.ui.simpleWindow;
import win.ui.atom;
import win.ui.tabs;
import win.ui.tooltip; 
import win.inputBox;
import win.util.tray;
import winex;
import winex.key;
//import access;
import sqlite.aes256;
import process;
//import console
//console.open()

//user lib
import solidedge.application;
import solidedge.DraftDocument;
import VBASettingIO;
/*DSG{{*/
mainForm = win.form(text="TBM";right=623;bottom=447;image=$"\res\SE Mainframe Background.png";bgcolor=15793151;border="none")
mainForm.add(
button={cls="button";text='\uF127';left=36;top=4;right=68;bottom=36;bgcolor=255;dl=1;dt=1;font=LOGFONT(h=-27;name='FontAwesome');hide=1;z=20};
caption={cls="bkplus";text="Title Block Manager";left=77;top=10;right=233;bottom=28;align="left";color=6052956;dl=1;dt=1;font=LOGFONT(h=-14);z=9};
custom={cls="custom";left=83;top=40;right=624;bottom=448;bgcolor=16777215;db=1;dl=1;dr=1;dt=1;z=10};
logo={cls="bkplus";left=36;top=5;right=65;bottom=30;background="D:\users\Personal LGP\personal Documents\code_space\icon\ENGINE.ICO";color=8388608;dl=1;dt=1;font=LOGFONT(h=-27;name='FontAwesome');z=8};
nav1={cls="plus";text="TBM";left=0;top=48;right=85;bottom=128;bkBottom=3;bkLeft=7;bkRight=8;bkTop=2;border={color=-65536};color=16777215;dl=1;dt=1;font=LOGFONT(h=-13);iconStyle={font=LOGFONT(h=-37;name='FontAwesome');padding={bottom=20}};iconText='\uF1EA';notify=1;textPadding={bottom=10};valign="bottom";x=0.5;y=0.2;z=3};
nav2={cls="plus";text="变更";left=0;top=129;right=85;bottom=209;bkBottom=3;bkLeft=7;bkRight=8;bkTop=2;border={color=-65536};color=16777215;dl=1;dt=1;font=LOGFONT(h=-13);iconStyle={font=LOGFONT(h=-37;name='FontAwesome');padding={bottom=20}};iconText='\uF277';notify=1;textPadding={bottom=10};valign="bottom";x=0.5;y=0.2;z=4};
nav3={cls="plus";text="SAP";left=0;top=210;right=85;bottom=290;bkBottom=3;bkLeft=7;bkRight=8;bkTop=2;border={color=-65536};color=16777215;dl=1;dt=1;font=LOGFONT(h=-13);iconStyle={font=LOGFONT(h=-37;name='FontAwesome');padding={bottom=20}};iconText='\uF1B0';notify=1;textPadding={bottom=10};valign="bottom";x=0.5;y=0.2;z=5};
nav4={cls="plus";text="技术要求";left=0;top=291;right=85;bottom=371;bkBottom=3;bkLeft=7;bkRight=8;bkTop=2;border={color=-65536};color=16777215;dl=1;dt=1;font=LOGFONT(h=-13);iconStyle={font=LOGFONT(h=-37;name='FontAwesome');padding={bottom=20}};iconText='\uF0CB';notify=1;textPadding={bottom=10};valign="bottom";x=0.5;y=0.2;z=6};
nav5={cls="plus";text="Print";left=0;top=372;right=85;bottom=456;bkBottom=3;bkLeft=7;bkRight=8;bkTop=2;border={color=-65536};color=16777215;dl=1;dt=1;font=LOGFONT(h=-13);iconStyle={font=LOGFONT(h=-37;name='FontAwesome');padding={bottom=20}};iconText='\uF1C1';notify=1;textPadding={bottom=10};valign="bottom";x=0.5;y=0.2;z=7};
nav6={cls="plus";text="About";left=0;top=448;right=85;bottom=532;bkBottom=3;bkLeft=7;bkRight=8;bkTop=2;border={color=-65536};color=16777215;dl=1;dt=1;font=LOGFONT(h=-13);iconStyle={font=LOGFONT(h=-37;name='FontAwesome');padding={bottom=20}};iconText='\uF262';notify=1;textPadding={bottom=10};valign="bottom";x=0.5;y=0.2;z=18};
navBar={cls="bkplus";left=0;top=37;right=83;bottom=552;bgcolor=11829830;db=1;dl=1;dt=1;z=1};
static2={cls="static";text='\uF03B';left=8;top=8;right=32;bottom=32;color=8388608;dl=1;dt=1;font=LOGFONT(h=-27;name='FontAwesome');notify=1;transparent=1;z=19};
tbutton1={cls="button";text='\uF01E';left=256;top=8;right=280;bottom=32;dr=1;dt=1;font=LOGFONT(name='FontAwesome');z=15};
tbutton2={cls="button";text='\uF07E';left=288;top=8;right=312;bottom=32;dr=1;dt=1;font=LOGFONT(h=-13;name='FontAwesome');z=11};
tbutton3={cls="button";text='\uF05A';left=320;top=8;right=344;bottom=32;dr=1;dt=1;font=LOGFONT(h=-13;name='FontAwesome');z=12};
tbutton4={cls="button";text='\uF05C';left=352;top=8;right=376;bottom=32;dr=1;dt=1;font=LOGFONT(h=-13;name='FontAwesome');z=13};
tbutton5={cls="button";text='\uF0EE';left=384;top=8;right=408;bottom=32;dr=1;dt=1;font=LOGFONT(h=-13;name='FontAwesome');z=14};
tbutton6={cls="button";text='\uF05E';left=416;top=8;right=440;bottom=32;dr=1;dt=1;font=LOGFONT(name='FontAwesome');z=16};
tbutton7={cls="button";text='\uF05B';left=448;top=8;right=472;bottom=32;dr=1;dt=1;font=LOGFONT(name='FontAwesome');z=17};
titleBar={cls="bkplus";left=0;top=0;right=624;bottom=38;bgcolor=11224832;dl=1;dr=1;dt=1;forecolor=16777215;linearGradient=180;z=2}
)
/*}}*/

/*只允许一个实例{{*/
var atom,hwnd = mainForm.atom("domisoft.aardio.tbm");
if(!atom){ 
	win.showForeground(hwnd);
	win.quitMessage();	
	return;
}
/*}}*/
/*closeOnEscPress{{*/
mainForm.isDialogMessage = function(hwnd,msg){ 
	if( msg.message == 0x100/*_WM_KEYDOWN*/){
		
/**
		if(  msg.wParam == 0xD/*_VK_RETURN*/ ){ 
			//return true;//告诉消息处理函数这是一个快捷键,阻止按键消息继续分发
		}
**/
		
		if( msg.wParam == 0x1B/*_VK_ESC*/ ){	//按Esc退出窗口
			mainForm.close();
			//return true;//告诉消息处理函数这是一个快捷键,阻止按键消息继续分发
		} 
	}
	
	//检测并响应默认快捷键
	return win.isDialogMessage(hwnd,msg);
}
/*}}*/

win.ui.simpleWindow( mainForm );
mainForm.enableDpiScaling();

var seApp = solidedge.application();
if (!seApp) mainForm.button.hide = 0;
if (seApp) winex.attach(seApp.hWnd);

/*托盘图标{{*/
mainForm.tray = win.util.tray(mainForm)  
mainForm.onMinimize = function(lParam){
//	mainForm.modifyStyleEx(0x40000/*_WS_EX_APPWINDOW*/,0x80/*_WS_EX_TOOLWINDOW*/)
	mainForm.show(false); 
	return true; 
}

mainForm.onClose = function(hwnd,message,wParam,lParam){
	mainForm.onMinimize();
	return true;    	
}

mainForm.onDestroy = function(){
	if(mainForm.tray){
		mainForm.tray.delete()
	}
}

mainForm.wndproc = {
	[0xACCF/*_WM_TRAYMESSAGE*/ ] = function(hwnd,message,wParam,lParam){
		select(lParam) {
			case 0x205/*_WM_RBUTTONUP*/ {
				win.setForeground(mainForm.hwnd)	//弹出托盘菜单以前,一定要前置主窗口中,不然不点击菜单不会消失
	    		var pt = ::POINT();
				::User32.GetCursorPos(pt);   		
	    		mainForm.popmenu.popup(pt.x,pt.y,true )
			}
			case 0x202/*_WM_LBUTTONUP*/ {
				win.setForeground(mainForm.hwnd)	//弹出托盘菜单以前,一定要前置主窗口中,不然不点击菜单不会消失
				mainForm.show(!win.isVisible(mainForm.hwnd)); //点击托盘图标切换窗口显示隐藏
			}
		}
	}
}

mainForm.popmenu = win.ui.popmenu(mainForm);//创建弹出菜单
mainForm.popmenu.add('&Open',function(id){
	//在下面输入菜单响应代码
	win.setForeground(mainForm.hwnd)
	mainForm.show();
	
});
mainForm.popmenu.add();//分隔线
mainForm.popmenu.add('检查专用号申请表',function(id){loadcodex("\forms\Excel\check_专用号申请表.aardio");});
mainForm.popmenu.add('智能填写专用号申请表',function(id){loadcodex("\forms\Excel\assist_专用号申请表.aardio");});
mainForm.popmenu.add('检查新BOM申请表',function(id){loadcodex("\forms\Excel\check_新BOM申请表.aardio");});
mainForm.popmenu.add('更新ItemMaster物料信息',function(id){loadcodex("\forms\Excel\Excel_update_info.aardio");});
mainForm.popmenu.add('原材料用量计算',function(id){loadcodex("\forms\Excel\BOM-CALC.aardio");});
mainForm.popmenu.add('根据下发清单更新ItemMaster',function(id){loadcodex("\forms\Excel\Update_Item_master_by_下发清单.aardio");});
mainForm.popmenu.add('打开图纸下发版次查询窗口',function(id){loadcodex("\forms\main\check_QHC_Release.aardio");});
mainForm.popmenu.add();//分隔线
mainForm.popmenu.add('&Exit',function(id){ win.quitMessage() })	
/*}}*/
/*左侧导航按钮{{*/

var tbs = win.ui.tabs( 
	mainForm.nav1,
	mainForm.nav2,
	mainForm.nav3,
	mainForm.nav4,
	mainForm.nav5,
	mainForm.nav6,
);

tbs.skin({
	background={
		active=0xFFFFFFFF;
		default=0x00FFFFFF;
		hover=0x38FFFFFF
	};
	color={
		default=0xFFFFFFFF; 
	};
	checked={
		background={default=0xFFFFFFFF;};
		color={default=0xff0047ab;};
	}
})

tbs.loadForm(1,"\forms\main\frmPage1.aardio" );
tbs.loadForm(2,"\forms\main\frmPage2.aardio" );
tbs.loadForm(3,"\forms\main\QTIP.aardio" );
tbs.loadForm(4,"\forms\main\frmPage4.aardio" );
tbs.loadForm(5,"\forms\main\frmPage5.aardio" );
tbs.loadForm(6,"\forms\main\frmPage3.aardio" );

var navindex = 6;
if (_ARGV.nav)	navindex = tonumber(_ARGV.nav);
tbs.selIndex = navindex;
/*}}*/
/*顶部按钮{{*/
mainForm.tbutton1.oncommand = function(id,event){
	solidedge.application.reOpenDocument();
	//win.quitMessage();
}

mainForm.tbutton2.oncommand = function(id,event){
    if seApp.Documents.Count==0 return ; 
    if seApp.ActiveDocumentType !== 2 /*Draft Document Type*/ return ; 
    var SelSet = seApp.ActiveDocument.SelectSet;
    if SelSet.Count !== 1 return; 
    if (SelSet(1).Type !== 488188096 /* igDimension */) return;

    var inputBox=win.inputBox();
    inputBox.info.text="input text to replace dimension";
    inputBox.text="Edit Dim";
    inputBox.input.text="L";
    var s=inputBox.doModal();
    if !s return; 
    if ..string.len(..string.trim(s)) == 0 return ; 
    SelSet(1).OverrideString = s;
    SelSet(1).Style.NTSSymbol = 1 /* igDimStyleNTSNone */;
    
    mainForm.show(false); //隐藏窗口
	win.setActive(seApp.hWnd);
}

mainForm.tbutton3.oncommand = function(id,event){
    if seApp.Documents.Count==0 return ; 
    if seApp.ActiveDocumentType !== 2 /*Draft Document Type*/ return ; 
	var seDFT = solidedge.DraftDocument(seApp.ActiveDocument);
	var seSht = seDFT.ActiveSheet;
	var BBS = seSht.Balloons;
	var ee = seDFT.getTopRecordMark(seSht);
	ee = ee ? string.pack( ee[1] + 1 ) : "a";
	var inputBox=win.inputBox();
	inputBox.info.text="What symbol change bolloons to ?";
	inputBox.text = "Edit Balloon";
	inputBox.input.text = ee ;
	var e = inputBox.doModal();
	if !e return ; 
	if (..string.len(..string.trim(e)) == 0) return ; 
	for (i = 1 ; BBS.Count) {	
		if BBS.Item(i).BalloonType == 1 /* igDimBalloonCircle */
    		if !BBS.Item(i).Leader
    			with BBS.Item(i){
    				BalloonText = e;
    				BalloonType = 1; /* DimBalloonTypeConstants.igDimBalloonCircle */
    				BalloonSize = 2; /* 形状是字体几倍数 */
    				Leader = false; 
    				Style.DrivenColor = 255;	/* ColorConstants.seColorRed */;    				
    			}
	}
    mainForm.show(false); //隐藏窗口
	win.setActive(seApp.hWnd);
}

mainForm.tbutton4.oncommand = function(id,event){
	if seApp.Documents.Count==0 return ; 
    if seApp.ActiveDocumentType !== 2 /*Draft Document Type*/ return ; 
	var seDFT = solidedge.DraftDocument(seApp.ActiveDocument);
	var seSht = seDFT.ActiveSheet;
	var BBS = seSht.Balloons;
	var ee = "a";
	for (i = 1 ; BBS.Count) {	
		if BBS.Item(i).BalloonType == 1 /* igDimBalloonCircle */
    		if !BBS.Item(i).Leader
        		ee = BBS.Item(i).BalloonText;
        		break;
	}
	
	var inputBox=win.inputBox();
	inputBox.info.text="What symbol bolloons you want to delete ?";
	inputBox.text="delete Balloon";
	inputBox.input.text= ee ;
	var e = inputBox.doModal();
	if !e return ; 
	if (..string.len(..string.trim(e)) == 0) return ; 
	
	for (i = BBS.Count; 1 ;- 1 ) {	
		if BBS.Item(i).BalloonType == 1 /* igDimBalloonCircle */
    		if !BBS.Item(i).Leader
        		if (BBS.Item(i).BalloonText==e)
        			BBS.Item(i).Delete();
	}
    mainForm.show(false); //隐藏窗口
	win.setActive(seApp.hWnd);
}

mainForm.tbutton5.oncommand = function(id,event){
	if (seApp.ActiveDocumentType != 3 /* igAssemblyDocument */) return;
	var seAsm = solidedge.AssemblyDocument(seApp.ActiveDocument); 
	if (seAsm.SelectSet.Count != 1) return;
	seAsm.openDummy(seAsm.SelectSet.Item(1));
    mainForm.show(false); //隐藏窗口
	win.setActive(seApp.hWnd);
}

mainForm.tbutton6.oncommand = function(id,event){
	solidedge.application.closeDft(false);
    mainForm.show(false); //隐藏窗口
	win.setActive(seApp.hWnd);
}

mainForm.tbutton7.oncommand = function(id,event){
	solidedge.application.closeDft(true);
    mainForm.show(false); //隐藏窗口
	win.setActive(seApp.hWnd);
}


var tooltipCtrl = win.ui.tooltip( mainForm );//在窗口上创建tooltip控件
tooltipCtrl.addTool(mainForm.tbutton1,"Re-Open as Read-Only" )
tooltipCtrl.addTool(mainForm.tbutton2,"Edit Dim" )
tooltipCtrl.addTool(mainForm.tbutton3,"Edit Balloon" )
tooltipCtrl.addTool(mainForm.tbutton4,"Delete Balloon" )
tooltipCtrl.addTool(mainForm.tbutton5,"Open Dummy" )
tooltipCtrl.addTool(mainForm.tbutton6,"Close All DFT (discard changes)" )
tooltipCtrl.addTool(mainForm.tbutton7,"Save && Close All DFT" )
tooltipCtrl.addTool(mainForm.static2,"显示/隐藏导航按钮" )
/*}}*/
/*折叠导航按钮{{*/
mainForm.static2.tag = true;
mainForm.static2.tag2 = mainForm.custom.left;
mainForm.static2.oncommand = function(id,event){
    if (mainForm.static2.tag){
        mainForm.static2.text = '\uF03C';
    	mainForm.static2.tag = false
		tbs.show ( false )
		tbs.dropButton.hide = true
		mainForm.custom.left = 0
		
	}else{
		mainForm.static2.text = '\uF03B';
    	mainForm.static2.tag = true
		tbs.show ( true )
		tbs.dropButton.hide = false
		mainForm.custom.left = mainForm.static2.tag2
	}
}
/*}}*/

/*快捷查询{{*/
var hkId;


//删除之前注册的热键,hkId如果是null空值，此函数可忽略不做任何操
mainForm.unreghotkey(hkId);

//重新注册系统热键;
hkId = mainForm.reghotkey(
	function(id,mod,vk){
		loadcodex("\forms\Excel\Excel_notify_selection.aardio");
	}
	
	,1/*_MOD_ALT*/, 'J'# 
	
);


/*}}*/

mainForm.button.oncommand = function(id,event){
	seApp = solidedge.application();
	if (seApp) mainForm.button.hide = 1;
}

mainForm.show();

return win.loopMessage();