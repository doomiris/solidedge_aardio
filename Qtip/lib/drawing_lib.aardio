//drawing_lib 库操作函数
import fsys;
import fsys.table;
import fsys.file;
namespace drawing_lib{
	searchDocument = function (filenumber){
    	if !(filenumber) return null;
		var searchFor = ..string.trimleft(filenumber,"H");
		var fileTab = ..table.create(,{fields={[1]="文件名";[2]="位置";[3]="日期";[4]="版本"}})	;
		var flistTab = ..fsys.table("S:\temp\addon\qtip\drawing_lists.table");
			flistTab.load();
		var flist = ..table.array();
			..table.push(flist,flistTab.a);
			..table.push(flist,flistTab.b);
			..table.push(flist,flistTab.c);
		var dlistTab = ..fsys.table("S:\temp\addon\qtip\drawing_map.table");
			dlistTab.load();
		var mapping = dlistTab.dlist;
	
		var maptab = ..table.filter(mapping, function(v,index){
			if (v.专用号==searchFor) return true; 
		}); 
		if (#maptab>0) searchFor = maptab[1].图号; 
	
		for (j=1; #flist; 1){
			var myfind = ..table.filter(flist[j],function(v,index){
				if (..string.indexOf(v,searchFor)) return true;
			});
			if (#myfind>0){
				for(k=1;#myfind;1){
					..table.push(fileTab,{
						文件名	=myfind[k];
						位置	=flist[j][myfind[k]];
						日期	=..fsys.file(flist[j][myfind[k]]).getFileTime().write;
						版本	=""
					});
				};
			};
		};
		return fileTab;
	}
	saveFolderTable = function(){
		_searchLoc = {
			"\\ccnsia1a\separts\Cabinet\PDF图纸库";
			"\\ccnsia1a\separts\Cabinet\QHC图纸库2020";
			"\\ccnsia1a\separts\Cabinet\QHC图纸库"
		};
		var dlist = ..fsys.table("S:\temp\addon\qtip\drawing_lists.table");
		dlist.load();
		dlist.mixin(
			a = ..fsys.list(_searchLoc[1],,"*.*");
			b = ..fsys.list(_searchLoc[2],,"*.*");
			c = ..fsys.list(_searchLoc[3],,"*.*")
		);
		dlist.save();
	};
	appendDrawingMapFromExcel = function(eFileLoc, shtName){
		import com.excel;
		import console;
		import console.progress;
		console.open();
/*codetrim{{*/
		var codetrim =  function(str){
			str = ..string.upper(str);
			str = ..string.trim(str);
			str = ..string.trimleft(str, "H");
			return str; 
		}
/*}}*/
		var xlApp = com.excel(true);
		xlApp.Visible=false;

		var xlWk = xlApp.Open(eFileLoc);
		var xlSht = xlWk.Sheets(shtName);
		xlSht.Activate();
		console.log(xlSht.name)
		var xlRng = xlSht.Range("1:1");
		var _s = 2;
		var _c = xlRng.find("专用号").column;
		var _d = xlRng.find("图号").column;
		console.log(_c, _d)
		var dmap = ..fsys.table("S:\temp\addon\qtip\drawing_map.table");
		dmap.load();
		console.log("总数:", #dmap.dlist);
		console.progress();
		var mapping = ..table.create(,{fields={[1]="专用号";[2]="图号";}})
		for (i=_s; xlSht.UsedRange.Rows.Count; 1){
			var pcell = xlSht.Cells(i, _c);
			if (pcell.Mergecells) pcell = pcell.MergeArea.Cells(1,1);
			var dcell = xlSht.Cells(i, _d);
			if (dcell.Mergecells) dcell = dcell.MergeArea.Cells(1,1);
			
			var pcode = codetrim(pcell.text);
			var dcode = codetrim(dcell.text);
			
			if !..string.startWith(pcode,"008") || !..string.startWith(dcode,"008")
				continue;
			
			var duptab = ..table.filter(dmap.dlist,function(v,index){
				if (v.专用号==pcode && v.图号==dcode) return true; 
			})
			
			if #duptab>0 continue;
			
			..table.push(mapping,{
				专用号	= pcode;
				图号	= dcode;
			});
		};
		if (dmap.dlist)
			dmap.dlist =  ..table.concat(dmap.dlist, mapping);
		else 
			dmap.dlist = mapping;
			
		console.log("总数:", #dmap.dlist)
		dmap.save();
		xlWk.close(false);
		xlApp.Quit();		
		console.pause(true);
	};
}
/**intellisense(drawing_lib)
searchDocument(_filenumber) = return table [filename,fullpath,write_date,version]
saveFolderTable()= enum all files and save to table.
appendDrawingMapFromExcel(_xlFilePath, _sheetName) = add part_number .vs. drawing_number info.
end intellisense**/