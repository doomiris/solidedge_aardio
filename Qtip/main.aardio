import fonts.fontAwesome;
import win.ui;
import win.ui.grid;
import win.ui.menu;
import win.ui.accelerator;
import win.clip;
import win.clip.file;
import sqlite.aes256;
import fsys;
import fsys.file;
import fsys.table;
//import console;
//import win.util.tray;
/*DSG{{*/
mainForm = win.form(text="QHC物料查询";right=638;bottom=502)
mainForm.add(
button={cls="button";text='\uF002  查询';left=521;top=10;right=620;bottom=40;default=1;dr=1;dt=1;font=LOGFONT(name='FontAwesome');tabstop=1;z=1};
button2={cls="button";text="安装驱动";left=184;top=472;right=270;bottom=495;db=1;dl=1;hide=1;z=3};
button3={cls="button";text='\uF053 ';left=9;top=7;right=44;bottom=41;color=16711680;font=LOGFONT(h=-16;name='FontAwesome');hide=1;z=5};
button4={cls="button";text="更新图号库";left=5;top=472;right=81;bottom=495;color=10789024;db=1;dl=1;flat=1;hide=1;z=6};
button5={cls="button";text='\uF103 ';left=477;top=39;right=515;bottom=54;color=8421504;dr=1;dt=1;flat=1;font=LOGFONT(h=-13;name='FontAwesome');z=7};
button6={cls="button";text="更新目录库";left=95;top=472;right=171;bottom=495;color=10789024;db=1;dl=1;flat=1;hide=1;z=10};
button7={cls="button";text="加密";left=280;top=472;right=360;bottom=496;hide=1;z=11};
buttonD={cls="button";text='\uF142';left=235;top=10;right=249;bottom=40;color=8421504;dl=1;dt=1;flat=1;font=LOGFONT(h=-13;name='FontAwesome');z=9};
editUser={cls="edit";left=250;top=10;right=515;bottom=40;autohscroll=false;autovscroll=false;aw=1;border=1;dl=1;dr=1;dt=1;font=LOGFONT(h=-13);tabstop=1;z=8};
listview={cls="listview";left=1;top=53;right=638;bottom=463;ah=1;aw=1;db=1;dl=1;dr=1;dt=1;edge=1;fullRow=1;z=4};
static={cls="static";text="Static";left=180;top=464;right=632;bottom=499;align="right";aw=1;center=1;color=12632256;db=1;dl=1;dr=1;font=LOGFONT(h=-11);transparent=1;z=2}
)
/*}}*/

/*只允许一个实例{{*/
import win.ui.atom;
var atom,hwnd = mainForm.atom("domisoft.aardio.qtip");
if(!atom){ 
	win.showForeground(hwnd);
	win.quitMessage();	
	return;
}
/*}}*/
var grid = win.ui.grid(mainForm.listview);//创建数据视图
var sapTab=table.create(,{fields={[1]="物料";[2]="物料描述";[3]="类型";[4]="价格";[5]="禁用"}});
/*sqliteDB{{*/
_dbpath = (function(){
    if io.exist("\qhcsap.db")
    	_path=io.fullpath("\qhcsap.db");
	else
		if io.exist("\\ccnsia1a\separts\Admin\SAP\db\QHCSAP.db")
			_path="\\ccnsia1a\separts\Admin\SAP\db\QHCSAP.db";
    	else
    		win.msgbox("无法访问数据库!");
    return _path; 
})()
var db,err = sqlite.aes256(_dbpath, "CCL100100");	//打开数据库连接
if (err) error(err)
/*}}*/
/*读取设置{{*/
var logInput,setting;
mainForm.popmenu = win.ui.popmenu(mainForm);//创建弹出菜单

var readInputList = function(){
	logInput = table.array()
	setting = fsys.table(fsys.getTempDir()++"/domisoft.qtip.setting.table") 
	setting.load(); //从文件载入表
	if !(setting.inputlog) setting.inputlog = table.array()
}
var updatePopmenu = function(){
    mainForm.popmenu = win.ui.popmenu(mainForm)
	mainForm.popmenu.add("输入历史:");
	mainForm.popmenu.add();//分隔线
	for (i=1; #setting.inputlog; 1){
    	mainForm.popmenu.add(
    		text = setting.inputlog[i];
    		proc = function(id){
    			mainForm.editUser.text= setting.inputlog[i];
    			mainForm.editUser.setFocus();
    		}
    	);
	}
}
var saveInputList = function(str){
    if !(table.find(setting.inputlog, str)){
    	table.unshift(setting.inputlog,str);
    	if (#setting.inputlog>15) table.pop(setting.inputlog);
    	setting.save();
    };
    table.push(logInput, str);
    updatePopmenu();
};

readInputList();
updatePopmenu();
/*}}*/
/*调整列宽{{*/
var formatListView = function(){
	mainForm.listview.clear(true)
	grid.setTable( sapTab );
	grid.readonlyColums = {
    	[1] = true;[2] = true;[3] = true;[4] = true;[5] = true;
    }
	mainForm.listview.setColumn({cx=90 ;fmt=0x2/*_LVCFMT_CENTER*/},1);
	mainForm.listview.setColumn({cx=350;fmt=0x0/*_LVCFMT_LEFT*/},  2);
	mainForm.listview.setColumn({cx=60 ;fmt=0x2/*_LVCFMT_CENTER*/},3);
	mainForm.listview.setColumn({cx=60 ;fmt=0x2/*_LVCFMT_CENTER*/},4);
	mainForm.listview.setColumn({cx=50 ;fmt=0x2/*_LVCFMT_CENTER*/},5);
	mainForm.listview.adjust = function(cx,cy){
    	mainForm.listview.fillParent(2);
	} 	
}
/*}}*/
/*搜索按钮事件{{*/
mainForm.button.oncommand = function(id,event){
    mainForm.button3.hide=true;
    mainForm.listview.tag=1;
    mainForm.listview.clear(true);
    mainForm.listview.insertColumn("正在搜索数据库, 请稍等...",300,1)
    if (mainForm.editUser.lineCount==1) saveInputList(mainForm.editUser.text);
    var userinputArr = string.split(mainForm.editUser.text, '\r\n');
 	table.clear(sapTab);
    for (i=1; #userinputArr; 1){
		var userinput = userinputArr[i];
		if (string.len(userinput)==0 ) continue;
    	if (string.startWith(userinput,"008",true)) userinput="H" ++ userinput;
    	userinput=string.replace(userinput,"@%","\%");
    	userinput=string.replace(userinput,"@_","\_");
    	userinput=string.replace(userinput,"@*","%");
    	userinput=string.replace(userinput,"@?","_");
    	var searchField;	
    	if (string.startWith(userinput,"H008",true) || string.startWith(userinput,"BG",true)) {
    		searchField = "物料";
		}else{
    		searchField = "物料描述";
		}
		var sqlstr = "SELECT [物料], [物料描述],[MTyp] AS [类型],[价格],[跨工厂物料状态] AS [禁用] FROM [Sheet1] WHERE [" ++ searchField ++"] LIKE '" ++ userinput ++"'";
		var t = db.getTable(sqlstr);
		//console.dumpTable(t)
		sapTab = table.concat(sapTab,t);
	}
	if (#sapTab==0) {
		table.push(sapTab,{物料=userinputArr[1];物料描述="没找到SAP物料, 双击可继续查找图纸";物料类型="";禁用=""})
	}
	formatListView();
}
/*}}*/
/*查找图纸{{*/
var searchDocument = function (filenumber){
    if !(filenumber) return;
    mainForm.listview.tag = 2;
    mainForm.listview.clear(true);
    mainForm.listview.insertColumn("正在查找图纸, 请稍等...",300,1);
	var searchFor = string.trimleft(filenumber,"H");
	var fileTab = table.create(,{fields={[1]="文件名";[2]="位置";[3]="日期";[4]="版本"}});
	var flistTab = fsys.table("S:\temp\addon\qtip\drawing_lists.table");
		flistTab.load();
	var flist = table.array();
		table.push(flist,flistTab.a);
		table.push(flist,flistTab.b);
		table.push(flist,flistTab.c);
	var dlistTab = fsys.table("S:\temp\addon\qtip\drawing_map.table");
		dlistTab.load();
	var mapping = dlistTab.dlist;
	var maptab = table.filter(mapping, function(v,index){
		if (v.专用号==searchFor) return true; 
	}); 
	if #maptab>0 searchFor = maptab[1].图号; 

	for (j=1; #flist; 1){
		var myfind = table.filter(flist[j],function(v,index){
			if (string.indexOf(v,searchFor)) return true;
		});
		if (#myfind>0){
			for(k=1;#myfind;1){
				table.push(fileTab,{
					文件名	=myfind[k];
					位置	=flist[j][myfind[k]];
					日期	=fsys.file(flist[j][myfind[k]]).getFileTime().write;
					版本	=""
				});
			};
		};
	};

	mainForm.listview.clear(true);
	grid.setTable(fileTab);
	grid.readonlyColums = {
    	[1] = true;[2] = true;[3] = true;[4] = true;
    }
//调整列宽		
	mainForm.listview.setColumn({cx=100;fmt=0x2/*_LVCFMT_CENTER*/},1)
	mainForm.listview.setColumn({cx=350;fmt=0x0/*_LVCFMT_LEFT*/},  2)
	mainForm.listview.setColumn({cx=100;fmt=0x2/*_LVCFMT_CENTER*/},3)
	mainForm.listview.setColumn({cx=50 ;fmt=0x2/*_LVCFMT_CENTER*/},4)
	mainForm.listview.adjust = function(cx,cy){
    	mainForm.listview.fillParent(1);
    	mainForm.listview.fillParent(3);
	} 	
}
/*}}*/
/*双击打开事件{{*/
mainForm.listview.onnotify = function(id,code,ptr){
    select(code) {
    	case 0xFFFFFFFD/*_NM_DBLCLK*/ {
    		if (mainForm.listview.tag==1) {
    			mainForm.button3.hide=false;
    			mainForm.button3.tag  =mainForm.listview.getSelection();
				searchDocument(mainForm.listview.items[mainForm.listview.getSelection()][[1]]);
			}else{
				if io.exist(mainForm.listview.items[mainForm.listview.getSelection()][[2]]) {
					process.execute(mainForm.listview.items[mainForm.listview.getSelection()][[2]]);
				}
			}
    	}
    }
}
/*}}*/
//todo: 搜索框Ctrl+C失效问题
/*CTRLC复制模块{{*/
var accelerator = win.ui.accelerator({
    { 
        ctrl = true; vkey = 'C'#; 
        oncommand = function() {
            if (grid.editBox.hide==1){
                if (mainForm.listview.tag==1) {
            		var tab=table.array();
            		for (i=1;#mainForm.listview.selected;1){
                		table.push(tab,string.join(mainForm.listview.items[mainForm.listview.selected[i]],'\t'))
            		}           
					win.clip.write(string.join(tab,'\n'))
				}else{
					win.clip.file.write(mainForm.listview.items[mainForm.listview.getSelection()][[2]])
				}
			}else {	//编辑状态直接调用copy
				grid.editBox.copy()
			}
        }; 
    };//  {more}
    
},mainForm );

/*}}*/
/*onclose{{*/
mainForm.onClose = function(hwnd,message,wParam,lParam){
	if (db) db.close();	
}
/*}}*/
/*后退返回搜索结果{{*/
mainForm.button3.oncommand = function(id,event){
    formatListView();
	mainForm.listview.setSelected(mainForm.button3.tag)
	mainForm.listview.tag=1;
	mainForm.button3.hide=true;	
}
/*}}*/
/*button5输入框多行功能切换{{*/
mainForm.button5.oncommand = function(id,event){
	var t=mainForm.editUser.text;
	var a,b,c,d=mainForm.getScale();
	if (mainForm.button5.tag==1){	//恢复搜索框
		mainForm.button5.tag = 0;
		mainForm.add(
			editUser={cls="edit";left=mainForm.editUser.left;top=10;right=mainForm.editUser.right;bottom=40;autohscroll=false;autovscroll=false;aw=1;border=1;dl=1;dr=1;dt=1;tabstop=1;font=LOGFONT(h=-13);z=8};
		);
		mainForm.listview.top -= 60;
		mainForm.button5.top -= 60;
		mainForm.button5.height = 16;
		mainForm.button5.text = '\uF078'; //向下	
	}else{	//放大搜索框
		mainForm.button5.tag=1;
		mainForm.add(
			editUser={cls="edit";left=mainForm.editUser.left;top=10;right=mainForm.editUser.right;bottom=100;autohscroll=false;autovscroll=true;aw=1;border=1;dl=1;dr=1;dt=1;multiline=1;tabstop=1;font=LOGFONT(h=-13);z=8};
		);
		mainForm.listview.top += 60
		mainForm.button5.top += 60
		mainForm.button5.height = 16
		mainForm.button5.text = '\uF077'; //向上
	}
	mainForm.editUser.text=string.split(t,'\n\r')[[1]];
	mainForm.editUser.setFocus();
}
/*}}*/
/*输入框重新定位{{*/
var relocCtl = function(){
   	mainForm.editUser.right = mainForm.button5.right;
}
mainForm.onActivate = function(state,hwndOther,minimized){
	if(state){
		relocCtl();	
	}
}
mainForm.wndproc = function(hwnd,message,wparam,lparam){
    select(message) {//判断消息类型
        case 0x112/*_WM_SYSCOMMAND*/ {
            if(wparam=0xF120/*_SC_RESTORE*/){   
                relocCtl();
            } 
            if(wparam=0xF030/*_SC_MAXIMIZE*/){   
                relocCtl();
            }
        }
    }
    //无返回值则继续调用默认回调函数
}
/*}}*/
/*弹出历史记录菜单{{*/
mainForm.buttonD.oncommand = function(id,event){
		    var pt = ::POINT();
			::User32.GetCursorPos(pt);
	    	//弹出托盘菜单以前,一定要前置主窗口中,不然不点击菜单不会消失
	    	win.setForeground(mainForm.hwnd)
	    	mainForm.popmenu.popup(pt.x,pt.y,true )
}
/*}}*/
mainForm.show();
mainForm.static.text = _dbpath;
mainForm.editUser.setFocus();
win.loopMessage();